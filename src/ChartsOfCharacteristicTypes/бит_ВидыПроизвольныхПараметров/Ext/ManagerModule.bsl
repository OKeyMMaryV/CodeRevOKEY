#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция формирует структуры связей имен измерений параметров и измерений бюджетирования.
// 
// Параметры:
//   ВидПараметра - ПланВидовХарактеристикСсылка.бит_ВидыПроизвольныхПараметров.
// 
// Возвращаемое значение:
//  Имена - Структура.
// 
Функция ПодготовитьСтруктурыСвязи(ВидПараметра) Экспорт
	
	Имена = Новый Структура;
	// По имени измерения бюджетирования определяем имя измерения значения параметров.
	Имена.Вставить("БП", Новый Структура);
	// По имени измерения параметров определяем имя измерения бюджетирования.
	Имена.Вставить("ПБ", Новый Структура);
	
	Если ЗначениеЗаполнено(ВидПараметра) Тогда
		
		ИзмеренияБюдж = бит_Бюджетирование.НастройкиИзмеренийБюджетирования();
		
		Для каждого СтрокаТаблицы Из ВидПараметра.ИспользованиеАналитики Цикл
			
			Аналитика              = СтрокаТаблицы.Аналитика;
			ИмяАналитики           = СокрЛП(Аналитика.Код);
			ИмяИзмеренияПараметров = СокрЛП(СтрокаТаблицы.ИмяИзмерения);
			
			СовпадениеПоАналитике      = Ложь;
			ИмяАналитикиБюджетирования = "";
			
			Для каждого КиЗ Из ИзмеренияБюдж Цикл
				
				НастройкиБюдж = КиЗ.Значение;
				ИмяБюдж       = СокрЛП(КиЗ.Ключ);
				
				Если НастройкиБюдж = Неопределено Тогда
					Продолжить;
				КонецЕсли;
					
				Если ВРег(ИмяАналитики) = ВРег(ИмяБюдж) Тогда
					// 1й приоритет: Совпадение по именам.
					ИмяАналитикиБюджетирования = ИмяБюдж;
					Прервать;
				КонецЕсли;
				
				Если СтрокаТаблицы.Аналитика = НастройкиБюдж.Аналитика Тогда
					// 2й приориет: Совпадение по аналитикам.
					ИмяАналитикиБюджетирования = ИмяБюдж;
					СовпадениеПоАналитике      = Истина;
				КонецЕсли;
				
				Если СовпадениеПоАналитике Тогда
					Продолжить; // Если уже найдено совпадение по аналитике, выполнять поиск по типам не требуется.
				КонецЕсли;
				
				Сравнение = Новый СравнениеЗначений;
				Если Сравнение.Сравнить(СтрокаТаблицы.Аналитика.ТипЗначения, НастройкиБюдж.ТипЗначения) = 0 Тогда
					ИмяАналитикиБюджетирования = ИмяБюдж;
				КонецЕсли;

			КонецЦикла; // ИзмеренияБюдж
			
			Если НЕ ПустаяСтрока(ИмяАналитикиБюджетирования) Тогда
				Имена.БП.Вставить(ИмяАналитикиБюджетирования, ИмяИзмеренияПараметров);
				Имена.ПБ.Вставить(ИмяИзмеренияПараметров, ИмяАналитикиБюджетирования);
			КонецЕсли;
			
		КонецЦикла; // ВидПараметра.ИспользованиеАналитики
		
	КонецЕсли;
	
	Возврат Имена;
	
КонецФункции // ПодготовитьСтруктурыСвязи()

#КонецОбласти
 
#КонецЕсли
