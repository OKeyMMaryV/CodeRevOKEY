#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Возвращает порядковый номер субконто по виду
//
// Возвращаемое значение:
//   Число - порядковый номер вида субконто. Если вид субконто не найден, возвращается 0.
//
Функция НомерСубконто(Счет, ВидСубконто) Экспорт
	
	Результат = 0;
	
	СвойстваСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(Счет);
	
	Для НомерСубконто = 1 По СвойстваСчета.КоличествоСубконто Цикл
		
		Если СвойстваСчета["ВидСубконто" + НомерСубконто] = ВидСубконто Тогда
			Результат = НомерСубконто;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ВосстановитьТипыСубконтоСФПолученные(Параметры) Экспорт

	Параметры.ОбработкаЗавершена = Истина;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыСубконтоХозрасчетные.Ссылка КАК Ссылка,
	|	ВидыСубконтоХозрасчетные.ТипЗначения КАК ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.ВидыСубконтоХозрасчетные КАК ВидыСубконтоХозрасчетные
	|ГДЕ
	|	ВидыСубконтоХозрасчетные.Ссылка = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СФПолученные)";
	
	Выборка = Запрос.Выполнить().Выбрать();
	ДобавитьСчетФактураВыданный = Истина;
	ДобавитьПутевойЛист = Истина;
	
	Если Выборка.Следующий() Тогда
		
		ТекущиеТипыЗначения = Выборка.ТипЗначения.Типы();

		Для каждого ТипСубконто Из ТекущиеТипыЗначения Цикл
			Если ТипСубконто = Тип("ДокументСсылка.СчетФактураВыданный") Тогда
				ДобавитьСчетФактураВыданный = Ложь;
			ИначеЕсли ТипСубконто = Тип("ДокументСсылка.ПутевойЛист") Тогда 
				ДобавитьПутевойЛист = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ПередатьЦентруМониторингаВыявленныеПроблемыСТипомСубконтоСФПолученные(ДобавитьСчетФактураВыданный, ДобавитьПутевойЛист);
		
		Если Не ДобавитьСчетФактураВыданный И Не ДобавитьПутевойЛист Тогда
			Возврат;
		КонецЕсли;
		
		Если ДобавитьСчетФактураВыданный Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.СчетФактураВыданный"));
		КонецЕсли;
		
		Если ДобавитьПутевойЛист Тогда
			ТекущиеТипыЗначения.Добавить(Тип("ДокументСсылка.ПутевойЛист"));
		КонецЕсли;
		
		НовыеТипыЗначения = Новый ОписаниеТипов(ТекущиеТипыЗначения);

		СФПолученныеОбъект = Выборка.Ссылка.ПолучитьОбъект();
		СФПолученныеОбъект.ТипЗначения = НовыеТипыЗначения;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(СФПолученныеОбъект);
	
	КонецЕсли;

КонецПроцедуры

Процедура ПередатьЦентруМониторингаВыявленныеПроблемыСТипомСубконтоСФПолученные(ОтсутствуетСчетФактураВыданный, ОтсутствуетПутевойЛист)
	
	Если Не ЦентрМониторинга.ЗаписыватьОперацииБизнесСтатистики() Тогда
		Возврат;
	КонецЕсли;
	
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(
		"СтатистикаБП.ПроблемыСТипомСубконтоСФПолученные.ОтсутствуетСчетФактураВыданный",
		Число(ОтсутствуетСчетФактураВыданный));
		
	ЦентрМониторинга.ЗаписатьОперациюБизнесСтатистики(
		"СтатистикаБП.ПроблемыСТипомСубконтоСФПолученные.ОтсутствуетПутевойЛист",
		Число(ОтсутствуетПутевойЛист));
	
КонецПроцедуры

#КонецЕсли

//ОКЕЙ Балыков А.Г.(ПервыйБИТ) Начало 2021-05-14 (#НТП_МП_04)

// Изменен реквизит предопределенный элемент ДокументыРасчетовСКонтрагентами - расширен тип на документ бит_ок_ОперацияАксапты

//ОКЕЙ Балыков А.Г.(ПервыйБИТ) Конец 2021-05-14 (#НТП_МП_04)

//ОКЕЙ Первухин В.С. (ПервыйБИТ) Начало 2021-07-13 (ФР-МП-11)      

// Изменен тип значения характеристик - расширен тип на документ бит_ок_ОперацияАксапты12
// Изменен предопределенный элемент ДокументыРасчетовСКонтрагентами - расширен тип на документ бит_ок_ОперацияАксапты12

//ОКЕЙ Первухин В.С. (ПервыйБИТ) Конец 2021-07-13 (ФР-МП-11)