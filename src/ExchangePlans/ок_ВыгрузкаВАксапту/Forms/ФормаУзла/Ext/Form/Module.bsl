
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СправочникОбъект = РеквизитФормыВЗначение("Объект");
	
	Расписание = СправочникОбъект.РасписаниеРегламентногоЗадания.Получить(); 
	
	УстановитьПредставлениеРасписания(ЭтотОбъект);
	
	ОбновитьИспользуемыеОбъекты();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.РасписаниеРегламентногоЗадания = Новый ХранилищеЗначения(Расписание);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПредставлениеРасписанияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Расписание = Неопределено Тогда
		Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Новый РасписаниеРегламентногоЗадания);
	Иначе
		Диалог = Новый ДиалогРасписанияРегламентногоЗадания(Расписание);
	КонецЕсли;
	ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("ИзменитьРасписание", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИзменитьРасписание(НовоеРасписание, ДополнительныеПараметры) Экспорт
	
	Если НовоеРасписание = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Расписание = НовоеРасписание;
	Модифицированность = Истина;
	УстановитьПредставлениеРасписания(ЭтотОбъект);
		
КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПредставлениеРасписания(Знач Форма)
	
	Расписание = Форма.Расписание;
	
	Если Расписание <> Неопределено Тогда
		Форма.ПредставлениеРасписания = Строка(Расписание);
	Иначе
		Форма.ПредставлениеРасписания = НСтр("ru = '<Не задано>'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИспользуемыеОбъекты()
	
	СоставПланаОбмена = Метаданные.ПланыОбмена.ок_ВыгрузкаВАксапту.Состав;
	Для каждого ЭлементСостава Из СоставПланаОбмена Цикл
		
		НайденныйОбъект = Справочники.бит_ОбъектыСистемы.НайтиПоРеквизиту("ИмяОбъектаПолное", ЭлементСостава.Метаданные.ПолноеИмя());
		
		Если НЕ ЗначениеЗаполнено(НайденныйОбъект) Тогда
			Продолжить;
		КонецЕсли; 
		
		ПараметрыОтбора = Новый Структура("Объект", НайденныйОбъект);
		НайденныеОбъекты = Объект.ОбъектыДляВыгрузки.НайтиСтроки(ПараметрыОтбора);
		Если НайденныеОбъекты.Количество() = 0 Тогда
		
			НоваяСтрока = Объект.ОбъектыДляВыгрузки.Добавить();
			НоваяСтрока.Объект = НайденныйОбъект;
			НоваяСтрока.Выгружается = Ложь;
		
		КонецЕсли; 
		
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти 
