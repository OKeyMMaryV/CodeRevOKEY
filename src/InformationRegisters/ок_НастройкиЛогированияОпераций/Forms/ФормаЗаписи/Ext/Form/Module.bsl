
#Область ОбработчикиСобытийЭлементов

&НаКлиенте
Процедура ИдентификаторОперацииНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка	=	Ложь;
	СписокВыбора			=	ИдентификаторОперацииНачалоВыбораНаСервере();
	
	ОписаниеОповещения		=	Новый ОписаниеОповещения("ИдентификаторОперацииНачалоВыбораЗавершение", ЭтаФорма);
	ТекущийЭлементСписка	=	СписокВыбора.НайтиПоЗначению(Запись.ИдентификаторОперации);
	
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокВыбора, Элементы.ИдентификаторОперации, ТекущийЭлементСписка); 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИдентификаторОперацииНачалоВыбораЗавершение(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если Не ВыбранныйЭлемент = Неопределено Тогда
		Запись.ИдентификаторОперации	=	ВыбранныйЭлемент.Значение;
		
		Позиция							=	Найти(ВыбранныйЭлемент.Представление, ":");
		
		Если Позиция = 0 Тогда
			Запись.Синоним		=	СокрЛП(ВыбранныйЭлемент.Представление);
			Запись.ВидОперации	=	"";
		Иначе
			Запись.Синоним		=	СокрЛП(Сред(ВыбранныйЭлемент.Представление, Позиция + 1));
			Запись.ВидОперации	=	СокрЛП(Лев(ВыбранныйЭлемент.Представление, Позиция - 1));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиПросмотрЛогов(Команда)
	
	ПараметрыОткрытия	=	Новый Структура("ИдентификаторОперации", Запись.ИдентификаторОперации);
	ОткрытьФорму("Обработка.ок_ПросмотрЛоговОпераций.Форма.Форма", ПараметрыОткрытия);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИдентификаторОперацииНачалоВыбораНаСервере()
	
	Возврат ок_ЛогированиеОпераций.ПолучитьЛогируемыеОперации();
	
КонецФункции

#КонецОбласти

