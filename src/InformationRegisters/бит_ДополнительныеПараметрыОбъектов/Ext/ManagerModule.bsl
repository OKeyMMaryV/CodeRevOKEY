#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления 3.1.42.1
// Заполняет ""Запретить перезаполнение аналитик"" у документов, 
// которые имеют записи в регистре "бит_ДополнительныеАналитики".
//	
Процедура ЗаполнитьПриПереходеНаНовуюВерсию(Параметры) Экспорт
		
	РежимЗаполнения     = Константы.бит_РежимАвтоЗаполненияАналитик.Получить();
	ПеречисленияРежимов = Перечисления.бит_РежимыАвтоЗаполненияАналитик;
	Запретить 		    = Ложь;
	
	// При переключении режима в константах на заполнение, этот прием исключит перезаполнение аналитик введенных вручную.
	Если РежимЗаполнения = ПеречисленияРежимов.НеЗаполнять Тогда
		Запретить = Истина;
	Иначе	
		Если бит_ОбщегоНазначения.ЭтоСемействоБП() Тогда
			Если РежимЗаполнения = ПеречисленияРежимов.ЗаполнятьТолькоДляНовыхДокументов Тогда
				Запретить = Истина;
			КонецЕсли; 
		КонецЕсли;
	КонецЕсли; 

	Если НЕ Запретить Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли; 	
	
	НачатьТранзакцию();
	
	// Заблокируем данные регистра сведений от чтения и записи
	Попытка
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.бит_ДополнительныеПараметрыОбъектов");
		Блокировка.Заблокировать();
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение;
	КонецПопытки;

	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Аналитики.Объект КАК Объект
	|ПОМЕСТИТЬ ДокументыДляОбработки
	|ИЗ
	|	РегистрСведений.бит_ДополнительныеАналитики КАК Аналитики
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1000
	|	ДокументыДляОбработки.Объект КАК Объект,
	|	ИСТИНА КАК ЗапретитьПерезаполнениеАналитик
	|ИЗ
	|	ДокументыДляОбработки КАК ДокументыДляОбработки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ДополнительныеПараметрыОбъектов КАК бит_ДополнительныеПараметрыОбъектов
	|		ПО ДокументыДляОбработки.Объект = бит_ДополнительныеПараметрыОбъектов.Объект
	|ГДЕ
	|	бит_ДополнительныеПараметрыОбъектов.Объект ЕСТЬ NULL";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Количество() = 0 Тогда
		Параметры.ОбработкаЗавершена = Истина;
		ЗафиксироватьТранзакцию();
		Возврат;
	КонецЕсли;
	
	Параметры.ОбработкаЗавершена = Ложь;
	ОбработаноОбъектов = 0;
	
	НаборЗаписей = РегистрыСведений.бит_ДополнительныеПараметрыОбъектов.СоздатьНаборЗаписей();
	
	Пока Выборка.Следующий() Цикл
		
		НаборЗаписей.Отбор.Объект.Установить(Выборка.Объект);
		ЗаполнитьЗначенияСвойств(НаборЗаписей.Добавить(), Выборка);
		
		Попытка
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей, Истина);
			ОбработаноОбъектов = ОбработаноОбъектов + 1;
			НаборЗаписей.Очистить();
		Исключение
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Не удалось заполнить ""Запретить перезаполнение аналитик"" для документа %1 по причине:
				|%2'", ОбщегоНазначения.КодОсновногоЯзыка()),
				Выборка.Объект,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Документы.ПоступлениеДопРасходов, 
				Выборка.Объект, 
				ТекстСообщения);
			ВызватьИсключение ТекстСообщения;	
		КонецПопытки;
	КонецЦикла;
	
	Если ОбработаноОбъектов > 0 Тогда
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Процедура ЗаполнитьПриПереходеНаНовуюВерсию: обработано документов: %1'", 
			ОбщегоНазначения.КодОсновногоЯзыка()), 
			ОбработаноОбъектов);
		ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), 
			УровеньЖурналаРегистрации.Информация,
			Метаданные.РегистрыСведений.бит_ДополнительныеПараметрыОбъектов,
			,
			ТекстСообщения);
	КонецЕсли;

	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
