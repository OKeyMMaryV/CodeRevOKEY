#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат;	
	КонецЕсли; 
	
	МаксКоличествоАналитик = Справочники.бит_ВидыСоответствийАналитик.МаксКоличествоАналитик();
	
	Для каждого Запись Из ЭтотОбъект Цикл
	
		Соотношение        = Запись.ВидСоответствия.Соотношение;
		ЕстьОшибки         = Ложь;
		ПроверкаНеобходима = Ложь;
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидСоответствия", Запись.ВидСоответствия);
		
		Для н = 1 По МаксКоличествоАналитик Цикл
		
			ИмяПоля = "ЛеваяАналитика_"+н;
			Запрос.УстановитьПараметр(ИмяПоля, Запись[ИмяПоля]);
		
			ИмяПоля = "ПраваяАналитика_"+н;
			Запрос.УстановитьПараметр(ИмяПоля, Запись[ИмяПоля]);
					
		КонецЦикла; 
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	Соотв.ВидСоответствия,
		               |	Соотв.ЛеваяАналитика_1,
		               |	Соотв.ЛеваяАналитика_2,
		               |	Соотв.ЛеваяАналитика_3,
		               |	Соотв.ПраваяАналитика_1,
		               |	Соотв.ПраваяАналитика_2,
		               |	Соотв.ПраваяАналитика_3
		               |ИЗ
		               |	РегистрСведений.бит_СоответствияАналитик КАК Соотв
		               |ГДЕ
		               |	Соотв.ВидСоответствия = &ВидСоответствия
		               |	%УсловиеНаАналитики%";
				 
		
		Если Соотношение = Перечисления.бит_ВидыСоотношенийАналитик.МногиеКоМногим Тогда
		
			// Проверка не нужна
			
		ИначеЕсли Соотношение = Перечисления.бит_ВидыСоотношенийАналитик.МногиеКОдному Тогда
			
			ТекстУсловия = "И Соотв.ЛеваяАналитика_1 = &ЛеваяАналитика_1
		                   |И Соотв.ЛеваяАналитика_2 = &ЛеваяАналитика_2
		                   |И Соотв.ЛеваяАналитика_3 = &ЛеваяАналитика_3";

			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеНаАналитики%", ТекстУсловия);
		    ПроверкаНеобходима = Истина;
			
		ИначеЕсли Соотношение = Перечисления.бит_ВидыСоотношенийАналитик.ОдинКоМногим Тогда
			
			ТекстУсловия = "И Соотв.ПраваяАналитика_1 = &ПраваяАналитика_1
		                   |И Соотв.ПраваяАналитика_2 = &ПраваяАналитика_2
		                   |И Соотв.ПраваяАналитика_3 = &ПраваяАналитика_3";
			
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеНаАналитики%", ТекстУсловия);
		    ПроверкаНеобходима = Истина;
			
		ИначеЕсли Соотношение = Перечисления.бит_ВидыСоотношенийАналитик.ОдинКОдному Тогда	
			
			ТекстУсловия = "И ((Соотв.ЛеваяАналитика_1 = &ЛеваяАналитика_1
		                   |   И Соотв.ЛеваяАналитика_2 = &ЛеваяАналитика_2
		                   |   И Соотв.ЛеваяАналитика_3 = &ЛеваяАналитика_3)
		                   |ИЛИ (Соотв.ПраваяАналитика_1 = &ПраваяАналитика_1
		                   |	 И Соотв.ПраваяАналитика_2 = &ПраваяАналитика_2
		                   |	 И Соотв.ПраваяАналитика_3 = &ПраваяАналитика_3))";

			Запрос.Текст = СтрЗаменить(Запрос.Текст, "%УсловиеНаАналитики%", ТекстУсловия);
		    ПроверкаНеобходима = Истина;
			
		КонецЕсли; 
		
		Если ПроверкаНеобходима Тогда
			
			Результат = Запрос.Выполнить();
			
			Если НЕ Результат.Пустой() Тогда
				
				ЕстьОшибки = Истина;
				
			КонецЕсли; 		
			
			Если ЕстьОшибки Тогда
				
				Отказ = Истина;
				ТекстСообщения = НСтр("en = 'Record does not correspond to relation kind ""%1%"".'; ru = 'Запись не соответствует соотношению ""%1%"".'"); 
				ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Соотношение);
				бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
				
			КонецЕсли;                                                                                                                                 
			                                                                                                                                       
		КонецЕсли; 
	
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
