#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Обработчик обновления ИБ.
// Добавляет записи с бюджетами у которых имеются дубли статей оборотов. 
//	
Процедура ЗаполнитьИнформациюОБюджетах() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	бит_СтатьиБюджета.Владелец КАК Бюджет,
	|	КОЛИЧЕСТВО(бит_СтатьиБюджета.СтатьяОборотов) КАК КоличествоСтатей
	|ПОМЕСТИТЬ ВТБюджеты
	|ИЗ
	|	Справочник.бит_СтатьиБюджета КАК бит_СтатьиБюджета
	|ГДЕ
	|	бит_СтатьиБюджета.СтатьяОборотов <> ЗНАЧЕНИЕ(Справочник.бит_СтатьиОборотов.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	бит_СтатьиБюджета.Владелец,
	|	бит_СтатьиБюджета.СтатьяОборотов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТБюджеты.Бюджет КАК Бюджет,
	|	МАКСИМУМ(ВТБюджеты.КоличествоСтатей) КАК КоличествоСтатей
	|ИЗ
	|	ВТБюджеты КАК ВТБюджеты
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТБюджеты.Бюджет";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НаборЗаписей = РегистрыСведений.бит_БюджетЗапрещенКИспользованиюВПланировании.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Бюджет.Установить(Выборка.Бюджет);
		Запись = НаборЗаписей.Добавить();
		Запись.Бюджет = Выборка.Бюджет;
		Если Выборка.КоличествоСтатей > 1 Тогда
			Запись.Запрещен	= Истина; 
		Иначе
			Запись.Запрещен	= Ложь; 
		КонецЕсли;
		ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
	КонецЦикла;	
		
КонецПроцедуры

#КонецОбласти

#КонецЕсли