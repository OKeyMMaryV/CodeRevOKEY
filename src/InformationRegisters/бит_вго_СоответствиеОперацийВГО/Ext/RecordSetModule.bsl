#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПередЗаписью" набора записей.
// 
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		// В случае выполнения обмена данными не производить проверку.
		Возврат;
	КонецЕсли;
	
	ВидыОпераций = Перечисления.бит_вго_ВидыОперацийВГО;

	Если ЭтотОбъект.Количество() <> 0 Тогда
        
        ТаблицаДляПроверки = ЭтотОбъект.Выгрузить();
		
		Для каждого СтрокаТаблицы Из ТаблицаДляПроверки Цикл
			
			Если СтрокаТаблицы.ОперацияПоставщика.ВидОперации = ВидыОпераций.Остатки
				ИЛИ СтрокаТаблицы.ОперацияПоставщика.ВидОперации = ВидыОпераций.Техническая 
				ИЛИ СтрокаТаблицы.ОперацияПокупателя.ВидОперации = ВидыОпераций.Остатки
				ИЛИ СтрокаТаблицы.ОперацияПокупателя.ВидОперации = ВидыОпераций.Техническая
				ИЛИ СтрокаТаблицы.ОперацияПоставщика.ВидОперации = ВидыОпераций.ОперацияПокупателя
				ИЛИ СтрокаТаблицы.ОперацияПокупателя.ВидОперации = ВидыОпераций.ОперацияПоставщика Тогда
			
				Отказ = Истина;				 
				 
				ТекстСообщения = Нстр("ru = 'Невозможно записать соответствие. Некорректные значения операций.'");
				бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
				
				Прервать;
				
			КонецЕсли;
			    		
		КонецЦикла;
		
		МассивОперацийПоставщика = ТаблицаДляПроверки.ВыгрузитьКолонку("ОперацияПоставщика");
		МассивОперацийПокупателя = ТаблицаДляПроверки.ВыгрузитьКолонку("ОперацияПокупателя");
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("МассивОперацийПоставщика", МассивОперацийПоставщика);
		Запрос.УстановитьПараметр("МассивОперацийПокупателя", МассивОперацийПокупателя);
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	СоответствиеОперацийВГО.ОперацияПоставщика,
		|	СоответствиеОперацийВГО.ОперацияПокупателя
		|ИЗ
		|	РегистрСведений.бит_вго_СоответствиеОперацийВГО КАК СоответствиеОперацийВГО
		|ГДЕ
		|	(СоответствиеОперацийВГО.ОперацияПоставщика В(&МассивОперацийПоставщика)
		|			ИЛИ СоответствиеОперацийВГО.ОперацияПокупателя В(&МассивОперацийПокупателя))
		|";
		РезультатЗапроса = Запрос.Выполнить();
		Если Не РезультатЗапроса.Пустой() Тогда
			
			Отказ = Истина;
			
			ТекстСообщения = Нстр("ru = 'Невозможно записать соответствие. Значения операций уже участвуют в записанных соответствиях.'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
											
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ПередЗаписью()

#КонецОбласти

#КонецЕсли
