#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем мНастройкиВидовОбластей Экспорт; // Хранит настройки видов областей доступа.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда	
		Возврат; 	
	КонецЕсли; 
	
	Для каждого Запись Из ЭтотОбъект Цикл
	
		Если Не ЗначениеЗаполнено(Запись.ВидНастройки) Тогда		 
			ТекстСообщения = Нстр("ru = 'Не заполнено значение реквизита ""Вид настройки""!'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения, Запись, "ВидНастройки", Отказ);
		КонецЕсли; 

		Если Не ЗначениеЗаполнено(Запись.ВидОбластиДоступа) Тогда 		 
			ТекстСообщения = Нстр("ru = 'Не заполнено значение реквизита ""Вид области доступа""!'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения, Запись, "ВидОбластиДоступа", Отказ);
		КонецЕсли; 

		Если Не ЗначениеЗаполнено(Запись.Пользователь) Тогда 		 
			ТекстСообщения = Нстр("ru = 'Не заполнено значение реквизита ""Пользователь""!'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения, Запись, "Пользователь", Отказ);
		КонецЕсли;		 
		        
		Если ЗначениеЗаполнено(Запись.ВидНастройки) И ЗначениеЗаполнено(Запись.ВидОбластиДоступа) И ЗначениеЗаполнено(Запись.Пользователь) Тогда
			
			ПротивоположныйВидНастройки = ?(Запись.ВидНастройки = Перечисления.бит_рлс_ВидыНастроекПрав.Доступ
	 										, Перечисления.бит_рлс_ВидыНастроекПрав.Запрет
											, Перечисления.бит_рлс_ВидыНастроекПрав.Доступ);
			
			НаборЗаписей = РегистрыСведений.бит_рлс_ПраваДоступа.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВидНастройки.Установить(ПротивоположныйВидНастройки);
			НаборЗаписей.Отбор.ВидОбластиДоступа.Установить(Запись.ВидОбластиДоступа);
			НаборЗаписей.Отбор.Пользователь.Установить(Запись.Пользователь);
			НаборЗаписей.Отбор.Разделитель_1.Установить(Запись.Разделитель_1);
			НаборЗаписей.Отбор.Разделитель_2.Установить(Запись.Разделитель_2);
			НаборЗаписей.Отбор.Разделитель_3.Установить(Запись.Разделитель_3);
			НаборЗаписей.Отбор.Разделитель_4.Установить(Запись.Разделитель_4);
			НаборЗаписей.Прочитать();
			Если НаборЗаписей.Количество() > 0 Тогда
				ТекстИзмерений = Нстр("ru = 'Вид настройки: '") + Запись.ВидНастройки 
								+ Нстр("ru = ', Вид области доступа: '") + Запись.ВидОбластиДоступа 
								+ Нстр("ru = ', Пользователь: '") + Запись.Пользователь;
				Если Запись.ВидОбластиДоступа = Перечисления.бит_рлс_ВидыОбластейДоступа.ЦФОСтатьиОборотов Тогда
					ТекстИзмерений = ТекстИзмерений	+ Нстр("ru = ', ЦФО: '") + Запись.Разделитель_1 + Нстр("ru = ', Статья оборотов: '") + Запись.Разделитель_2;
				ИначеЕсли Запись.ВидОбластиДоступа = Перечисления.бит_рлс_ВидыОбластейДоступа.Пользователи Тогда	
					ТекстИзмерений = ТекстИзмерений	+ Нстр("ru = ', Пользователь: '") + Запись.Разделитель_3;
				ИначеЕсли Запись.ВидОбластиДоступа = Перечисления.бит_рлс_ВидыОбластейДоступа.Проекты Тогда					
				    ТекстИзмерений = ТекстИзмерений	+ Нстр("ru = ', Проект: '") + Запись.Разделитель_4;
				КонецЕсли;
				ТекстСообщения = Нстр("ru = 'Существует аналогичная запись в регистре с противоположным значением вида настройки.'") 
									+ Символы.ПС + ТекстИзмерений;
				бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения, , , Отказ);
			КонецЕсли;                                                                                                                   	 	
		 
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()

#КонецОбласти

#Область Инициализация

мНастройкиВидовОбластей = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_НастройкиВидовОбластейДоступа");

#КонецОбласти

#КонецЕсли
