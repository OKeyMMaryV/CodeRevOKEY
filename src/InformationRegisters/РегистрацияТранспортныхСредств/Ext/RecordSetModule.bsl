#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Справочники.Организации.ДополнитьДанныеЗаполненияПриОднофирменномУчете(ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Количество() = 1 Тогда
		Запись = Получить(0);
		
		Если Запись.ВидЗаписи = Перечисления.ВидЗаписиОРегистрации.СнятиеСРегистрационногоУчета Тогда
			
			МассивНепроверяемыхРеквизитов.Добавить("РегистрационныйЗнак");
			МассивНепроверяемыхРеквизитов.Добавить("ИдентификационныйНомер");
			МассивНепроверяемыхРеквизитов.Добавить("Марка");
			МассивНепроверяемыхРеквизитов.Добавить("ПостановкаНаУчетВНалоговомОргане");
			МассивНепроверяемыхРеквизитов.Добавить("КодВидаТранспортногоСредства");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяБаза");
			МассивНепроверяемыхРеквизитов.Добавить("ЕдиницаИзмеренияНалоговойБазы");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяСтавка");
			МассивНепроверяемыхРеквизитов.Добавить("НалоговаяЛьгота");
			МассивНепроверяемыхРеквизитов.Добавить("ЭкологическийКласс");
			МассивНепроверяемыхРеквизитов.Добавить("ВидЗаписи");
			
		КонецЕсли;
		
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	РегистрыСведений.РегистрацияТранспортныхСредств.ПроверитьЗначениеОпцииВедетсяУчетПлатежейВПлатон(ЭтотОбъект, Отказ, Замещение);
	
	Если ПолучитьФункциональнуюОпцию("ВедетсяУчетПоПутевымЛистам") Тогда
		СоздатьТранспортныеСредства();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СоздатьТранспортныеСредства()
	
	ОсновныеСредства = Выгрузить(, "ОсновноеСредство, ИдентификационныйНомер, Марка, РегистрационныйЗнак, Организация");
	ГруппыОС = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(ОсновныеСредства.ВыгрузитьКолонку("ОсновноеСредство"), "ГруппаОС");
	
	ТипТранспортныеСредства = Перечисления.ГруппыОС.ТранспортныеСредства;
	
	ТранспортныеСредства = ОсновныеСредства.СкопироватьКолонки();
	Для Каждого ТекущаяСтрока Из ОсновныеСредства Цикл
		
		Если Не ПустаяСтрока(ТекущаяСтрока.ИдентификационныйНомер)
			И ГруппыОС[ТекущаяСтрока.ОсновноеСредство].ГруппаОС = ТипТранспортныеСредства Тогда
			
			ЗаполнитьЗначенияСвойств(ТранспортныеСредства.Добавить(), ТекущаяСтрока);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(ТранспортныеСредства) Тогда
		
		Попытка
			
			Справочники.ТранспортныеСредства.СоздатьТранспортныеСредства(ТранспортныеСредства);
			
		Исключение
			
			ТекстОшибки = СтрШаблон(НСтр("ru = 'Не удалось создать транспортное средство по причине:
				|%1.'"), ОписаниеОшибки());
			ИмяСобытия = НСтр("ru = 'Данные.Добавление'", ОбщегоНазначения.КодОсновногоЯзыка());
			
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка,
				Метаданные.РегистрыСведений.РегистрацияТранспортныхСредств,, ТекстОшибки);
			
			ВызватьИсключение НСтр("ru = 'Не удалось создать транспортное средство.'");
			
		КонецПопытки;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли