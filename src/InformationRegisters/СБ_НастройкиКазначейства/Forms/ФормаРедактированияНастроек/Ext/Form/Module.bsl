//ОКЕЙ Рычаков А.C. (СофтЛаб) Начало 2019-11-30 (#3437)
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПолучитьСБ_НастройкиКазначейства();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьСБ_НастройкиКазначейства()
		
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	СБ_НастройкиКазначейства.Идентификатор КАК Идентификатор,
	                      |	СБ_НастройкиКазначейства.Значение КАК Значение,
	                      |	СБ_НастройкиКазначейства.ЗначениеВХранилище КАК ЗначениеВХранилище,
	                      |	СБ_НастройкиКазначейства.ХранилищеЗначения КАК ХранилищеЗначения,
	                      |	СБ_НастройкиКазначейства.ок_ПредставлениеНастройки КАК ПредставлениеНастройки,
	                      |	СБ_НастройкиКазначейства.ок_ПредставлениеРаздела КАК ПредставлениеРаздела,
	                      |	СБ_НастройкиКазначейства.ок_Комментарий КАК Комментарий
	                      |ИЗ
	                      |	РегистрСведений.СБ_НастройкиКазначейства КАК СБ_НастройкиКазначейства");
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗНастроек = РезультатЗапроса.Выгрузить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		
		КомандаЗаписать = Команды.Добавить("КомандаЗаписать"); 
		КомандаЗаписать.Заголовок  = "Записать"; 
		КомандаЗаписать.Действие   = "ВыполнитьДействиеКомандаЗаписать"; 	
		
		НовыйЭлементКоманды = Элементы.Добавить("КомандаЗаписать", Тип("КнопкаФормы"), ЭтаФорма.КоманднаяПанель);
		НовыйЭлементКоманды.Вид = ВидКнопкиФормы.ОбычнаяКнопка; 
		НовыйЭлементКоманды.Заголовок  = "Записать";
		НовыйЭлементКоманды.ИмяКоманды = "КомандаЗаписать";
		
	КонецЕсли;

	МассивРеквизитов = Новый Массив;
	
	ТЗНастроек.Колонки.Добавить("ID_Реквизита", Новый ОписаниеТипов("Строка"));
	
	Для каждого Настройка Из ТЗНастроек Цикл
		
		Настройка.ID_Реквизита = "Реквизит" + ТЗНастроек.Индекс(Настройка);
		
		МассивТипов = Новый Массив;
			
		Если Настройка.ЗначениеВХранилище Тогда
			МассивТипов.Добавить(ТипЗНЧ(Настройка.ХранилищеЗначения.Получить()));	
		Иначе	
			МассивТипов.Добавить(ТипЗНЧ(Настройка.Значение));
		КонецЕсли;
			
		мОписаниеТипов = Новый ОписаниеТипов(МассивТипов);
			
		МассивРеквизитов.Добавить(Новый РеквизитФормы(Настройка.ID_Реквизита, мОписаниеТипов,,Настройка.ПредставлениеНастройки));
		
	КонецЦикла;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ТЗ.Идентификатор КАК Идентификатор,
	                      |	ТЗ.Значение КАК Значение,
	                      |	ТЗ.ID_Реквизита КАК ID_Реквизита,
	                      |	ТЗ.ЗначениеВХранилище КАК ЗначениеВХранилище,
	                      |	ТЗ.ХранилищеЗначения КАК ХранилищеЗначения,
	                      |	ТЗ.ПредставлениеНастройки КАК ПредставлениеНастройки,
	                      |	ТЗ.ПредставлениеРаздела КАК ПредставлениеРаздела,
	                      |	ТЗ.Комментарий КАК Комментарий
						  |ПОМЕСТИТЬ ВТ
	                      |ИЗ
	                      |	&ТЗ КАК ТЗ
						  |;
						  |ВЫБРАТЬ
	                      |	ВТ.Идентификатор КАК Идентификатор,
	                      |	ВТ.Значение КАК Значение,
	                      |	ВТ.ID_Реквизита КАК ID_Реквизита,
	                      |	ВТ.ЗначениеВХранилище КАК ЗначениеВХранилище,
	                      |	ВТ.ХранилищеЗначения КАК ХранилищеЗначения,
	                      |	ВТ.ПредставлениеНастройки КАК ПредставлениеНастройки,
	                      |	ВТ.ПредставлениеРаздела КАК ПредставлениеРаздела,
	                      |	ВТ.Комментарий КАК Комментарий
	                      |ИЗ
	                      |	ВТ КАК ВТ
						  |ИТОГИ ПО
	                      |	ПредставлениеРаздела");
						  
	Запрос.Параметры.Вставить("ТЗ",ТЗНастроек);
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаЭлементовФормы.Загрузить(ТЗНастроек);

	ВыборкаПредставлениеРаздела = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ИзменитьРеквизиты(МассивРеквизитов);

	Страница = 0;

	Пока ВыборкаПредставлениеРаздела.Следующий() Цикл
		
		ПредставлениеРаздела = ?(ПустаяСтрока(ВыборкаПредставлениеРаздела.ПредставлениеРаздела),"Без группы",ВыборкаПредставлениеРаздела.ПредставлениеРаздела);
	
		ВыборкаДетальныеЗаписи = ВыборкаПредставлениеРаздела.Выбрать();
		
		ГруппаВкладка = Элементы.Добавить("ГруппаСтраница" + Страница, Тип("ГруппаФормы"), Элементы.ГруппаСтраницы);
		ГруппаВкладка.Вид = ВидГруппыФормы.Страница;
		ГруппаВкладка.Заголовок	= ПредставлениеРаздела;
		ГруппаВкладка.РастягиватьПоВертикали = Истина;
		ГруппаВкладка.РастягиватьПоГоризонтали = Истина;
		Страница = Страница + 1;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			ПредставлениеНастройки = ?(ПустаяСтрока(ВыборкаДетальныеЗаписи.ПредставлениеНастройки),ВыборкаДетальныеЗаписи.Идентификатор,ВыборкаДетальныеЗаписи.ПредставлениеНастройки);
			
			НовыйЭлемент = Элементы.Добавить(ВыборкаДетальныеЗаписи.ID_Реквизита, Тип("ПолеФормы"), ГруппаВкладка);
			НовыйЭлемент.ПутьКДанным = ВыборкаДетальныеЗаписи.ID_Реквизита;
			НовыйЭлемент.Вид = ВидПоляФормы.ПолеВвода;
			НовыйЭлемент.Заголовок = ПредставлениеНастройки;
			НовыйЭлемент.Подсказка = ВыборкаДетальныеЗаписи.Комментарий;
			НовыйЭлемент.УстановитьДействие("ПриИзменении", "ЭлементФормыПриИзменении");
			
			Если ВыборкаДетальныеЗаписи.ЗначениеВХранилище Тогда
				ЗначениеИзХранилища = ВыборкаДетальныеЗаписи.ХранилищеЗначения.Получить();
				ЭтаФорма[ВыборкаДетальныеЗаписи.ID_Реквизита] = ЗначениеИзХранилища;
				Если ТипЗНЧ(ЗначениеИзХранилища) = Тип("СписокЗначений") И 
					ЗначениеИзХранилища.Количество() > 0 Тогда
					МассивТиповСпискаЗначений = Новый Массив;
					МассивТиповСпискаЗначений.Добавить(ТипЗнч(ЗначениеИзХранилища.Получить(0).Значение));
					ЭтаФорма[ВыборкаДетальныеЗаписи.ID_Реквизита].ТипЗначения = Новый ОписаниеТипов(МассивТиповСпискаЗначений);
				КонецЕсли;
			Иначе	
				ЭтаФорма[ВыборкаДетальныеЗаписи.ID_Реквизита] = ВыборкаДетальныеЗаписи.Значение;		
			КонецЕсли; 	

		КонецЦикла;
	КонецЦикла;
	
КонецФункции

&НаКлиенте
Процедура ЭлементФормыПриИзменении(Элемент)
	ЭлементФормыПриИзмененииСервере();
КонецПроцедуры

&НаСервере
Процедура ЭлементФормыПриИзмененииСервере()
	ТекущийЭлемент.ШрифтЗаголовка = Новый Шрифт(ТекущийЭлемент.Шрифт,,,Истина);
	НоваяСтрока = ТаблицаИзмененныхЭлементов.Добавить();
	НоваяСтрока.ID_Реквизита = ТекущийЭлемент.Имя;
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействиеКомандаЗаписать()
	ВыполнитьДействиеКомандаЗаписатьНаСервере()
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействиеКомандаЗаписатьНаСервере()
	Для Каждого ИзмененныйЭлемент Из ТаблицаИзмененныхЭлементов Цикл
		ИдентификаторРеквизитаФормы = ИзмененныйЭлемент.ID_Реквизита;
		МассивID_Реквизита = ТаблицаЭлементовФормы.НайтиСтроки(Новый Структура("ID_Реквизита",ИдентификаторРеквизитаФормы));
		Если МассивID_Реквизита.количество() > 0 Тогда 
			ИдентификаторРеквизитаРС = МассивID_Реквизита[0].Идентификатор;
			Если ТипЗнч(ЭтаФорма[ИдентификаторРеквизитаФормы]) = Тип("СписокЗначений") Тогда
				НаборСБ_НастройкиКазначейства = РегистрыСведений.СБ_НастройкиКазначейства.СоздатьНаборЗаписей();
				НаборСБ_НастройкиКазначейства.Отбор.Идентификатор.Установить(ИдентификаторРеквизитаРС);
				НаборСБ_НастройкиКазначейства.Прочитать();
				Для Каждого ЗаписьНабора из НаборСБ_НастройкиКазначейства Цикл
					ЗаписьНабора.ХранилищеЗначения = Новый ХранилищеЗначения(ЭтаФорма[ИдентификаторРеквизитаФормы]);
				КонецЦикла;
				НаборСБ_НастройкиКазначейства.Записать();
			Иначе
				НаборСБ_НастройкиКазначейства = РегистрыСведений.СБ_НастройкиКазначейства.СоздатьНаборЗаписей();
				НаборСБ_НастройкиКазначейства.Отбор.Идентификатор.Установить(ИдентификаторРеквизитаРС);
				НаборСБ_НастройкиКазначейства.Прочитать();
				Для Каждого ЗаписьНабора из НаборСБ_НастройкиКазначейства Цикл
					ЗаписьНабора.Значение = ЭтаФорма[ИдентификаторРеквизитаФормы];
				КонецЦикла;
				НаборСБ_НастройкиКазначейства.Записать();
			КонецЕсли;
		КонецЕсли;
		Элементы[ИдентификаторРеквизитаФормы].ШрифтЗаголовка = Новый Шрифт(ТекущийЭлемент.Шрифт,,,Ложь);
	КонецЦикла;
	ТаблицаИзмененныхЭлементов.Очистить();
КонецПроцедуры
//ОКЕЙ Рычаков А.C. (СофтЛаб) Конец 2019-11-30 (#3437)
