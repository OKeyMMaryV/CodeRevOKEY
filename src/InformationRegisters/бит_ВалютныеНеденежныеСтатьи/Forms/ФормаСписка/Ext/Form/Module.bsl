
#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если фОтказ Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	МетаданныеОбъекта = Метаданные.РегистрыСведений.бит_ВалютныеНеденежныеСтатьи;
	
	// Вызов механизма защиты.
	
	
	бит_РаботаСДиалогамиСервер.ФормаСпискаРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);

	Если фОтказ Тогда
		Возврат;
	КонецЕсли;
	
	// Заполним значения реквизитов формы.
	МетаРегБухБюджетирование = Метаданные.РегистрыБухгалтерии.бит_Бюджетирование;	
	фРегБухБюджетирование    = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(МетаРегБухБюджетирование);
	
	Если бит_ОбщегоНазначения.ЕстьОбъектыМСФО() Тогда	
		МетаРегБухМеждународный = Метаданные.РегистрыБухгалтерии.бит_Дополнительный_2;
		фРегБухМеждународный    = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(МетаРегБухМеждународный);
	КонецЕсли;

	Если Параметры.Свойство("РФ_РегистрБухгалтерии") Тогда
		фИмяРегистраОтбор = Параметры.РФ_РегистрБухгалтерии.ИмяОбъекта;
		фФормаСпискаОткрытаСОтбором = Истина; 	
	КонецЕсли;
		
	// При необходимости установим отбора по объекту системы.
	УстановитьОтборВСписке();
	
	// Установить видимость реквизитов и заголовков колонок.	
	УправлениеЭлементамиФормы();
	
	// Установим заголовок формы.
	Если фФормаСпискаОткрытаСОтбором Тогда
		
		Если фИмяРегистраОтбор = "бит_Бюджетирование" Тогда
			Заголовок = Заголовок + " бюджетирования";
			
		ИначеЕсли бит_му_ОбщегоНазначения.ЭтоМеждународныйПланСчетовИлиРегистр(фИмяРегистраОтбор) Тогда 
			Заголовок = Заголовок + " (МСФО)";
			
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьУсловноеОформление();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура УстановитьОтборВСписке()
	
	Если Не фФормаСпискаОткрытаСОтбором Тогда
		Возврат;
	КонецЕсли;
	
	МетаРегБухДляОтбора    = Метаданные.РегистрыБухгалтерии[фИмяРегистраОтбор];
	ОбъектСистемыДляОтбора = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(МетаРегБухДляОтбора);
	
	бит_ОбщегоНазначенияКлиентСервер.УстановитьОтборУСписка(Список.Отбор
														   , Новый ПолеКомпоновкиДанных("ОбъектСистемы")
														   , ОбъектСистемыДляОтбора);
	
КонецПроцедуры // УстановитьОтборВСписке()

&НаСервере 
Процедура УправлениеЭлементамиФормы()
	
	ТолькоБюджетирование = фИмяРегистраОтбор = "бит_Бюджетирование"  ;
	ТолькоМУ 			 = бит_му_ОбщегоНазначения.ЭтоМеждународныйПланСчетовИлиРегистр(фИмяРегистраОтбор);
	
	Элементы.ОбъектСистемы.Видимость = Не (ТолькоБюджетирование Или ТолькоМУ);
	Элементы.СуммаСценарий.Видимость = Не ТолькоМУ;
	Если бит_ОбщегоНазначения.ЕстьОбъектыМСФО() Тогда
		Элементы.СуммаМУ.Видимость = Не ТолькоБюджетирование;	
	КонецЕсли; 	
	
КонецПроцедуры // УправлениеЭлементамиФормы()

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	// СуммаМУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СуммаМУ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ОбъектСистемы", ВидСравненияКомпоновкиДанных.НеРавно, фРегБухМеждународный);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.бит_ЦветНедоступногоПоля);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// СуммаСценарий
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "СуммаСценарий");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Список.ОбъектСистемы", ВидСравненияКомпоновкиДанных.НеРавно, фРегБухБюджетирование);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветФона", ЦветаСтиля.бит_ТекстНедоступнойНадписи);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры
 
#КонецОбласти

