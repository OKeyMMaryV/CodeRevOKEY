#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ РеквизитФормыВЗначение("Запись").Выбран() Тогда
		
		Запись.Очередность = ПолучитьПоследнийНомерОчередности();
		Запись.Используется = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолучитьЗначениеИзВспомогательныхДанныхПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаПравилПриИзменении(Элемент)
	
	ГруппаПравилПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ГруппаПравилПриИзмененииНаСервере()
	
	Если НЕ РеквизитФормыВЗначение("Запись").Выбран() Тогда
		
		Запись.Очередность = ПолучитьПоследнийНомерОчередности();
	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ГруппаАлгоритм.Видимость 				= НЕ Запись.ПолучитьЗначениеИзВспомогательныхДанных;
	Элементы.ГруппаВспомогательныеДанные.Видимость 	= Запись.ПолучитьЗначениеИзВспомогательныхДанных;
	
	Если Запись.ПолучитьЗначениеИзВспомогательныхДанных 
		И ТипЗнч(Запись.Алгоритм) <> Тип("Строка") Тогда
		Запись.Алгоритм = "";
	ИначеЕсли НЕ Запись.ПолучитьЗначениеИзВспомогательныхДанных
		И ТипЗнч(Запись.Алгоритм) <> Тип("СправочникСсылка.бит_ПользовательскиеФункции") Тогда
		Запись.Алгоритм = ПредопределенноеЗначение("Справочник.бит_ПользовательскиеФункции.ПустаяСсылка");
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПоследнийНомерОчередности()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(МАКСИМУМ(ок_ПравилаКонвертацииЗначений.Очередность), 0) КАК Очередность
		|ИЗ
		|	РегистрСведений.ок_ПравилаКонвертацииЗначений КАК ок_ПравилаКонвертацииЗначений
		|ГДЕ
		|	ок_ПравилаКонвертацииЗначений.ГруппаПравил = &ГруппаПравил";
	
	Запрос.УстановитьПараметр("ГруппаПравил", Запись.ГруппаПравил);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат (ВыборкаДетальныеЗаписи.Очередность + 1);
	Иначе
		Возврат 1;
	КонецЕсли;
	
КонецФункции



#КонецОбласти 