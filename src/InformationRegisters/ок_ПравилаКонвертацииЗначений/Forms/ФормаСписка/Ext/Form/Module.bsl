#Область ОбработчикиСобытийФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если Поле.Имя = "Используется" Тогда
			
			СтандартнаяОбработка = Ложь;
			ИзменитьФлагИспользуется(ВыбраннаяСтрока, НЕ ТекущиеДанные.Используется);
			Элементы.Список.Обновить();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаПереместитьВверх(Команда)
	
	КомандаПереместитьВверхНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура КомандаПереместитьВверхНаСервере()
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	НаборЗаписей = РегистрыСведений.ок_ПравилаКонвертацииЗначений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГруппаПравил.Установить(ТекущаяСтрока.ГруппаПравил);
	НаборЗаписей.Прочитать();
	
	Очередность = 0;
	
	Для каждого ЭлементНабора Из НаборЗаписей Цикл
	
		Если ЭлементНабора.ИмяПравила = ТекущаяСтрока.ИмяПравила Тогда
			
			Очередность = ЭлементНабора.Очередность;
			Прервать;
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	Если Очередность = 0 Тогда
		Возврат;
	КонецЕсли; 
	
	Для каждого ЭлементНабора Из НаборЗаписей Цикл
	
		Если ЭлементНабора.ИмяПравила = ТекущаяСтрока.ИмяПравила Тогда
			
			ЭлементНабора.Очередность = ЭлементНабора.Очередность - 1;
			
		ИначеЕсли ЭлементНабора.Очередность = (Очередность - 1) Тогда
			
			ЭлементНабора.Очередность = ЭлементНабора.Очередность + 1;
			
		КонецЕсли; 
	
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПереместитьВниз(Команда)
	
	КомандаПереместитьВнизНаСервере();
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаСервере
Процедура КомандаПереместитьВнизНаСервере()
	
	ТекущаяСтрока = Элементы.Список.ТекущаяСтрока;
	
	НаборЗаписей = РегистрыСведений.ок_ПравилаКонвертацииЗначений.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.ГруппаПравил.Установить(ТекущаяСтрока.ГруппаПравил);
	НаборЗаписей.Прочитать();
	
	Очередность = 0;
	
	Для каждого ЭлементНабора Из НаборЗаписей Цикл
	
		Если ЭлементНабора.ИмяПравила = ТекущаяСтрока.ИмяПравила Тогда
			
			Очередность = ЭлементНабора.Очередность;
			Прервать;
		
		КонецЕсли; 
	
	КонецЦикла; 
	
	Для каждого ЭлементНабора Из НаборЗаписей Цикл
	
		Если ЭлементНабора.ИмяПравила = ТекущаяСтрока.ИмяПравила Тогда
			
			ЭлементНабора.Очередность = ЭлементНабора.Очередность + 1;
			
		ИначеЕсли ЭлементНабора.Очередность <> 0
			И ЭлементНабора.Очередность = (Очередность + 1) Тогда
			
			ЭлементНабора.Очередность = ЭлементНабора.Очередность - 1;
			
		КонецЕсли; 
	
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ИзменитьФлагИспользуется(КлючЗаписи, Флаг)

	Запись = РегистрыСведений.ок_ПравилаКонвертацииЗначений.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, КлючЗаписи);
	Запись.Прочитать();
	
	Запись.Используется = Флаг;
	
	Запись.Записать();
	
КонецПроцедуры 

#КонецОбласти 
