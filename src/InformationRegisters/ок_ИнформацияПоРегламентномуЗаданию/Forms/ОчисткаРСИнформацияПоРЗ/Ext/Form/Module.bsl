
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СКД = РегистрыСведений.ок_ИнформацияПоРегламентномуЗаданию.ПолучитьМакет("Макет");
	
	АдресВМ = ПоместитьВоВременноеХранилище(СКД, УникальныйИдентификатор);
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресВМ);
	КомпоновщикНастроек.Инициализировать(ИсточникНастроек);
	КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОчистку(Команда)
	
	Режим = РежимДиалогаВопрос.ДаНет;
	Ответ = Вопрос("Вы действительно хотите удалить данные из РС «Информация по регламентным заданиям (ОК)", Режим, 0);
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ВыполнитьОчисткуНаСервере();
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОчисткуНаСервере()
	
	СКД = РегистрыСведений.ок_ИнформацияПоРегламентномуЗаданию.ПолучитьМакет("Макет");
	НастройкиКомпановки = КомпоновщикНастроек.ПолучитьНастройки();
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СКД, НастройкиКомпановки, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	
	ТЗ = Новый ТаблицаЗначений;
	ПроцессорВывода.УстановитьОбъект(ТЗ);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Для каждого Строка Из ТЗ Цикл
		
		МенежерЗаписиРС = РегистрыСведений.ок_ИнформацияПоРегламентномуЗаданию.СоздатьМенеджерЗаписи();
		МенежерЗаписиРС.Период			= Строка.Период;
		МенежерЗаписиРС.НаименованиеРЗ	= Строка.НаименованиеРЗ;
		МенежерЗаписиРС.Пользователь	= Строка.Пользователь;
		МенежерЗаписиРС.ЭтоРЗ			= Строка.ЭтоРЗ;
		МенежерЗаписиРС.Удалить();
		
	КонецЦикла; 
	
КонецПроцедуры

