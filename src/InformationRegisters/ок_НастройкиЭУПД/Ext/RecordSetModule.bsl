
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ТекстОшибки = "";
	
	Если ЭтотОбъект.Количество() <> 1 Тогда
	
		МассивНепроверяемыхРеквизитов = Новый Массив;
		МассивНепроверяемыхРеквизитов.Добавить("ДатаПередачиВПродуктив");
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
		Для каждого ПроверяемаяЗапись Из ЭтотОбъект Цикл
	
			Если НЕ ЗначениеЗаполнено(ПроверяемаяЗапись.ДатаПередачиВПродуктив)
				И ПроверяемаяЗапись.Продуктив Тогда
				
				ТекстОшибки = ТекстОшибки + "Для контрагента """ + ПроверяемаяЗапись.Контрагент + """ и функции УПД """ + ПроверяемаяЗапись.ФункцияУПД + """ поле ""Дата передачи в продуктив"" не заполнено;" + Символы.ПС;
			
			КонецЕсли; 
		
		КонецЦикла;
		
	Иначе
		
		ПроверяемаяЗапись = ЭтотОбъект[0];
		МассивНепроверяемыхРеквизитов = Новый Массив;
		
		Если НЕ ПроверяемаяЗапись.Продуктив Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ДатаПередачиВПродуктив");
		КонецЕсли; 

		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
		
	КонецЕсли; 
	
	Если ТекстОшибки <> "" Тогда
		
		ТекстОшибки = ТекстОшибки + "Запись набора прервана.";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки,,,,Отказ);
	
	КонецЕсли; 
	
КонецПроцедуры