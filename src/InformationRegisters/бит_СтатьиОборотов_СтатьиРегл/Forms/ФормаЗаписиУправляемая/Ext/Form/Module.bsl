
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.бит_СтатьиОборотов_СтатьиРегл;
		
	бит_РаботаСДиалогамиСервер.ФормаЗаписиРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	МассивТипов = МетаданныеОбъекта.Измерения.СтатьяРегл.Тип.Типы();
	СписокРез = бит_ОбщегоНазначения.ПодготовитьСписокВыбораТипа(МассивТипов);
	Для каждого Элемент Из СписокРез Цикл
	
		СписокТиповСтатьяРегл.Добавить(Элемент.Значение, Элемент.Представление);
	
	КонецЦикла; 
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтатьяРеглНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	бит_РаботаСДиалогамиКлиент.НачалоВыбораСоставного(ЭтаФорма
	                                                   ,Элемент
	                                                   ,Запись
	                                                   ,"СтатьяРегл"
													   ,СписокТиповСтатьяРегл
													   ,СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРеглПриИзменении(Элемент)
	
	Если ТипЗнч(Запись.СтатьяРегл) <> Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
		
		Запись.СчетУчетаБУ = Неопределено;
		
	КонецЕсли;
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяРеглОчистка(Элемент, СтандартнаяОбработка)
	
	Элемент.ВыбиратьТип = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеЭлементамиФормы()
	
	Если ТипЗнч(Запись.СтатьяРегл) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда
		Элементы.СчетУчетаБУ.Доступность = Истина;
	Иначе
		Элементы.СчетУчетаБУ.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
