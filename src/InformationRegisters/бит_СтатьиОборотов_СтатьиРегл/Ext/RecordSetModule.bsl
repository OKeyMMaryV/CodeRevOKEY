#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		// В случае выполнения обмена данными не производить проверку.
		Возврат;
	КонецЕсли;
	
	МассивПолей = Новый Массив;
	МассивПолей.Добавить("СтатьяРегл");
	МассивПолей.Добавить("СчетУчетаБУ");
	
    СтруктураИзмерений = Новый Структура;
    СтруктураИзмерений.Вставить("ИмяРегистра"          , "бит_СтатьиОборотов_СтатьиРегл");
    СтруктураИзмерений.Вставить("ИмяИзмерения_Объект"  , "СтатьяОборотов");
    СтруктураИзмерений.Вставить("ИмяИзмерения_Значения", МассивПолей);
    
    // Проверим уникальность набора записей.
    бит_Бюджетирование.ПроверитьУникальностьСопоставленногоМассиваЗначений_ОбъектамВНабореЗаписей(ЭтотОбъект, СтруктураИзмерений, Отказ);
    
КонецПроцедуры

//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-05-21 (#3713)
Процедура ПриЗаписи(Отказ, Замещение)
	
	ТекущийНаборЗаписей = ЭтотОбъект.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТекущийНаборЗаписей.СтатьяОборотов КАК СтатьяОборотов,
		|	ТекущийНаборЗаписей.СтатьяРегл КАК СтатьяРегл,
		|	ТекущийНаборЗаписей.СчетУчетаБУ КАК СчетУчетаБУ
		|ПОМЕСТИТЬ ВТ_ТекущийНаборЗаписей
		|ИЗ
		|	&ТекущийНаборЗаписей КАК ТекущийНаборЗаписей
		|ГДЕ
		|	ТекущийНаборЗаписей.ок_ОсновноеСоответствие
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТекущийНаборЗаписей.СтатьяОборотов КАК СтатьяОборотов,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл КАК СтатьяРегл,
		|	бит_СтатьиОборотов_СтатьиРегл.СчетУчетаБУ КАК СчетУчетаБУ,
		|	бит_СтатьиОборотов_СтатьиРегл.ок_ОсновноеСоответствие КАК ок_ОсновноеСоответствие
		|ИЗ
		|	ВТ_ТекущийНаборЗаписей КАК ТекущийНаборЗаписей
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_СтатьиОборотов_СтатьиРегл КАК бит_СтатьиОборотов_СтатьиРегл
		|		ПО ТекущийНаборЗаписей.СтатьяОборотов = бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов
		|			И (ТекущийНаборЗаписей.СтатьяРегл <> бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл
		|				ИЛИ ТекущийНаборЗаписей.СчетУчетаБУ <> бит_СтатьиОборотов_СтатьиРегл.СчетУчетаБУ)
		|			И (бит_СтатьиОборотов_СтатьиРегл.ок_ОсновноеСоответствие)
		|ГДЕ
		|	НЕ бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл ЕСТЬ NULL";
	
	Запрос.УстановитьПараметр("ТекущийНаборЗаписей", ТекущийНаборЗаписей);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Попытка
		
			МенеджерЗаписи = РегистрыСведений.бит_СтатьиОборотов_СтатьиРегл.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.СтатьяОборотов 	= ВыборкаДетальныеЗаписи.СтатьяОборотов;
			МенеджерЗаписи.СтатьяРегл 		= ВыборкаДетальныеЗаписи.СтатьяРегл;
			МенеджерЗаписи.СчетУчетаБУ 		= ВыборкаДетальныеЗаписи.СчетУчетаБУ;
			МенеджерЗаписи.Прочитать();
			
			МенеджерЗаписи.ок_ОсновноеСоответствие = Ложь;
			МенеджерЗаписи.Записать();
		
		Исключение
			
			Отказ = Истина;
			ВызватьИсключение ОписаниеОшибки();
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры
//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-05-21 (#3713)

#КонецОбласти

#КонецЕсли
