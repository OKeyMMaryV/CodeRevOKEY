#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс
	
// Функция регистрирует состояние мдм.
// 
// Параметры:
//  Объект - СправочникСсылка.бит_мдм_ОбъектОбмена, СправочникСсылка.бит_мдм_ОбъектОбменаИерархияДанных
//  вхСостояние - Строка, ПеречислениеСсылка.бит_мдм_СостоянияОбъектов
//  вхПользователь - СправочникСсылка.Пользователи
//  Комментарий - Строка.
// 
// Возвращаемое значение:
//  флВыполнено - Булево.
// 
Функция Зарегистрировать(Объект, вхСостояние, вхПользователь = Неопределено) Экспорт

	флВыполнено = Ложь;
	
	Если НЕ ЗначениеЗаполнено(вхПользователь) Тогда
		
		ТекПользователь = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("ТекущийПользователь");
		
	Иначе	
		
		ТекПользователь = вхПользователь;
		
	КонецЕсли; 
	
	Если ТипЗнч(вхСостояние) = Тип("Строка") Тогда
		
		ТекСостояние = Перечисления.бит_мдм_СостоянияОбъектов[вхСостояние];
		
	Иначе	
		
		ТекСостояние = вхСостояние;
		
	КонецЕсли; 
	
	Менеджер = РегистрыСведений.бит_мдм_СостоянияОбъектов.СоздатьМенеджерЗаписи();
	Менеджер.ПериодИзменения       = ТекущаяДата();
	Менеджер.Объект       = Объект;
	Менеджер.Пользователь = ТекПользователь;
	Менеджер.Состояние    = ТекСостояние;
	
	Попытка
		
		Менеджер.Записать();
		флВыполнено = Истина;
		
	Исключение
		
		ТекстСообщения = "Не удалось зарегистрировать состояние %1% для объекта %2%. Описание ошибки: %3%";
		ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, ТекСостояние, Объект, ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("МДМ.РегистрацияСостояния",УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.бит_мдм_СобытияОбъектов, ТекстСообщения);
		
	КонецПопытки;

	Возврат флВыполнено;
	
КонецФункции // Зарегистрировать()

// Функция получает состояние объекта обмена.
// 
// Параметры:
//  ОбъектОбмена - СправочникСсылка.бит_мдм_ОбъектыОбмена, СправочникСсылка.бит_мдм_ОбъектыОбменаИерархияЭлементов.
// 
// Возвращаемое значение:
//  Состояние - РегистрСведений.бит_мдм_СостоянияОбъектов.
// 
Функция СостояниеОбъекта(ОбъектОбмена) Экспорт

	Состояние = Перечисления.бит_мдм_СостоянияОбъектов.ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Объект", ОбъектОбмена);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_мдм_СостоянияОбъектов.Состояние
	               |ИЗ
	               |	РегистрСведений.бит_мдм_СостоянияОбъектов КАК бит_мдм_СостоянияОбъектов
	               |ГДЕ
	               |	бит_мдм_СостоянияОбъектов.Объект = &Объект";
	
				   
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		 Состояние = Выборка.Состояние;
	
	КонецЕсли; 

	Возврат Состояние;
	
КонецФункции // СостояниеОбъекта()

#КонецОбласти

#КонецЕсли
