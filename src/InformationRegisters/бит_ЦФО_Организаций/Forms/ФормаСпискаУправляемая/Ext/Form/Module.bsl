
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если бит_ОбщегоНазначения.ЭтоСемействоERP() Тогда
		
		ТекстСообщения = НСтр("ru='Объект не используется в данной версии программы!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения,,,Отказ);
		
		СтандартнаяОбработка = Ложь;
		
    КонецЕсли;
    
	МетаданныеОбъекта = Метаданные.РегистрыСведений.бит_ЦФО_Организаций;
		
	бит_РаботаСДиалогамиСервер.ФормаСпискаРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаполнитьПоРегистру_ЦФО_Подразделения(Команда)
	
	ЗаполнитьПоРегистру_ЦФО_Подразделения();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаЗаполнитьПоРодителю(Команда)
	
	Обработчик = Новый ОписаниеОповещения("КомандаЗаполнитьПоРодителюЗавершение", ЭтотОбъект);
	Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	ОткрытьФорму("РегистрСведений.бит_ЦФО_Организаций.Форма.ФормаНастройкиЗаполнения",,ЭтаФорма,,,, Обработчик, Режим);
	
КонецПроцедуры

// Процедура обработчик оповещения "КомандаЗаполнитьПоРодителюЗавершение".
// 
// Параметры:
// Ответ - КодВозвратаДиалога
// ДополнительныеДанные - Структура.
// 
&НаКлиенте
Процедура КомандаЗаполнитьПоРодителюЗавершение(СтруктураПараметров, Параметры) Экспорт
   
	Если НЕ ТипЗнч(СтруктураПараметров) = Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоРодителю(СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет регистр на основе данных из регистра "Соответствие ЦФО и подразделений".
// 
// Параметры:
// 	нет
// 
&НаСервере
Процедура ЗаполнитьПоРегистру_ЦФО_Подразделения()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	бит_ЦФО_Подразделения.ЦФО,
	|	бит_ЦФО_Подразделения.Подразделение.Владелец КАК Организация
	|ИЗ
	|	РегистрСведений.бит_ЦФО_Подразделения КАК бит_ЦФО_Подразделения
	|ГДЕ
	|	бит_ЦФО_Подразделения.Подразделение ССЫЛКА Справочник.ПодразделенияОрганизаций
	|
	|СГРУППИРОВАТЬ ПО
	|	бит_ЦФО_Подразделения.ЦФО,
	|	бит_ЦФО_Подразделения.Подразделение.Владелец";
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ЕстьОшибки = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.бит_ЦФО_Организаций.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Организация 	= Выборка.Организация;
		МенеджерЗаписи.ЦФО 			= Выборка.ЦФО;
		
		МенеджерЗаписи.Прочитать();
		
		// Такая запись уже есть
		Если МенеджерЗаписи.Выбран() Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписи.Организация 	= Выборка.Организация;
		МенеджерЗаписи.ЦФО 			= Выборка.ЦФО;
		
		Попытка
			МенеджерЗаписи.Записать();
		Исключение
			ЕстьОшибки = Истина;
		КонецПопытки;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		ТекстСообщения = НСтр("ru='Заполнение регистра сведений ""ЦФО организаций"" произошло с ошибками!'");
	Иначе
		ТекстСообщения = НСтр("ru='Выполнено заполнение регистра сведений ""ЦФО организаций""!'");
	КонецЕсли;
	
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
	
КонецПроцедуры                                                           

// Процедура заполняет регистр подчиненными элементами переданного ЦФО.
// 
// Параметры:
// 	нет
// 
&НаСервере
Процедура ЗаполнитьПоРодителю(СтруктураПараметров)
	
	ЦФОРодитель = СтруктураПараметров.ЦФО;
	Организация = СтруктураПараметров.Организация;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	бит_ЦФО.Ссылка КАК ЦФО
	|ИЗ
	|	Справочник.Подразделения КАК бит_ЦФО
	|ГДЕ
	|	бит_ЦФО.Родитель В ИЕРАРХИИ(&ЦФОРодитель)
	|	И бит_ЦФО.ПометкаУдаления = ЛОЖЬ";
	
	Запрос.УстановитьПараметр("ЦФОРодитель", ЦФОРодитель);
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ЕстьОшибки = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		МенеджерЗаписи = РегистрыСведений.бит_ЦФО_Организаций.СоздатьМенеджерЗаписи();
		
		МенеджерЗаписи.Организация 	= Организация;
		МенеджерЗаписи.ЦФО 			= Выборка.ЦФО;
		
		МенеджерЗаписи.Прочитать();
		
		// Такая запись уже есть
		Если МенеджерЗаписи.Выбран() Тогда
			Продолжить;
		КонецЕсли;
		
		МенеджерЗаписи.Организация 	= Организация;
		МенеджерЗаписи.ЦФО 			= Выборка.ЦФО;
		
		Попытка
			МенеджерЗаписи.Записать();
		Исключение
			ЕстьОшибки = Истина;
		КонецПопытки;
		
	КонецЦикла;
	
	Если ЕстьОшибки Тогда
		ТекстСообщения = НСтр("ru='Заполнение регистра сведений ""ЦФО организаций"" произошло с ошибками!'");
	Иначе
		ТекстСообщения = НСтр("ru='Выполнено заполнение регистра сведений ""ЦФО организаций""!'");
	КонецЕсли;
	
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

