
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МетаданныеОбъекта = Метаданные.РегистрыСведений.бит_НазначениеДополнительныхИзмерений;
		
	бит_РаботаСДиалогамиСервер.ФормаСпискаРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
    
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМонопольный();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		СнятьМонопольный();
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если бит_МеханизмДопИзмерений.ЕстьДвиженияПоАналитике(ТекущаяСтрока.ИмяИзмерения, Истина) Тогда		
		Отказ = Истина;		
	КонецЕсли;
	
	бит_МеханизмДопИзмерений.УдалитьОбязательностьЗаполненияВСтатьеОборотов(ТекущаяСтрока.ИмяИзмерения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаУстановитьМонопольный(Команда)
	
	УстановитьМонопольный();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаменитьТипыАналитикВДокументах(Команда)
	
	ОткрытьФорму("Обработка.бит_ЗаменаТиповАналитикВДокументах.Форма",, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает монопольный режим, если не удалось блокируем форму.
// 
&НаСервере
Процедура УстановитьМонопольный()
	
	Попытка
		
		УстановитьМонопольныйРежим(Истина);
		
		ТекстИнформация = НСтр("ru='Редактирование настроек дополнительных измерений осуществляется в"
		+ " МОНОПОЛЬНОМ режиме. Пока открыта данная форма другие пользователи не могут работать с базой!'");
		
		Элементы.ТекстИнформация.ЦветТекста = Новый Цвет(0,0,255);		
		
		ТолькоПросмотр = Ложь;
		
	Исключение
		ТолькоПросмотр = Истина;
		
		ТекстИнформация = НСтр("ru='Не удалось установить МОНОПОЛЬНЫЙ режим (с базой работают другие пользователи или недостаточно прав)!"
		+ " Редактирование настроек дополнительных измерений осуществляется в МОНОПОЛЬНОМ режиме.'");
		
		Элементы.ТекстИнформация.ЦветТекста = Новый Цвет(255,0,0);
	КонецПопытки;
	
	
КонецПроцедуры

// Процедура снимает монопольный режим.
// 
&НаСервере
Процедура СнятьМонопольный()
	
	Попытка
		
		УстановитьМонопольныйРежим(Ложь);
		
	Исключение
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

