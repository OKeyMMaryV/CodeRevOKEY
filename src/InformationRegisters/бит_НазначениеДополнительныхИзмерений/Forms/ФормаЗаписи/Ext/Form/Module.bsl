
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	бит_РаботаСДиалогамиСервер.ФормаЗаписиРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	
	ПоказатьДекарацию = Ложь;
	ТекстСообщения 	  = ""; 
	
	Если НЕ Параметры.Ключ.Пустой()
		И бит_МеханизмДопИзмерений.ЕстьДвиженияПоАналитике(Запись.ИмяИзмерения, Ложь, ТекстСообщения) Тогда
		
		Элементы.Аналитика.ТолькоПросмотр	 = Истина;
		Элементы.ИмяИзмерения.ТолькоПросмотр = Истина;
		Элементы.ДекорацияПредупреждение.Заголовок = ТекстСообщения + Символы.ПС + Нстр("ru = 'Изменения ограничены.'"); 
		ПоказатьДекарацию = Истина;
		
	КонецЕсли;
	
	Элементы.ДекорацияПредупреждение.Видимость = ПоказатьДекарацию;
	УправлениеЭлементамиФормы();
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура АналитикаПриИзменении(Элемент)
	
	Если ВидАналитикиВыбранПравильно() Тогда
		Если ПустаяСтрока(Запись.Синоним) Тогда  		
			Запись.Синоним = Запись.Аналитика;		
		КонецЕсли;
	Иначе
		Запись.Аналитика = ПредопределенноеЗначение("ПланВидовХарактеристик.бит_ВидыДополнительныхАналитик.ПустаяСсылка");
	КонецЕсли;
	
	Запись.ИмяТипаПоУмолчанию = "";
	УправлениеЭлементамиФормы();
	
КонецПроцедуры // АналитикаПриИзменении()

&НаКлиенте
Процедура ИмяИзмеренияНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	МассивВыбора = бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Дополнительные","Синоним","Массив");
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
	
КонецПроцедуры // ИмяИзмеренияНачалоВыбораИзСписка()

&НаКлиенте
Процедура ИмяИзмеренияАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	МассивВыбора = бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Дополнительные","Синоним","Массив");
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);

КонецПроцедуры // ИмяИзмеренияАвтоПодбор()

&НаКлиенте
Процедура ИмяТипаПоУмолчаниюНачалоВыбораИзСписка(Элемент, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Запись.Аналитика) 
		И бит_МеханизмДопИзмерений.ЭтоАналитикаСоставногоТипа(Запись.Аналитика) Тогда
		
		МассивВыбора = ПолучитьМассивТиповПоУмолчанию(Запись.Аналитика);  		
		Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
		
	Иначе
		
		СтандартнаяОбработка = Ложь;		
		
	КонецЕсли;
	
КонецПроцедуры // ИмяТипаПоУмолчаниюНачалоВыбораИзСписка()

&НаКлиенте
Процедура ИмяТипаПоУмолчаниюАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если Ожидание = 0 Тогда
		
		Если ЗначениеЗаполнено(Запись.Аналитика) 
			И бит_МеханизмДопИзмерений.ЭтоАналитикаСоставногоТипа(Запись.Аналитика) Тогда
			
			МассивВыбора = ПолучитьМассивТиповПоУмолчанию(Запись.Аналитика);  			
			Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВыбора);
			
		Иначе
			
			СтандартнаяОбработка = Ложь;		
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ИмяТипаПоУмолчаниюАвтоПодбор()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
&НаСервереБезКонтекста
Функция ПолучитьМассивТиповПоУмолчанию(Аналитика)
	
	МассивВыбора = РегистрыСведений.бит_НазначениеДополнительныхИзмерений.ПолучитьМассивТиповПоУмолчанию(Аналитика);	
	Возврат МассивВыбора;
	
КонецФункции // ПолучитьМассивТиповПоУмолчанию()

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.ИмяТипаПоУмолчанию.ТолькоПросмотр = НЕ бит_МеханизмДопИзмерений.ЭтоАналитикаСоставногоТипа(Запись.Аналитика);
	
КонецПроцедуры // УправлениеЭлементамиФормы()

&НаКлиенте
Функция ВидАналитикиВыбранПравильно()

    Правильно = Истина;
    // ВАЖНО. При переносе в ЕРП нужно вернуть ИСТИНА.
    
	Если бит_ОбщегоНазначения.ЭтоСемействоБП() Тогда
               	
    	Мернеджер = "ПланВидовХарактеристик.бит_ВидыДополнительныхАналитик.";
    	Зарезвированные = Новый Массив(); 
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "ПлатежнаяПозиция"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "Проект"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "СтатьяОборотов"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "ЦФО"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "НоменклатурнаяГруппа"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "Контрагент"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "ДоговорКонтрагента"));
    	Зарезвированные.Добавить(ПредопределенноеЗначение(Мернеджер + "БанковскийСчет"));
    	
    	Если Зарезвированные.Найти(Запись.Аналитика) <> Неопределено Тогда
    		Правильно = Ложь;
    		ТекстСообщения = НСтр("ru = 'Этот вид аналитики не поддерживается в дополнительных измерениях.'");
    		ПоказатьПредупреждение(,ТекстСообщения);	
    	КонецЕсли; 
        
    КонецЕсли;

	Возврат Правильно;
	
КонецФункции

#КонецОбласти