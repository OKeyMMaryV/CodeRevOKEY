#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
// Процедура - обработчик события "ПередЗаписью".
// 
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
	
		Возврат;
	
	КонецЕсли; 	
	
	// Проверим нет ли записей по периметру консолидации в будущих периодах.
	СтрокаСообщения = "";
	
	ТабЗнач = ЭтотОбъект.Выгрузить();
	ТабЗнач.Свернуть("Период, ПериметрКонсолидации");
	
	Для каждого ТекСтр Из ТабЗнач Цикл
		
		ТекстЗапроса = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		               |	бит_му_СоставПериметровКонсолидации.Период КАК Период
		               |ИЗ
		               |	РегистрСведений.бит_му_СоставПериметровКонсолидации КАК бит_му_СоставПериметровКонсолидации
		               |ГДЕ
		               |	бит_му_СоставПериметровКонсолидации.Период > &Период
		               |	И бит_му_СоставПериметровКонсолидации.ПериметрКонсолидации = &ПериметрКонсолидации
		               |
		               |СГРУППИРОВАТЬ ПО
		               |	бит_му_СоставПериметровКонсолидации.Период
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Период";
		
		Запрос = Новый Запрос(ТекстЗапроса);
		
		Запрос.УстановитьПараметр("Период", 				ТекСтр.Период);
		Запрос.УстановитьПараметр("ПериметрКонсолидации", 	ТекСтр.ПериметрКонсолидации);
		
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			СтрокаСообщения = СтрокаСообщения + НСтр("ru = '	""%1%"" период %2%'") + Символы.ПС;
			СтрокаСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(СтрокаСообщения, ТекСтр.ПериметрКонсолидации, Формат(Выборка.Период, "ДЛФ=D"));
		КонецЦикла;
		
	КонецЦикла;
	
	Если СтрокаСообщения <> "" Тогда
		СтрокаСообщения = НСтр("ru = 'Найдены записи в будущих периодах для периметров консолидации:'") + Символы.ПС + СтрокаСообщения;
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(СтрокаСообщения);
	КонецЕсли;
	
	// Проверим нет ли записей, где дата окончания меньше периода.
	СтрокаСообщения = "";
	
	Для каждого ТекСтр Из ЭтотОбъект Цикл
		
		Если ЗначениеЗаполнено(ТекСтр.ДатаОкончания) И (ТекСтр.ДатаОкончания < ТекСтр.Период) Тогда
			СтрокаСообщения = СтрокаСообщения + НСтр("ru = '	""%1%"" период %2%, дата окончания %3%'") + Символы.ПС;
			СтрокаСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(СтрокаСообщения, ТекСтр.ПериметрКонсолидации, Формат(ТекСтр.Период, "ДЛФ=D"), Формат(ТекСтр.ДатаОкончания, "ДЛФ=D"));
		КонецЕсли;
		
	КонецЦикла;
	
	Если СтрокаСообщения <> "" Тогда
		СтрокаСообщения = НСтр("ru = 'Найдены записи, дата окончания которых меньше даты начала, для периметров консолидации:'") + Символы.ПС + СтрокаСообщения;
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(СтрокаСообщения, , , Отказ);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
