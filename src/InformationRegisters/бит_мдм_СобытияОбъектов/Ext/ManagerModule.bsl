#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс
	
// Функция регистрирует событие мдм.
// 
// Параметры:
//  Объект - СправочникСсылка.бит_мдм_ОбъектОбмена, СправочникСсылка.бит_мдм_ОбъектОбменаИерархияДанных
//  вхСобытие - Строка, ПеречислениеСсылка.бит_мдм_События
//  вхПользователь - СправочникСсылка.Пользователи
//  Комментарий - Строка.
// 
// Возвращаемое значение:
//  флВыполнено - Булево.
// 
Функция Зарегистрировать(Объект, вхСобытие, Комментарий = "", вхПользователь = Неопределено ) Экспорт

	флВыполнено = Ложь;
	
	Если НЕ ЗначениеЗаполнено(вхПользователь) Тогда
		
		ТекПользователь = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("ТекущийПользователь");
		
	Иначе	
		
		ТекПользователь = вхПользователь;
		
	КонецЕсли; 
	
	Если ТипЗнч(вхСобытие) = Тип("Строка") Тогда
		
		ТекСобытие = Перечисления.бит_мдм_События[вхСобытие];
		
	Иначе	
		
		ТекСобытие = вхСобытие;
		
	КонецЕсли; 
	
	Менеджер = РегистрыСведений.бит_мдм_СобытияОбъектов.СоздатьМенеджерЗаписи();
	Менеджер.Период       = ТекущаяДата();
	Менеджер.Объект       = Объект;
	Менеджер.Пользователь = ТекПользователь;
	Менеджер.Событие      = ТекСобытие;
	Менеджер.Комментарий  = Комментарий;
	
	Попытка
		
		Менеджер.Записать();
		флВыполнено = Истина;
		
	Исключение
		
		ТекстСообщения = "Не удалось зарегистрировать событие %1% для объекта %2%. Описание ошибки: %3%";
		ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, ТекСобытие, Объект, ОписаниеОшибки());
		ЗаписьЖурналаРегистрации("МДМ.РегистрацияСобытия",УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.бит_мдм_СобытияОбъектов, ТекстСообщения);
		
	КонецПопытки;

	Возврат флВыполнено;
	
КонецФункции // Зарегистрировать()

#КонецОбласти

#КонецЕсли
