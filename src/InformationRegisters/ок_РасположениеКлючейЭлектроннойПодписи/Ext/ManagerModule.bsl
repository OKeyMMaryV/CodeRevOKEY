//ОКЕЙ Вдовиченко Г.В(СофтЛаб) 2019-09-02 (#3436)

#Область ПрограммныйИнтерфейс

Функция ОставитьТолькоСертификатыСДоступнымиКлючами(МассивСертификатов, ИмяКомпьютера, ТолькоОдин = Ложь, Пользователь = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	Данные.Сертификат КАК Сертификат,
		|	ВЫБОР
		|		КОГДА Данные.Сертификат.Пользователь = &Пользователь
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК Порядок
		|ИЗ
		|	РегистрСведений.ок_РасположениеКлючейЭлектроннойПодписи КАК Данные
		|ГДЕ
		|	Данные.ИмяКомпьютера = &ИмяКомпьютера
		|	И Данные.Сертификат В(&МассивСертификатов)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	Данные.Сертификат.ДействителенДо УБЫВ,
		|	Данные.Сертификат.Отпечаток";
	
	Запрос.УстановитьПараметр("ИмяКомпьютера", Врег(ИмяКомпьютера));
	Запрос.УстановитьПараметр("МассивСертификатов", МассивСертификатов);
	Запрос.УстановитьПараметр("Пользователь", ?(Пользователь = Неопределено, Пользователи.АвторизованныйПользователь(), Пользователь));
	
	Если Не ТолькоОдин Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "ВЫБРАТЬ ПЕРВЫЕ 1", "ВЫБРАТЬ");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Сертификат");
	
КонецФункции

Функция КлючСертификатаУстановлен(Сертификат, ИмяКомпьютера) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ИСТИНА КАК Поле1
		|ИЗ
		|	РегистрСведений.ок_РасположениеКлючейЭлектроннойПодписи КАК Данные
		|ГДЕ
		|	Данные.Сертификат = &Сертификат
		|	И Данные.ИмяКомпьютера = &ИмяКомпьютера";
	
	Запрос.УстановитьПараметр("ИмяКомпьютера", Врег(ИмяКомпьютера));
	Запрос.УстановитьПараметр("Сертификат", Сертификат);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции

#КонецОбласти
