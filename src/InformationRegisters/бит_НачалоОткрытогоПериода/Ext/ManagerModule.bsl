#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ПроверитьПередУдалением(Организация, ОбъектСистемы, Отказ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НачалоОткрытогоПериода.Организация КАК Организация,
	|	НачалоОткрытогоПериода.ОбъектСистемы КАК ОбъектСистемы,
	|	НачалоОткрытогоПериода.ДатаОткрытогоПериода КАК ДатаОткрытогоПериода
	|ИЗ
	|	РегистрСведений.бит_НачалоОткрытогоПериода КАК НачалоОткрытогоПериода
	|ГДЕ
	|	НачалоОткрытогоПериода.Организация = &Организация
	|	И НачалоОткрытогоПериода.ОбъектСистемы = &ОбъектСистемы";
	
	Запрос.УстановитьПараметр("Организация",   Организация);
	Запрос.УстановитьПараметр("ОбъектСистемы", ОбъектСистемы);

	РезультатЗапроса = Запрос.Выполнить();		
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЕстьКорректировки = бит_КонтрольЗакрытогоПериода.ЕстьКорректировки(Выборка.ОбъектСистемы, 
								Выборка.Организация, Дата('00010101'), Выборка.ДатаОткрытогоПериода);
								
		Если ЕстьКорректировки Тогда
			
			ТекстСообщения = Нстр("ru = 'По организации ""%1"" и объекту системы ""%2"" в периоде "
						   + "(начало ведения учета - %3) есть корректировочные записи.'"); 
			
			ТекстСообщения = СтрШаблон(ТекстСообщения, Выборка.Организация, Выборка.ОбъектСистемы, 
									Формат(Выборка.ДатаОткрытогоПериода,"ДФ=dd.MM.yyyy")); 
			
			Ключ = Новый Структура("Организация, ОбъектСистемы"); 
			ЗаполнитьЗначенияСвойств(Ключ, Выборка);
			
			КлючЗаписи = РегистрыСведений.бит_НачалоОткрытогоПериода.СоздатьКлючЗаписи(Ключ);					
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, КлючЗаписи, , ,Отказ);
		КонецЕсли; 						
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли