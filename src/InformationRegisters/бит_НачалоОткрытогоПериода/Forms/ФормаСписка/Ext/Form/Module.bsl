
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	бит_РаботаСДиалогамиСервер.ФормаСпискаРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	
	Если Параметры.Отбор.Свойство("ОбъектСистемы") Тогда
		ОтборОбъектСистемы = Параметры.Отбор.ОбъектСистемы;
	КонецЕсли; 
	
	УправлениеФормой();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура СписокПередУдалением(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	ТекстСообщения = "";
	СписокПередУдалениемНаСервере(ТекущиеДанные.Организация, ТекущиеДанные.ОбъектСистемы, ТекстСообщения, Отказ);
	Если Отказ Тогда
		ПоказатьПредупреждение(,ТекстСообщения,,Нстр("ru = 'Ошибка при удалении по причине:'"));
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере 
Процедура УправлениеФормой()
	
	Элементы.ОтборОбъектСистемы.Видимость = ЗначениеЗаполнено(ОтборОбъектСистемы);
	Элементы.ОбъектСистемы.Видимость      = НЕ ЗначениеЗаполнено(ОтборОбъектСистемы);
	
КонецПроцедуры

&НаСервере
Процедура СписокПередУдалениемНаСервере(Организация, ОбъектСистемы, ТекстСообщения, Отказ)
	
	РегистрыСведений.бит_НачалоОткрытогоПериода.ПроверитьПередУдалением(Организация, ОбъектСистемы, Отказ);
	Сообщения = ПолучитьСообщенияПользователю(Истина);
	Для каждого Сообщение Из Сообщения Цикл
		ТекстСообщения = ТекстСообщения + ?(ПустаяСтрока(ТекстСообщения), "", Символы.ПС) + Сообщение.Текст;
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти