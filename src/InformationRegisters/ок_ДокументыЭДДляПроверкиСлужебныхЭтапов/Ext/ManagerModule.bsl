//ОКЕЙ Вдовиченко Г.В(СофтЛаб) 2019-11-25 (#3529)

Процедура ЗарегистрироватьДокумент(Документ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	1
		|ИЗ
		|	РегистрСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов КАК Данные
		|ГДЕ
		|	Данные.Документ = &Документ";
	
	Запрос.УстановитьПараметр("Документ", Документ);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	ТипЗначения = ТипЗнч(Документ);
	Если ТипЗначения <> Тип("ДокументСсылка.ЭлектронныйДокументВходящий")
		И ТипЗначения <> Тип("ДокументСсылка.ЭлектронныйДокументИсходящий") Тогда
		Возврат;
	КонецЕсли;
	
	//1С-ИжТиСи, Кондратьев, 03.2020, обновление (
	//Профиль = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ПрофильНастроекЭДО");
	Профиль = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "ИдентификаторОрганизации");
	//1С-ИжТиСи, Кондратьев, 03.2020, обновление )
	
	Если Не ЗначениеЗаполнено(Профиль) Тогда
		Возврат;
	КонецЕсли;
	
	//1С-ИжТиСи, Кондратьев, 03.2020, обновление (
	//ок_СпособОбменаЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Профиль, "ок_СпособОбменаЭД");
	ок_СпособОбменаЭД = РегистрыСведений.УчетныеЗаписиЭДО.ЗначениеРеквизитаРегистра(Профиль, "ок_СпособОбменаЭД");
	//1С-ИжТиСи, Кондратьев, 03.2020, обновление )
	Если Не ЗначениеЗаполнено(ок_СпособОбменаЭД) Тогда
		Возврат;
	КонецЕсли;
	
	Набор = РегистрыСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов.СоздатьНаборЗаписей();
	Набор.Отбор.Документ.Установить(Документ);
	
	Запись = Набор.Добавить();
	Запись.Документ = Документ;
	Запись.Идентификатор = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Документ, "УникальныйИД");
	Запись.ДатаРегистрации = ТекущаяДатаСеанса();
	
	Набор.Записать();
	
КонецПроцедуры	

//может быть несколько служебных документов, поэтому снимаем по одному
Процедура СнятьСРегистрацииДокумент(ИдентификаторДокумента, ДатаСеанса = Неопределено, Безусловно = Ложь) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Количество = 0;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Данные.Документ КАК Документ,
			|	Данные.Количество КАК Количество,
			|	Данные.Идентификатор КАК Идентификатор,
			|	Данные.ДатаСеанса КАК ДатаСеанса
			|ИЗ
			|	РегистрСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов КАК Данные
			|ГДЕ
			|	Данные.Идентификатор = &ИдентификаторДокумента
			|	И (Данные.ДатаСеанса = &ДатаСеанса
			|			ИЛИ &ПоВсемДатамСеанса)
			|	И Данные.ДатаРегистрации < &ДатаРегистрации";
		
	ТипЗначения = ТипЗнч(ИдентификаторДокумента);
	Если ТипЗначения <> Тип("Строка") Тогда // не уникальный идентификатор
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Данные.Идентификатор = &ИдентификаторДокумента", "Данные.Документ = &ИдентификаторДокумента");
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("ИдентификаторДокумента", ИдентификаторДокумента);
	Запрос.УстановитьПараметр("ДатаСеанса", ДатаСеанса);
	Запрос.УстановитьПараметр("ПоВсемДатамСеанса", ДатаСеанса = Неопределено);
	
	Задержка = 60 * 10; //10 минут задержки даем от момента регистрации последнего события, так как СБИС может запаздывать
	Запрос.УстановитьПараметр("ДатаРегистрации", ТекущаяДатаСеанса() - Задержка);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Прервать;
	КонецЦикла;
		
	Набор = РегистрыСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов.СоздатьНаборЗаписей();
	Набор.Отбор.Документ.Установить(Выборка.Документ);
	
	Если Не Безусловно Тогда 
		
		Количество = Выборка.Количество - 1;
		Если Количество > 0 Тогда
			Запись = Набор.Добавить();
			Запись.Документ = Выборка.Документ;
			Запись.Количество = Количество;
			Запись.Идентификатор = Выборка.Идентификатор;
			Запись.ДатаСеанса = Выборка.ДатаСеанса;
		КонецЕсли;
			
	КонецЕсли;
	
	Набор.Записать();
	
КонецПроцедуры	

Процедура ЗарегистрироватьКоличествоСлужебныхДокументов(ИдентификаторДокумента, ДатаСеанса, Количество) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	Данные.Документ КАК Документ,
			|	Данные.Идентификатор КАК Идентификатор,
			|	Данные.ДатаРегистрации КАК ДатаРегистрации
			|ИЗ
			|	РегистрСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов КАК Данные
			|ГДЕ
			|	Данные.Идентификатор = &ИдентификаторДокумента";
		
	ТипЗначения = ТипЗнч(ИдентификаторДокумента);
	Если ТипЗначения <> Тип("Строка") Тогда // не уникальный идентификатор
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Данные.Идентификатор = &ИдентификаторДокумента", "Данные.Документ = &ИдентификаторДокумента");
	КонецЕсли;	
	
	Запрос.УстановитьПараметр("ИдентификаторДокумента", ИдентификаторДокумента);
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат;
	КонецЕсли;	
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		Прервать;
	КонецЦикла;
	
	Набор = РегистрыСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов.СоздатьНаборЗаписей();
	Набор.Отбор.Документ.Установить(Выборка.Документ);
	
	Запись = Набор.Добавить();
	ЗаполнитьЗначенияСвойств(Запись, Выборка);

	Запись.Количество = Количество;
	Запись.ДатаСеанса = ДатаСеанса;
	
	Набор.Записать();
	
КонецПроцедуры	

Функция ПолучитьИдентификаторыДокументовДляПроверкиСлужебныхЭтапов(Профиль, ДатаСеанса, РазмерПорции = 100) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	БезФильтра = бит_БК_Общий.ПолучитьЗначениеНастройкиБК("Тестирование", "СлужебныеЭтапыБезФильтра");
	Если БезФильтра <> Неопределено И БезФильтра = Истина Тогда
		Возврат Неопределено;
	КонецЕсли;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
		|	Данные.Идентификатор КАК ИдентификаторДокумента
		|ИЗ
		|	РегистрСведений.ок_ДокументыЭДДляПроверкиСлужебныхЭтапов КАК Данные
		|ГДЕ
		|	Данные.Идентификатор <> """"
		//1С-ИжТиСи, Кондратьев, 03.2020, обновление (
		//|	И Данные.Документ.ПрофильНастроекЭДО = &Профиль
		|	И Данные.Документ.ИдентификаторОрганизации = &Профиль
		//1С-ИжТиСи, Кондратьев, 03.2020, обновление )
		|	И Данные.ДатаСеанса <> &ДатаСеанса";
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ПЕРВЫЕ 100", "ПЕРВЫЕ " + Формат(РазмерПорции, "ЧГ=0"));
	Запрос.УстановитьПараметр("Профиль", Профиль);
	Запрос.УстановитьПараметр("ДатаСеанса", ДатаСеанса);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторДокумента");
	
КонецФункции	