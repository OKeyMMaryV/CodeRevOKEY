#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция ДанныеКодовМаркировки(КодыМаркировки) Экспорт
	
	ИсходныеДанные = Новый ТаблицаЗначений;
	ИсходныеДанные.Колонки.Добавить("КодМаркировки",          Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(200)));
	ИсходныеДанные.Колонки.Добавить("ХешКодаМаркировки", Новый ОписаниеТипов("Строка",,,,Новый КвалификаторыСтроки(50)));
	
	Для Каждого КодМаркировки Из КодыМаркировки Цикл
		
		НоваяСтрока = ИсходныеДанные.Добавить();
		НоваяСтрока.КодМаркировки     = КодМаркировки;
		НоваяСтрока.ХешКодаМаркировки = ИнтеграцияИС.ХешированиеДанныхSHA256(КодМаркировки);
		
	КонецЦикла;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	ИсходныеДанные.КодМаркировки     КАК КодМаркировки,
	|	ИсходныеДанные.ХешКодаМаркировки КАК ХешКодаМаркировки
	|ПОМЕСТИТЬ ИсходныеДанные
	|ИЗ
	|	&ИсходныеДанные КАК ИсходныеДанные
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПулКодовМаркировкиСУЗ.ДокументОснование         КАК ДокументОснование,
	|	ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию            КАК ЗаказНаЭмиссию,
	|	ПулКодовМаркировкиСУЗ.КодМаркировки             КАК КодМаркировки,
	|	ПулКодовМаркировкиСУЗ.ХешСуммаКодаМаркировки    КАК ХешСуммаКодаМаркировки,
	|	ПулКодовМаркировкиСУЗ.Статус                    КАК Статус,
	|	ПулКодовМаркировкиСУЗ.Организация               КАК Организация,
	|	ПулКодовМаркировкиСУЗ.СтанцияУправленияЗаказами КАК СтанцияУправленияЗаказами,
	|	ПулКодовМаркировкиСУЗ.Номенклатура              КАК Номенклатура,
	|	ПулКодовМаркировкиСУЗ.Характеристика            КАК Характеристика,
	|	ПулКодовМаркировкиСУЗ.ПолныйКодМаркировки       КАК ПолныйКодМаркировки,
	|	ПулКодовМаркировкиСУЗ.ДатаЭмиссииУниверсальная  КАК ДатаЭмиссииУниверсальная,
	|	ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная   КАК ДатаПечатиУниверсальная,
	|	ПулКодовМаркировкиСУЗ.ШтрихкодУпаковки          КАК ШтрихкодУпаковки,
	|	ПулКодовМаркировкиСУЗ.GTIN                      КАК GTIN,
	|	ПулКодовМаркировкиСУЗ.ВидПродукции              КАК ВидПродукции,
	|	ПулКодовМаркировкиСУЗ.МаксимальнаяРозничнаяЦена КАК МаксимальнаяРозничнаяЦена,
	|	ПулКодовМаркировкиСУЗ.СпособВводаВОборот        КАК СпособВводаВОборот,
	|	ПулКодовМаркировкиСУЗ.Шаблон                    КАК Шаблон,
	|	ПулКодовМаркировкиСУЗ.ХешСуммаУпаковки          КАК ХешСуммаУпаковки,
	|	ПулКодовМаркировкиСУЗ.МаркировкаОстатков        КАК МаркировкаОстатков,
	|	ПулКодовМаркировкиСУЗ.ТипШтрихкода              КАК ТипШтрихкода
	|ИЗ
	|	ИсходныеДанные КАК ИсходныеДанные
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
	|		ПО ИсходныеДанные.КодМаркировки = ПулКодовМаркировкиСУЗ.КодМаркировки
	|			И ИсходныеДанные.ХешКодаМаркировки = ПулКодовМаркировкиСУЗ.ХешСуммаКодаМаркировки");
	
	Запрос.УстановитьПараметр("ИсходныеДанные", ИсходныеДанные);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ДанныеПула = Новый Соответствие;
	
	Пока Выборка.Следующий() Цикл
		СведенийКода = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(Выборка);
		ДанныеПула.Вставить(Выборка.КодМаркировки, СведенийКода);
	КонецЦикла;
	
	Возврат ДанныеПула;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	УправлениеДоступомИСПереопределяемый.ПриЗаполненииОграниченияДоступа(
		Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ, Ограничение);
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция СтатусыВыведенИзОборота() Экспорт
	
	Статусы = Новый Массив();
	Статусы.Добавить(Перечисления.СтатусыКодовМаркировкиСУЗ.Использован);
	Статусы.Добавить(Перечисления.СтатусыКодовМаркировкиСУЗ.Отбракован);
	
	Возврат Статусы;
	
КонецФункции

Функция РассчитатьТаблицуСостоянияПоЗаказу(Заказ) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = ЗапросСостоянияКодовПоЗаказу(Заказ);
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ЕстьНераспределенныеКоды(ДокументОснование) Экспорт
	
	ТаблицаСостояния = РассчитатьТаблицуСостоянияПоЗаказу(ДокументОснование);
	Возврат ТаблицаСостояния.Итог("КоличествоРезервировать") > 0;
	
КонецФункции

Процедура ЗарезервироватьКодыПодЗаказ(Параметры, АдресРезультата, МаркировкаОстатков = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ТаблицаНоменклатуры = НоваяТаблицаНоменклатуры(Параметры);

	Запрос = ЗапросСвободныхКодовПоЗаказу(Параметры.Документ, ТаблицаНоменклатуры, МаркировкаОстатков);
	Запрос.Текст = Запрос.Текст + "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СвободныеКоды.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	СвободныеКоды КАК СвободныеКоды
	|СГРУППИРОВАТЬ ПО
	|	СвободныеКоды.ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозможныеСвободныеКоды.ДокументОснование
	|ИЗ
	|	ВозможныеСвободныеКоды КАК ВозможныеСвободныеКоды
	|СГРУППИРОВАТЬ ПО
	|	ВозможныеСвободныеКоды.ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Заказ
	|";
	Результат = Запрос.Выполнить();
	
	НачатьТранзакцию();
	
	Попытка
		
		Блокировка = Новый БлокировкаДанных();
		ЭлементБлокировки = Блокировка.Добавить("РегистрСведений.ПулКодовМаркировкиСУЗ");
		ЭлементБлокировки.ИсточникДанных = Результат;
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных("ДокументОснование", "ДокументОснование");
		ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
		Блокировка.Заблокировать();
		
		Запрос = ЗапросСостоянияКодовПоЗаказу(Параметры.Документ, Ложь, ТаблицаНоменклатуры, МаркировкаОстатков);
		Запрос.Текст = Запрос.Текст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СвободныеКоды.КодМаркировки       КАК КодМаркировки,
		|	СвободныеКоды.ПолныйКодМаркировки КАК ПолныйКодМаркировки,
		|	СвободныеКоды.ДокументОснование   КАК ДокументОснование,
		|	СвободныеКоды.ВидПродукции        КАК ВидПродукции,
		|	СвободныеКоды.Номенклатура        КАК Номенклатура,
		|	СвободныеКоды.Характеристика      КАК Характеристика
		|ИЗ
		|	СвободныеКоды КАК СвободныеКоды
		|ГДЕ
		|	СвободныеКоды.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВозможныеСвободныеКоды.КодМаркировки       КАК КодМаркировки,
		|	ВозможныеСвободныеКоды.ПолныйКодМаркировки КАК ПолныйКодМаркировки,
		|	ВозможныеСвободныеКоды.ДокументОснование   КАК ДокументОснование,
		|	ВозможныеСвободныеКоды.ВидПродукции        КАК ВидПродукции,
		|	ВозможныеСвободныеКоды.Номенклатура        КАК Номенклатура,
		|	ВозможныеСвободныеКоды.Характеристика      КАК Характеристика
		|ИЗ
		|	ВозможныеСвободныеКоды КАК ВозможныеСвободныеКоды
		|ГДЕ
		|	ВозможныеСвободныеКоды.Организация = &Организация
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ДокументОснование КАК ДокументОснование,
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ВидПродукции      КАК ВидПродукции,
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Номенклатура      КАК Номенклатура,
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Характеристика    КАК Характеристика,
		|	СУММА(ДоступноКРезрвированиюПоИзлишкамПредварительно.Количество) КАК Количество
		|ИЗ
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно КАК ДоступноКРезрвированиюПоИзлишкамПредварительно
		|ГДЕ
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Организация = &Организация
		|СГРУППИРОВАТЬ ПО
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ДокументОснование,
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ВидПродукции,
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Номенклатура,
		|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Характеристика
		|ИМЕЮЩИЕ
		|	СУММА(ДоступноКРезрвированиюПоИзлишкамПредварительно.Количество) > 0
		|";
		Результаты = Запрос.ВыполнитьПакет();
		КоличествоРезультатов = Результаты.Количество();
		
		ВыборкаДоступноКРезервированию = Результаты[КоличествоРезультатов - 1].Выбрать();
		ВыборкаВозможныеСвободныеКоды  = Результаты[КоличествоРезультатов - 2].Выбрать();
		ВыборкаСвободныеКоды           = Результаты[КоличествоРезультатов - 3].Выбрать();
		
		ТаблицаСостояниеКодов = Результаты[КоличествоРезультатов - 4].Выгрузить();
		
		ТаблицаКодыМаркировки = НоваяТаблицаРезультатаРезервирования();
				
		СоответствиеКодовОснованиям = Новый Соответствие();
		
		Для Каждого СтрокаСостоянияКодов Из ТаблицаСостояниеКодов Цикл
			КоличествоРезервировать = СтрокаСостоянияКодов.КоличествоРезервировать;
			
			Если КоличествоРезервировать = 0 Тогда
				Продолжить;
			КонецЕсли;
			
			СтруктураПоиска = Новый Структура("Номенклатура,Характеристика");
			ЗаполнитьЗначенияСвойств(СтруктураПоиска, СтрокаСостоянияКодов);
			
			ВыборкаСвободныеКоды.Сбросить();
			ВыборкаДоступноКРезервированию.Сбросить();

			Пока ВыборкаСвободныеКоды.НайтиСледующий(СтруктураПоиска)
				И КоличествоРезервировать > 0 Цикл
				ДобавитьКодВСоответствиеКодовОснованиям(ВыборкаСвободныеКоды, СоответствиеКодовОснованиям);
				КоличествоРезервировать = КоличествоРезервировать - 1;
			КонецЦикла;
			
			Пока ВыборкаДоступноКРезервированию.НайтиСледующий(СтруктураПоиска)
				И КоличествоРезервировать > 0 Цикл
				ДоступноКРезервированию = ВыборкаДоступноКРезервированию.Количество;
				
				СтруктураПоискаДоступных = Новый Структура("ДокументОснование,Номенклатура,Характеристика");
				ЗаполнитьЗначенияСвойств(СтруктураПоискаДоступных, ВыборкаДоступноКРезервированию);
				
				ВыборкаВозможныеСвободныеКоды.Сбросить();
				
				Пока ВыборкаВозможныеСвободныеКоды.НайтиСледующий(СтруктураПоискаДоступных)
					И ДоступноКРезервированию > 0 И КоличествоРезервировать > 0 Цикл
					ДобавитьКодВСоответствиеКодовОснованиям(ВыборкаВозможныеСвободныеКоды, СоответствиеКодовОснованиям);
					ДоступноКРезервированию = ДоступноКРезервированию - 1;
					КоличествоРезервировать = КоличествоРезервировать - 1;
				КонецЦикла;
			КонецЦикла;
			
			КоличествоЗарезервировано = СтрокаСостоянияКодов.КоличествоРезервировать - КоличествоРезервировать;
			
			СтрокаСостоянияКодов.КоличествоПечатать = СтрокаСостоянияКодов.КоличествоПечатать + КоличествоЗарезервировано;
			СтрокаСостоянияКодов.КоличествоРезервировать = КоличествоРезервировать;
		КонецЦикла;
		
		НаборЗаписейПоЗаказу = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьНаборЗаписей();
		НаборЗаписейПоЗаказу.Отбор["ДокументОснование"].Установить(Параметры.Документ);
		
		Для Каждого КлючИЗначение Из СоответствиеКодовОснованиям Цикл
			ДокументОснование = ДокументОснованиеИзСоответствияКодам(КлючИЗначение);

			НаборЗаписейПоОснованию = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьНаборЗаписей();
			НаборЗаписейПоОснованию.Отбор["ДокументОснование"].Установить(ДокументОснование);
			НаборЗаписейПоОснованию.Прочитать();
			
			КоличествоЗаписей   = НаборЗаписейПоОснованию.Количество();
			РезервированныеКоды = КлючИЗначение.Значение;
			
			Для ОбратныйИндекс = 1 По КоличествоЗаписей Цикл
				ПроверяемаяЗапись = НаборЗаписейПоОснованию[КоличествоЗаписей - ОбратныйИндекс];
				Если РезервированныеКоды.Найти(ПроверяемаяЗапись.КодМаркировки) <> Неопределено Тогда
					НоваяЗаписьПоЗаказу = НаборЗаписейПоЗаказу.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяЗаписьПоЗаказу, ПроверяемаяЗапись,, "ДокументОснование");
					НоваяЗаписьПоЗаказу.ДокументОснование = Параметры.Документ;
					НаборЗаписейПоОснованию.Удалить(КоличествоЗаписей - ОбратныйИндекс);
					
					НоваяСтрокаПоКоду = ТаблицаКодыМаркировки.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаПоКоду, НоваяЗаписьПоЗаказу);
				КонецЕсли;
			КонецЦикла;
			
			НаборЗаписейПоОснованию.Записать(Истина);
		КонецЦикла;
		
		НаборЗаписейПоЗаказу.Записать(Ложь);
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОтменитьТранзакцию();
		
		ТекстОшибки = НСтр("ru = 'Произошла ошибка при резервировании кодов маркировки.'");
		ТекстОшибки = ТекстОшибки + " " + КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		
		ВызватьИсключение ТекстОшибки;
	
	КонецПопытки;
	
	РезультатРезервирования = Новый Структура();
	РезультатРезервирования.Вставить("ТаблицаСостояниеКодов", ТаблицаСостояниеКодов);
	РезультатРезервирования.Вставить("ТаблицаКодыМаркировки", ТаблицаКодыМаркировки);

	ПоместитьВоВременноеХранилище(РезультатРезервирования, АдресРезультата);
	
КонецПроцедуры

Процедура ЗаполнитьДеревоСуществующихУпаковокПоДокументу(ДеревоУпаковок,
	Документ, ВидПродукции = Неопределено, УчитыватьСтатусПечати = Истина) Экспорт
	
	ЭтоЗаказНаЭмиссию        = ТипЗнч(Документ) = Тип("ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ");
	РазрешенаПовторнаяПечать = ИнтеграцияИСМП.РазрешенаПовторнаяПечатьКодовМаркировки();
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ПулКодовМаркировки.ХешСуммаУпаковки          КАК ХешСуммаУпаковки,
	|	ПулКодовМаркировки.ШтрихкодУпаковки          КАК ШтрихкодУпаковки,
	|	ПулКодовМаркировки.Номенклатура              КАК Номенклатура,
	|	ПулКодовМаркировки.Характеристика            КАК Характеристика,
	|	ПулКодовМаркировки.Шаблон                    КАК Шаблон,
	|	ПулКодовМаркировки.СпособВводаВОборот        КАК СпособВводаВОборот,
	|	КОЛИЧЕСТВО(ПулКодовМаркировки.КодМаркировки) КАК Количество,
	|	МАКСИМУМ(ПулКодовМаркировки.ДатаПечатиУниверсальная <> ДАТАВРЕМЯ(1,1,1)) КАК Распечатан
	|ПОМЕСТИТЬ
	|	ВТАгрегированныеКоды
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировки
	|ГДЕ
	|	ПулКодовМаркировки." + ?(ЭтоЗаказНаЭмиссию, "ЗаказНаЭмиссию", "ДокументОснование") + " = &Документ
	|	И ПулКодовМаркировки.ШтрихкодУпаковки <> ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
	|	И " + ?(ЗначениеЗаполнено(ВидПродукции), "ПулКодовМаркировки.ВидПродукции = &ВидПродукции", "ИСТИНА") + "
	|СГРУППИРОВАТЬ ПО
	|	ПулКодовМаркировки.ХешСуммаУпаковки,
	|	ПулКодовМаркировки.ШтрихкодУпаковки,
	|	ПулКодовМаркировки.Номенклатура,
	|	ПулКодовМаркировки.Шаблон,
	|	ПулКодовМаркировки.СпособВводаВОборот,
	|	ПулКодовМаркировки.Характеристика
	|
	|;
	|
	|ВЫБРАТЬ
	|	АгрегированныеКоды.ХешСуммаУпаковки     КАК ХешСуммаУпаковки,
	|	АгрегированныеКоды.ШтрихкодУпаковки     КАК ШтрихкодУпаковки,
	|	МАКСИМУМ(АгрегированныеКоды.Распечатан) КАК Распечатана
	|ПОМЕСТИТЬ
	|	ВТАгрегированныеУпаковки
	|ИЗ
	|	ВТАгрегированныеКоды КАК АгрегированныеКоды
	|СГРУППИРОВАТЬ ПО
	|	АгрегированныеКоды.ХешСуммаУпаковки,
	|	АгрегированныеКоды.ШтрихкодУпаковки
	|
	|;
	|
	|ВЫБРАТЬ
	|	АгрегированныеКоды.ХешСуммаУпаковки                    КАК ХешСуммаУпаковки,
	|	АгрегированныеКоды.Номенклатура                        КАК Номенклатура,
	|	АгрегированныеКоды.Характеристика                      КАК Характеристика,
	|	АгрегированныеКоды.Шаблон                              КАК Шаблон,
	|	АгрегированныеКоды.СпособВводаВОборот                  КАК СпособВводаВОборот,
	|	АгрегированныеКоды.Количество                          КАК Количество,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(АгрегированныеКоды.Номенклатура)   КАК НоменклатураПредставление,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(АгрегированныеКоды.Характеристика) КАК ХарактеристикаПредставление,
	|	КОЛИЧЕСТВО(АгрегированныеКоды.ШтрихкодУпаковки)        КАК КоличествоУпаковок,
	|	СУММА(ВЫБОР
	|		КОГДА АгрегированныеУпаковки.Распечатана
	|			ТОГДА 0
	|		ИНАЧЕ 1
	|	КОНЕЦ)                                                 КАК КоличествоПечать
	|ИЗ
	|	ВТАгрегированныеКоды КАК АгрегированныеКоды
	|	ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТАгрегированныеУпаковки КАК АгрегированныеУпаковки
	|		ПО АгрегированныеКоды.ХешСуммаУпаковки = АгрегированныеУпаковки.ХешСуммаУпаковки
	|		 И АгрегированныеКоды.ШтрихкодУпаковки = АгрегированныеУпаковки.ШтрихкодУпаковки
	|СГРУППИРОВАТЬ ПО
	|	АгрегированныеКоды.ХешСуммаУпаковки,
	|	АгрегированныеКоды.Номенклатура,
	|	АгрегированныеКоды.Характеристика,
	|	АгрегированныеКоды.Шаблон,
	|	АгрегированныеКоды.СпособВводаВОборот,
	|	АгрегированныеКоды.Количество
	|ИТОГИ
	|	МАКСИМУМ(КоличествоУпаковок),
	|	МАКСИМУМ(Шаблон),
	|	МАКСИМУМ(СпособВводаВОборот),
	|	МАКСИМУМ(КоличествоПечать)
	|ПО
	|	ХешСуммаУпаковки
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("Документ",     Документ);
	Запрос.УстановитьПараметр("ВидПродукции", ВидПродукции);
	Результат = Запрос.Выполнить();
	
	ВыборкаХешСуммаУпаковки = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаХешСуммаУпаковки.Следующий() Цикл
		
		Если УчитыватьСтатусПечати
			И НЕ РазрешенаПовторнаяПечать
			И ВыборкаХешСуммаУпаковки.КоличествоПечать = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаУпаковки = ДеревоУпаковок.Строки.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаУпаковки, ВыборкаХешСуммаУпаковки, "Шаблон,СпособВводаВОборот");
		
		Если НЕ УчитыватьСтатусПечати Тогда
			СтрокаУпаковки.Количество = ВыборкаХешСуммаУпаковки.КоличествоУпаковок;
			СтрокаУпаковки.ХешСуммаУпаковки = ВыборкаХешСуммаУпаковки.ХешСуммаУпаковки;
		Иначе
			Если РазрешенаПовторнаяПечать Тогда
				СтрокаУпаковки.Количество = ВыборкаХешСуммаУпаковки.КоличествоУпаковок;
			Иначе
				СтрокаУпаковки.Количество = ВыборкаХешСуммаУпаковки.КоличествоПечать;
			КонецЕсли;
			
			СтрокаУпаковки.КоличествоПечать = ВыборкаХешСуммаУпаковки.КоличествоПечать;
			СтрокаУпаковки.ХешСуммаУпаковки = ВыборкаХешСуммаУпаковки.ХешСуммаУпаковки;
			
			Если СтрокаУпаковки.КоличествоПечать > 0 И СтрокаУпаковки.КоличествоЭкземпляров = 0 Тогда
				СтрокаУпаковки.КоличествоЭкземпляров = 1;
			КонецЕсли;
		КонецЕсли;
		
		НоменклатураУпаковки = Новый Массив();
		
		Выборка = ВыборкаХешСуммаУпаковки.Выбрать();
		Пока Выборка.Следующий() Цикл
			СтрокаПродукции = СтрокаУпаковки.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(
				СтрокаПродукции, Выборка, "Номенклатура,Характеристика,Количество,Шаблон,СпособВводаВОборот");
			Если УчитыватьСтатусПечати Тогда
				СтрокаПродукции.КоличествоПечать = Выборка.Количество;
			КонецЕсли;
			СтрокаПродукции.Содержимое = ИнтеграцияИС.ПредставлениеНоменклатуры(
				Выборка.Номенклатура, Выборка.Характеристика);
			НоменклатураУпаковки.Добавить(Выборка.Номенклатура);
		КонецЦикла;
		
		СтрокаУпаковки.Содержимое = ИнтеграцияИС.ПредставлениеСоставаУпаковки(НоменклатураУпаковки);
	КонецЦикла;
	
КонецПроцедуры

//Возвращает структуру для наложения отборов на регистр ПулКодовМаркировкиСУЗ
// 
//Параметры:
//   Документ  - ДокументСсылка - источник отбора
//
//Возвращаемое значение:
//   Структура - Где ключ - это имя измерения регистра по типу документа, значение - ссылка на документ
//
Функция СтруктураОтбораДокументаПоТипуДокумента(Документ) Экспорт
	
	СтруктураОтбораДокументы = Новый Структура;
	
	ТипДокумента = ТипЗнч(Документ);
	Если ТипДокумента = Тип("ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ") Тогда
		СтруктураОтбораДокументы.Вставить("ЗаказНаЭмиссию", Документ);
	ИначеЕсли Метаданные.ОпределяемыеТипы["ОснованиеЗаказНаЭмиссиюКодовМаркировкиИСМП"].Тип.СодержитТип(ТипДокумента) Тогда
		СтруктураОтбораДокументы.Вставить("ДокументОснование", Документ);
	КонецЕсли;
	
	Возврат СтруктураОтбораДокументы;
	
КонецФункции

// Процедура выполняет блокировку записей пула, выполняет печать этикеток и сохраняет дату печати
// 
// Параметры:
// 	ДанныеПечати - Структура - Входные данные печаети
// 	ТабличныйДокумент - ТабличныйДокумент - Табличный документ для вывода результатов
// 	СтруктураНастроек - Структура - Дополнительные настройки печати
Процедура РаспечататьЭтикеткиИЗафиксироватьДатуПечати(
	ДанныеПечати,
	ТабличныйДокумент,
	СтруктураНастроек = Неопределено,
	РезультатРезервирования = Неопределено,
	СтруктураМакетаШаблона = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Документ                    = Неопределено;
	СтруктураОбъектовПечати     = Новый Структура();
	СоответствиеКодовМаркировки = Новый Соответствие();
	ОбъектыПечати               = Новый Массив();
	РежимПечати                 = "";
	
	Если ЭтоАдресВременногоХранилища(ДанныеПечати.АдресВХранилище) Тогда
		СтруктураОбъектовПечати = ПолучитьИзВременногоХранилища(ДанныеПечати.АдресВХранилище);
		ОбъектыПечати = СтруктураОбъектовПечати.ОбъектыПечати;
		СтруктураОбъектовПечати.Свойство("РежимПечати", РежимПечати);
	КонецЕсли;
	
	Если СтруктураНастроек = Неопределено Тогда
		СтруктураНастроек = ПечатьЭтикетокИСМП.СтруктураНастроекЭтикеткаИСМП();
	КонецЕсли;
	
	Если ДанныеПечати.Свойство("КаждаяЭтикеткаНаНовомЛисте") Тогда
		СтруктураНастроек.Вставить("КаждаяЭтикеткаНаНовомЛисте", ДанныеПечати.КаждаяЭтикеткаНаНовомЛисте);
	КонецЕсли;
	
	Если СтруктураМакетаШаблона <> Неопределено Тогда
		СтруктураНастроек.Вставить("СтруктураМакетаШаблона", СтруктураМакетаШаблона);
	КонецЕсли;
	
	ИсходнаяТаблица = НоваяТаблицаДанныхДляПечатиЭтикеток();
	НомерСтроки = 1;
	
	Для Каждого ОбъектПечати Из ОбъектыПечати Цикл
		НоваяСтрока = ИсходнаяТаблица.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ОбъектПечати);
		НоваяСтрока.Порядок = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
		Если Не ЗначениеЗаполнено(НоваяСтрока.ХешСуммаКодаМаркировки) Тогда
			Если ЗначениеЗаполнено(НоваяСтрока.КодМаркировки) Тогда
				НоваяСтрока.ХешСуммаКодаМаркировки = Справочники.ШтрихкодыУпаковокТоваров.ХэшСуммаСтроки(
					НоваяСтрока.КодМаркировки);
			ИначеЕсли ЗначениеЗаполнено(НоваяСтрока.Штрихкод) Тогда
				НоваяСтрока.ХешСуммаКодаМаркировки = Справочники.ШтрихкодыУпаковокТоваров.ХэшСуммаСтроки(
					НоваяСтрока.Штрихкод);
			КонецЕсли;
		КонецЕсли;
		СоответствиеКодовМаркировки.Вставить(НоваяСтрока.Штрихкод);
	КонецЦикла;
	
	БлокировкаЗаписей = Новый БлокировкаДанных;
	
	Если ДанныеПечати.Свойство("Документ", Документ) Тогда
		СтруктураОтбораДокументы = СтруктураОтбораДокументаПоТипуДокумента(Документ);
		Для Каждого ПолеОтбора Из СтруктураОтбораДокументы Цикл
			ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ПулКодовМаркировкиСУЗ");
			ЭлементБлокировки.УстановитьЗначение(ПолеОтбора.Ключ, ПолеОтбора.Значение);
		КонецЦикла;
	Иначе
		Для Каждого КлючЗначение Из СоответствиеКодовМаркировки Цикл
			ЭлементБлокировки = БлокировкаЗаписей.Добавить("РегистрСведений.ПулКодовМаркировкиСУЗ");
			ЭлементБлокировки.УстановитьЗначение("КодМаркировки", КлючЗначение.Ключ);
		КонецЦикла;
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Попытка
		
		БлокировкаЗаписей.Заблокировать();
		
		Если РежимПечати = "РезервироватьСвободныеКоды" Тогда
			
			СтруктураДанныхПоДокументу = СтруктураДанныхДляПечатиСвободныхКодов(
				ИсходнаяТаблица, СтруктураОтбораДокументы);
			ВывестиВТабличныйДокументЭтикеткиОбувь(
				ТабличныйДокумент, СтруктураДанныхПоДокументу.ТаблицаПечати, СтруктураНастроек);
			УстановитьСостояниеРаспечатанПоСтруктуреДокумента(СтруктураДанныхПоДокументу);
			
		ИначеЕсли РежимПечати = "АгрегированныеКоды" Тогда
			
			СтруктураДанныхПоДокументу = СтруктураДанныхДляПечатиАгрегированныеКоды(
				ИсходнаяТаблица, СтруктураОтбораДокументы);
			ВывестиВТабличныйДокументЭтикеткиОбувь(
				ТабличныйДокумент, СтруктураДанныхПоДокументу.ТаблицаПечати, СтруктураНастроек);
			УстановитьСостояниеРаспечатанПоСтруктуреДокумента(СтруктураДанныхПоДокументу);
			
		ИначеЕсли РежимПечати = "Выборочно" Тогда
			
			РазрешитьПовторнуюПечать = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(
				ДанныеПечати, "РазрешитьПовторнуюПечать", Ложь);
			СтруктураДанныхПоДокументу = СтруктураДанныхДляПечатиВыборочно(ИсходнаяТаблица, РазрешитьПовторнуюПечать);
			ВывестиВТабличныйДокументЭтикеткиОбувь(
				ТабличныйДокумент, СтруктураДанныхПоДокументу.ТаблицаПечати, СтруктураНастроек);
			УстановитьСостояниеРаспечатанПоКаждомуКодуМаркировки(СтруктураДанныхПоДокументу.ТаблицаПечати);
		
		ИначеЕсли РежимПечати = "ПоКодамМаркировки" Тогда
			
			СтруктураДанныхПоДокументу = СтруктураДанныхДляПечатиПоКодамМаркировки(
				ИсходнаяТаблица, Документ, РезультатРезервирования);
			ВывестиВТабличныйДокументЭтикеткиОбувь(
				ТабличныйДокумент, СтруктураДанныхПоДокументу.ТаблицаПечати, СтруктураНастроек);
			УстановитьСостояниеРаспечатанПоКаждомуКодуМаркировки(СтруктураДанныхПоДокументу.ТаблицаПечати);
			
		Иначе
			ВызватьИсключение НСтр("ru = 'Ошика печати этикеток: Не указан режим печати'");
		КонецЕсли;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		ОтменитьТранзакцию();
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

// Удаляет использованные коды маркировки из пула в случаях:
//   * Все коды маркировки по основанию распечатаны, основание полностью оформлено (по основанию)
//   * Основание не указано или архивировано, коды маркировки распечатаны (по использованным кодам).
//
Процедура ОчисткаПулаКодовМаркировки() Экспорт
	
	СрокХраненияКодовВПуле = Константы.СрокХраненияИспользованныхКодовМаркировки.Получить();
	Если СрокХраненияКодовВПуле = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДатаОчисткиПула", НачалоДня(ТекущаяДатаСеанса()) - 86400 * СрокХраненияКодовВПуле);
	Запрос.УстановитьПараметр("ПустоеОснование", ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("ОснованиеСтатусыОформленияДокументовИСМП"));
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ПулКодовМаркировкиСУЗ.ДокументОснование
	|ПОМЕСТИТЬ ПроверяемыеОснования
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеОснования.ДокументОснование
	|ПОМЕСТИТЬ ОснованияДляАрхивирования
	|ИЗ
	|	ПроверяемыеОснования КАК ПроверяемыеОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
	|		ПО ПроверяемыеОснования.ДокументОснование = ПулКодовМаркировкиСУЗ.ДокументОснование
	|		И ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1, 1, 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОформленияДокументовИСМП КАК СтатусыОформленияДокументовИСМП
	|		ПО ПроверяемыеОснования.ДокументОснование = СтатусыОформленияДокументовИСМП.Основание
	|		И
	|			СтатусыОформленияДокументовИСМП.СтатусОформления <> ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовГосИС.Оформлено)
	|ГДЕ
	|	ПулКодовМаркировкиСУЗ.ДокументОснование ЕСТЬ NULL
	|	И СтатусыОформленияДокументовИСМП.Основание ЕСТЬ NULL
	|;
	|
	|// Полностью распечатанные по основаниям/////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПроверяемыеОснования.ДокументОснование
	|ИЗ
	|	ОснованияДляАрхивирования КАК ПроверяемыеОснования
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
	|		ПО ПроверяемыеОснования.ДокументОснование = ПулКодовМаркировкиСУЗ.ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ПроверяемыеОснования.ДокументОснование
	|ИМЕЮЩИЕ
	|	МАКСИМУМ(ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная) < &ДатаОчисткиПула
	|;
	|
	|// Распечатанные без оснований///////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПулКодовМаркировкиСУЗ.КодМаркировки
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОформленияДокументовИСМП КАК СтатусыОформленияДокументовИСМП
	|		ПО ПулКодовМаркировкиСУЗ.ДокументОснование = СтатусыОформленияДокументовИСМП.Основание
	|ГДЕ
	|	(ПулКодовМаркировкиСУЗ.ДокументОснование В(&ПустоеОснование)
	|		ИЛИ ЕСТЬNULL(СтатусыОформленияДокументовИСМП.Архивный, ЛОЖЬ))
	|	И ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная <> ДАТАВРЕМЯ(1,1,1)
	|	И ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная < &ДатаОчисткиПула
	|СГРУППИРОВАТЬ ПО
	|	ПулКодовМаркировкиСУЗ.КодМаркировки
	|";
	Пакет = Запрос.ВыполнитьПакет();
	
	ВыборкаОснования = Пакет[2].Выбрать();
	
	Набор = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьНаборЗаписей();
	Пока ВыборкаОснования.Следующий() Цикл
		
		Набор.Отбор.ДокументОснование.Установить(ВыборкаОснования.ДокументОснование);
		НачатьТранзакцию();
		Попытка
			
			Набор.Записать();
			
			Основания = Новый Массив;
			Основания.Добавить(ВыборкаОснования.ДокументОснование);
			РегистрыСведений.СтатусыОформленияДокументовИСМП.АрхивироватьРаспоряженияКОформлению(
				Основания,
				Документы.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.ПустаяСсылка());
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(
				НСтр("ru='ИСМП'", ОбщегоНазначения.КодОсновногоЯзыка()),
				УровеньЖурналаРегистрации.Ошибка,
				РегистрыСведений.ПулКодовМаркировкиСУЗ,
				ВыборкаОснования.ДокументОснование,
				СтрШаблон(
					НСтр("ru='Ошибка при очистке пула кодов маркировки по причине: %1'"),
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке())));
		КонецПопытки;
		
	КонецЦикла;
	
	ВыборкаКоды = Пакет[3].Выбрать();
	
	Набор = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьНаборЗаписей();
	Пока ВыборкаКоды.Следующий() Цикл
		Набор.Отбор.КодМаркировки.Установить(ВыборкаКоды.КодМаркировки);
		Набор.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Используя объект платформы СхемаЗапроса добавляет условие по документу в зависимости от типа документа
// 
// Параметры:
// 	Запрос - Запрос - Экземпляр объекта запроса, текст которого необходимо модифицировать
// 	СтруктураОтбораДокументы - Структура - Описание:
// * Ключ - Строка - Имя поля, на значение которог необходимо установить отбор
// * Значение - ДокументСсылка - Значение отбора, которое необходимо установить
// 	ИндексПакета - Число, Неопределено - Индекс запроса в пакете, в который необходимо добавить условие,
//	если Неопределено - условие добавляется в последний запрос.
Процедура ДобавитьВЗапросПечатиУсловияПоДокументу(Запрос, СтруктураОтбораДокументы, ИндексПакета=Неопределено) Экспорт
	
	СхемаЗапроса = Новый СхемаЗапроса();
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	Если ИндексПакета = Неопределено Тогда
		ПоследнийПакет = СхемаЗапроса.ПакетЗапросов.Получить(СхемаЗапроса.ПакетЗапросов.Количество() - 1);
	Иначе
		ПоследнийПакет = СхемаЗапроса.ПакетЗапросов.Получить(ИндексПакета);
	КонецЕсли;
	
	Операторы = ПоследнийПакет.Операторы.Получить(0);
	Для Каждого КлючЗначение Из СтруктураОтбораДокументы Цикл
		Операторы.Отбор.Добавить(СтрШаблон("%1 = &%2", КлючЗначение.Ключ, КлючЗначение.Ключ));
		Запрос.УстановитьПараметр(КлючЗначение.Ключ, КлючЗначение.Значение);
	КонецЦикла;
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();
	
КонецПроцедуры

#Область Печать

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов - Массив - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати - СписокЗначений - значение - ссылка на объект;
//  ПараметрыВывода - Структура - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ЭтикеткаКодМаркировкиИСМП") Тогда
		
		ТабличныйДокумент = Новый ТабличныйДокумент;
		ТабличныйДокумент.АвтоМасштаб = Истина;
		ТабличныйДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПулКодовМаркировкиСУЗ_ЭтикеткаИСМП";
		
		ВходящиеДанныеПечати = ПолучитьИзВременногоХранилища(ПараметрыПечати.АдресВХранилище);
		ДанныеПечати = Новый Структура;
		ДанныеПечати.Вставить("АдресВХранилище",            ПараметрыПечати.АдресВХранилище);
		ДанныеПечати.Вставить("Документ",                   ВходящиеДанныеПечати.Документ);
		ДанныеПечати.Вставить("КаждаяЭтикеткаНаНовомЛисте", ВходящиеДанныеПечати.КаждаяЭтикеткаНаНовомЛисте);
		ДанныеПечати.Вставить("РазрешитьПовторнуюПечать",   ИнтеграцияИСМП.РазрешенаПовторнаяПечатьКодовМаркировки());
		
		РаспечататьЭтикеткиИЗафиксироватьДатуПечати(ДанныеПечати, ТабличныйДокумент);
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ЭтикеткаКодМаркировкиИСМП",
			НСтр("ru = 'Этикетки (обувь/табак)'"),
			ТабличныйДокумент);
		
	КонецЕсли;
	
КонецПроцедуры

// Выводит на печать этикетки
// 
// Параметры:
// 	ТабличныйДокумент - ТабличныйДокумент - Результат печати
// 	ТаблицаОбъектыПечати - ТаблицаЗначений - Исходные данные для печати
// 	СтруктураНастроек - Структура - (См. ПечатьЭтикетокИСМП.СтруктураНастроекЭтикеткаИСМП).
Процедура ВывестиВТабличныйДокументЭтикеткиОбувь(ТабличныйДокумент, ТаблицаОбъектыПечати, СтруктураНастроек) Экспорт
	
	СтандартнаяОбработка = Истина;
	ПечатьЭтикетокИСМППереопределяемый.ПечатьЭтикетокИСМП(
		ТаблицаОбъектыПечати, ТабличныйДокумент, СтруктураНастроек, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		
		ПечатьЭтикетокИСМП(ТабличныйДокумент, ТаблицаОбъектыПечати, СтруктураНастроек);
		
	КонецЕсли;
	
КонецПроцедуры

// Подготавливает данные для печати этикеток ИС
// 
// Параметры:
// 	ДанныеПечати - Структура - Данные для печати
// Возвращаемое значение:
// 	Структура, Неопределено - Описание:
// * РезультатРезервирования - Массив - Результат резервированных кодов маркировки
// * ТабличныйДокумент - ТабличныйДокумент - Напечатанные коды маркировки
Функция ПечатьЭтикетокСРезервированиемПоДокументу(ДанныеПечати) Экспорт
	
	СтруктураНастроек = ПечатьЭтикетокИСМП.СтруктураНастроекЭтикеткаИСМП();
	СтруктураНастроек.КаждаяЭтикеткаНаНовомЛисте = ДанныеПечати.КаждаяЭтикеткаНаНовомЛисте;
	
	ТабличныйДокумент = Новый ТабличныйДокумент();
	РезультатРезервирования = Неопределено;
	Попытка
		РаспечататьЭтикеткиИЗафиксироватьДатуПечати(
			ДанныеПечати, ТабличныйДокумент, СтруктураНастроек, РезультатРезервирования);
	Исключение
		Возврат Неопределено;
	КонецПопытки;
	
	СтруктураРезультат = Новый Структура();
	СтруктураРезультат.Вставить("ТабличныйДокумент",       ТабличныйДокумент);
	СтруктураРезультат.Вставить("РезультатРезервирования", РезультатРезервирования);
	
	Возврат СтруктураРезультат;
	
КонецФункции

// Библиотечная функция печати
// 
// Параметры:
// 	ТабличныйДокумент - ТабличныйДокумент - Результат печати
// 	ОбъектыПечати - ТаблицаЗначений - Исходные данные для печати
// 	СтруктураНастроек - Структура - структура дополнительных настроек.
Процедура ПечатьЭтикетокИСМП(ТабличныйДокумент, ОбъектыПечати, СтруктураНастроек) Экспорт
	
	Макет         = УправлениеПечатью.МакетПечатнойФормы("РегистрСведений.ПулКодовМаркировкиСУЗ.ПФ_MXL_ЭтикеткаОбувь");
	МакетУпаковки = УправлениеПечатью.МакетПечатнойФормы(
		"РегистрСведений.ПулКодовМаркировкиСУЗ.ПФ_MXL_ЭтикеткаОбувьУпаковка");
	
	Эталон = УправлениеПечатью.МакетПечатнойФормы("Документ.ЗапросАкцизныхМарокЕГАИС.Эталон");
	КоличествоМиллиметровВПикселе = Эталон.Рисунки.Квадрат100Пикселей.Высота / 100;
	
	Область = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
	
	ПредыдущейНомерВГруппе = Неопределено;
	
	Для Каждого СтрокаДанных Из ОбъектыПечати Цикл
		
		ТекущийНомерВГруппе = СтрокаДанных.НомерВГруппе;
		Для НомерЭкземпляра = 1 По СтрокаДанных.Количество Цикл
			
			Если СтрокаДанных.ЭтоКодМаркировки Тогда
				Область                        = Макет.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
				Если СтрокаДанных.ТипШтрихкода = Перечисления.ТипыШтрихкодов.DataMatrix Тогда
					ТипКода = 18;
				ИначеЕсли СтрокаДанных.ТипШтрихкода = Перечисления.ТипыШтрихкодов.GS1_DataMatrix Тогда
					ТипКода = 24;
				КонецЕсли;
					
				Область.Параметры.Артикул      = СтрокаДанных.Номенклатура.Артикул;
				Область.Параметры.НомерВГруппе = СтрокаДанных.НомерВГруппе;
			Иначе
				Область                                = МакетУпаковки.ПолучитьОбласть(Макет.ОбластьПечати.Имя);
				ТипКода                                = 17;
				Область.Параметры.НомерВГруппе         = СтрокаДанных.НомерВГруппе;
				Область.Параметры.СодержимоеКоличество = СтрокаДанных.СодержимоеКоличество;
			КонецЕсли;
			
			Для Каждого Рисунок Из Область.Рисунки Цикл
				Штрихкод = СтрокаДанных.Штрихкод;
				Область.Параметры.ПредставлениеШтрихкода = СтрокаДанных.КодМаркировки;
				
				Штрихкод = ШтрихкодированиеИС.КодGS1ДляКомпонентыПечати(СтрокаДанных.Штрихкод, ТипКода);
				
				ПараметрыШтрихкода = Новый Структура;
				ПараметрыШтрихкода.Вставить("Ширина",          Окр(Рисунок.Ширина / КоличествоМиллиметровВПикселе));
				ПараметрыШтрихкода.Вставить("Высота",          Окр(Рисунок.Высота / КоличествоМиллиметровВПикселе));
				ПараметрыШтрихкода.Вставить("Штрихкод",        Штрихкод);
				ПараметрыШтрихкода.Вставить("ТипКода",         ТипКода);
				ПараметрыШтрихкода.Вставить("ОтображатьТекст", Истина);
				
				Рисунок.Картинка = МенеджерОборудованияВызовСервера.ПолучитьКартинкуШтрихкода(ПараметрыШтрихкода);
			КонецЦикла;
			
		КонецЦикла;
		
		Если СтруктураНастроек.КаждаяЭтикеткаНаНовомЛисте Тогда
					
			ТабличныйДокумент.Вывести(Область);
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			
		Иначе
			
			Если ПредыдущейНомерВГруппе = Неопределено Тогда
				ПредыдущейНомерВГруппе = ТекущийНомерВГруппе;
			КонецЕсли;
			Присоединять = Истина;
			Если ПредыдущейНомерВГруппе <> ТекущийНомерВГруппе Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				ПредыдущейНомерВГруппе = ТекущийНомерВГруппе;
				Присоединять = Ложь;
			КонецЕсли;
			
			Если Не ТабличныйДокумент.АвтоМасштаб И Присоединять И ТабличныйДокумент.ПроверитьПрисоединение(Область) Тогда
				
				ТабличныйДокумент.Присоединить(Область);
				
			Иначе
				
				Если Не ТабличныйДокумент.ПроверитьВывод(Область) И Присоединять Тогда
					ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
				КонецЕсли;
				
				ТабличныйДокумент.Вывести(Область);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры

Процедура ПриОпределенииКомандПодключенныхКОбъекту(Команды) Экспорт
	
КонецПроцедуры

// Возвращает структуру данных для печати свободных кодов.
// 
// Параметры:
// 	ТаблицаНоменклатуры - ТаблицаЗначений - Исходные данныех для печати
// 	СтруктураОтбораДокументы - Структура - Описание:
// * ДокументОснование - ДокументСсылка - Документ-основание для резервирования свободных кодов
// Возвращаемое значение:
// 	Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - Данные для печати, по структуре частично повторяют ПулКодовМаркировки
// * Отбор - Структура - Структура с полями для отбора по документу, используется для передачи в 
//		(см. РегистрСведений.ПулКодовМаркировкиСУЗ.ДобавитьВЗапросПечатиУсловияПоДокументу)
Функция СтруктураДанныхДляПечатиСвободныхКодов(ТаблицаНоменклатуры, СтруктураОтбораДокументы) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.Организация          КАК Организация,
		|	ИсходнаяТаблица.Номенклатура         КАК Номенклатура,
		|	ИсходнаяТаблица.Характеристика       КАК Характеристика,
		|	ИсходнаяТаблица.Шаблон               КАК Шаблон,
		|	ИсходнаяТаблица.СпособВводаВОборот   КАК СпособВводаВОборот,
		|	ИсходнаяТаблица.ШаблонЭтикетки       КАК ШаблонЭтикетки,
		|	ИсходнаяТаблица.НомерВГруппе         КАК НомерВГруппе,
		|	ИсходнаяТаблица.СодержимоеКоличество КАК СодержимоеКоличество,
		|	ИсходнаяТаблица.КоличествоПечать     КАК КоличествоПечать,
		|	ИсходнаяТаблица.Количество           КАК Количество
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК ИсходнаяТаблица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Номенклатура,
		|	Характеристика,
		|	Шаблон,
		|	СпособВводаВОборот
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПулКодовМаркировкиСУЗ.КодМаркировки,
		|	ПулКодовМаркировкиСУЗ.ПолныйКодМаркировки,
		|	ПулКодовМаркировкиСУЗ.ХешСуммаКодаМаркировки,
		|	ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию,
		|	ПулКодовМаркировкиСУЗ.ДокументОснование,
		|	ВЫБОР
		|		КОГДА ПулКодовМаркировкиСУЗ.Статус В (&НедоступныеСтатусы)
		|			ТОГДА 0
		|		КОГДА ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 1 
		|		КОГДА &РазрешенаПовторнаяПечать
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Доступно,
		|	ПулКодовМаркировкиСУЗ.Статус,
		|	ПулКодовМаркировкиСУЗ.Организация,
		|	ПулКодовМаркировкиСУЗ.СтанцияУправленияЗаказами,
		|	ПулКодовМаркировкиСУЗ.Номенклатура,
		|	ПулКодовМаркировкиСУЗ.Характеристика,
		|	ПулКодовМаркировкиСУЗ.GTIN,
		|	ПулКодовМаркировкиСУЗ.ДатаЭмиссииУниверсальная,
		|	ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная,
		|	ПулКодовМаркировкиСУЗ.ШтрихкодУпаковки,
		|	ПулКодовМаркировкиСУЗ.Шаблон,
		|	ПулКодовМаркировкиСУЗ.ТипШтрихкода,
		|	ПулКодовМаркировкиСУЗ.СпособВводаВОборот,
		|	ПулКодовМаркировкиСУЗ.ХешСуммаУпаковки
		|ПОМЕСТИТЬ ДанныеРегистра
		|ИЗ
		|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	Организация,
		|	Номенклатура,
		|	Характеристика,
		|	Шаблон,
		|	СпособВводаВОборот,
		|	ДатаПечатиУниверсальная,
		|	Доступно
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистра.КодМаркировки,
		|	ДанныеРегистра.ПолныйКодМаркировки Как Штрихкод,
		|	ДанныеРегистра.ПолныйКодМаркировки,
		|	ДанныеРегистра.ХешСуммаКодаМаркировки,
		|	ДанныеРегистра.ЗаказНаЭмиссию,
		|	ДанныеРегистра.ДокументОснование,
		|	ДанныеРегистра.Доступно,
		|	ДанныеРегистра.Статус,
		|	ДанныеРегистра.Организация,
		|	ДанныеРегистра.СтанцияУправленияЗаказами,
		|	ДанныеРегистра.Номенклатура,
		|	ДанныеРегистра.Характеристика,
		|	ДанныеРегистра.GTIN,
		|	ДанныеРегистра.ДатаЭмиссииУниверсальная,
		|	ДанныеРегистра.ДатаПечатиУниверсальная,
		|	ДанныеРегистра.ШтрихкодУпаковки,
		|	ДанныеРегистра.ХешСуммаУпаковки,
		|	ДанныеРегистра.Шаблон,
		|	ДанныеРегистра.СпособВводаВОборот,
		|	ДанныеРегистра.ТипШтрихкода Как ТипШтрихкода,
		|	ЛОЖЬ КАК ДляПечати,
		|	ИСТИНА КАК ЭтоКодМаркировки,
		|	ТаблицаНоменклатуры.ШаблонЭтикетки,
		|	ТаблицаНоменклатуры.НомерВГруппе,
		|	ТаблицаНоменклатуры.СодержимоеКоличество,
		|	ТаблицаНоменклатуры.КоличествоПечать Как Количество
		|ИЗ
		|	ДанныеРегистра КАК ДанныеРегистра
		|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ПО ДанныеРегистра.Организация       = ТаблицаНоменклатуры.Организация
		|		И ДанныеРегистра.Номенклатура       = ТаблицаНоменклатуры.Номенклатура
		|		И ДанныеРегистра.Характеристика     = ТаблицаНоменклатуры.Характеристика
		|		И ДанныеРегистра.Шаблон             = ТаблицаНоменклатуры.Шаблон
		|		И ДанныеРегистра.СпособВводаВОборот = ТаблицаНоменклатуры.СпособВводаВОборот
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаНоменклатуры.Организация,
		|	ТаблицаНоменклатуры.Номенклатура,
		|	ТаблицаНоменклатуры.Характеристика,
		|	ТаблицаНоменклатуры.Шаблон,
		|	ТаблицаНоменклатуры.СпособВводаВОборот,
		|	ТаблицаНоменклатуры.Количество,
		|	СУММА(ЕСТЬNULL(ДанныеРегистра.Доступно, 0)) КАК Доступно
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРегистра КАК ДанныеРегистра
		|		ПО ТаблицаНоменклатуры.Организация       = ДанныеРегистра.Организация
		|		И ТаблицаНоменклатуры.Номенклатура       = ДанныеРегистра.Номенклатура
		|		И ТаблицаНоменклатуры.Характеристика     = ДанныеРегистра.Характеристика
		|		И ТаблицаНоменклатуры.Шаблон             = ДанныеРегистра.Шаблон
		|		И ТаблицаНоменклатуры.СпособВводаВОборот = ДанныеРегистра.СпособВводаВОборот
		|		И ДанныеРегистра.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1, 1, 1)
		|		И ДанныеРегистра.Доступно > 0
		|СГРУППИРОВАТЬ ПО
		|	ТаблицаНоменклатуры.Организация,
		|	ТаблицаНоменклатуры.Номенклатура,
		|	ТаблицаНоменклатуры.Характеристика,
		|	ТаблицаНоменклатуры.Шаблон,
		|	ТаблицаНоменклатуры.СпособВводаВОборот,
		|	ТаблицаНоменклатуры.Количество
		|ИМЕЮЩИЕ
		|	СУММА(ЕСТЬNULL(ДанныеРегистра.Доступно, 0)) < ТаблицаНоменклатуры.Количество";
	
	ДобавитьВЗапросПечатиУсловияПоДокументу(Запрос, СтруктураОтбораДокументы, 1);
	
	РазрешенаПовторнаяПечать = ИнтеграцияИСМП.РазрешенаПовторнаяПечатьКодовМаркировки();
	Запрос.УстановитьПараметр("НедоступныеСтатусы",       СтатусыВыведенИзОборота());
	Запрос.УстановитьПараметр("РазрешенаПовторнаяПечать", РазрешенаПовторнаяПечать);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",      ТаблицаНоменклатуры);
	
	СтруктураВозвращаемыхДанных = Новый Структура();
	СтруктураВозвращаемыхДанных.Вставить("Отбор", СтруктураОтбораДокументы);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаЗаписей = РезультатЗапроса.Получить(2).Выгрузить();
	ТаблицаЗаписей.Индексы.Добавить("Организация, Номенклатура, Характеристика, Шаблон, СпособВводаВОборот");
	
	// Проверка общего количества доступных кодов
	Отказ = Ложь;
	ТаблицаНедостаточноСводобныхКодов = РезультатЗапроса.Получить(3).Выгрузить();
	Для Каждого СтрокаТЧ Из ТаблицаНедостаточноСводобныхКодов Цикл
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Ошибка резервирования кода для %1 %2, требуется кодов: %3, доступно %4'"),
			СтрокаТЧ.Номенклатура, СтрокаТЧ.Характеристика,
			СтрокаТЧ.Количество, СтрокаТЧ.Доступно);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЦикла;
	Если Отказ Тогда
		ВызватьИсключение НСтр("ru = 'Недостаточно свободных кодов маркировки'");
	КонецЕсли;
	
	// Подготовка таблицы с кодами по количеству кодов маркировки для печати
	Для Каждого СтрокаТЧ Из ТаблицаНоменклатуры Цикл
		
		СтруктураПоиска = Новый Структура;
		СтруктураПоиска.Вставить("Организация",        СтрокаТЧ.Организация);
		СтруктураПоиска.Вставить("Номенклатура",       СтрокаТЧ.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика",     СтрокаТЧ.Характеристика);
		СтруктураПоиска.Вставить("Шаблон",             СтрокаТЧ.Шаблон);
		СтруктураПоиска.Вставить("СпособВводаВОборот", СтрокаТЧ.СпособВводаВОборот);
		
		ПоискСтрокКодов = ТаблицаЗаписей.НайтиСтроки(СтруктураПоиска);
		ТекущееКоличество = 0;
		
		Для Каждого СтрокаКода Из ПоискСтрокКодов Цикл
			СтрокаКода.ДляПечати = Истина;
			СтрокаКода.Количество = 1;
			СтрокаКода.Штрихкод = ШтрихкодированиеИСКлиентСервер.Base64ВШтрихкод(
				СтрокаКода.ПолныйКодМаркировки);
			ТекущееКоличество = ТекущееКоличество + 1;
			Если ТекущееКоличество = СтрокаТЧ.Количество Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	ТаблицаЗаписей.Индексы.Добавить("ДляПечати");
	ТаблицаПечати = ТаблицаЗаписей.Скопировать(Новый Структура("ДляПечати", Истина));
	ТаблицаПечати.Индексы.Добавить("ДокументОснование, ЗаказНаЭмиссию, КодМаркировки");
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаПечати);
	
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

// Возвращает структуру данных для печати агрегированных кодов.
// 
// Параметры:
// 	ТаблицаНоменклатуры - ТаблицаЗначений - Исходные данные для печати
// 	СтруктураОтбораДокументы - Структура - Описание:
// * ДокументОснование - ДокументСсылка - Документ-основание для резервирования свободных кодов
// Возвращаемое значение:
// 	Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - Данные для печати, по структуре частично повторяют ПулКодовМаркировки
// * Отбор - Структура - Структура с полями для отбора по документу, используется для передачи в 
//		(см. РегистрСведений.ПулКодовМаркировкиСУЗ.ДобавитьВЗапросПечатиУсловияПоДокументу)
Функция СтруктураДанныхДляПечатиАгрегированныеКоды(ТаблицаНоменклатуры, СтруктураОтбораДокументы, Отказ=Ложь) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.Порядок                КАК Порядок,
		|	ИсходнаяТаблица.Штрихкод               КАК Штрихкод,
		|	ИсходнаяТаблица.Штрихкод               КАК КодМаркировки,
		|	ИсходнаяТаблица.ХешСуммаКодаМаркировки КАК ХешСуммаКодаМаркировки,
		|	ИсходнаяТаблица.Номенклатура           КАК Номенклатура,
		|	ИсходнаяТаблица.Характеристика         КАК Характеристика,
		|	ИсходнаяТаблица.ШаблонЭтикетки         КАК ШаблонЭтикетки,
		|	ИсходнаяТаблица.НомерВГруппе           КАК НомерВГруппе,
		|	ИсходнаяТаблица.СодержимоеКоличество   КАК СодержимоеКоличество,
		|	ИсходнаяТаблица.КоличествоПечать       КАК КоличествоПечать,
		|	ИсходнаяТаблица.ЭтоКодМаркировки       КАК ЭтоКодМаркировки,
		|	ИсходнаяТаблица.ТипШтрихкода           КАК ТипШтрихкода,
		|	ИсходнаяТаблица.Количество             КАК Количество
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК ИсходнаяТаблица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодМаркировки,
		|	ХешСуммаКодаМаркировки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПулКодовМаркировкиСУЗ.КодМаркировки,
		|	ПулКодовМаркировкиСУЗ.ПолныйКодМаркировки,
		|	ПулКодовМаркировкиСУЗ.ХешСуммаКодаМаркировки,
		|	ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию,
		|	ПулКодовМаркировкиСУЗ.ДокументОснование,
		|	ВЫБОР
		|		КОГДА ПулКодовМаркировкиСУЗ.Статус В (&НедоступныеСтатусы)
		|			ТОГДА 0
		|		КОГДА ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА 1 
		|		КОГДА &РазрешенаПовторнаяПечать
		|			ТОГДА 1
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Доступно,
		|	ПулКодовМаркировкиСУЗ.Статус,
		|	ПулКодовМаркировкиСУЗ.Организация,
		|	ПулКодовМаркировкиСУЗ.СтанцияУправленияЗаказами,
		|	ПулКодовМаркировкиСУЗ.Номенклатура,
		|	ПулКодовМаркировкиСУЗ.Характеристика,
		|	ПулКодовМаркировкиСУЗ.GTIN,
		|	ПулКодовМаркировкиСУЗ.ДатаЭмиссииУниверсальная,
		|	ПулКодовМаркировкиСУЗ.ДатаПечатиУниверсальная,
		|	ПулКодовМаркировкиСУЗ.ШтрихкодУпаковки,
		|	ПулКодовМаркировкиСУЗ.ХешСуммаУпаковки
		|ПОМЕСТИТЬ ДанныеРегистра
		|ИЗ
		|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК ПулКодовМаркировкиСУЗ
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодМаркировки,
		|	ХешСуммаКодаМаркировки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВЫБОР
		|		КОГДА ТаблицаНоменклатуры.ЭтоКодМаркировки
		|			ТОГДА ЕСТЬNULL(ПулКодовМаркировкиСУЗ.ПолныйКодМаркировки, """")
		|		ИНАЧЕ ТаблицаНоменклатуры.Штрихкод
		|	КОНЕЦ КАК Штрихкод,
		|	ТаблицаНоменклатуры.КодМаркировки,
		|	ПулКодовМаркировкиСУЗ.ПолныйКодМаркировки,
		|	ПулКодовМаркировкиСУЗ.ХешСуммаКодаМаркировки,
		|	ТаблицаНоменклатуры.Номенклатура,
		|	ТаблицаНоменклатуры.Характеристика,
		|	ТаблицаНоменклатуры.ШаблонЭтикетки,
		|	ТаблицаНоменклатуры.НомерВГруппе,
		|	ТаблицаНоменклатуры.СодержимоеКоличество,
		|	ТаблицаНоменклатуры.КоличествоПечать,
		|	ТаблицаНоменклатуры.Количество,
		|	ПулКодовМаркировкиСУЗ.Организация,
		|	ВЫБОР
		|		КОГДА ТаблицаНоменклатуры.ЭтоКодМаркировки
		|			ТОГДА ЕСТЬNULL(ПулКодовМаркировкиСУЗ.Доступно, ЛОЖЬ)
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ Как Доступно,
		|	ТаблицаНоменклатуры.ЭтоКодМаркировки КАК ЭтоКодМаркировки,
		|	ТаблицаНоменклатуры.ТипШтрихкода КАК ТипШтрихкода,
		|	ПулКодовМаркировкиСУЗ.GTIN,
		|	ПулКодовМаркировкиСУЗ.ЗаказНаЭмиссию,
		|	ПулКодовМаркировкиСУЗ.ДокументОснование,
		|	ТаблицаНоменклатуры.Порядок КАК Порядок
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ ДанныеРегистра КАК ПулКодовМаркировкиСУЗ
		|		ПО ТаблицаНоменклатуры.КодМаркировки          = ПулКодовМаркировкиСУЗ.КодМаркировки
		|		И ТаблицаНоменклатуры.ХешСуммаКодаМаркировки = ПулКодовМаркировкиСУЗ.ХешСуммаКодаМаркировки
		|УПОРЯДОЧИТЬ ПО
		|	Порядок";
	
	ДобавитьВЗапросПечатиУсловияПоДокументу(Запрос, СтруктураОтбораДокументы, 1);
	
	Запрос.УстановитьПараметр("НедоступныеСтатусы",       СтатусыВыведенИзОборота());
	Запрос.УстановитьПараметр("РазрешенаПовторнаяПечать", ИнтеграцияИСМП.РазрешенаПовторнаяПечатьКодовМаркировки());
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",      ТаблицаНоменклатуры);
	
	СтруктураВозвращаемыхДанных = Новый Структура();
	СтруктураВозвращаемыхДанных.Вставить("Отбор", СтруктураОтбораДокументы);

	ТаблицаПечати = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаТЧ Из ТаблицаПечати Цикл
		Если СтрокаТЧ.ЭтоКодМаркировки Тогда
			СтрокаТЧ.Штрихкод = ШтрихкодированиеИСКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.ПолныйКодМаркировки);
		КонецЕсли;
	КонецЦикла;
	
	// Проверка на доступность
	НедоступныеКоды = ТаблицаПечати.НайтиСтроки(Новый Структура("Доступно", Ложь));
	Для Каждого СтрокаТЧ Из НедоступныеКоды Цикл
		ТекстСообщения = СтрШаблон(
			НСтр("ru = 'Для %1 %2: код маркировки недоступен или уже распечатан'"),
			СтрокаТЧ.Номенклатура,
			СтрокаТЧ.Характеристика);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЦикла;
	
	Если Отказ Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка печати этикеток'");
	КонецЕсли;
	
	ТаблицаПечати.Индексы.Добавить("ДокументОснование, ЗаказНаЭмиссию, КодМаркировки");
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаПечати);
	
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

// Возвращает структуру данных для печати конкретного кода маркировки.
// 
// Параметры:
// 	ТаблицаНоменклатуры - ТаблицаЗначений - Исходные данные
// Возвращаемое значение:
// 	Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - ТаблицаЗначений - Данные для печати
Функция СтруктураДанныхДляПечатиВыборочно(ТаблицаНоменклатуры, РазрешитьПовторнуюПечать=Ложь) Экспорт

	СтруктураВозвращаемыхДанных = Новый Структура();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.КодМаркировки          КАК КодМаркировки,
		|	ИсходнаяТаблица.ХешСуммаКодаМаркировки КАК ХешСуммаКодаМаркировки,
		|	ИсходнаяТаблица.ТипШтрихкода           КАК ТипШтрихкода,
		|	ИсходнаяТаблица.Номенклатура           КАК Номенклатура,
		|	ИсходнаяТаблица.Характеристика         КАК Характеристика,
		|	ИсходнаяТаблица.ШаблонЭтикетки         КАК ШаблонЭтикетки,
		|	ИсходнаяТаблица.Количество             КАК КоличествоПечать,
		|	ИсходнаяТаблица.Количество             КАК Количество
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК ИсходнаяТаблица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодМаркировки,
		|	ХешСуммаКодаМаркировки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(ДанныеРегистра.КодМаркировки,       ТаблицаНоменклатуры.КодМаркировки) КАК КодМаркировки,
		|	ЕСТЬNULL(ДанныеРегистра.ХешСуммаКодаМаркировки, ТаблицаНоменклатуры.ХешСуммаКодаМаркировки) КАК ХешСуммаКодаМаркировки,
		|	ЕСТЬNULL(ДанныеРегистра.ПолныйКодМаркировки, ТаблицаНоменклатуры.КодМаркировки) КАК Штрихкод,
		|	ЕСТЬNULL(ДанныеРегистра.ПолныйКодМаркировки, ТаблицаНоменклатуры.КодМаркировки) КАК ПолныйКодМаркировки,
		|	ДанныеРегистра.ЗаказНаЭмиссию,
		|	ДанныеРегистра.ДокументОснование,
		|	ДанныеРегистра.Статус,
		|	ДанныеРегистра.Организация,
		|	ЕСТЬNULL(ДанныеРегистра.Номенклатура, ТаблицаНоменклатуры.Номенклатура)     КАК Номенклатура,
		|	ЕСТЬNULL(ДанныеРегистра.Характеристика, ТаблицаНоменклатуры.Характеристика) КАК Характеристика,
		|	ДанныеРегистра.GTIN,
		|	ДанныеРегистра.ДатаПечатиУниверсальная,
		|	ДанныеРегистра.ШтрихкодУпаковки,
		|	ТаблицаНоменклатуры.ТипШтрихкода Как ТипШтрихкода,
		|	ВЫБОР
		|		КОГДА  ДанныеРегистра.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА 
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ДляЗаписи,
		|	ИСТИНА КАК ЭтоКодМаркировки,
		|	ТаблицаНоменклатуры.ШаблонЭтикетки,
		|	0 КАК НомерВГруппе,
		|	0 КАК СодержимоеКоличество,
		|	ВЫБОР
		|		КОГДА ДанныеРегистра.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1, 1, 1)
		|			ТОГДА ИСТИНА 
		|		КОГДА &РазрешенаПовторнаяПечать
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК МожноПечататьПовторно,
		|	ТаблицаНоменклатуры.КоличествоПечать Как Количество
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗ КАК ДанныеРегистра
		|		ПО ДанныеРегистра.КодМаркировки          = ТаблицаНоменклатуры.КодМаркировки
		|		И ДанныеРегистра.ХешСуммаКодаМаркировки  = ТаблицаНоменклатуры.ХешСуммаКодаМаркировки
		|";
	РазрешенаПовторнаяПечать = ИнтеграцияИСМП.РазрешенаПовторнаяПечатьКодовМаркировки();
	Запрос.УстановитьПараметр("РазрешенаПовторнаяПечать", РазрешенаПовторнаяПечать);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",      ТаблицаНоменклатуры);
	
	ТаблицаПечати = Запрос.Выполнить().Выгрузить();
	
	Отказ = Ложь;
	
	Для Каждого СтрокаТЧ Из ТаблицаПечати Цикл
		
		СтрокаТЧ.Штрихкод = ШтрихкодированиеИСКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.ПолныйКодМаркировки);
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Штрихкод) Тогда
			СтрокаТЧ.Штрихкод = СтрокаТЧ.КодМаркировки;
		КонецЕсли;
		
		Если Не РазрешитьПовторнуюПечать И Не СтрокаТЧ.МожноПечататьПовторно Тогда
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Ошибка печати этикетки по %1: уже распечатан. Повторная печать недоступна'"),
				СтрокаТЧ.КодМаркировки);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
		КонецЕсли;
		
	КонецЦикла;
	
	Если Не ТаблицаПечати.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Не найдено данных для печати'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,,,, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка печати этикеток'");
	КонецЕсли;
	
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаПечати);

	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

// Резервирует и выполняет печать этикеток по указанным кодам с резервированием кодов маркировки и соззданием
// элемента справочника ШтрихкодыУпаковокТоваров.
// 
// Параметры:
// 	ТаблицаНоменклатуры - ТаблицаЗначений - Исходные данные для печати
// 	Документ - ДокументСсылка.ЗаказНаЭмиссиюКодовМаркировкиСУЗ - Документ-основание для резервирования свободных кодов
// 	РезультатРезервирования - Массив - Данные резервирования, которые будут напечатаны
// Возвращаемое значение:
// 	Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - итоговые данные для печати этикеток
Функция СтруктураДанныхДляПечатиПоКодамМаркировки(ТаблицаНоменклатуры, Документ, РезультатРезервирования) Экспорт
	
	СтруктураВозвращаемыхДанных = Новый Структура();
	
	ПараметрыРезервирования = Новый Структура();
	ПараметрыРезервирования.Вставить("Документ",            Документ);
	ПараметрыРезервирования.Вставить("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	
	АдресРезультатаРезервирования = ПоместитьВоВременноеХранилище(Неопределено);
	
	МаркировкаОстатков = Неопределено;
	Если ТаблицаНоменклатуры.Количество() Тогда
		МаркировкаОстатков = ТаблицаНоменклатуры.Получить(0).МаркировкаОстатков;
	КонецЕсли;
	// Резервируем коды маркировки
	ЗарезервироватьКодыПодЗаказ(ПараметрыРезервирования, АдресРезультатаРезервирования, МаркировкаОстатков);
	Если Не ЭтоАдресВременногоХранилища(АдресРезультатаРезервирования) Тогда
		ВызватьИсключение НСтр("ru = 'Ошибка резервирования кодов: не верные данные результата'");
	КонецЕсли;
	
	СтруктураРезультатРезервирования = ПолучитьИзВременногоХранилища(АдресРезультатаРезервирования);
	РезультатРезервирования          = СтруктураРезультатРезервирования.ТаблицаКодыМаркировки;
	
	ДеревоУпаковок = Обработки.ВиртуальнаяАгрегацияУпаковокИСМП.НовоеДеревоУпаковок();
	ДеревоУпаковок.Колонки.Добавить("ИсходнаяСтрока");
	ДеревоУпаковок.Колонки.Добавить("ИННВладельца");
	
	Для Каждого СтрокаТаблицы Из ТаблицаНоменклатуры Цикл
		
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("Номенклатура",   СтрокаТаблицы.Номенклатура);
		СтруктураПоиска.Вставить("Характеристика", СтрокаТаблицы.Характеристика);
	
		ПоискСтрокКодов = РезультатРезервирования.НайтиСтроки(СтруктураПоиска);
			
		Если ПоискСтрокКодов.Количество() = 0 Тогда
			МассивПолейПредставления = Новый Массив();
			МассивПолейПредставления.Добавить(
				ИнтеграцияИС.ПредставлениеНоменклатуры(СтрокаТаблицы.Номенклатура, СтрокаТаблицы.Характеристика));
			Если ЗначениеЗаполнено(СтрокаТаблицы.СпособВводаВОборот) Тогда
				МассивПолейПредставления.Добавить(СтрокаТаблицы.СпособВводаВОборот);
			КонецЕсли;
			ТекстОшибки = СтрШаблон(
				НСтр("ru = 'Не удалось зарезервировать код маркировки для:
					       | ""%1""
					       |по причине:
					       |Не найдено свободных кодов'"),
				СтрСоединить(МассивПолейПредставления, ", "));
			ВызватьИсключение ТекстОшибки;
		КонецЕсли;
		
		СтрокаРезервирования = ПоискСтрокКодов.Получить(0);
		
		СтрокаДерева                = ДеревоУпаковок.Строки.Добавить();
		СтрокаДерева.Штрихкод       = СтрокаРезервирования.КодМаркировки;
		СтрокаДерева.Номенклатура   = СтрокаРезервирования.Номенклатура;
		СтрокаДерева.Характеристика = СтрокаРезервирования.Характеристика;
		СтрокаДерева.Серия          = СтрокаТаблицы.Серия;
		СтрокаДерева.ИННВладельца   = СтрокаРезервирования.ИННВладельца;
		СтрокаДерева.Количество     = 1;
		
		Если Не ЗначениеЗаполнено(СтрокаТаблицы.Серия) Тогда
			СтрокаДерева.Серия          = СтрокаТаблицы.ШтрихкодУпаковки.Серия;
		КонецЕсли;
		
		СтрокаДерева.ТипШтрихкода = СтрокаРезервирования.ТипШтрихкода;
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ШтрихкодУпаковки.ТипУпаковки) Тогда
			СтрокаДерева.ТипУпаковки = СтрокаТаблицы.ШтрихкодУпаковки.ТипУпаковки;
		Иначе
			СтрокаДерева.ТипУпаковки = ПредопределенноеЗначение("Перечисление.ТипыУпаковок.МаркированныйТовар");
		КонецЕсли;
		
		СтрокаДерева.ИсходнаяСтрока = СтрокаТаблицы;
		СтрокаТаблицы.Штрихкод      = СтрокаРезервирования.ПолныйКодМаркировки;
		СтрокаТаблицы.ХешСуммаКодаМаркировки = СтрокаРезервирования.ХешСуммаКодаМаркировки;
		
		РезультатРезервирования.Удалить(СтрокаРезервирования);
		
	КонецЦикла;
	
	РезультатРезервирования = Новый Массив();
	
	Справочники.ШтрихкодыУпаковокТоваров.СоздатьШтрихкодыУпаковокПоДаннымДерева(ДеревоУпаковок);
	
	Для Каждого СтрокаДерева Из ДеревоУпаковок.Строки Цикл
		
		Если ЗначениеЗаполнено(СтрокаТаблицы.ШтрихкодУпаковки) Тогда
			Обработки.РаботаСКМПриПеремаркировкеТоваровИСМП.УстановитьЗначенияНовогоКодаМаркировки(
				СтрокаТаблицы.ШтрихкодУпаковки, СтрокаДерева.ШтрихкодУпаковки);
		КонецЕсли;
		
		СтрокаТаблицы                  = СтрокаДерева.ИсходнаяСтрока;
		СтрокаТаблицы.ШтрихкодУпаковки = СтрокаДерева.ШтрихкодУпаковки;
		СтрокаТаблицы.ТипШтрихкода     = СтрокаДерева.ТипШтрихкода;
		СтрокаТаблицы.КодМаркировки    = СтрокаДерева.Штрихкод;
		
		// Результат резервирования. Передается в обработчики оповещения на вызывающих формах
		СтруктураРезультат = Новый Структура();
		СтруктураРезультат.Вставить("Номенклатура",           СтрокаДерева.Номенклатура);
		СтруктураРезультат.Вставить("Характеристика",         СтрокаДерева.Характеристика);
		СтруктураРезультат.Вставить("Серия",                  СтрокаДерева.Серия);
		СтруктураРезультат.Вставить("ШтрихкодУпаковки",       СтрокаДерева.ШтрихкодУпаковки);
		СтруктураРезультат.Вставить("ПолныйКодМаркировки",    СтрокаДерева.Штрихкод);
		СтруктураРезультат.Вставить("ИННВладельца",           СтрокаДерева.ИННВладельца);
		СтруктураРезультат.Вставить("ХешСуммаКодаМаркировки", СтрокаТаблицы.ХешСуммаКодаМаркировки);
		СтруктураРезультат.Вставить("КодМаркировки",          СтрокаТаблицы.КодМаркировки);
		
		РезультатРезервирования.Добавить(СтруктураРезультат);
		
	КонецЦикла;
		
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документ", Документ);
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ИсходнаяТаблица.КодМаркировки          КАК КодМаркировки,
		|	ИсходнаяТаблица.ХешСуммаКодаМаркировки КАК ХешСуммаКодаМаркировки,
		|	ИсходнаяТаблица.ТипШтрихкода           КАК ТипШтрихкода,
		|	ИсходнаяТаблица.Номенклатура           КАК Номенклатура,
		|	ИсходнаяТаблица.Характеристика         КАК Характеристика,
		|	ИсходнаяТаблица.ШаблонЭтикетки         КАК ШаблонЭтикетки,
		|	ИсходнаяТаблица.Количество             КАК КоличествоПечать,
		|	ИсходнаяТаблица.Количество             КАК Количество
		|ПОМЕСТИТЬ ТаблицаНоменклатуры
		|ИЗ
		|	&ТаблицаНоменклатуры КАК ИсходнаяТаблица
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	КодМаркировки
		|;
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДанныеРегистра.КодМаркировки,
		|	ДанныеРегистра.ПолныйКодМаркировки Как Штрихкод,
		|	ДанныеРегистра.ПолныйКодМаркировки,
		|	ДанныеРегистра.ХешСуммаКодаМаркировки,
		|	ДанныеРегистра.ЗаказНаЭмиссию,
		|	ДанныеРегистра.ДокументОснование,
		|	ДанныеРегистра.Статус,
		|	ДанныеРегистра.Организация,
		|	ДанныеРегистра.СтанцияУправленияЗаказами,
		|	ДанныеРегистра.Номенклатура,
		|	ДанныеРегистра.GTIN,
		|	ДанныеРегистра.Характеристика,
		|	ДанныеРегистра.ДатаЭмиссииУниверсальная,
		|	ДанныеРегистра.ДатаПечатиУниверсальная,
		|	ДанныеРегистра.ШтрихкодУпаковки,
		|	ДанныеРегистра.ХешСуммаУпаковки,
		|	ТаблицаНоменклатуры.ТипШтрихкода,
		|	ИСТИНА КАК ЭтоКодМаркировки,
		|	ТаблицаНоменклатуры.ШаблонЭтикетки,
		|	0 КАК НомерВГруппе,
		|	0 КАК СодержимоеКоличество,
		|	ТаблицаНоменклатуры.КоличествоПечать Как Количество
		|ИЗ
		|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПулКодовМаркировкиСУЗ КАК ДанныеРегистра
		|		ПО ДанныеРегистра.КодМаркировки         = ТаблицаНоменклатуры.КодМаркировки
		|		И ДанныеРегистра.ХешСуммаКодаМаркировки = ТаблицаНоменклатуры.ХешСуммаКодаМаркировки
		|";
	
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры", ТаблицаНоменклатуры);
	ТаблицаПечати = Запрос.Выполнить().Выгрузить();
	Для Каждого СтрокаТЧ Из ТаблицаПечати Цикл
		СтрокаТЧ.Штрихкод = ШтрихкодированиеИСКлиентСервер.Base64ВШтрихкод(СтрокаТЧ.ПолныйКодМаркировки);
	КонецЦикла;
	СтруктураВозвращаемыхДанных.Вставить("ТаблицаПечати", ТаблицаПечати);
	Возврат СтруктураВозвращаемыхДанных;
	
КонецФункции

// Устанавливает признак печати кода маркировки в регистре ПулКодовМаркировкиСУЗ текущей универсальной датой
// 
// Параметры:
// 	СтруктураДанныхПоДокументу - Структура - Описание:
// * ТаблицаПечати - ТаблицаЗначений - Список записей, для которых нужно установить признак печати
// * Отбор - Структура - Структура данных для отбора и установки блокировки по документу
Процедура УстановитьСостояниеРаспечатанПоСтруктуреДокумента(СтруктураДанныхПоДокументу) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборДанныхДляОбновления = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьНаборЗаписей();
		
	Для Каждого ЭлементОтбораСтруктура Из СтруктураДанныхПоДокументу.Отбор Цикл
		НаборДанныхДляОбновления.Отбор[ЭлементОтбораСтруктура.Ключ].Установить(ЭлементОтбораСтруктура.Значение);
	КонецЦикла;
	
	НаборДанныхДляОбновления.Прочитать();
		
	ДатаПечати = ТекущаяУниверсальнаяДата();
	
	Для Каждого СтрокаТаблицы Из НаборДанныхДляОбновления Цикл
		
		СтруктураПоиска = Новый Структура();
		СтруктураПоиска.Вставить("ДокументОснование", СтрокаТаблицы.ДокументОснование);
		СтруктураПоиска.Вставить("ЗаказНаЭмиссию", СтрокаТаблицы.ЗаказНаЭмиссию);
		СтруктураПоиска.Вставить("КодМаркировки", СтрокаТаблицы.КодМаркировки);
		
		ПоискСтрокиНабора = СтруктураДанныхПоДокументу.ТаблицаПечати.НайтиСтроки(СтруктураПоиска);
		Если ПоискСтрокиНабора.Количество() Тогда
			СтрокаТаблицы.ДатаПечатиУниверсальная = ДатаПечати;
		КонецЕсли;
		
	КонецЦикла;
	
	НаборДанныхДляОбновления.Записать();
	
КонецПроцедуры

// Устанавливает признак печати конкретных кодов маркировки
// 
// Параметры:
// 	КодыМаркировок - ТаблицаЗначений - Таблица кодов маркировки для установки признака печати
Процедура УстановитьСостояниеРаспечатанПоКаждомуКодуМаркировки(КодыМаркировок) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДатаПечати = ТекущаяУниверсальнаяДата();
	
	Для Каждого Запись Из КодыМаркировок Цикл
		
		МенеджерЗаписиРегистра = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписиРегистра, Запись);
		МенеджерЗаписиРегистра.Прочитать();
		
		Если МенеджерЗаписиРегистра.Выбран() Тогда
			МенеджерЗаписиРегистра.ДатаПечатиУниверсальная = ДатаПечати;
			МенеджерЗаписиРегистра.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Возвращает новую таблицу для печати этикеток
// 
// Возвращаемое значение:
// 	ТаблицаЗначений - Новая пустая таблица для заполнения данными печати
Функция НоваяТаблицаДанныхДляПечатиЭтикеток() Экспорт
	
	НаборВременный = РегистрыСведений.ПулКодовМаркировкиСУЗ.СоздатьНаборЗаписей();
	
	ОписаниеТипаСтрока = Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ.Измерения.КодМаркировки.Тип;
	ОписаниеТипаЧисло  = ОбщегоНазначения.ОписаниеТипаЧисло(10);
	ТаблицаИтог        = НаборВременный.Выгрузить(,"Номенклатура, Характеристика");
	ТаблицаИтог.Колонки.Добавить("Серия",                Метаданные.ОпределяемыеТипы.СерияНоменклатуры.Тип);
	
	ТаблицаИтог.Колонки.Добавить("Количество",           ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("КоличествоПечать",     ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("Порядок",              ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("Штрихкод",             ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("ТипШтрихкода",         Новый ОписаниеТипов("ПеречислениеСсылка.ТипыШтрихкодов"));
	ТаблицаИтог.Колонки.Добавить("ШтрихкодУпаковки",
		Новый ОписаниеТипов("СправочникСсылка.ШтрихкодыУпаковокТоваров"));
	ТаблицаИтог.Колонки.Добавить("КодМаркировки",        ОписаниеТипаСтрока);
	ТаблицаИтог.Колонки.Добавить("НомерВГруппе",         ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("СодержимоеКоличество", ОписаниеТипаЧисло);
	ТаблицаИтог.Колонки.Добавить("ШаблонЭтикетки",       Метаданные.ОпределяемыеТипы.ШаблонЭтикеткиИС.Тип);
	ТаблицаИтог.Колонки.Добавить("Шаблон", Новый ОписаниеТипов("ПеречислениеСсылка.ШаблоныКодовМаркировкиСУЗ"));
	ТаблицаИтог.Колонки.Добавить("СпособВводаВОборот",
		Новый ОписаниеТипов("ПеречислениеСсылка.СпособыВводаВОборотСУЗ"));
	ТаблицаИтог.Колонки.Добавить("ЭтоКодМаркировки",     Новый ОписаниеТипов("Булево"));
	ТаблицаИтог.Колонки.Добавить("Организация",          Метаданные.ОпределяемыеТипы.Организация.Тип);
	ТаблицаИтог.Колонки.Добавить("ВидПродукции",         Новый ОписаниеТипов("ПеречислениеСсылка.ВидыПродукцииИС"));
	ТаблицаИтог.Колонки.Добавить(
		"ХешСуммаКодаМаркировки",
		Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ.Измерения.ХешСуммаКодаМаркировки.Тип);
		
	МассивТиповПризнакаМаркировкиОстатков = Новый Массив();
	МассивТиповПризнакаМаркировкиОстатков.Добавить(Тип("Булево"));
	МассивТиповПризнакаМаркировкиОстатков.Добавить(Тип("ПеречислениеСсылка.ВидыПродукцииИС"));
	ТаблицаИтог.Колонки.Добавить("МаркировкаОстатков", Новый ОписаниеТипов(МассивТиповПризнакаМаркировкиОстатков));
	
	Возврат ТаблицаИтог;
	
КонецФункции

Функция НоваяТаблицаРезультатаРезервирования() Экспорт
	
	ТаблицаИтог = Новый ТаблицаЗначений;
	ТаблицаИтог.Колонки.Добавить(
		"Номенклатура", Метаданные.ОпределяемыеТипы.Номенклатура.Тип);
	ТаблицаИтог.Колонки.Добавить(
		"Характеристика", Метаданные.ОпределяемыеТипы.ХарактеристикаНоменклатуры.Тип);
	ТаблицаИтог.Колонки.Добавить(
		"КодМаркировки", Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ.Измерения.КодМаркировки.Тип);
	ТаблицаИтог.Колонки.Добавить(
		"ПолныйКодМаркировки",
		Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ.Реквизиты.ПолныйКодМаркировки.Тип);
	ТаблицаИтог.Колонки.Добавить(
		"ХешСуммаКодаМаркировки",
		Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ.Измерения.ХешСуммаКодаМаркировки.Тип);
	ТаблицаИтог.Колонки.Добавить(
		"ТипШтрихкода",
		Метаданные.РегистрыСведений.ПулКодовМаркировкиСУЗ.Реквизиты.ТипШтрихкода.Тип);
	ТаблицаИтог.Колонки.Добавить("ИННВладельца", Новый ОписаниеТипов("Строка"));
	ТаблицаИтог.Индексы.Добавить("Номенклатура, Характеристика");
	
	Возврат ТаблицаИтог;
	
КонецФункции

// Возвращает признак наличия в конфигурации-потребителе справочника шаблонов для печати этикеток
// 
// Возвращаемое значение:
// 	Булево - Описание
Функция ВидимостьШаблонаЭтикетки() Экспорт
	
	ШаблонЭтикеткиЭтоСправочник = Ложь;
	
	ТипыШаблонаЭтикетки = Метаданные.ОпределяемыеТипы["ШаблонЭтикеткиИС"].Тип.Типы();
	ТипВсеСправочники   = Справочники.ТипВсеСсылки();
	
	Для Каждого ТипШаблонаЭтикетки Из ТипыШаблонаЭтикетки Цикл
		Если ТипВсеСправочники.СодержитТип(ТипШаблонаЭтикетки) Тогда
			ШаблонЭтикеткиЭтоСправочник = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ШаблонЭтикеткиЭтоСправочник;
	
КонецФункции

#КонецОбласти

#Область РасчетДанныхПоДокументу

Функция ЗапросСвободныхКодовПоЗаказу(Заказ, ТаблицаНоменклатуры, МаркировкаОстатков = Неопределено)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Заказ",                   Заказ);
	Запрос.УстановитьПараметр("ТаблицаНоменклатуры",     ТаблицаНоменклатуры);
	Запрос.УстановитьПараметр("СтатусыВыведенИзОборота", СтатусыВыведенИзОборота());
	Запрос.УстановитьПараметр("МаркировкаОстатков",      МаркировкаОстатков);
	Запрос.УстановитьПараметр(
		"Организация",
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Заказ, "Организация"));
	Запрос.УстановитьПараметр(
		"ПустоеОснование",
		ИнтеграцияИС.НезаполненныеЗначенияОпределяемогоТипа("ОснованиеЗаказНаЭмиссиюКодовМаркировкиИСМП"));
	
	Запрос.Текст = "ВЫБРАТЬ
	|	&Заказ КАК ДокументОснование
	|ПОМЕСТИТЬ ДокументВРаботе
	|;";
	Запрос.Выполнить();
	
	СформироватьТаблицуМаркированныхТоваровОснований(Запрос, "ДокументВРаботе");
	
	Запрос.Текст = "ВЫБРАТЬ
	|	МаркированныеТоварыОснований.Организация        КАК Организация,
	|	МаркированныеТоварыОснований.ВидПродукции       КАК ВидПродукции,
	|	МаркированныеТоварыОснований.Номенклатура       КАК Номенклатура,
	|	МаркированныеТоварыОснований.Характеристика     КАК Характеристика,
	|	МаркированныеТоварыОснований.Шаблон             КАК Шаблон,
	|	МаркированныеТоварыОснований.СпособВводаВОборот КАК СпособВводаВОборот,
	|	МаркированныеТоварыОснований.Количество         КАК Количество
	|ПОМЕСТИТЬ Товары
	|ИЗ
	|	МаркированныеТоварыОснований КАК МаркированныеТоварыОснований
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ВидПродукции,
	|	Номенклатура,
	|	Характеристика,
	|	Шаблон,
	|	СпособВводаВОборот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ МаркированныеТоварыОснований;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ДокументВРаботе;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пул.КодМаркировки               КАК КодМаркировки,
	|	Пул.ПолныйКодМаркировки         КАК ПолныйКодМаркировки,
	|	Пул.ДокументОснование           КАК ДокументОснование,
	|	Пул.ЗаказНаЭмиссию.Организация  КАК Организация,
	|	Пул.ЗаказНаЭмиссию.ВидПродукции КАК ВидПродукции,
	|	Пул.Номенклатура                КАК Номенклатура,
	|	Пул.Характеристика              КАК Характеристика
	|ПОМЕСТИТЬ СвободныеКоды
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК Пул
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|			ПО Товары.Организация       = Пул.ЗаказНаЭмиссию.Организация
	|			И Товары.ВидПродукции       = Пул.ЗаказНаЭмиссию.ВидПродукции
	|			И Товары.Номенклатура       = Пул.Номенклатура
	|			И Товары.Характеристика     = Пул.Характеристика
	|			И Товары.Шаблон             = Пул.Шаблон
	|			И Товары.СпособВводаВОборот В
	|				(Пул.СпособВводаВОборот, Значение(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка))
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОформленияДокументовИСМП КАК СтатусыОформленияДокументовИСМП
	|			ПО СтатусыОформленияДокументовИСМП.Основание = Пул.ДокументОснование
	|			И Пул.ДокументОснование <> &Заказ
	|			И СтатусыОформленияДокументовИСМП.Архивный
	|ГДЕ
	|	НЕ Пул.Статус В (&СтатусыВыведенИзОборота)
	|	И Пул.ШтрихкодУпаковки = ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
	|	И Пул.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1,1,1)
	|	И (Пул.ДокументОснование В(&ПустоеОснование)
	|		ИЛИ НЕ СтатусыОформленияДокументовИСМП.Архивный ЕСТЬ NULL)
	|	И ВЫБОР
	|		КОГДА &МаркировкаОстатков = НЕОПРЕДЕЛЕНО ТОГДА ИСТИНА
	|		ИНАЧЕ Пул.МаркировкаОстатков = &МаркировкаОстатков
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	Пул.КодМаркировки               КАК КодМаркировки,
	|	Пул.ПолныйКодМаркировки         КАК ПолныйКодМаркировки,
	|	Пул.ДокументОснование           КАК ДокументОснование,
	|	Пул.ЗаказНаЭмиссию.Организация  КАК Организация,
	|	Пул.ЗаказНаЭмиссию.ВидПродукции КАК ВидПродукции,
	|	Пул.Номенклатура                КАК Номенклатура,
	|	Пул.Характеристика              КАК Характеристика,
	|	Пул.Шаблон                      КАК Шаблон,
	|	Пул.СпособВводаВОборот          КАК СпособВводаВОборот
	|ПОМЕСТИТЬ ВозможныеСвободныеКоды
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК Пул
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Товары КАК Товары
	|			ПО Товары.Организация       = Пул.ЗаказНаЭмиссию.Организация
	|			И Товары.ВидПродукции       = Пул.ЗаказНаЭмиссию.ВидПродукции
	|			И Товары.Номенклатура       = Пул.Номенклатура
	|			И Товары.Характеристика     = Пул.Характеристика
	|			И Товары.Шаблон             = Пул.Шаблон
	|			И Товары.СпособВводаВОборот В
	|				(Пул.СпособВводаВОборот, Значение(Перечисление.СпособыВводаВОборотСУЗ.ПустаяСсылка))
	|			И Пул.ДокументОснование <> &Заказ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СтатусыОформленияДокументовИСМП КАК СтатусыОформленияДокументовИСМП
	|			ПО СтатусыОформленияДокументовИСМП.Основание = Пул.ДокументОснование
	|			И НЕ СтатусыОформленияДокументовИСМП.Архивный
	|			И СтатусыОформленияДокументовИСМП.СтатусОформления = ЗНАЧЕНИЕ(Перечисление.СтатусыОформленияДокументовГосИС.ЕстьОшибкиОформления)
	|ГДЕ
	|	НЕ Пул.Статус В (&СтатусыВыведенИзОборота)
	|	И Пул.ШтрихкодУпаковки = ЗНАЧЕНИЕ(Справочник.ШтрихкодыУпаковокТоваров.ПустаяСсылка)
	|	И Пул.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1,1,1)
	|	И ВЫБОР
	|		КОГДА &МаркировкаОстатков = НЕОПРЕДЕЛЕНО ТОГДА ИСТИНА
	|		ИНАЧЕ Пул.МаркировкаОстатков = &МаркировкаОстатков
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВозможныеСвободныеКоды.ДокументОснование,
	|	ВозможныеСвободныеКоды.Организация,
	|	ВозможныеСвободныеКоды.ВидПродукции,
	|	ВозможныеСвободныеКоды.Номенклатура,
	|	ВозможныеСвободныеКоды.Характеристика,
	|	ВозможныеСвободныеКоды.Шаблон,
	|	ВозможныеСвободныеКоды.СпособВводаВОборот,
	|	КОЛИЧЕСТВО(ВозможныеСвободныеКоды.КодМаркировки) КАК Количество
	|ПОМЕСТИТЬ ВозможныйИсточникСвободныхКодов
	|ИЗ
	|	ВозможныеСвободныеКоды КАК ВозможныеСвободныеКоды
	|СГРУППИРОВАТЬ ПО
	|	ВозможныеСвободныеКоды.ДокументОснование,
	|	ВозможныеСвободныеКоды.Организация,
	|	ВозможныеСвободныеКоды.ВидПродукции,
	|	ВозможныеСвободныеКоды.Номенклатура,
	|	ВозможныеСвободныеКоды.Характеристика,
	|	ВозможныеСвободныеКоды.Шаблон,
	|	ВозможныеСвободныеКоды.СпособВводаВОборот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ВозможныйИсточникСвободныхКодов.ДокументОснование,
	|	ВозможныйИсточникСвободныхКодов.Организация,
	|	ВозможныйИсточникСвободныхКодов.ВидПродукции
	|ПОМЕСТИТЬ ДокументыСОшибкамиОформления
	|ИЗ
	|	ВозможныйИсточникСвободныхКодов КАК ВозможныйИсточникСвободныхКодов
	|СГРУППИРОВАТЬ ПО
	|	ВозможныйИсточникСвободныхКодов.ДокументОснование,
	|	ВозможныйИсточникСвободныхКодов.Организация,
	|	ВозможныйИсточникСвободныхКодов.ВидПродукции
	|
	|";
	Запрос.Выполнить();
	
	СформироватьТаблицуМаркированныхТоваровОснований(Запрос, "ДокументыСОшибкамиОформления");
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЗаказНаЭмиссиюТовары.Ссылка                    КАК ЗаказНаЭмиссию,
	|	ЗаказНаЭмиссиюТовары.Ссылка.ДокументОснование  КАК ДокументОснование,
	|	ЗаказНаЭмиссиюТовары.Ссылка.Организация        КАК Организация,
	|	ЗаказНаЭмиссиюТовары.Ссылка.ВидПродукции       КАК ВидПродукции,
	|	ЗаказНаЭмиссиюТовары.Номенклатура              КАК Номенклатура,
	|	ЗаказНаЭмиссиюТовары.Характеристика            КАК Характеристика,
	|	ЗаказНаЭмиссиюТовары.Шаблон                    КАК Шаблон,
	|	ЗаказНаЭмиссиюТовары.Ссылка.СпособВводаВОборот КАК СпособВводаВОборот,
	|	ЗаказНаЭмиссиюТовары.Количество                КАК Количество
	|ПОМЕСТИТЬ ОжидаетсяПоДокументамСОшибкамиПредварительно
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссиюТовары
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСОшибкамиОформления КАК ДокументыСОшибкамиОформления
	|		ПО ДокументыСОшибкамиОформления.ДокументОснование = ЗаказНаЭмиссиюТовары.Ссылка.ДокументОснование
	|		 И ДокументыСОшибкамиОформления.Организация  = ЗаказНаЭмиссиюТовары.Ссылка.Организация
	|		 И ДокументыСОшибкамиОформления.ВидПродукции = ЗаказНаЭмиссиюТовары.Ссылка.ВидПродукции
	|ГДЕ
	|	ЗаказНаЭмиссиюТовары.Ссылка.Проведен
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Пул.ЗаказНаЭмиссию,
	|	Пул.ЗаказНаЭмиссию.ДокументОснование,
	|	Пул.ЗаказНаЭмиссию.Организация,
	|	Пул.ЗаказНаЭмиссию.ВидПродукции,
	|	Пул.Номенклатура,
	|	Пул.Характеристика,
	|	Пул.Шаблон,
	|	Пул.СпособВводаВОборот,
	|	-1
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК Пул
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСОшибкамиОформления КАК ДокументыСОшибкамиОформления
	|		ПО ДокументыСОшибкамиОформления.ДокументОснование = Пул.ЗаказНаЭмиссию.ДокументОснование
	|		 И ДокументыСОшибкамиОформления.Организация  = Пул.ЗаказНаЭмиссию.Организация
	|		 И ДокументыСОшибкамиОформления.ВидПродукции = Пул.ЗаказНаЭмиссию.ВидПродукции
	|
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.ЗаказНаЭмиссию,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.ДокументОснование,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.Организация,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.ВидПродукции,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.Номенклатура,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.Характеристика,
	|	СУММА(ОжидаетсяПоДокументамСОшибкамиПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ ОжидаетсяПоДокументамСОшибкамиОформления
	|ИЗ
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно КАК ОжидаетсяПоДокументамСОшибкамиПредварительно
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВозможныйИсточникСвободныхКодов КАК ВозможныйИсточникСвободныхКодов
	|		ПО ВозможныйИсточникСвободныхКодов.Номенклатура = ОжидаетсяПоДокументамСОшибкамиПредварительно.Номенклатура
	|		И ВозможныйИсточникСвободныхКодов.Характеристика = ОжидаетсяПоДокументамСОшибкамиПредварительно.Характеристика
	|		И ВозможныйИсточникСвободныхКодов.Шаблон = ОжидаетсяПоДокументамСОшибкамиПредварительно.Шаблон
	|		И ВозможныйИсточникСвободныхКодов.СпособВводаВОборот = ОжидаетсяПоДокументамСОшибкамиПредварительно.СпособВводаВОборот
	|		И ВозможныйИсточникСвободныхКодов.ДокументОснование = ОжидаетсяПоДокументамСОшибкамиПредварительно.ДокументОснование
	|СГРУППИРОВАТЬ ПО
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.ЗаказНаЭмиссию,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.ДокументОснование,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.Организация,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.ВидПродукции,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.Номенклатура,
	|	ОжидаетсяПоДокументамСОшибкамиПредварительно.Характеристика
	|ИМЕЮЩИЕ
	|	СУММА(ОжидаетсяПоДокументамСОшибкамиПредварительно.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МаркированныеТоварыОснований.ДокументОснование КАК ДокументОснование,
	|	МаркированныеТоварыОснований.Организация       КАК Организация,
	|	МаркированныеТоварыОснований.ВидПродукции      КАК ВидПродукции,
	|	МаркированныеТоварыОснований.Номенклатура      КАК Номенклатура,
	|	МаркированныеТоварыОснований.Характеристика    КАК Характеристика,
	|	-МаркированныеТоварыОснований.Количество       КАК Количество
	|ПОМЕСТИТЬ ДоступноКРезрвированиюПоИзлишкамПредварительно
	|ИЗ
	|	МаркированныеТоварыОснований КАК МаркированныеТоварыОснований
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДокументыСОшибкамиОформления КАК ДокументыСОшибкамиОформления
	|		ПО ДокументыСОшибкамиОформления.ДокументОснование = МаркированныеТоварыОснований.ДокументОснование
	|		 И ДокументыСОшибкамиОформления.Организация  = МаркированныеТоварыОснований.Организация
	|		 И ДокументыСОшибкамиОформления.ВидПродукции = МаркированныеТоварыОснований.ВидПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Ожидается.ДокументОснование,
	|	Ожидается.Организация,
	|	Ожидается.ВидПродукции,
	|	Ожидается.Номенклатура,
	|	Ожидается.Характеристика,
	|	Ожидается.Количество
	|ИЗ
	|	ОжидаетсяПоДокументамСОшибкамиОформления КАК Ожидается
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВозможныйИсточникСвободныхКодов.ДокументОснование,
	|	ВозможныйИсточникСвободныхКодов.Организация,
	|	ВозможныйИсточникСвободныхКодов.ВидПродукции,
	|	ВозможныйИсточникСвободныхКодов.Номенклатура,
	|	ВозможныйИсточникСвободныхКодов.Характеристика,
	|	ВозможныйИсточникСвободныхКодов.Количество
	|ИЗ
	|	ВозможныйИсточникСвободныхКодов КАК ВозможныйИсточникСвободныхКодов
	|;
	|";
	
	Возврат Запрос;
	
КонецФункции

Функция ЗапросСостоянияКодовПоЗаказу(
	Заказ,
	УничтожитьТаблицыСвободныхКодов = Истина,
	ТаблицаНоменклатуры = Неопределено,
	МаркировкаОстатков = Неопределено)
	
	Если ТаблицаНоменклатуры = Неопределено Тогда
		ТаблицаНоменклатуры = НоваяТаблицаНоменклатуры();
	КонецЕсли;
	
	Запрос = ЗапросСвободныхКодовПоЗаказу(Заказ, ТаблицаНоменклатуры, МаркировкаОстатков);
	Запрос.Текст = Запрос.Текст + "
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ДокументОснование КАК ДокументОснование,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Организация       КАК Организация,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ВидПродукции      КАК ВидПродукции,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Номенклатура      КАК Номенклатура,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Характеристика    КАК Характеристика,
	|	СУММА(ДоступноКРезрвированиюПоИзлишкамПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ ДоступноКРезрвированию
	|ИЗ
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно КАК ДоступноКРезрвированиюПоИзлишкамПредварительно
	|СГРУППИРОВАТЬ ПО
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ДокументОснование,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Организация,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.ВидПродукции,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Номенклатура,
	|	ДоступноКРезрвированиюПоИзлишкамПредварительно.Характеристика
	|ИМЕЮЩИЕ
	|	СУММА(ДоступноКРезрвированиюПоИзлишкамПредварительно.Количество) > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	СвободныеКоды.ДокументОснование,
	|	СвободныеКоды.Организация,
	|	СвободныеКоды.ВидПродукции,
	|	СвободныеКоды.Номенклатура,
	|	СвободныеКоды.Характеристика,
	|	КОЛИЧЕСТВО(СвободныеКоды.КодМаркировки)
	|ИЗ
	|	СвободныеКоды КАК СвободныеКоды
	|СГРУППИРОВАТЬ ПО
	|	СвободныеКоды.ДокументОснование,
	|	СвободныеКоды.Организация,
	|	СвободныеКоды.ВидПродукции,
	|	СвободныеКоды.Номенклатура,
	|	СвободныеКоды.Характеристика
	|;
	|";
	
	Если УничтожитьТаблицыСвободныхКодов Тогда
		Запрос.Текст = Запрос.Текст + "
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ СвободныеКоды;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ВозможныеСвободныеКоды;
		|";
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + "
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВозможныйИсточникСвободныхКодов;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОжидаетсяПоДокументамСОшибкамиПредварительно;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ОжидаетсяПоДокументамСОшибкамиОформления;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаказНаЭмиссиюТовары.Ссылка              КАК ЗаказНаЭмиссию,
	|	ЗаказНаЭмиссиюТовары.Ссылка.Организация  КАК Организация,
	|	ЗаказНаЭмиссиюТовары.Ссылка.ВидПродукции КАК ВидПродукции,
	|	ЗаказНаЭмиссиюТовары.Номенклатура        КАК Номенклатура,
	|	ЗаказНаЭмиссиюТовары.Характеристика      КАК Характеристика,
	|	ЗаказНаЭмиссиюТовары.Количество          КАК Количество
	|ПОМЕСТИТЬ ОжидаетсяПоДокументуОснованиюПредварительно
	|ИЗ
	|	Документ.ЗаказНаЭмиссиюКодовМаркировкиСУЗ.Товары КАК ЗаказНаЭмиссиюТовары
	|ГДЕ
	|	ЗаказНаЭмиссиюТовары.Ссылка.Проведен
	|	И ЗаказНаЭмиссиюТовары.Ссылка.ДокументОснование = &Заказ
	|	
	|ОБЪЕДИНИТЬ ВСЕ
	|	
	|ВЫБРАТЬ
	|	Пул.ЗаказНаЭмиссию,
	|	Пул.ЗаказНаЭмиссию.Организация,
	|	Пул.ЗаказНаЭмиссию.ВидПродукции,
	|	Пул.Номенклатура,
	|	Пул.Характеристика,
	|	-1
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК Пул
	|ГДЕ
	|	Пул.ЗаказНаЭмиссию.ДокументОснование = &Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОжидаетсяПоДокументуОснованиюПредварительно.ЗаказНаЭмиссию    КАК ЗаказНаЭмиссию,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.Организация       КАК Организация,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.ВидПродукции      КАК ВидПродукции,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.Номенклатура      КАК Номенклатура,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.Характеристика    КАК Характеристика,
	|	СУММА(ОжидаетсяПоДокументуОснованиюПредварительно.Количество) КАК Количество
	|ПОМЕСТИТЬ Ожидается
	|ИЗ
	|	ОжидаетсяПоДокументуОснованиюПредварительно КАК ОжидаетсяПоДокументуОснованиюПредварительно
	|СГРУППИРОВАТЬ ПО
	|	ОжидаетсяПоДокументуОснованиюПредварительно.ЗаказНаЭмиссию,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.Организация,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.ВидПродукции,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.Номенклатура,
	|	ОжидаетсяПоДокументуОснованиюПредварительно.Характеристика
	|ИМЕЮЩИЕ
	|	СУММА(ОжидаетсяПоДокументуОснованиюПредварительно.Количество) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Пул.ЗаказНаЭмиссию.Организация  КАК Организация,
	|	Пул.ЗаказНаЭмиссию.ВидПродукции КАК ВидПродукции,
	|	Пул.Номенклатура                КАК Номенклатура,
	|	Пул.Характеристика              КАК Характеристика,
	|	0                               КАК КоличествоВЗаказе,
	|	0                               КАК КоличествоРезервировать,
	|	0                               КАК КоличествоЗаказано,
	|	СУММА(ВЫБОР КОГДА Пул.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1,1,1) ТОГДА 1 ИНАЧЕ 0 КОНЕЦ) КАК КоличествоПечатать,
	|	СУММА(ВЫБОР КОГДА Пул.ДатаПечатиУниверсальная = ДАТАВРЕМЯ(1,1,1) ТОГДА 0 ИНАЧЕ 1 КОНЕЦ) КАК КоличествоРаспечатано
	|ПОМЕСТИТЬ ОбщийРасчетПредварительно
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК Пул
	|ГДЕ
	|	Пул.ДокументОснование = &Заказ
	|СГРУППИРОВАТЬ ПО
	|	Пул.ЗаказНаЭмиссию.Организация,
	|	Пул.ЗаказНаЭмиссию.ВидПродукции,
	|	Пул.Номенклатура,
	|	Пул.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Товары.Организация,
	|	Товары.ВидПродукции,
	|	Товары.Номенклатура,
	|	Товары.Характеристика,
	|	Товары.Количество,
	|	0,
	|	0,
	|	0,
	|	0
	|ИЗ
	|	Товары КАК Товары
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Ожидается.Организация,
	|	Ожидается.ВидПродукции,
	|	Ожидается.Номенклатура,
	|	Ожидается.Характеристика,
	|	0,
	|	0,
	|	СУММА(Ожидается.Количество),
	|	0,
	|	0
	|ИЗ
	|	Ожидается КАК Ожидается
	|СГРУППИРОВАТЬ ПО
	|	Ожидается.Организация,
	|	Ожидается.ВидПродукции,
	|	Ожидается.Номенклатура,
	|	Ожидается.Характеристика
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДоступноКРезрвированию.Организация,
	|	ДоступноКРезрвированию.ВидПродукции,
	|	ДоступноКРезрвированию.Номенклатура,
	|	ДоступноКРезрвированию.Характеристика,
	|	0,
	|	СУММА(ДоступноКРезрвированию.Количество),
	|	0,
	|	0,
	|	0
	|ИЗ
	|	ДоступноКРезрвированию КАК ДоступноКРезрвированию
	|СГРУППИРОВАТЬ ПО
	|	ДоступноКРезрвированию.Организация,
	|	ДоступноКРезрвированию.ВидПродукции,
	|	ДоступноКРезрвированию.Номенклатура,
	|	ДоступноКРезрвированию.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбщийРасчет.ВидПродукции                   КАК ВидПродукции,
	|	ОбщийРасчет.Номенклатура                   КАК Номенклатура,
	|	ОбщийРасчет.Характеристика                 КАК Характеристика,
	|	СУММА(ОбщийРасчет.КоличествоВЗаказе)       КАК КоличествоВЗаказе,
	|	СУММА(ОбщийРасчет.КоличествоРезервировать) КАК КоличествоРезервировать,
	|	СУММА(ОбщийРасчет.КоличествоЗаказано)      КАК КоличествоЗаказано,
	|	СУММА(ОбщийРасчет.КоличествоПечатать)      КАК КоличествоПечатать,
	|	СУММА(ОбщийРасчет.КоличествоРаспечатано)   КАК КоличествоРаспечатано,
	|	СУММА(ОбщийРасчет.КоличествоВЗаказе)
	|		- СУММА(ОбщийРасчет.КоличествоЗаказано)
	|		- СУММА(ОбщийРасчет.КоличествоПечатать)
	|		- СУММА(ОбщийРасчет.КоличествоРаспечатано) КАК Распределить
	|ПОМЕСТИТЬ ОбщийРасчет
	|ИЗ
	|	ОбщийРасчетПредварительно КАК ОбщийРасчет
	|ГДЕ
	|	ОбщийРасчет.Организация = &Организация
	|СГРУППИРОВАТЬ ПО
	|	ОбщийРасчет.ВидПродукции,
	|	ОбщийРасчет.Номенклатура,
	|	ОбщийРасчет.Характеристика
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОбщийРасчет.ВидПродукции          КАК ВидПродукции,
	|	ОбщийРасчет.Номенклатура          КАК Номенклатура,
	|	ОбщийРасчет.Характеристика        КАК Характеристика,
	|	ОбщийРасчет.КоличествоВЗаказе     КАК КоличествоВЗаказе,
	|	ОбщийРасчет.КоличествоЗаказано    КАК КоличествоЗаказано,
	|	ОбщийРасчет.КоличествоПечатать    КАК КоличествоПечатать,
	|	ОбщийРасчет.КоличествоРаспечатано КАК КоличествоРаспечатано,
	|	ВЫБОР
	|		КОГДА ОбщийРасчет.Распределить < 0 ТОГДА 0
	|		КОГДА ОбщийРасчет.Распределить < ОбщийРасчет.КоличествоРезервировать ТОГДА ОбщийРасчет.Распределить
	|		ИНАЧЕ ОбщийРасчет.КоличествоРезервировать
	|	КОНЕЦ КАК КоличествоРезервировать,
	|	ВЫБОР
	|		КОГДА ОбщийРасчет.Распределить - ОбщийРасчет.КоличествоРезервировать < 0 ТОГДА 0
	|		ИНАЧЕ ОбщийРасчет.Распределить - ОбщийРасчет.КоличествоРезервировать
	|	КОНЕЦ КАК КоличествоЗаказать
	|ИЗ
	|	ОбщийРасчет КАК ОбщийРасчет
	|";
	
	Возврат Запрос;
	
КонецФункции

Процедура СформироватьТаблицуМаркированныхТоваровОснований(Запрос, ИмяВременнойТаблицыСКолонкойДокументОснование)
	
	СтандартнаяОбработка = Истина;
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Товары.Ссылка             КАК Ссылка,
	|	Товары.Организация        КАК Организация,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.ВидПродукции       КАК ВидПродукции,
	|	Товары.Шаблон             КАК Шаблон,
	|	Товары.СпособВводаВОборот КАК СпособВводаВОборот,
	|	Товары.Количество         КАК Количество
	|ПОМЕСТИТЬ
	|	ТаблицаНоменклатурыВременная
	|ИЗ
	|	&ТаблицаНоменклатуры КАК Товары
	|;
	|////////////////////////////////////////////////////////////////////////
	|
	|ВЫБРАТЬ
	|	Товары.Ссылка             КАК Ссылка,
	|	Товары.Организация        КАК Организация,
	|	Товары.Номенклатура       КАК Номенклатура,
	|	Товары.Характеристика     КАК Характеристика,
	|	Товары.ВидПродукции       КАК ВидПродукции,
	|	Товары.Шаблон             КАК Шаблон,
	|	Товары.СпособВводаВОборот КАК СпособВводаВОборот,
	|	Товары.Количество         КАК Количество
	|ПОМЕСТИТЬ
	|	ТоварыПредварительно
	|ИЗ
	|	ТаблицаНоменклатурыВременная КАК Товары
	|";
	
	ИнтеграцияИСМППереопределяемый.СформироватьТаблицуМаркированныхТоваровОснований(
		Запрос, ИмяВременнойТаблицыСКолонкойДокументОснование, СтандартнаяОбработка);
	
	Запрос.Текст = Запрос.Текст + "
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаНоменклатурыВременная
	|";
	
	Если СтандартнаяОбработка Тогда
		Запрос.Текст = Запрос.Текст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТоварыПредварительно.Ссылка             КАК ДокументОснование,
		|	ТоварыПредварительно.Организация        КАК Организация,
		|	ТоварыПредварительно.Номенклатура       КАК Номенклатура,
		|	ТоварыПредварительно.Характеристика     КАК Характеристика,
		|	ТоварыПредварительно.ВидПродукции       КАК ВидПродукции,
		|	ТоварыПредварительно.Шаблон             КАК Шаблон,
		|	ТоварыПредварительно.СпособВводаВОборот КАК СпособВводаВОборот,
		|	СУММА(ТоварыПредварительно.Количество)  КАК Количество
		|ПОМЕСТИТЬ МаркированныеТоварыОснований
		|ИЗ
		|	ТоварыПредварительно КАК ТоварыПредварительно
		|СГРУППИРОВАТЬ ПО
		|	ТоварыПредварительно.Ссылка,
		|	ТоварыПредварительно.Организация,
		|	ТоварыПредварительно.Номенклатура,
		|	ТоварыПредварительно.Характеристика,
		|	ТоварыПредварительно.ВидПродукции,
		|	ТоварыПредварительно.Шаблон,
		|	ТоварыПредварительно.СпособВводаВОборот
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТоварыПредварительно
		|";
	Иначе
		Запрос.Текст = Запрос.Текст + "
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ТоварыПредварительно
		|";
	КонецЕсли;
	
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область РезервированиеКодовМаркировки

// Подготавливает таблицу для передачи в проецедуру резервирования
// (См. РегистрСведений.ПулКодовМаркировкиСУЗ.ЗарезервироватьКодыПодЗаказ)
// 
// Параметры:
// 	Параметры - Неопределено, Структура - Описание:
// * ТаблицаНоменклатуры - ТаблицаЗначений - Исходная таблица, которой необходимо инициализировать итоговую таблицу
// * Документ  - ДокументСсылка - Документ, по которому необходимо получить существующие записи из пула
// Возвращаемое значение:
// 	ТаблицаЗначений - Таблица номенклатуры для расчета резервирования
Функция НоваяТаблицаНоменклатуры(Параметры = Неопределено)
	
	ПереданаТаблицаНоменклатуры = Параметры <> Неопределено И Параметры.Свойство("ТаблицаНоменклатуры");
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Пул.ДокументОснование  КАК Ссылка,
	|	Пул.Организация        КАК Организация,
	|	Пул.Номенклатура       КАК Номенклатура,
	|	Пул.Характеристика     КАК Характеристика,
	|	Пул.ВидПродукции       КАК ВидПродукции,
	|	Пул.Шаблон             КАК Шаблон,
	|	Пул.СпособВводаВОборот КАК СпособВводаВОборот,
	|	1                      КАК Количество
	|ИЗ
	|	РегистрСведений.ПулКодовМаркировкиСУЗ КАК Пул
	|ГДЕ
	|	&ВыбратьДанныеПула
	|	И Пул.ДокументОснование = &Документ";
	
	Если Параметры = Неопределено Тогда
		Запрос.УстановитьПараметр("Документ", Неопределено);
	Иначе
		Запрос.УстановитьПараметр("Документ", Параметры.Документ);
	КонецЕсли;
		
	Запрос.УстановитьПараметр("ВыбратьДанныеПула", ПереданаТаблицаНоменклатуры);
	ТаблицаНоменклатуры = Запрос.Выполнить().Выгрузить();
	
	Если ПереданаТаблицаНоменклатуры Тогда
		
		Для Каждого СтрокаТаблицы Из Параметры.ТаблицаНоменклатуры Цикл
			НоваяСтрока = ТаблицаНоменклатуры.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			НоваяСтрока.Ссылка = Параметры.Документ;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ТаблицаНоменклатуры;
	
КонецФункции

// Добавляет в соответствие с ключем документа массив кодов маркировки ,в случае значения Неопределено - в качестве 
// ключа используется текстовое значение "Неопределено".
// 
// Параметры:
// 	ВходноеЗначение - ВыборкаИзРезультатаЗапроса, Структура - сущетствующая структура данных с полями КодМаркировки и
// 	ДокументОснование.
// 	СоответствиеКодовОснованиям - Соответствие - Выходной параметр соответствие, в качестве ключа используется значение
// ДокументОснование, в качестве значения - массив кодов маркировки.
Процедура ДобавитьКодВСоответствиеКодовОснованиям(ВходноеЗначение, СоответствиеКодовОснованиям)
	
	КлючСоответствия = ВходноеЗначение.ДокументОснование;

	Если КлючСоответствия = Неопределено Тогда
		КлючСоответствия = "Неопределено";
	КонецЕсли;

	МассивКодов = СоответствиеКодовОснованиям[КлючСоответствия];
	
	Если МассивКодов = Неопределено Тогда
		МассивКодов = Новый Массив();
	КонецЕсли;
	
	МассивКодов.Добавить(ВходноеЗначение.КодМаркировки);
	СоответствиеКодовОснованиям.Вставить(КлючСоответствия, МассивКодов);
	
КонецПроцедуры

// Возвращает значение документа основания из ключа соответствия. Используется для пребразования значения
// "Неопределено" в ключе соответствия в значение Неопределено.
// 
// Параметры:
// 	КлючИЗначение - КлючИЗначение - Описание
// Возвращаемое значение:
// 	Произвольный, Неопределено - Описание
Функция ДокументОснованиеИзСоответствияКодам(КлючИЗначение)

	ДокументОснование = КлючИЗначение.Ключ;

	Если ДокументОснование = "Неопределено" Тогда
		ДокументОснование = Неопределено;
	КонецЕсли;

	Возврат ДокументОснование;

КонецФункции

#КонецОбласти

#КонецОбласти

#КонецЕсли