
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Вызов механизма защиты.
	бит_РаботаСДиалогамиСервер.ФормаЗаписиРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОбъектСистемыНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СписокВидовОбъектов = Новый СписокЗначений;
	СписокВидовОбъектов.Добавить(ПредопределенноеЗначение("Перечисление.бит_ВидыОбъектовСистемы.Документ"));
	Если ЭтоСемействоERP() Тогда
		СписокВидовОбъектов.Добавить(ПредопределенноеЗначение("Перечисление.бит_ВидыОбъектовСистемы.Справочник"));
	КонецЕсли;
	
	ДоступныеОбъектыСистемы = ДоступныеОбъектыСистемы();
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидыОбъектов",								СписокВидовОбъектов);
	ПараметрыФормы.Вставить("ТекущийОбъектСистемы",						Запись.ОбъектСистемы);
	ПараметрыФормы.Вставить("ДоступныеОбъектыСистемы",					ДоступныеОбъектыСистемы);
	ОткрытьФорму("ОбщаяФорма.бит_ФормаВыбораОбъектовСистемыУправляемая",ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбъектСистемыАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ПодобратьОбъектСистемыПоТексту(Текст, ДанныеВыбора);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбъектСистемыОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		ПодобратьОбъектСистемыПоТексту(Текст, ДанныеВыбора);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВидАлгоритмаПриИзменении(Элемент)
	
	Если Запись.ВидАлгоритма <> ПредопределенноеЗначение("Перечисление.бит_уп_ВидыАлгоритмов.Визирование") Тогда
		Запись.ПользовательскоеУсловие = ПредопределенноеЗначение("Справочник.бит_ПользовательскиеУсловия.ПустаяСсылка");
	КонецЕсли; 
	УстановитьДоступность();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура выполнения комадны "ОткрытьНастройкуПериода".
// 
&НаКлиенте
Процедура НастроитьПериод(Команда)
	
	Диалог						= Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период.ДатаНачала    = Запись.Период;
	Диалог.Период.ДатаОкончания = Запись.ДатаОкончания;
	
	Оповещение = Новый ОписаниеОповещения("НастроитьПериодЗавершение", ЭтотОбъект);
	Диалог.Показать(Оповещение);
	
КонецПроцедуры

// Процедура обработчик оповещения "НастроитьПериодЗавершение".
// 
&НаКлиенте
Процедура НастроитьПериодЗавершение(Период, ДополнительныеПараметры) Экспорт
	
	Если Период <> Неопределено Тогда
		  Запись.Период			= Период.ДатаНачала;
		  Запись.ДатаОкончания	= Период.ДатаОкончания;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость элементов формы.
// 
&НаКлиенте
Процедура УстановитьДоступность()
	
	Если Запись.ВидАлгоритма = ПредопределенноеЗначение("Перечисление.бит_уп_ВидыАлгоритмов.Визирование") Тогда
		Элементы.ПользовательскоеУсловие.Доступность = Истина;
	Иначе
		Элементы.ПользовательскоеУсловие.Доступность = Ложь;
	КонецЕсли; 
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ЭтоСемействоERP()
	
	Возврат бит_ОбщегоНазначения.ЭтоСемействоERP();
	
КонецФункции

&НаКлиенте 
Процедура ПодобратьОбъектСистемыПоТексту(Текст, ДанныеВыбора)
	
	СписокВизируемыхОбъектов = ДоступныеОбъектыСистемы();
	ДанныеВыбора = Новый СписокЗначений;
	Для каждого ЭлементСписка Из СписокВизируемыхОбъектов Цикл
		Если СтрНачинаетсяС(ВРег(ЭлементСписка.Значение), ВРег(Текст)) Тогда
			ДанныеВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеВыбора.Количество() = 1 Тогда
		Запись.ОбъектСистемы = ДанныеВыбора[0].Значение;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста 
Функция ДоступныеОбъектыСистемы()
	
	СписокОбъектовСистемы = Новый СписокЗначений;
	ОбъектыСистемы = Новый Массив;
	
	ТипыОбъектов = Метаданные.РегистрыСведений.бит_УстановленныеВизы.Измерения.Объект.Тип.Типы();
	Для каждого ТекущийТип Из ТипыОбъектов Цикл
		МетаОбъект = Метаданные.НайтиПоТипу(ТекущийТип);
		Если МетаОбъект = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		Если СтрЧислоВхождений(МетаОбъект.Имя, "удалить") <> 0 Тогда
			Продолжить;	
		КонецЕсли; 
		
		ОбъектСистемы = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(МетаОбъект);
		Если ЗначениеЗаполнено(ОбъектСистемы) Тогда  
			ОбъектыСистемы.Добавить(ОбъектСистемы);			
		КонецЕсли; 
	КонецЦикла;
	
	Запрос = Новый Запрос();
	ВидыОбъектов = Новый Массив;
	ВидыОбъектов.Добавить(Перечисления.бит_ВидыОбъектовСистемы.Документ);
	Если ЭтоСемействоERP() Тогда
		ВидыОбъектов.Добавить(Перечисления.бит_ВидыОбъектовСистемы.Справочник);
	КонецЕсли;
	Запрос.УстановитьПараметр("ВидОбъекта", ВидыОбъектов);
	Запрос.УстановитьПараметр("ОбъектыСистемы", ОбъектыСистемы);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	бит_ОбъектыСистемы.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.бит_ОбъектыСистемы КАК бит_ОбъектыСистемы
	|ГДЕ
	|	бит_ОбъектыСистемы.Ссылка В(&ОбъектыСистемы)
	|	И бит_ОбъектыСистемы.ВидОбъекта В(&ВидОбъекта)";
	
	Результат = Запрос.Выполнить();
	
	Если НЕ Результат.Пустой() Тогда
		СписокОбъектовСистемы.ЗагрузитьЗначения(Результат.Выгрузить().ВыгрузитьКолонку("Ссылка"));	
	КонецЕсли;
	
	Возврат СписокОбъектовСистемы;
	
КонецФункции

#КонецОбласти
