#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Механизм является аналогом методов платформы СохранитьЗначение(), ПолучитьЗначение()
// Хранение в регистре, а не в системных таблицах, является более удобным, 
// т.к. обеспечивает возможность редактирования/администрирования сохраненных параметров из других сеансов.

#Область ПрограммныйИнтерфейс

// Функция сохраняет значение для пользователя и объекта системы.
//
// Параметры:
//  Пользователь		 - СправочникСсылка.Пользователи - Пользователи.
//  ОбъектСистемы		 - СправочникСсылка.бит_ОбъектыСистемы	 - объект системы.
//  КлючЗначения		 - Строка - ключ значения.
//  СохраняемоеЗначение	 - Произвольный	 - сохраняемое значение.
// 
// Возвращаемое значение:
//  флДействиеВыполнено - Булево.
//
Функция СохранитьЗнч(Пользователь, ОбъектСистемы, КлючЗначения, СохраняемоеЗначение) Экспорт

	ТекОбъектСистемы = ПолучитьОбъектСистемы(ОбъектСистемы);
	
	МенеджерЗаписи = РегистрыСведений.бит_СохраненныеЗначения.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Пользователь      = Пользователь;
	МенеджерЗаписи.ОбъектСистемы     = ТекОбъектСистемы;
	МенеджерЗаписи.КлючЗначения      = КлючЗначения;
	МенеджерЗаписи.ХранилищеЗначения = Новый ХранилищеЗначения(СохраняемоеЗначение);
	
	Попытка
	
		МенеджерЗаписи.Записать();
		флДействиеВыполнено = Истина;
	
	Исключение
		
		флДействиеВыполнено = Ложь;
		
	КонецПопытки; 
	

	Возврат флДействиеВыполнено;
	
КонецФункции // СохранитьЗначения()

// Функция получает сохраненное значение.
//
// Параметры:
//  Пользователь		 - СправочникСсылка.Пользователи - пользователи.
//  СтруктураПараметров	 - Структура - структура.
// 
// Возвращаемое значение:
//  РезЗначение - Произвольный.
//
Функция ПолучитьЗнч(Пользователь, СтруктураПараметров) Экспорт

	Результат = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_СохраненныеЗначения.ХранилищеЗначения
	               |ИЗ
	               |	РегистрСведений.бит_СохраненныеЗначения КАК бит_СохраненныеЗначения
	               |ГДЕ
	               |	бит_СохраненныеЗначения.Пользователь = &Пользователь";
				   
	Запрос.УстановитьПараметр("Пользователь", Пользователь);
    
    Если СтруктураПараметров.Свойство("ОбъектСистемы") Тогда
		
	    ТекОбъектСистемы = ПолучитьОбъектСистемы(СтруктураПараметров.ОбъектСистемы);		
		Запрос.УстановитьПараметр("ОбъектСистемы", ТекОбъектСистемы);
		Запрос.Текст = Запрос.Текст + " И бит_СохраненныеЗначения.ОбъектСистемы = &ОбъектСистемы";
		
    КонецЕсли; 
    
    Если СтруктураПараметров.Свойство("КлючЗначения") Тогда
	
		Запрос.УстановитьПараметр("КлючЗначения", СтруктураПараметров.КлючЗначения);
		Запрос.Текст = Запрос.Текст + " И бит_СохраненныеЗначения.КлючЗначения = &КлючЗначения"; 
	
	КонецЕсли; 
	
	РезЗначение = Неопределено;
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
	
		Хран = Выборка.ХранилищеЗначения;
		РезЗначение = Хран.Получить();
	
	КонецЕсли; 
	
	Возврат РезЗначение;
	
КонецФункции // ПолучитьЗначения()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция получает объект системы по строке, ОбъектуМетаданных.
//  Если передается ссылка на справочник она же и возвращается.
//
// Параметры:
//  ОбъектСистемы	 - 	Строка,ОбъектМетаданных,СправочникСсылка.бит_ОбъектыСистемы - объект системы.
// 
// Возвращаемое значение:
//  ТекОбъектСистемы - СправочникСсылка.бит_ОбъектыСистемы.
//
Функция ПолучитьОбъектСистемы(ОбъектСистемы)

	Если ТипЗнч(ОбъектСистемы) = Тип("ОбъектМетаданных") Тогда
	     ТекОбъектСистемы = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(ОбъектСистемы);
	 ИначеЕсли ТипЗнч(ОбъектСистемы) = Тип("Строка") Тогда
		 ТекОбъектСистемы = Справочники.бит_ОбъектыСистемы.НайтиПоРеквизиту("ИмяОбъектаПолное", ОбъектСистемы);
	Иначе	
	     ТекОбъектСистемы = ОбъектСистемы;
	КонецЕсли; 

	Возврат ТекОбъектСистемы;
	
КонецФункции // ПолучитьОбъектСистемы()

#КонецОбласти

#КонецЕсли
