#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда		
		Возврат;		
	КонецЕсли; 
	
	// Проверка на пересчечение интервалов контроля.
	
	МассивСценариев = ЭтотОбъект.ВыгрузитьКолонку("Сценарий");
	
	МассивСценариев = бит_РаботаСКоллекциями.УдалитьПовторяющиесяЭлементыМассива(МассивСценариев);
	
	Для каждого Запись Из ЭтотОбъект Цикл
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	бит_НастройкаКонтроляПоСценариям.Период,
		|	бит_НастройкаКонтроляПоСценариям.ДатаОкончания
		|ИЗ
		|	РегистрСведений.бит_НастройкаКонтроляПоСценариям КАК бит_НастройкаКонтроляПоСценариям
		|ГДЕ
		|	бит_НастройкаКонтроляПоСценариям.Сценарий = &Сценарий";
		
		Запрос.УстановитьПараметр("Сценарий", Запись.Сценарий);
		
		Результат = Запрос.Выполнить();
		ТаблицаДат = Результат.Выгрузить();
		
		НоваяСтрока = ТаблицаДат.Добавить();
		НоваяСтрока.Период = Запись.Период;
		НоваяСтрока.ДатаОкончания = Запись.ДатаОкончания;
		
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ
		|	ТаблицаДат.Период,
		|	ТаблицаДат.ДатаОкончания
		|ПОМЕСТИТЬ ПериодыКонтроля
		|ИЗ
		|	&ТаблицаДат КАК ТаблицаДат
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПериодыКонтроля.Период,
		|	ПериодыКонтроля.ДатаОкончания,
		|	ПериодыКонтроляКопия.Период КАК ПериодКопия,
		|	ПериодыКонтроляКопия.ДатаОкончания КАК ДатаОкончанияКопия
		|ИЗ
		|	ПериодыКонтроля КАК ПериодыКонтроля
		|		ЛЕВОЕ СОЕДИНЕНИЕ ПериодыКонтроля КАК ПериодыКонтроляКопия
		|		ПО ПериодыКонтроля.Период <> ПериодыКонтроляКопия.Период
		|			И (ПериодыКонтроля.ДатаОкончания >= ПериодыКонтроляКопия.Период
		|					И ПериодыКонтроля.Период <= ПериодыКонтроляКопия.Период
		|					И ПериодыКонтроля.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ПериодыКонтроля.Период <= ПериодыКонтроляКопия.Период
		|					И ПериодыКонтроля.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ПериодыКонтроля.ДатаОкончания >= ПериодыКонтроляКопия.ДатаОкончания
		|					И ПериодыКонтроля.Период <= ПериодыКонтроляКопия.ДатаОкончания
		|					И ПериодыКонтроля.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
		|					И ПериодыКонтроляКопия.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
		|				ИЛИ ПериодыКонтроля.Период <= ПериодыКонтроляКопия.ДатаОкончания
		|					И ПериодыКонтроля.ДатаОкончания = ДАТАВРЕМЯ(1, 1, 1)
		|					И ПериодыКонтроляКопия.ДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1))
		|ГДЕ
		|	(НЕ ПериодыКонтроляКопия.Период ЕСТЬ NULL )
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|УНИЧТОЖИТЬ ПериодыКонтроля";
		
		Запрос.УстановитьПараметр("Сценарий", Запись.Сценарий);
		Запрос.УстановитьПараметр("ТаблицаДат", ТаблицаДат);
		
		Результат = Запрос.Выполнить();
		
		Заголовок = "Проверка интервалов контроля сценария """ + Запись.Сценарий + """";
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			ТекстСообщения = "Обнаружено пересечение интервалов контроля ["
			+Формат(Выборка.Период,"ДФ=dd.MM.yyyy; ДП=∞")
			+","
			+Формат(Выборка.ДатаОкончания,"ДФ=dd.MM.yyyy; ДП=∞")
			+"] и ["
			+Формат(Выборка.ПериодКопия,"ДФ=dd.MM.yyyy; ДП=∞")
			+","
			+Формат(Выборка.ДатаОкончанияКопия,"ДФ=dd.MM.yyyy; ДП=∞")
			+"]";
			
			бит_ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения, Отказ, Заголовок, СтатусСообщения.Важное);							 
			
		КонецЦикла; 
		
	КонецЦикла; 	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
