#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 
	
	ПроверитьПравоНаУстановкуИзмененВЗакрытомПериоде(Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПравоНаУстановкуИзмененВЗакрытомПериоде(Отказ)

	 Запрос = Новый Запрос;
	 Запрос.Текст = 
	 "ВЫБРАТЬ
	 |	НаборЗаписей.Объект,
	 |	НаборЗаписей.Аналитика,
	 |	НаборЗаписей.ЗначениеАналитики
	 |ПОМЕСТИТЬ НаборЗаписей
	 |ИЗ
	 |	&НаборЗаписей КАК НаборЗаписей
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	Объединение.ЕстьИзменения
	 |ИЗ
	 |	(ВЫБРАТЬ
	 |		ИСТИНА КАК ЕстьИзменения
	 |	ИЗ
	 |		НаборЗаписей КАК НаборЗаписей
	 |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ДополнительныеАналитики КАК бит_ДополнительныеАналитики
	 |			ПО НаборЗаписей.Объект = бит_ДополнительныеАналитики.Объект
	 |				И НаборЗаписей.Аналитика = бит_ДополнительныеАналитики.Аналитика
	 |	ГДЕ
	 |		НаборЗаписей.Аналитика = &Аналитика
	 |		И НаборЗаписей.Объект = &Объект
	 |		И НЕ НаборЗаписей.ЗначениеАналитики = ЕСТЬNULL(бит_ДополнительныеАналитики.ЗначениеАналитики, НЕОПРЕДЕЛЕНО)
	 |	
	 |	ОБЪЕДИНИТЬ ВСЕ
	 |	
	 |	ВЫБРАТЬ
	 |		ИСТИНА
	 |	ИЗ
	 |		РегистрСведений.бит_ДополнительныеАналитики КАК бит_ДополнительныеАналитики
	 |			ЛЕВОЕ СОЕДИНЕНИЕ НаборЗаписей КАК НаборЗаписей
	 |			ПО (НаборЗаписей.Объект = бит_ДополнительныеАналитики.Объект)
	 |				И (НаборЗаписей.Аналитика = бит_ДополнительныеАналитики.Аналитика)
	 |	ГДЕ
	 |		бит_ДополнительныеАналитики.Аналитика = &Аналитика
	 |		И бит_ДополнительныеАналитики.Объект = &Объект
	 |		И НЕ ЕСТЬNULL(НаборЗаписей.ЗначениеАналитики, НЕОПРЕДЕЛЕНО) = бит_ДополнительныеАналитики.ЗначениеАналитики
	 |		И НаборЗаписей.Аналитика ЕСТЬ NULL ) КАК Объединение
	 |ГДЕ
	 |	Объединение.ЕстьИзменения";

	 Запрос.УстановитьПараметр("Аналитика",    ПланыВидовХарактеристик.бит_ВидыДополнительныхАналитик.ОтразитьВЗакрытомПериоде);
	 Запрос.УстановитьПараметр("Объект",       ЭтотОбъект.Отбор.Объект.Значение);
	 Запрос.УстановитьПараметр("НаборЗаписей", ЭтотОбъект);

	 РезультатЗапроса = Запрос.Выполнить();
	 ЕстьИзменения 	  = НЕ РезультатЗапроса.Пустой();
	 РольДоступна 	  = бит_ПраваДоступа.ПолучитьЗначениеДопПраваПользователя(
	 					бит_ОбщиеПеременныеСервер.ЗначениеПеременной("ТекущийПользователь"),
	 					ПланыВидовХарактеристик.бит_ДополнительныеПраваПользователей.РазрешеноОтражениеДокументовВЗакрытомПериоде);
	 
	 Если НЕ РольДоступна И ЕстьИзменения Тогда
		 ТекстСообщения = Нстр("ru = 'Нарушение права доступа к дополнительной аналитике ""Изменен в закрытом периоде""'"); 
		 ЗаписьЖурналаРегистрации("НарушениеПраваДоступа", 
		 УровеньЖурналаРегистрации.Предупреждение, Метаданные(), 
		 ЭтотОбъект, ТекстСообщения);
		 
		 ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , , Отказ); 
		 
	 КонецЕсли; 
	 
КонецПроцедуры

#КонецОбласти

#КонецЕсли