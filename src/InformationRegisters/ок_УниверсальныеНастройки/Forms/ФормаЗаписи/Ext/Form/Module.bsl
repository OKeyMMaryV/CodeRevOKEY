// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-07-11 (#4276) 
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокНастроек.ТипЗначения = Метаданные.РегистрыСведений.ок_УниверсальныеНастройки.Ресурсы.ЗначениеНастройки.Тип;
	
	УправлениеЭлементамиФормы();

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ТекущийОбъект.ЗначениеВХранилище Тогда
		СписокНастроек = ТекущийОбъект.ХранилищеЗначения.Получить()
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ЗначениеВХранилище Тогда
		ТекущийОбъект.ХранилищеЗначения = Новый ХранилищеЗначения(СписокНастроек);
		ТекущийОбъект.ЗначениеНастройки = Неопределено;
		ТекущийОбъект.ПредставлениеСписка = Строка(СписокНастроек);
	Иначе
		ТекущийОбъект.ХранилищеЗначения = Неопределено;
		ТекущийОбъект.ПредставлениеСписка = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЗначениеВХранилищеПриИзменении(Элемент)
	
	Если Запись.ЗначениеВХранилище И ЗначениеЗаполнено(Запись.ЗначениеНастройки) Тогда
		
		СписокНастроек.Добавить(Запись.ЗначениеНастройки);
		Запись.ЗначениеНастройки = Неопределено;
		Модифицированность = Истина;
		
		УправлениеЭлементамиФормы();
		
	ИначеЕсли Не Запись.ЗначениеВХранилище И СписокНастроек.Количество() > 0 Тогда
		
		СтруктураПараметров =  Новый Структура("ОчиститьСписокНастроек");
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗначениеВХранилищеПриИзмененииЗавершение", ЭтотОбъект, СтруктураПараметров),
						НСтр("ru = 'Список настроек будет очищен, продолжить?'"),
						РежимДиалогаВопрос.ДаНет);
						
	Иначе
		УправлениеЭлементамиФормы();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеВХранилищеПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если ДополнительныеПараметры.Свойство("ОчиститьСписокНастроек") Тогда
			
			СписокНастроек.Очистить();
			Модифицированность = Истина;
			
		КонецЕсли;
		
		УправлениеЭлементамиФормы();
		
	Иначе
		Запись.ЗначениеВХранилище = Не Запись.ЗначениеВХранилище;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.СписокНастроек.Видимость = Запись.ЗначениеВХранилище;
	Элементы.ЗначениеНастройки.Видимость = Не Элементы.СписокНастроек.Видимость;
	
КонецПроцедуры

#КонецОбласти
// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-07-11 (#4276)