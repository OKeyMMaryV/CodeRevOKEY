
#Область ОписаниеПеременных

&НаКлиенте
Перем мПолеКомпановкиСтатьяОборотов; // Хранит параметр отбора по статье.

&НаКлиенте
Перем мПолеКомпановкиЦФО; // Хранит параметр отбора по ЦФО.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	бит_РаботаСДиалогамиСервер.ФормаСпискаРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
		
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Запомним параметр отбора по статье
	ПолеКомпановкиСтатьяОборотов = Новый ПолеКомпоновкиДанных("СтатьяОборотов");
	ПолеКомпановкиЦФО 			 = Новый ПолеКомпоновкиДанных("ЦФО");
	
	Для каждого Элемент Из Список.Отбор.Элементы Цикл

		Если Элемент.ЛевоеЗначение = ПолеКомпановкиСтатьяОборотов Тогда
			мПолеКомпановкиСтатьяОборотов = Элемент.ПравоеЗначение;
			Прервать;
		ИначеЕсли Элемент.ЛевоеЗначение = ПолеКомпановкиЦФО Тогда
			мПолеКомпановкиЦФО		      = Элемент.ПравоеЗначение;
			Элементы.СписокПодборЦФО.Видимость = Ложь;
			Прервать;
	  	КонецЕсли; 
		
	КонецЦикла; 

КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Не Копирование Тогда
		
		Отказ = Истина;

		ПараметрыФормы = Новый Структура;
		Если мПолеКомпановкиСтатьяОборотов <> Неопределено Тогда
			ПараметрыФормы.Вставить("СтатьяОборотов", мПолеКомпановкиСтатьяОборотов);
		ИначеЕсли мПолеКомпановкиЦФО <> Неопределено Тогда
			ПараметрыФормы.Вставить("ЦФО", мПолеКомпановкиЦФО);		
		КонецЕсли; 
 						
		ОткрытьФорму("РегистрСведений.бит_ПринадлежностьСтатейОборотов.ФормаЗаписи", ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
		
КонецПроцедуры // СписокПередНачаломДобавления()

&НаКлиенте
Процедура СписокОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если мПолеКомпановкиСтатьяОборотов <> Неопределено Тогда
		ПолеКомпановки =  мПолеКомпановкиСтатьяОборотов;
		ВидОтбора = "СтатьяОборотов";
	Иначе
		ПолеКомпановки = мПолеКомпановкиЦФО;
		ВидОтбора = "ЦФО";
	КонецЕсли;
	
	// Произведем запись в регистр сведений бит_ПринадлежностиСтатейОборотов.
	СоздатьПринадлежность(ВыбранноеЗначение, ПолеКомпановки);
	
	// Обновим отображение. 
	УстановитьОтбор(ПолеКомпановки, ВидОтбора);
	
КонецПроцедуры // СписокОбработкаВыбора()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаРазблокировать(Команда)
    
	КомандаРазблокироватьНаСервере();
    
КонецПроцедуры

&НаКлиенте
Процедура ПодборЦФО(Команда)
		 	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыФормы.Вставить("РежимВыбора"       , Истина);
	ОткрытьФорму("Справочник.Подразделения.ФормаВыбора", ПараметрыФормы, Элементы.Список);
	
КонецПроцедуры // ПодборЦФО

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
// Процедура устанавливает отбор в форме.
// 
&НаКлиенте
Процедура УстановитьОтбор(ПолеКомпановки, ВидОтбора)

	// Установим отбор по статье оборотов.
	бит_ОбщегоНазначенияКлиентСервер.УстановитьОтборУСписка(Список.Отбор 
												, Новый ПолеКомпоновкиДанных(ВидОтбора) 
												, ПолеКомпановки);

КонецПроцедуры // УстановитьОтбор()

&НаСервереБезКонтекста 
Процедура СоздатьПринадлежность(ЦФО, СтатьяОборота)

    МенеджерЗаписи = РегистрыСведений.бит_ПринадлежностьСтатейОборотов.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ЦФО 			  = ЦФО;
	МенеджерЗаписи.СтатьяОборотов = СтатьяОборота;

	МенеджерЗаписи.Записать();
	
КонецПроцедуры // СоздатьПринадлежность()

&НаСервере
Процедура КомандаРазблокироватьНаСервере()
	
	Элементы.Список.ТолькоПросмотр      = Ложь;
	Элементы.Список.Доступность         = Истина;
	Элементы.Список.ИзменятьСоставСтрок = Истина;
	
КонецПроцедуры

#КонецОбласти

