#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция получает значение Статьи оборотов.
// 
Функция ПолучитьПравуюАналитику(ВидСоответствия, ЛеваяАналитика_1) Экспорт 
    
    ПраваяАналитика_1 = Неопределено;
    
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидСоответствия" , ВидСоответствия);
	Запрос.УстановитьПараметр("ЛеваяАналитика_1", ЛеваяАналитика_1);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_СоответствияАналитик.ПраваяАналитика_1
	               |ИЗ
	               |	РегистрСведений.бит_СоответствияАналитик КАК бит_СоответствияАналитик
	               |ГДЕ
	               |	бит_СоответствияАналитик.ВидСоответствия = &ВидСоответствия
	               |	И бит_СоответствияАналитик.ЛеваяАналитика_1 = &ЛеваяАналитика_1";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		ПраваяАналитика_1 = Выборка.ПраваяАналитика_1;
	
	КонецЦикла;
	
    Возврат ПраваяАналитика_1;
	
КонецФункции // ПолучитьСтатьюОборотов()

// Функция получает значение Статьи ДДС по соответствию Статей оборотов.
// 
Функция ПолучитьЛевуюАналитику(ВидСоответствия, ПраваяАналитика_1) Экспорт 
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидСоответствия" , ВидСоответствия);
	Запрос.УстановитьПараметр("ПраваяАналитика_1", ПраваяАналитика_1);
	
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_СоответствияАналитик.ЛеваяАналитика_1
	               |ИЗ
	               |	РегистрСведений.бит_СоответствияАналитик КАК бит_СоответствияАналитик
	               |ГДЕ
	               |	бит_СоответствияАналитик.ВидСоответствия = &ВидСоответствия
	               |	И бит_СоответствияАналитик.ПраваяАналитика_1 = &ПраваяАналитика_1";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	МассивЛевыхАналитик = Новый Массив;
	Пока Выборка.Следующий() Цикл
	
		ЛеваяАналитика_1 = Выборка.ЛеваяАналитика_1;
		МассивЛевыхАналитик.Добавить(ЛеваяАналитика_1);
	
	КонецЦикла;
	
    Возврат МассивЛевыхАналитик;
	
КонецФункции // ПолучитьСтатьюДДС()

// Функция получает аналитики по соответствиям.
//
// Параметры:
//   вхВидСоответствия - Строка, СправочникСсылка.бит_ВидыСоответствийАналитик
//   СтронаОтбор - Строка - "Левая","Правая"
//   СтрОтбор - Структура - Ключ:"Аналитика_<N>",Значение: ЗначениеАналитики.
//
// Возвращаемое значение:
//  МассивАналитик - Массив.
//
Функция ПолучитьАналитикуПоСоответствию(вхВидСоответствия, СторонаОтбор = "Левая", СтрОтбор) Экспорт

	КоличествоАналитик = Справочники.бит_ВидыСоответствийАналитик.МаксКоличествоАналитик();
	МассивАналитик = Новый Массив;
	
	Если ТипЗнч(вхВидСоответствия) = Тип("Строка") Тогда		
		ВидСоответствия = Справочники.бит_ВидыСоответствийАналитик.НайтиПоКоду(вхВидСоответствия);		
	Иначе			
		ВидСоответствия = вхВидСоответствия;		
	КонецЕсли; 
	
	СторонаРезультат = ?(СторонаОтбор = "Левая", "Правая", "Левая");
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ВидСоответствия", ВидСоответствия);
	Запрос.Текст = "ВЫБРАТЬ *
	               |ИЗ
	               |	РегистрСведений.бит_СоответствияАналитик КАК бит_СоответствияАналитик
	               |ГДЕ
	               |	бит_СоответствияАналитик.ВидСоответствия = &ВидСоответствия";
	
				   
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	
	Для каждого КиЗ Из СтрОтбор Цикл
	
		ИмяПараметра = СторонаОтбор+КиЗ.Ключ;
		Запрос.УстановитьПараметр(ИмяПараметра, КиЗ.Значение);
		
		ВыражениеОтбор = "бит_СоответствияАналитик.%ИмяПараметра% = &%ИмяПараметра%";
		ВыражениеОтбор = СтрЗаменить(ВыражениеОтбор, "%ИмяПараметра%", ИмяПараметра);
        СхемаЗапроса.ПакетЗапросов[0].Операторы[0].Отбор.Добавить(ВыражениеОтбор);
		
	КонецЦикла; 
	
	Запрос.Текст = СхемаЗапроса.ПолучитьТекстЗапроса();	
	
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
	
		 СтрАналитика = Новый Структура;
		 
		 Для Н = 1 По КоличествоАналитик Цикл
			 
			 ИмяАналитики = "Аналитика_" + Н;
		 	 ИмяПоля = СторонаРезультат + ИмяАналитики;
			 
			 СтрАналитика.Вставить(ИмяАналитики, Выборка[ИмяПоля]);
		 
		 КонецЦикла; 
		 
		 МассивАналитик.Добавить(СтрАналитика);
		 
	КонецЦикла; 

	Возврат МассивАналитик;
	
КонецФункции // ПолучитьАналитикуПоСоответствию()

#КонецОбласти

#КонецЕсли
