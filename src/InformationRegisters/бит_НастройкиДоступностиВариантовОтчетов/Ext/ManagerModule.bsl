#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура конвертирует настройки вариантов/отчетов. 
// Переносит признак "Использовать при открытии" из справочника в регистр сведений.
// 
Процедура УстановитьИспользоватьПриОткрытии() Экспорт

	ГруппаВсеПользователи = Справочники.ГруппыПользователей.ВсеПользователи;
		
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_ВариантыОтчетов.Ссылка КАК ВариантНастройка,
	|	бит_ВариантыОтчетов.КлючОбъекта КАК КлючОбъекта,
	|	бит_ВариантыОтчетов.КлючВарианта КАК КлючВариантаНастройки,
	|	бит_ВариантыОтчетов.Ответственный КАК Ответственный
	|ПОМЕСТИТЬ Таб_ВариантыНастройкиПоУмолчанию
	|ИЗ
	|	Справочник.бит_ВариантыОтчетов КАК бит_ВариантыОтчетов
	|ГДЕ
	|	бит_ВариантыОтчетов.ИспользоватьПриОткрытии = ИСТИНА
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	бит_СохраненныеНастройки.Ссылка,
	|	бит_СохраненныеНастройки.КлючОбъекта,
	|	бит_СохраненныеНастройки.КлючНастройки,
	|	бит_СохраненныеНастройки.Ответственный
	|ИЗ
	|	Справочник.бит_СохраненныеНастройки КАК бит_СохраненныеНастройки
	|ГДЕ
	|	бит_СохраненныеНастройки.ИспользоватьПриОткрытии = ИСТИНА
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Таб_ВариантыНастройкиПоУмолчанию.ВариантНастройка КАК ВариантНастройка,
	|	Таб_ВариантыНастройкиПоУмолчанию.КлючОбъекта КАК КлючОбъекта,
	|	Таб_ВариантыНастройкиПоУмолчанию.КлючВариантаНастройки КАК КлючВариантаНастройки,
	|	Таб_ВариантыНастройкиПоУмолчанию.Ответственный КАК Ответственный,
	|	бит_НастройкиДоступностиВариантовОтчетов.Пользователь КАК Пользователь,
	|	бит_НастройкиДоступностиВариантовОтчетов.Просмотр КАК Просмотр,
	|	бит_НастройкиДоступностиВариантовОтчетов.Изменение КАК Изменение
	|ИЗ
	|	Таб_ВариантыНастройкиПоУмолчанию КАК Таб_ВариантыНастройкиПоУмолчанию
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_НастройкиДоступностиВариантовОтчетов КАК бит_НастройкиДоступностиВариантовОтчетов
	|		ПО Таб_ВариантыНастройкиПоУмолчанию.ВариантНастройка = бит_НастройкиДоступностиВариантовОтчетов.ВариантНастройка
	|ИТОГИ
	|	МАКСИМУМ(КлючОбъекта),
	|	МАКСИМУМ(КлючВариантаНастройки),
	|	МАКСИМУМ(Ответственный)
	|ПО
	|	ВариантНастройка";
	Результат = Запрос.Выполнить();
	
	ВыборкаВерх = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	Пока ВыборкаВерх.Следующий() Цикл
		ТекущаяНастройка = ВыборкаВерх.ВариантНастройка;
		ЕстьНастройка = Ложь;
		Выборка = ВыборкаВерх.Выбрать();
		Пока Выборка.Следующий() Цикл
			Если ЗначениеЗаполнено(Выборка.Пользователь) Тогда			
				ЕстьНастройка = Истина;
				Если Выборка.Просмотр Тогда
					НаборЗаписей = РегистрыСведений.бит_НастройкиДоступностиВариантовОтчетов.СоздатьНаборЗаписей();
					НаборЗаписей.Отбор.ВариантНастройка.Установить(ТекущаяНастройка);
					НаборЗаписей.Отбор.Пользователь.Установить(Выборка.Пользователь);
					НаборЗаписей.Прочитать();
					НаборЗаписей[0].ИспользоватьПриОткрытии = Истина;
					ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		Если Не ЕстьНастройка Тогда
			НаборЗаписей = РегистрыСведений.бит_НастройкиДоступностиВариантовОтчетов.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.ВариантНастройка.Установить(ТекущаяНастройка);
			НаборЗаписей.Прочитать();
			НаборЗаписей.Очистить();
			Запись = НаборЗаписей.Добавить();
			Запись.ВариантНастройка 	   = ТекущаяНастройка;
			Запись.Пользователь 		   = ГруппаВсеПользователи;
			Запись.Просмотр 			   = Истина;
			Запись.Изменение 			   = Истина;
			Запись.ИспользоватьПриОткрытии = Истина;
			ОбновлениеИнформационнойБазы.ЗаписатьНаборЗаписей(НаборЗаписей);
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры // УстановитьИспользоватьПриОткрытии()

#КонецОбласти

#КонецЕсли