#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	бит_РаботаСДиалогамиСервер.ФормаЗаписиРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);

	Если Запись.Настройка = ПланыВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.ВалютаМеждународногоУчета Тогда
        
        РазрешеноУдаление = НЕ бит_му_ОбщегоНазначения.ЕстьДвиженияПоМУ(Запись.Организация);
		
		Если НЕ РазрешеноУдаление Тогда
			ТолькоПросмотр = Истина;
			
			ТекстСообщения = НСтр("ru='Для организации ""%1%"" обнаружены движения по международному учету. Редактирование запрещено!'");
			ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Запись.Организация);
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
            
        КонецЕсли;
        
    КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПериодПриИзменении(Элемент)
	
	ПривестиПериодКНачалу();
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаПриИзменении(Элемент)
	
	ПривестиПериодКНачалу();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначениеНастройкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Запись.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.СпособОценкиМПЗ") Тогда
		
		СтандартнаяОбработка = Ложь;
		
		СписокВыбора = Новый СписокЗначений;
		СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.бит_СпособыОценки.ФИФО"));
		СписокВыбора.Добавить(ПредопределенноеЗначение("Перечисление.бит_СпособыОценки.ПоСредней"));
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗначениеНастройкиНачалоВыбораЗавершение", ЭтотОбъект);
		
		ПоказатьВыборИзСписка(ОписаниеОповещения, СписокВыбора, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // ЗначениеНастройкиНачалоВыбора()

// Процедура - завершение обработки выбора реквизита "ЗначениеНастройки".
// 
&НаКлиенте
Процедура ЗначениеНастройкиНачалоВыбораЗавершение(ВыбранноеЗначение, Элемент) Экспорт
	
	Если НЕ ВыбранноеЗначение = Неопределено Тогда
		Запись.ЗначениеНастройки = ВыбранноеЗначение.Значение;
	КонецЕсли;
	
КонецПроцедуры // ЗначениеНастройкиНачалоВыбораЗавершение()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура приводит дату к 1980 году в случае если настройка - валюта международного учета.
// 
&НаКлиенте
Процедура ПривестиПериодКНачалу()
	
	// Валюту даем устанавливать только один раз.
	Если Запись.Настройка = ПредопределенноеЗначение("ПланВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.ВалютаМеждународногоУчета") Тогда
		
		Запись.Период = Дата('19800101');
		
	КонецЕсли; 
		
КонецПроцедуры // ПривестиПериодКНачалу()

#КонецОбласти


