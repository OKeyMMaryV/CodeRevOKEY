#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события "ПередЗаписью" НабораЗаписей.
// 
Процедура ПередЗаписью(Отказ, Замещение)
	
 Если ОбменДанными.Загрузка Тогда 
 	  Возврат; 
 КонецЕсли; 	
 
 Для каждого Запись Из ЭтотОбъект Цикл
	 
	 // Проверка на обязательное заполнение параметров.
	 Если НЕ (ТипЗнч(Запись.ЗначениеНастройки) = Тип("Булево") 
		       ИЛИ ТипЗнч(Запись.ЗначениеНастройки) = Тип("Число")) Тогда
	
		Если НЕ ЗначениеЗаполнено(Запись.ЗначениеНастройки) Тогда
		
			ТекстСообщения = "Для организации """
			                  +Запись.Организация
							  +""" на дату """
							  +Запись.Период 
							  +""" не заполнено значение настройки """
							  +Запись.Настройка
							  +"""!";
							  
            бит_ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения,Отказ,,СтатусСообщения.Важное);							  
		
		КонецЕсли; 
	
	КонецЕсли; 
	
	Если Запись.Настройка = ПланыВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.ВалютаМеждународногоУчета Тогда
		
		Если НЕ ЗначениеЗаполнено(Запись.Организация) Тогда
			
			ТекстСообщения = "Настройка """
							+Запись.Настройка
							+""" может быть указана только для конкретной организации!";
			
			бит_ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения,Отказ,,СтатусСообщения.Важное);							  
			
		КонецЕсли; 
		
		// Проверка возможности редактирования валюты.
		Если бит_му_ОбщегоНазначения.ЕстьДвиженияПоМУ(Запись.Организация) Тогда
		
			ТекстСообщения = "Для организации """
							+Запись.Организация
							+""" обнаружены движения по международному учету. Изменение валюты невозможно!";
			
			бит_ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения,Отказ,,СтатусСообщения.Важное);							  
			
		
		КонецЕсли; 
		
	КонецЕсли;
	
	Если Запись.Настройка = ПланыВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.ПромежуточныйСчетДляКонсолидации Тогда
		
		Если НЕ РегистрыСведений.бит_му_УчетнаяПолитика.РазрешеноРедактироватьПромежуточныйСчетДляКонсолидации(Запись.Организация) Тогда
		
			ТекстСообщения = "Невозможно назначить промежуточный счет для консолидации! Организация """
							+ Запись.Организация
							+ """ не установлена консолидирующей ни в одном периметре консолидации!";
			
			бит_ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения,Отказ,,СтатусСообщения.Важное);
			
		КонецЕсли;
		
	КонецЕсли; 
	
 КонецЦикла; 	
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события "ПриЗаписи" НабораЗаписей.
// 
Процедура ПриЗаписи(Отказ, Замещение)
	
	// Изменение кода. Начало. 08.04.2016{{
	ПроверитьКалендарь = Ложь;
	
	Если ЭтотОбъект.Отбор.Настройка.Значение = ПланыВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.РасчетРезервовДебиторскойЗадолженности
	 ИЛИ ЭтотОбъект.Отбор.Настройка.Значение = ПланыВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики.РасчетРезервовМПЗ Тогда

		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Период"	   , ЭтотОбъект.Отбор.Период.Значение);
		Запрос.УстановитьПараметр("Организация", ЭтотОбъект.Отбор.Организация.Значение);
		Запрос.Текст = "ВЫБРАТЬ
		               |	Настройки.Ссылка КАК Настройка,
		               |	Настройки.ЭтоГруппа,
		               |	УчетнаяПолитика.ЗначениеНастройки,
		               |	УчетнаяПолитика.ЗначениеНастройки КАК ПервоначальноеЗначение
		               |ИЗ
		               |	ПланВидовХарактеристик.бит_му_ВидыПараметровУчетнойПолитики КАК Настройки
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_УчетнаяПолитика.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитика
		               |		ПО Настройки.Ссылка = УчетнаяПолитика.Настройка
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	Настройки.Наименование УБЫВ";
		РезультатЗапроса = Запрос.Выполнить();
		Выборка = РезультатЗапроса.Выбрать();

		Пока Выборка.Следующий() Цикл

			Если ЗначениеЗаполнено(Выборка.Настройка.ТипЗначения)
				И (Выборка.Настройка.ТипЗначения.Типы()[0] = Тип("ПеречислениеСсылка.бит_му_РасчетРезервовДебиторскойЗадолженности")
				ИЛИ Выборка.Настройка.ТипЗначения.Типы()[0] = Тип("ПеречислениеСсылка.бит_му_РасчетРезервовМПЗ"))
				И ЗначениеЗаполнено(Выборка.ЗначениеНастройки)
				И (Выборка.ЗначениеНастройки = Перечисления.бит_му_РасчетРезервовДебиторскойЗадолженности.СУчетомВыходных
				ИЛИ Выборка.ЗначениеНастройки = Перечисления.бит_му_РасчетРезервовМПЗ.СУчетомВыходных) Тогда
			
				ПроверитьКалендарь = Истина;
				
			КонецЕсли; 
			
			Если ЗначениеЗаполнено(Выборка.Настройка.ТипЗначения)
				И Выборка.Настройка.ТипЗначения.Типы()[0] = Тип("СправочникСсылка.ПроизводственныеКалендари")
				И НЕ ЗначениеЗаполнено(Выборка.ЗначениеНастройки)
				И ПроверитьКалендарь Тогда
			
				ТекстСообщения = Нстр("ru = 'Необходимо заполнить производственный календарь!'");
				бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
				Отказ = Истина;
				Возврат;
				
			КонецЕсли; 

		КонецЦикла; 

	КонецЕсли; 
	// Изменение кода. Конец. 08.04.2016}}
	
	Если Не Отказ Тогда		
		бит_ОбщиеПеременныеСервер.ЗначениеПеременнойУстановить("бит_му_УчетнаяПолитика", бит_ОбщегоНазначения.ЗаполнениеУчетнойПолитикиМУ(), ИСТИНА);		
	КонецЕсли;

КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли
