#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборСчетПриИзменении(Элемент)
	
	ИзменитьЭлементОтбораСписка("Счет", ОтборСчет, ВидСравненияКомпоновкиДанных.ВИерархии);
	
КонецПроцедуры // ОтборСчетПриИзменении()

&НаКлиенте
Процедура ОтборРегистраторНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ОтборРегистратор) Тогда
		Элементы.ОтборРегистратор.ВыбиратьТип = Ложь;	
	Иначе
		Элементы.ОтборРегистратор.ВыбиратьТип = Истина;
	КонецЕсли;	
	
КонецПроцедуры // ОтборРегистраторНачалоВыбора()

&НаКлиенте
Процедура ОтборРегистраторПриИзменении(Элемент)
	
	ИзменитьЭлементОтбораСписка("Регистратор", ОтборРегистратор);
	
КонецПроцедуры // ОтборРегистраторПриИзменении()

&НаКлиенте
Процедура ОтборСценарийПриИзменении(Элемент)
	
	ИзменитьЭлементОтбораСписка("Сценарий", ОтборСценарий);
	
КонецПроцедуры // ОтборСценарийПриИзменении()

&НаКлиенте
Процедура ОтборОрганизацияПриИзменении(Элемент)
	
	ИзменитьЭлементОтбораСписка("Организация", ОтборОрганизация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЦФОПриИзменении(Элемент)
	
	ИзменитьЭлементОтбораСписка("ЦФО", ОтборЦФО);
	
КонецПроцедуры // ОтборЦФОПриИзменении()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПереключитьАктивностьПроводок(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные; 	
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбран документ'"));
		Возврат;
	КонецЕсли;
	
	ТекДокумент = ТекущиеДанные.Регистратор;
	Если НЕ ЗначениеЗаполнено(ТекДокумент) Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Не выбран документ'"));
		Возврат;
	КонецЕсли;
	
	ПереключитьАктивностьПроводокСервер(ТекДокумент);
		
КонецПроцедуры // ПереключитьАктивностьПроводок()

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МетаданныеОбъекта = Метаданные.РегистрыБухгалтерии.бит_Бюджетирование;
	фИмяОбъекта = МетаданныеОбъекта.Имя;
	
	бит_РаботаСДиалогамиСервер.ФормаСпискаРегистраПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);

	// Отбор по счету
	Если Параметры.Свойство("Счет") Тогда
		ОтборСчет = Параметры.Счет;
		ИзменитьЭлементОтбораСписка("Счет", ОтборСчет, ВидСравненияКомпоновкиДанных.ВИерархии);
	КонецЕсли;
	
	// Формирование списка доступных регистраторов.
	ОписаниеТиповРегистраторов = Метаданные.РегистрыБухгалтерии[фИмяОбъекта].СтандартныеРеквизиты.Регистратор.Тип;
	Элементы.ОтборРегистратор.ОграничениеТипа = ОписаниеТиповРегистраторов;
	
	// Видимость организации
	ЕстьОрганизация = бит_МеханизмДопИзмерений.ЕстьДопИзмерениеОрганизация();
	Элементы.Организация.Видимость = ЕстьОрганизация;
	Элементы.ОтборОрганизация.Видимость = ЕстьОрганизация;
	Если ЕстьОрганизация Тогда  	
		// Отбор по организации
		ОтборОрганизация = бит_РаботаСДиалогамиСервер.УстановитьОтборПоОсновнойОрганизацииБит(ЭтаФорма);         	
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура переключает активность для движений документа.
// 
// Параметры:
//  Документ - ДокументСсылка
// 
&НаСервере
Процедура ПереключитьАктивностьПроводокСервер(Документ)
	
	бит_ОбщегоНазначения.ПереключитьАктивностьПроводокДокументаДляРегистраБухгалтерии(Документ, фИмяОбъекта);

	Элементы.Список.Обновить();
	
КонецПроцедуры // ПереключитьАктивностьПроводокСервер()

// Процедура меняет отбор списка.
// 
// Параметры:
//  ИмяПоля        - Строка - имя поля компоновки, отбор по которому нужно установить
//  ПравоеЗначение - Произвольный
//  ВидСравнения   - ВидСравненияКомпоновкиДанных (По умолчанию = Равно).
// 
&НаСервере
Процедура ИзменитьЭлементОтбораСписка(ИмяПоля, ПравоеЗначение, ВидСравнения = Неопределено)

	бит_РаботаСДиалогамиСервер.ИзменитьЭлементОтбораСписка(Список, ИмяПоля, ПравоеЗначение, ЗначениеЗаполнено(ПравоеЗначение), ВидСравнения);

КонецПроцедуры // ИзменитьЭлементОтбораСписка()

#КонецОбласти

