
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.СтруктураПодключения = Неопределено Тогда
		ИД = Новый УникальныйИдентификатор;
		ИД = СтрЗаменить(ИД, "-", "");
		ИД = "ИД_" + ИД;
	Иначе
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.СтруктураПодключения);
	КонецЕсли;
	
	ВидимостьДоступностьЭлементов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипРасположенияПриИзменении(Элемент)
	
	ВидимостьДоступностьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если ТипРасположения <> 1
		И НЕ ПустаяСтрока(Текст) Тогда
		ТипРасположения = 1;
		Путь            = Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяСервераИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если ТипРасположения <> 2
		И НЕ ПустаяСтрока(Текст) Тогда
		ТипРасположения = 2;
		ИмяСервера      = Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяБазыИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Если ТипРасположения <> 2
		И НЕ ПустаяСтрока(Текст) Тогда
		ТипРасположения = 2;
		ИмяБазы         = Текст;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	Диалог.Заголовок = НСтр("ru = 'Каталог информационной базы'");
	Диалог.Каталог   = Путь;
	
	ОписаниеОповещенияДиалогаВыбора = Новый ОписаниеОповещения("ПутьЗавершениеВыбора", ЭтотОбъект);
	Диалог.Показать(ОписаниеОповещенияДиалогаВыбора);
	
КонецПроцедуры

// Продолжение процедуры (см. выше).
// 
Процедура ПутьЗавершениеВыбора(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы <> Неопределено Тогда
		
		Путь = ВыбранныеФайлы[0];
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаСохранитьИзменения(Команда)
	
	Если ПустаяСтрока(Наименование) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите наименование информационной базы'");
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Поле  = "Наименование";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	Если ТипРасположения = 0 Тогда
		ПредставлениеПути = "Текущая ИБ";
	ИначеЕсли ТипРасположения = 1 Тогда
		ПредставлениеПути = "File = ""%1"";";
		ПредставлениеПути = СтрШаблон(ПредставлениеПути, Путь);
	ИначеЕсли ТипРасположения = 2 Тогда
		ПредставлениеПути = "Srvr=""%1"";Ref=""%2"";";
		ПредставлениеПути = СтрШаблон(ПредставлениеПути, ИмяСервера, ИмяБазы);
	КонецЕсли;
	
	НастройкиПодключения = Новый Структура;
	НастройкиПодключения.Вставить("ИД"               , ИД);
	НастройкиПодключения.Вставить("Наименование"     , Наименование);
	НастройкиПодключения.Вставить("ПредставлениеПути", ПредставлениеПути);
	НастройкиПодключения.Вставить("ТипРасположения"  , ТипРасположения);
	НастройкиПодключения.Вставить("ИмяСервера"       , ИмяСервера);
	НастройкиПодключения.Вставить("ИмяБазы"          , ИмяБазы);
	НастройкиПодключения.Вставить("Путь"             , Путь);
	НастройкиПодключения.Вставить("Пользователь"     , Пользователь);
	НастройкиПодключения.Вставить("Пароль"           , Пароль);
	НастройкиПодключения.Вставить("НеАнализировать"  , НеАнализировать);
	
	Закрыть(НастройкиПодключения);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ВидимостьДоступностьЭлементов()
	
	//Элементы.Путь.Доступность       = (ТипРасположения = 0);
	//Элементы.ИмяСервера.Доступность = (ТипРасположения = 1);
	//Элементы.ИмяБазы.Доступность    = (ТипРасположения = 1);
	
КонецПроцедуры

#КонецОбласти