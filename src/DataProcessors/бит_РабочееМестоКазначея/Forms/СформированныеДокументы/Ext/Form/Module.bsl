

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Перем СписокДокументовСписания;
	Перем СписокРеестры;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    Массив = Новый Массив();
	РегистрируемыеДокументы = Метаданные.ЖурналыДокументов.бит_ДокументыСписанияДенежныхСредств.РегистрируемыеДокументы;
	Для каждого РегДок Из РегистрируемыеДокументы Цикл
		Массив.Добавить(ТипЗнч(ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(РегДок.ПолноеИмя()).ПустаяСсылка()));
	КонецЦикла;
    Источники = Новый ОписаниеТипов(Массив);
	
	ПараметрыРазмещения					= ПодключаемыеКоманды.ПараметрыРазмещения();
	ПараметрыРазмещения.Источники		= Источники;
	ПараметрыРазмещения.КоманднаяПанель = Элементы.ДокументыСписанияДенежныхСредствКоманднаяПанель;
	
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект, ПараметрыРазмещения);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Параметры.Свойство("СпискиДокументов") Тогда
		ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено.'");
		ВызватьИсключение ТекстСообщения;
	КонецЕсли;
	
	Параметры.СпискиДокументов.Свойство("СписокДокументовСписания", СписокДокументовСписания);
	Параметры.СпискиДокументов.Свойство("СписокРеестры", 		    СписокРеестры);
	
	УстановитьАктивнуюСтраницу(СписокДокументовСписания, СписокРеестры);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ДокументыСписанияДенежныхСредств, "Ссылка", СписокДокументовСписания, ВидСравненияКомпоновкиДанных.ВСписке,,Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			РеестрПлатежей, "Ссылка", СписокРеестры, ВидСравненияКомпоновкиДанных.ВСписке,,Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);	
								
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДокументыСписанияДенежныхСредств

&НаКлиенте
Процедура ДокументыСписанияДенежныхСредствПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СтандартныеПодсистемыОбработчикиКоманд

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.ДокументыСписанияДенежныхСредств);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.ДокументыСписанияДенежныхСредств, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.ДокументыСписанияДенежныхСредств);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьАктивнуюСтраницу(СписокДокументовСписания, СписокРеестры)

	ТекущаяСтраница = Элементы.СтраницаДокументыСписанияДенежныхСредств;
	
	КоличествоДокументовСписания = СписокДокументовСписания.Количество();
	КоличествоРеестрПлатежей 	 = СписокРеестры.Количество();
		
	Если КоличествоДокументовСписания = 0 И КоличествоРеестрПлатежей <> 0 Тогда	
		ТекущаяСтраница = Элементы.СтраницаРеестрПлатежей;
	КонецЕсли; 
	
	Элементы.Страницы.ТекущаяСтраница = ТекущаяСтраница;
	
КонецПроцедуры
 
#КонецОбласти