&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Документ") ИЛИ НЕ ЗначениеЗаполнено(Параметры.Документ) Тогда
		Отказ = Истина;		
		ЗаписьЖурналаРегистрации("Открытие Формы",,,,"При открытии формы Обработка.ОбменСКонтрагентами.Формы.ок_ФормаВводаКомментарияИВложениеФайлов не был передан параметр <Документ>");
		Возврат;
	КонецЕсли;
	
	Документ = Параметры.Документ;
	
КонецПроцедуры
				
&НаСервере
Процедура ОбновитьСоздатьПрисоединенныеФайлы()
	
	Для каждого Строка Из Файлы Цикл
			
		Если ПустаяСтрока(Строка.Адрес) Тогда
			Продолжить;
		КонецЕсли;
		
		ПараметрыФайла = Новый Структура;
		ПараметрыФайла.Вставить("ВремяИзмененияУниверсальное", "");
		ПараметрыФайла.Вставить("Автор", ПользователиКлиентСервер.ТекущийПользователь());
		ПараметрыФайла.Вставить("ВладелецФайлов", Документ);
		ПараметрыФайла.Вставить("ИмяБезРасширения", Строка.ИмяБезРасширения);
		ПараметрыФайла.Вставить("РасширениеБезТочки", Строка.Расширение);
		ПараметрыФайла.Вставить("НаправлениеЭД", ПредопределенноеЗначение("Перечисление.НаправленияЭД.Входящий"));
		ПараметрыФайла.Вставить("ТипЭлементаВерсииЭД", ПредопределенноеЗначение("Перечисление.ТипыЭлементовВерсииЭД.ок_ВнутренняяПереписка"));
		ПараметрыФайла.Вставить("ок_НомерВходящегоДокумента", Строка.НомерВходящегоДокумента);
		ПараметрыФайла.Вставить("ок_ДатаВходящегоДокумента", Строка.ДатаВходящегоДокумента);
		ПараметрыФайла.Вставить("ок_ТипДокумента", Строка.ТипДокумента);		
		ПрисоединенныйФайл = РаботаСФайлами.ДобавитьФайл(ПараметрыФайла, Строка.Адрес);
							
	КонецЦикла;	
	
КонецПроцедуры

&НаКлиенте
Процедура Перенести(Команда)
	
	Если Файлы.Количество() > 0 Тогда
		ОбновитьСоздатьПрисоединенныеФайлы();
	КонецЕсли;
	
	Закрыть(Комментарий);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьФайл(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ЗавершениеВыбораФайла", ЭтотОбъект);
	СтруктураИмяФайла = Новый Структура("ИмяФайла", "");
	ОбменДаннымиКлиент.ОбработчикВыбораФайла(СтруктураИмяФайла,
											 "ИмяФайла",
											 Ложь,
											 Новый Структура("ПроверятьСуществованиеФайла", Истина),
											 Оповещение
											);

КонецПроцедуры
										
&НаКлиенте
Процедура ЗавершениеВыбораФайла(Результат, ДополнительныеПараметры) Экспорт 
	
	Если ПустаяСтрока(Результат) тогда
		Возврат;
	КонецЕсли;	
	
	Файл = Новый Файл(Результат);
	Если Не Файл.Существует() Тогда
		Сообщить("Не найден Файл: " + Результат);
		Возврат;
	КонецЕсли;
	
	Если Файл.Размер() = 0 Тогда
		Сообщить("Файл пустой: " + Результат);
		Возврат;
	КонецЕсли;
	
	Строка = Файлы.Добавить();
	Строка.Имя = Файл.Имя;
	Строка.ИмяБезРасширения = Файл.ИмяБезРасширения;
	Строка.Расширение = СтрЗаменить(Файл.Расширение, ".", "");
	Строка.Адрес = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(Результат), Новый УникальныйИдентификатор);
			
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Файлы.Количество() > 0 Тогда
		ТекстПредупреждения = "В таблице есть файлы, если вы закроете форму, то они не будут подгружены к входящему документу";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	Закрыть();
КонецПроцедуры
						
