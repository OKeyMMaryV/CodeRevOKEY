
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Документ", Документ);
	Параметры.Свойство("МаксимальныйРазмерПисьма", МаксимальныйРазмерПисьма);
	ЗаполнитьФайлы();
	ОтсортироватьВложенияСообщения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановленаПрограммаОтображенияПредставленияHTML = ок_УправлениеФормамиКлиент.УстановленаПрограммаОтображенияПредставленияHTML();
КонецПроцедуры

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьФайлы()
	
	Файлы.Очистить();
	Если ЗначениеЗаполнено(Документ) Тогда
		Данные = ок_ОбменСКонтрагентамиВнутренний.ПолучитьПрисоединенныеФайлыВходящегоЭДДляОтправки(Документ);
		Если Данные <> Неопределено Тогда
			Для каждого СтрокаДанных Из Данные Цикл
				СтрокаФайлов = Файлы.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаФайлов, СтрокаДанных);
				СтрокаФайлов.Размер = СтрокаДанных.Размер / 1024 / 1024;
			КонецЦикла; 
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлыПриАктивизацииСтроки(Элемент)
	Если Элемент.ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(Элемент.ТекущиеДанные.ПрисоединенныйФайл) Тогда 

		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		ДоступныеЭлементы = Новый Массив;
	
		ПрисоединенныйФайл 	= ТекущиеДанные.ПрисоединенныйФайл;
		Расширение 			= ТекущиеДанные.Расширение;
		
		Если НРег(Расширение) = "pdf" Тогда
			Если УстановленаПрограммаОтображенияПредставленияHTML Тогда
				ПредставлениеФайлаHTML = "";
				Данные = ПолучитьИзВременногоХранилища(ок_ОбменСКонтрагентамиСлужебныйВызовСервера.АдресДанныхПрисоединенныйФайл(ПрисоединенныйФайл, УникальныйИдентификатор));
				Если ТипЗнч(Данные) = Тип("ДвоичныеДанные") Тогда
					ок_ОбменСКонтрагентамиКлиент.ОчиститьВременныйФайл(ИмяВременногоФайла);
					ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
					Данные.Записать(ИмяВременногоФайла);
					ПредставлениеФайлаHTML = ок_ОбменСКонтрагентамиКлиент.HTMLПредставлениеФайлаЭД(ИмяВременногоФайла, Расширение);
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;	
		
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ОтсортироватьВложенияСообщения()
	
	ПриоритыВложений 			= бит_БК_Общий.ПолучитьЗначениеНастройкиБК("Согласование по почте", "Приоритеты вложений письма", Новый СписокЗначений);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаДляСортировки.Флаг КАК Флаг,
		|	ТаблицаДляСортировки.Имя КАК Имя,
		|	ТаблицаДляСортировки.Размер КАК Размер,
		|	ТаблицаДляСортировки.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ТаблицаДляСортировки.ВнутренняяПереписка КАК ВнутренняяПереписка,
		|	ТаблицаДляСортировки.Расширение КАК Расширение,
		|	ВЫБОР
		|		" + "КОГДА 1 = 2 ТОГДА 1" + "
		|		ИНАЧЕ ТаблицаДляСортировки.Приоритет
		|	КОНЕЦ КАК Приоритет
		|ПОМЕСТИТЬ ВТ_ТаблицаДляСортировки
		|ИЗ
		|	&ТаблицаДляСортировки КАК ТаблицаДляСортировки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаДляСортировки.Флаг КАК Флаг,
		|	ТаблицаДляСортировки.Имя КАК Имя,
		|	ТаблицаДляСортировки.Размер КАК Размер,
		|	ТаблицаДляСортировки.ПрисоединенныйФайл КАК ПрисоединенныйФайл,
		|	ТаблицаДляСортировки.ВнутренняяПереписка КАК ВнутренняяПереписка,
		|	ТаблицаДляСортировки.Расширение КАК Расширение,
		|	ТаблицаДляСортировки.Приоритет КАК Приоритет
		|ИЗ
		|	ВТ_ТаблицаДляСортировки КАК ТаблицаДляСортировки
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет,
		|	Размер";
	
	Для Инд = 1 По ПриоритыВложений.Количество() Цикл
	
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "КОГДА 1 = 2 ТОГДА 1", "КОГДА 1 = 2 ТОГДА 1
			|		КОГДА ТаблицаДляСортировки.Имя ПОДОБНО """ + ПриоритыВложений[Инд-1].Значение + """ ТОГДА " + Инд);
	
	КонецЦикла; 
	
	МассивИменФайловДляВключенияВСообщение = Новый Массив;
	НакопленныйРазмерПисьма = 0;
	
	Запрос.Параметры.Вставить("ТаблицаДляСортировки", Файлы.Выгрузить());
	РезультатЗапроса = Запрос.Выполнить();
	Файлы.Загрузить(РезультатЗапроса.Выгрузить());
	
	НакопленныйРазмер = 0;
	Для каждого СтрокаФайлов Из Файлы Цикл
		Если НакопленныйРазмер + СтрокаФайлов.Размер < МаксимальныйРазмерПисьма Тогда 
			СтрокаФайлов.Флаг = Истина;
			НакопленныйРазмер = НакопленныйРазмер + СтрокаФайлов.Размер;
		Иначе
			Прервать;
		КонецЕсли;	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьРазмерВыбранныхВложений()
	НакопленныйРазмер = 0;
	Для каждого СтрокаФайлов Из Файлы Цикл
		Если СтрокаФайлов.Флаг Тогда
			НакопленныйРазмер = НакопленныйРазмер + СтрокаФайлов.Размер;
		КонецЕсли;	
	КонецЦикла; 
КонецПроцедуры	

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	ИзменитьФлажки(Истина);
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	ИзменитьФлажки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьФлажки(Признак)
	Для каждого СтрокаФайлов Из Файлы Цикл
		СтрокаФайлов.Флаг = Признак;
	КонецЦикла; 
	ПересчитатьРазмерВыбранныхВложений();
КонецПроцедуры

&НаКлиенте
Процедура ФайлыФлагПриИзменении(Элемент)
	ПересчитатьРазмерВыбранныхВложений();
	Если НакопленныйРазмер > МаксимальныйРазмерПисьма Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Невозможно выбрать файл. Превышен максимальный размер вложений.");
		Элементы.Файлы.ТекущиеДанные.Флаг = Ложь;
		НакопленныйРазмер = НакопленныйРазмер - Элементы.Файлы.ТекущиеДанные.Размер;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Процедура ПрименитьНаСервере()
	Для каждого СтрокаФайлов Из Файлы Цикл
		ок_ОбменСКонтрагентамиСлужебныйВызовСервера.УстановитьФлагКОтправкеФайлаЭД(СтрокаФайлов.ПрисоединенныйФайл, СтрокаФайлов.Флаг);
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
	Если НакопленныйРазмер > МаксимальныйРазмерПисьма Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Превышен максимальный размер вложений.");
		Возврат;
	КонецЕсли;	
	Если НакопленныйРазмер = 0 И Файлы.Количество() > 0 Тогда 
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Необходимо выбрать файлы к отправке.");
		Возврат;
	КонецЕсли;	
	ПрименитьНаСервере();
	Закрыть(Истина);
КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	 ОтсортироватьВложенияСообщения();
КонецПроцедуры

#КонецОбласти