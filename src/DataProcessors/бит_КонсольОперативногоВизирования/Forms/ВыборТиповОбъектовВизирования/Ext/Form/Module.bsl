
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановленныеФильтры = Параметры.ТипыОбъектовВизирования;
	ЗаполнитьСписокОбъектовВизирования(УстановленныеФильтры);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура КомандаДокументыУстановитьВсеНаСервере()
	
	ТипыОбъектовВизирования.ЗаполнитьПометки(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДокументыУстановитьВсе(Команда)
	КомандаДокументыУстановитьВсеНаСервере();
КонецПроцедуры

&НаСервере
Процедура КомандаДокументыСнятьВсеНаСервере()
	
	ТипыОбъектовВизирования.ЗаполнитьПометки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаДокументыСнятьВсе(Команда)
	КомандаДокументыСнятьВсеНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФильтр(Команда)
	
	УстановленныеФильтры = Новый СписокЗначений;
	
	ЕстьФильтры    = Ложь;
	Для каждого ЭлементСписка Из ТипыОбъектовВизирования Цикл
		Если ЭлементСписка.Пометка Тогда
			УстановленныеФильтры.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
			ЕстьВключенные = Истина;
		Иначе			
			ЕстьФильтры = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если УстановленныеФильтры.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не отмечено ни одного документа для отображения.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"ТипыОбъектовВизированияЗначение");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЕстьФильтры Тогда
		УстановленныеФильтры.Очистить();
	КонецЕсли;
	
	Закрыть(УстановленныеФильтры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокОбъектовВизирования(УстановленныеФильтры)
	
	ЕстьФильтр = (УстановленныеФильтры.Количество() > 0);
	
	Для каждого ТипОбъекта Из Метаданные.ОпределяемыеТипы.бит_ОбъектыВизирования.Тип.Типы() Цикл
		ПредставлениеТипа = Строка(ТипОбъекта);
		Если СтрНайти(ПредставлениеТипа, "(Не используется)") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Отметка = Истина;
		Если ЕстьФильтр Тогда
			Если УстановленныеФильтры.НайтиПоЗначению(ТипОбъекта) = Неопределено Тогда
				Отметка = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		ТипыОбъектовВизирования.Добавить(ТипОбъекта,ПредставлениеТипа,Отметка);
	КонецЦикла;	
	
	ТипыОбъектовВизирования.СортироватьПоПредставлению();
	
КонецПроцедуры

#КонецОбласти
