
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокПрофилей = Обработки.ок_НастройкиОтборовДляОчисткиСубконто3.ПолучитьЗначениеКонстанты("Ок_СписокПрофилейРазноскиДляВыгрузкиВАХАРТА", Неопределено);
	
	Если СписокПрофилей <> Неопределено Тогда
		СписокПрофилейРазноски = СписокПрофилей;
	КонецЕсли;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПрофилейРазноскиЗначениеНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ЗначениеПрофиля = "";
	Если Элементы.СписокПрофилейРазноски.ТекущиеДанные <> Неопределено Тогда
		ЗначениеПрофиля = Элементы.СписокПрофилейРазноски.ТекущиеДанные.Значение;	
	КонецЕсли;
	
	ЗначениеПрофиля = ОткрытьФормуПрофилейИВернутьЗначение(ЗначениеПрофиля);
	
	Если ЗначениеПрофиля <> Неопределено
		И Элементы.СписокПрофилейРазноски.ТекущиеДанные <> Неопределено Тогда
		//ОКЕЙ Балыков А.Г. (ПервыйБИТ) Начало 2021-06-24 (#НТП_МП_ФР06)
		//СписокПрофилейРазноски.Получить(Элементы.СписокПрофилейРазноски.ТекущаяСтрока).Значение = ЗначениеПрофиля;
		// Заменено на:
		СтрокаСпискаЗначений = СписокПрофилейРазноски.НайтиПоИдентификатору(Элементы.СписокПрофилейРазноски.ТекущаяСтрока);
		СтрокаСпискаЗначений.Значение = ЗначениеПрофиля;
		СтрокаСпискаЗначений.Представление = ЗначениеПрофиля;
		//ОКЕЙ Балыков А.Г. (ПервыйБИТ) Конец 2021-06-24 (#НТП_МП_ФР06)
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьПрофиль(Команда)
	
	ЗначениеПрофиля = Неопределено;
	Если Элементы.СписокПрофилейРазноски.ТекущиеДанные <> Неопределено Тогда
		ЗначениеПрофиля = Элементы.СписокПрофилейРазноски.ТекущиеДанные.Значение;	
	КонецЕсли;
	
	ЗначениеПрофиля = ОткрытьФормуПрофилейИВернутьЗначение(ЗначениеПрофиля);

	Если ЗначениеПрофиля <> Неопределено Тогда
		СписокПрофилейРазноски.Добавить(ЗначениеПрофиля, ЗначениеПрофиля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОткрытьФормуПрофилейИВернутьЗначение(ТекущееЗначение = "")
	
	ПараметрыФормы = Новый Структура("Организация, СБ_ПрофильРазноски", Организация, ТекущееЗначение);
	
	ВыбраннаяСтрока = ОткрытьФормуМодально("ОбщаяФорма.СБ_ФормаВыбораПрофиляРазноски", ПараметрыФормы, ЭтаФорма);
	
	Если ВыбраннаяСтрока <> Неопределено Тогда
		Возврат ВыбраннаяСтрока.Код;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура СохранитьЗначения(Команда)
	СохранитьЗначенияНаСервере();
	Закрыть();
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначенияНаСервере()
	
	НаборЗаписей = РегистрыСведений.СБ_НастройкиКазначейства.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Идентификатор.Установить("Ок_СписокПрофилейРазноскиДляВыгрузкиВАХАРТА");
	
	НовСтр = НаборЗаписей.Добавить();
	НовСтр.Идентификатор				= "Ок_СписокПрофилейРазноскиДляВыгрузкиВАХАРТА";
	НовСтр.ок_ПредставлениеРаздела 		= "Константы";
	НовСтр.ок_ПредставлениеНастройки 	= "Список профилей разноски для выгрузки в АХАРТА";
	НовСтр.ЗначениеВХранилище           = Истина;
	НовСтр.ХранилищеЗначения 			= Новый ХранилищеЗначения(СписокПрофилейРазноски);
	НовСтр.ок_Комментарий 				= "#НТП_МП_06";
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры
