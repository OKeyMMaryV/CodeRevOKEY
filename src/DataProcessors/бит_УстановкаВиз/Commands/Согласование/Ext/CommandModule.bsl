
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	 
	 Если ПараметрКоманды = Неопределено Тогда
		 Возврат;
	 КонецЕсли;
	 
	 //ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-01-24 (#2969)
	 Если ТипЗнч(ПараметрыВыполненияКоманды.Источник) = Тип("УправляемаяФорма") И
		  ПараметрыВыполненияКоманды.Источник.Модифицированность 
	 Тогда 
		 
		 ДопПараметры = Новый Структура;
		 ДопПараметры.Вставить("ПараметрКоманды"			, ПараметрКоманды);
		 ДопПараметры.Вставить("ПараметрыВыполненияКоманды" , ПараметрыВыполненияКоманды);
		 
		 ОповещениеОтвет = Новый ОписаниеОповещения("ОповещениеОтветНаВопрос",ЭтотОбъект,ДопПараметры);
		 ПоказатьВопрос(ОповещениеОтвет, НСтр("ru = 'Данные были изменены. Сохранить изменения?'"),РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет,,КодВозвратаДиалога.Нет);
		 Возврат;
		 
	 КонецЕсли;
	 //ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-01-24 (#2969)
	 
	 СтруктураОтбор = Новый Структура;
	 СтруктураОтбор.Вставить("Объект",ПараметрКоманды);
	 
	 
	 ПараметрыФормы = Новый Структура();
	 ПараметрыФормы.Вставить("Отбор",СтруктураОтбор);
	 ПараметрыФормы.Вставить("ОбъектВизирования",ПараметрКоманды);
	 
	 ОткрытьФорму("Обработка.бит_УстановкаВиз.Форма"
	              , ПараметрыФормы
				  , ПараметрыВыполненияКоманды.Источник
				  , ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор
				  , ПараметрыВыполненияКоманды.Окно);
КонецПроцедуры

//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-01-24 (#2969)
&НаКлиенте
Процедура ОповещениеОтветНаВопрос(Результат, Параметры) Экспорт
	
	Если Не Результат = КодВозвратаДиалога.Да Тогда 
		Параметры.ПараметрыВыполненияКоманды.Источник.Активизировать();
		Возврат;
	КонецЕсли;
	
	Попытка
		Параметры.ПараметрыВыполненияКоманды.Источник.Записать(Новый Структура("РежимЗаписи", РежимЗаписиДокумента.Запись));
	Исключение
		ТекстОшибки = "ru = 'Не удалось записать документ по причине: '" + ОписаниеОшибки();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр(ТекстОшибки));
		Возврат;
	КонецПопытки;
	
	ОбработкаКоманды(Параметры.ПараметрКоманды, Параметры.ПараметрыВыполненияКоманды);
	
КонецПроцедуры
//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-01-24 (#2969)

#КонецОбласти