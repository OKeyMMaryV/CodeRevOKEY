////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура производит заполнение табличной части данными графиков
//
Процедура ЗаполнитьТабличнуюЧасть() Экспорт
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	бит_му_СоставПериметровКонсолидацииСрезПоследних.Организация КАК Организация
	|ПОМЕСТИТЬ ВТ_ОрганизацииПериметра
	|ИЗ
	|	РегистрСведений.бит_му_СоставПериметровКонсолидации.СрезПоследних(&Дата, ПериметрКонсолидации = &ПериметрКонсолидации) КАК бит_му_СоставПериметровКонсолидацииСрезПоследних
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бит_му_Настройки.Значение
	|ПОМЕСТИТЬ ВТ_СчетаДляОтбора
	|ИЗ
	|	РегистрСведений.бит_му_Настройки КАК бит_му_Настройки
	|ГДЕ
	|	бит_му_Настройки.Группа = ""Актуализация графиков платежей""
	|	И бит_му_Настройки.ИмяНастройки = ""Счета учета кредитов и займов""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бит_му_Настройки.Значение
	|ПОМЕСТИТЬ ВТ_СтатьиОборотов
	|ИЗ
	|	РегистрСведений.бит_му_Настройки КАК бит_му_Настройки
	|ГДЕ
	|	бит_му_Настройки.Группа = ""Актуализация графиков платежей""
	|	И бит_му_Настройки.ИмяНастройки = ""Статьи оборотов""
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента.Организация КАК Организация,
	|	бит_ГрафикиДоговоровОбороты.Контрагент,
	|	бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента,
	|	бит_ГрафикиДоговоровОбороты.Сценарий,
	|	СУММА(0) КАК ОстатокПоЗаймуМУ,
	|	СУММА(ВЫБОР
	|			КОГДА бит_ГрафикиДоговоровОбороты.Период < бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента.Дата
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА бит_ГрафикиДоговоровОбороты.СтатьяОборотов.РасходДоход = ЗНАЧЕНИЕ(Перечисление.бит_РасходДоход.Поступление)
	|						ТОГДА бит_ГрафикиДоговоровОбороты.СуммаСценарийОборот
	|					КОГДА бит_ГрафикиДоговоровОбороты.СтатьяОборотов.РасходДоход = ЗНАЧЕНИЕ(Перечисление.бит_РасходДоход.Расходование)
	|						ТОГДА -бит_ГрафикиДоговоровОбороты.СуммаСценарийОборот
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		КОНЕЦ) КАК ОстатокПоЗаймуГрафик
	|ПОМЕСТИТЬ ВТ_ИтоговыеДанные
	|ИЗ
	|	РегистрНакопления.бит_ГрафикиДоговоров.Обороты(
	|			,
	|			&Дата,
	|			День,
	|			ДоговорКонтрагента.Организация В
	|					(ВЫБРАТЬ
	|						ВТ.Организация
	|					ИЗ
	|						ВТ_ОрганизацииПериметра КАК ВТ)
	|				И (ДоговорКонтрагента.бит_ТипДоговора = ЗНАЧЕНИЕ(Справочник.бит_ТипыДоговоров.КредитыПолученные)
	|					ИЛИ ДоговорКонтрагента.бит_ТипДоговора = ЗНАЧЕНИЕ(Справочник.бит_ТипыДоговоров.ЗаймыПолученные)
	|					ИЛИ ДоговорКонтрагента.бит_ТипДоговора = ЗНАЧЕНИЕ(Справочник.бит_ТипыДоговоров.ЗаймыВыданные))
	|				И СтатьяОборотов В ИЕРАРХИИ
	|					(ВЫБРАТЬ
	|						ВТ.Значение
	|					ИЗ
	|						ВТ_СтатьиОборотов КАК ВТ)) КАК бит_ГрафикиДоговоровОбороты
	|
	|СГРУППИРОВАТЬ ПО
	|	бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента.Организация,
	|	бит_ГрафикиДоговоровОбороты.Контрагент,
	|	бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента,
	|	бит_ГрафикиДоговоровОбороты.Сценарий
	|
	|ИМЕЮЩИЕ
	|	СУММА(ВЫБОР
	|			КОГДА бит_ГрафикиДоговоровОбороты.Период < бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента.Дата
	|				ТОГДА 0
	|			ИНАЧЕ бит_ГрафикиДоговоровОбороты.СуммаСценарийОборот
	|		КОНЕЦ) <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	бит_Дополнительный_2Остатки.Субконто2.Организация,
	|	бит_Дополнительный_2Остатки.Субконто1,
	|	бит_Дополнительный_2Остатки.Субконто2,
	|	НЕОПРЕДЕЛЕНО,
	|	СУММА(ВЫБОР
	|			КОГДА бит_Дополнительный_2Остатки.ВалютнаяСуммаОстаток = 0
	|				ТОГДА -бит_Дополнительный_2Остатки.СуммаУпрОстаток
	|			ИНАЧЕ -бит_Дополнительный_2Остатки.ВалютнаяСуммаОстаток
	|		КОНЕЦ),
	|	СУММА(0)
	|ИЗ
	|	РегистрБухгалтерии.бит_Дополнительный_2.Остатки(
	|			&Дата,
	|			Счет В ИЕРАРХИИ
	|				(ВЫБРАТЬ
	|					ВТ.Значение
	|				ИЗ
	|					ВТ_СчетаДляОтбора КАК ВТ),
	|			&Субконто,
	|			Организация В
	|					(ВЫБРАТЬ
	|						ВТ.Организация
	|					ИЗ
	|						ВТ_ОрганизацииПериметра КАК ВТ)
	|				И (Субконто2.бит_ТипДоговора = ЗНАЧЕНИЕ(Справочник.бит_ТипыДоговоров.КредитыПолученные)
	|					ИЛИ Субконто2.бит_ТипДоговора = ЗНАЧЕНИЕ(Справочник.бит_ТипыДоговоров.ЗаймыПолученные)
	|					ИЛИ Субконто2.бит_ТипДоговора = ЗНАЧЕНИЕ(Справочник.бит_ТипыДоговоров.ЗаймыВыданные))) КАК бит_Дополнительный_2Остатки
	|
	|СГРУППИРОВАТЬ ПО
	|	бит_Дополнительный_2Остатки.Субконто2.Организация,
	|	бит_Дополнительный_2Остатки.Субконто1,
	|	бит_Дополнительный_2Остатки.Субконто2
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИтоговыеДанные.Организация,
	|	ВТ_ИтоговыеДанные.Контрагент,
	|	ВТ_ИтоговыеДанные.ДоговорКонтрагента,
	|	МАКСИМУМ(ВТ_ИтоговыеДанные.Сценарий) КАК Сценарий,
	|	СУММА(ВТ_ИтоговыеДанные.ОстатокПоЗаймуМУ) КАК ОстатокПоЗаймуМУ,
	|	СУММА(ВТ_ИтоговыеДанные.ОстатокПоЗаймуГрафик) КАК ОстатокПоЗаймуГрафик
	|ПОМЕСТИТЬ ВТ_ИтогиСвернуто
	|ИЗ
	|	ВТ_ИтоговыеДанные КАК ВТ_ИтоговыеДанные
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_ИтоговыеДанные.Организация,
	|	ВТ_ИтоговыеДанные.Контрагент,
	|	ВТ_ИтоговыеДанные.ДоговорКонтрагента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ИтогиСвернуто.Организация,
	|	ВТ_ИтогиСвернуто.Контрагент,
	|	ВТ_ИтогиСвернуто.ДоговорКонтрагента,
	|	ВТ_ИтогиСвернуто.Сценарий,
	|	ВТ_ИтогиСвернуто.ОстатокПоЗаймуМУ,
	|	ВТ_ИтогиСвернуто.ОстатокПоЗаймуГрафик,
	|	ВТ_ИтогиСвернуто.ОстатокПоЗаймуМУ - ВТ_ИтогиСвернуто.ОстатокПоЗаймуГрафик КАК Дельта
	|ИЗ
	|	ВТ_ИтогиСвернуто КАК ВТ_ИтогиСвернуто";
	Запрос.Текст = ТекстЗапроса; 
	Запрос.УстановитьПараметр("Дата", Дата);
	Запрос.УстановитьПараметр("ПериметрКонсолидации", ПериметрКонсолидации);
	Субконто = Новый Массив;
	Субконто.Добавить(ПланыВидовХарактеристик.бит_ВидыСубконтоДополнительные_2.Контрагенты);
	Субконто.Добавить(ПланыВидовХарактеристик.бит_ВидыСубконтоДополнительные_2.ДоговорыКонтрагентов);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ДанныеАктуализации.Загрузить(РезультатЗапроса.Выгрузить());
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьТабличнуюЧасть()

// Процедура производит заполнение актуализацию графиков договоров
//
Процедура ВыполнитьАктуализациюГрафиков() Экспорт
	
	Для Каждого СтрокаТаблицы Из ДанныеАктуализации Цикл
		
		Если Не СтрокаТаблицы.СкорректироватьГрафик И Не СтрокаТаблицы.ЗакрытьГрафик Тогда
			Продолжить;
		КонецЕсли;
		
		// создание и заполнение документа
		НовыйДокумент = Документы.СБ_КорректировкаГрафиковПлатежейПоДополнительнымУсловиямПоДоговору.СоздатьДокумент();
		бит_ОбщегоНазначения.ЗаполнитьШапкуДокумента(НовыйДокумент, бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь"));
		НовыйДокумент.Дата = Дата;
		НовыйДокумент.Организация = СтрокаТаблицы.Организация;
		НовыйДокумент.Контрагент = СтрокаТаблицы.Контрагент;
		НовыйДокумент.ДоговорКонтрагента = СтрокаТаблицы.ДоговорКонтрагента;
		НовыйДокумент.СуммаЗадолженностиПоГрафику = СтрокаТаблицы.ОстатокПоЗаймуГрафик;
		
		Если СтрокаТаблицы.СкорректироватьГрафик Тогда
			
			НовыйДокумент.СуммаЗадолженностиНовая = СтрокаТаблицы.ОстатокПоЗаймуМУ;
			
		ИначеЕсли СтрокаТаблицы.ЗакрытьГрафик Тогда
			
			НовыйДокумент.СуммаЗадолженностиНовая = 0;
			
		КонецЕсли;	
		
		// формирование нового графика
		Запрос = Новый Запрос;
		Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
		               |	бит_ДополнительныеУсловияПоДоговору.Ссылка,
		               |	бит_ДополнительныеУсловияПоДоговору.ЦФО,
		               |	бит_ДополнительныеУсловияПоДоговору.Сценарий
		               |ИЗ
		               |	Документ.бит_ДополнительныеУсловияПоДоговору КАК бит_ДополнительныеУсловияПоДоговору
		               |ГДЕ
		               |	бит_ДополнительныеУсловияПоДоговору.ДоговорКонтрагента = &ДоговорКонтрагента
		               |	И бит_ДополнительныеУсловияПоДоговору.Проведен
		               |
		               |УПОРЯДОЧИТЬ ПО
		               |	бит_ДополнительныеУсловияПоДоговору.Дата УБЫВ"; 
		Запрос.УстановитьПараметр("ДоговорКонтрагента", СтрокаТаблицы.ДоговорКонтрагента);			   
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "По договору """ + СтрокаТаблицы.ДоговорКонтрагента + """ контрагента """ + СтрокаТаблицы.Контрагент + """ с организацией """ + СтрокаТаблицы.Организация + """ отсутствуют документы ""Дополнительные условия по договору"" для корректировки!";
			Сообщение.Сообщить(); 
			Продолжить;
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
	
		Запрос.Текст = "ВЫБРАТЬ
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.ДатаТранша,
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.ДатаПогашения,
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.СуммаТранша,
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.МесяцПервогоПлатежа,
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.ПредставлениеМесяцаПервогоПлатежа,
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.КоличествоПериодовВыплат,
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.СБ_ПроцентнаяСтавкаПоТраншу
		               |ИЗ
		               |	Документ.бит_ДополнительныеУсловияПоДоговору.ГрафикВыдачиТраншей КАК бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей
		               |ГДЕ
		               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка = &Ссылка";
		Запрос.УстановитьПараметр("Ссылка", Выборка.Ссылка);			   
		РезультатЗапроса = Запрос.Выполнить();
		ГрафикТраншей = РезультатЗапроса.Выгрузить();
		ГрафикТраншей.Колонки.Добавить("ИД", ОбщегоНазначенияБПКлиентСервер.ПолучитьОписаниеТиповСтроки(36));
		
		Для Каждого СтрокаТранша Из ГрафикТраншей Цикл
			СтрокаТранша.ИД = Строка(Новый УникальныйИдентификатор);
		КонецЦикла;
		
   		ДеревоПараметров = бит_ДоговораСервер.ПолучитьДеревоПараметровФинДоговоров(СтрокаТаблицы.ДоговорКонтрагента);
		ДеревоПараметров.Колонки.ТипПлатежа.Имя = "ТипПлатежаПоФинДоговору";
		ДеревоПараметров.Колонки.Значение.Имя = "ЗначениеПараметра";
		
		Если ДеревоПараметров.Строки.Количество() = 0 Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "По договору """ + СтрокаТаблицы.ДоговорКонтрагента + """ контрагента """ + СтрокаТаблицы.Контрагент + """ с организацией """ + СтрокаТаблицы.Организация + """ отсутствуют параметры финансовых договоров!";
			Сообщение.Сообщить(); 
			Продолжить;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ДоговорКонтрагента", СтрокаТаблицы.ДоговорКонтрагента);
		СтруктураПараметров.Вставить("СуммаДоговора"	 , СтрокаТаблицы.ДоговорКонтрагента.бит_СуммаДоговора);
		СтруктураПараметров.Вставить("НаправлениеДДС"	 , "Получение");
		СтруктураПараметров.Вставить("ВидГрафика"		 , Перечисления.бит_ТипыСтатейОборотов.БДДС);
	
		ТаблицаПериодов = Обработки.бит_ФормированиеГрафиковПлатежейПоФинансовымДоговорам.СформироватьГрафикПлатежей(ГрафикТраншей, ДеревоПараметров, СтруктураПараметров);
		
		// корректировка графика
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ДоговорКонтрагента"		, СтрокаТаблицы.ДоговорКонтрагента);
		СтруктураПараметров.Вставить("СуммаДоговора"	 		, СтрокаТаблицы.ДоговорКонтрагента.бит_СуммаДоговора);
		СтруктураПараметров.Вставить("НаправлениеДДС"	 		, "Получение");
		СтруктураПараметров.Вставить("ВидГрафика"		 		, Перечисления.бит_ТипыСтатейОборотов.БДДС);
		СтруктураПараметров.Вставить("СБ_ДатаНачалаРасчета"	 	, Дата + 86400);
		СтруктураПараметров.Вставить("СБ_КорректировкаГрафика"	, Истина);
		СтруктураПараметров.Вставить("СБ_СуммаКорректировки"	, НовыйДокумент.СуммаЗадолженностиНовая);
		СтруктураПараметров.Вставить("ДокументДопУсловий"		, Выборка.Ссылка);
		
		Обработки.бит_ФормированиеГрафиковПлатежейПоФинансовымДоговорам.СкорректироватьГрафикПлатежей(ТаблицаПериодов, ГрафикТраншей, ДеревоПараметров, СтруктураПараметров);	
		
		// заполнение табличной части
		Запрос = Новый Запрос;
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаПлатежей.Период,
		|	ТаблицаПлатежей.СтатьяОборотов,
		|	ТаблицаПлатежей.СоставляющаяПлатежа,
		|	ТаблицаПлатежей.Сумма
		|ПОМЕСТИТЬ ВТ
		|ИЗ
		|	&ТаблицаПлатежей КАК ТаблицаПлатежей
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТРасход.Период,
		|	ВТРасход.СтатьяОборотов,
		|	ВТРасход.СоставляющаяПлатежа,
		|	СУММА(-ВТРасход.Сумма) КАК СуммаСтарая,
		|	СУММА(0) КАК СуммаНовая
		|ПОМЕСТИТЬ ВТ_Объединение
		|ИЗ
		|	ВТ КАК ВТРасход
		|ГДЕ
		|	ВТРасход.Сумма < 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТРасход.Период,
		|	ВТРасход.СоставляющаяПлатежа,
		|	ВТРасход.СтатьяОборотов
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ВТПриход.Период,
		|	ВТПриход.СтатьяОборотов,
		|	ВТПриход.СоставляющаяПлатежа,
		|	СУММА(0),
		|	СУММА(ВТПриход.Сумма)
		|ИЗ
		|	ВТ КАК ВТПриход
		|ГДЕ
		|	ВТПриход.Сумма >= 0
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТПриход.Период,
		|	ВТПриход.СтатьяОборотов,
		|	ВТПриход.СоставляющаяПлатежа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Объединение.Период,
		|	ВТ_Объединение.СтатьяОборотов,
		|	ВТ_Объединение.СоставляющаяПлатежа,
		|	СУММА(ВТ_Объединение.СуммаСтарая) КАК СуммаСтарая,
		|	СУММА(ВТ_Объединение.СуммаНовая) КАК СуммаНовая
		|ИЗ
		|	ВТ_Объединение КАК ВТ_Объединение
		|
		|СГРУППИРОВАТЬ ПО
		|	ВТ_Объединение.Период,
		|	ВТ_Объединение.СоставляющаяПлатежа,
		|	ВТ_Объединение.СтатьяОборотов";
		Запрос.Текст = ТекстЗапроса; 
		Запрос.УстановитьПараметр("ТаблицаПлатежей", ТаблицаПериодов);
		ТаблицаСумм = Запрос.Выполнить().Выгрузить();
		
		СтруктураПоиска = Новый Структура("Период, СоставляющаяПлатежа");
		
		Для Каждого ТекущаяСтрока Из ТаблицаСумм Цикл
			
			НоваяСтрока = НовыйДокумент.ТаблицаКорректировки.Добавить();
			
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			НоваяСтрока.ЦФО = Выборка.ЦФО;
			НоваяСтрока.Сценарий = Выборка.Сценарий;
			
		КонецЦикла;
		
		НовыйДокумент.Комментарий = "# Сформирован из обработки ""Актуализация графиков платежей""";
		
		// проведение документа
		Попытка
			НовыйДокумент.Записать(РежимЗаписиДокумента.Проведение);
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Сформирован документ " + НовыйДокумент.Ссылка + " по контрагенту """ + СтрокаТаблицы.Контрагент + """, договору """ + СтрокаТаблицы.ДоговорКонтрагента + """, организации """ + СтрокаТаблицы.Организация + """";
			Сообщение.Сообщить(); 
			НоваяСтрока = СформированныеДокументы.Добавить();
			НоваяСтрока.Документ = НовыйДокумент.Ссылка;
		Исключение
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не удалось провести документ корректировки графика! " + ОписаниеОшибки();
			Сообщение.Сообщить(); 
		КонецПопытки;
		
	КонецЦикла;	
		
КонецПроцедуры
