&НаКлиенте
Перем ИдентификаторСтрокиПроводки;

&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если НЕ Параметры.Свойство("ДокументДвижений") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ДокументДвижений    = Параметры.ДокументДвижений;
	
	ОбновитьДвиженияНаСервере();
	
	УстановитьУсловноеОформление();
	
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	ИспользуетсяОтложенноеПроведение = НЕ Объект.РучнаяКорректировка
		И ПроведениеСервер.ИспользуетсяОтложенноеПроведение(Объект.Организация, Объект.Дата);

	Если Параметры.Свойство("АдресХранилищаОтложенныеРасчеты") Тогда
		// Адрес может быть передан при вызове, если фоновое задание запущено одновременно с вызовом формы.
		АдресХранилищаОтложенныеРасчеты = Параметры.АдресХранилищаОтложенныеРасчеты;
	КонецЕсли;
	
	Если ИспользуетсяОтложенноеПроведение Тогда

		Элементы.Актуализировать.Видимость = ЕстьПравоНаРедактирование;
			
		ДокументПоддерживаетВосстановлениеОтложенныхРасчетов = 
			УчетВзаиморасчетовОтложенноеПроведение.ДокументПоддерживаетВосстановлениеОтложенныхРасчетовСКонтрагентами(ДокументДвижений);
	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыОбъекта(МетаданныеДокумента)

	Объект.Проведен = БухгалтерскийУчетПереопределяемый.ДокументПроведен(ДокументДвижений);
	Объект.ЕстьОрганизация = МетаданныеДокумента.Реквизиты.Найти("Организация") <> Неопределено;
	Объект.ЕстьРучнаяКорректировка = МетаданныеДокумента.Реквизиты.Найти("РучнаяКорректировка") <> Неопределено;
	ИменаРеквизитов = "Дата,ПометкаУдаления,Организация,РучнаяКорректировка";
	Если НЕ Объект.ЕстьОрганизация Тогда
		ИменаРеквизитов = СтрЗаменить(ИменаРеквизитов, ",Организация", "");
	КонецЕсли;
	Если НЕ Объект.ЕстьРучнаяКорректировка Тогда
		ИменаРеквизитов = СтрЗаменить(ИменаРеквизитов, ",РучнаяКорректировка", "");
	КонецЕсли;
	РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументДвижений, ИменаРеквизитов);
	ЗаполнитьЗначенияСвойств(Объект, РеквизитыДокумента);
	РучнаяКорректировка = Объект.РучнаяКорректировка;
	
	ДоступныеОрганизации = ОбщегоНазначенияБПВызовСервераПовтИсп.ВсеОрганизацииДанныеКоторыхДоступныПоRLS(Истина);
	
	ЕстьПравоНаРедактирование = ПравоДоступа("Изменение", МетаданныеДокумента)
		И ДоступныеОрганизации.Найти(Объект.Организация) <> Неопределено;
	
	ИнформацияОПроведении = Обработки.КорректировкаДвижений.ИнформацияОПроведенииДокумента(ДокументДвижений);
	
КонецПроцедуры 

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()

	ПоляОбъектаДт = Новый Структура(
		"Субконто1, Субконто2, Субконто3, Подразделение, Валютный, Количественный",
		"СубконтоДт1", "СубконтоДт2", "СубконтоДт3", "ПодразделениеДт", "ВалютныйДт", "КоличественныйДт");
	ПоляОбъектаКт = Новый Структура(
		"Субконто1, Субконто2, Субконто3, Подразделение, Валютный, Количественный",
		"СубконтоКт1", "СубконтоКт2", "СубконтоКт3", "ПодразделениеКт", "ВалютныйКт", "КоличественныйКт");

	Для каждого Проводка Из ХозрасчетныйНаборЗаписей Цикл
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетДт, Проводка, ПоляОбъектаДт, Ложь);
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетКт, Проводка, ПоляОбъектаКт, Ложь);
		ЗаполнитьНадписиВПроводке(Проводка);
	КонецЦикла;
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУНаборЗаписей
	Для каждого Проводка Из СБ_КорректировкаРСБУНаборЗаписей Цикл
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетДт, Проводка, ПоляОбъектаДт, Ложь);
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетКт, Проводка, ПоляОбъектаКт, Ложь);
		ЗаполнитьНадписиВПроводке(Проводка);
	КонецЦикла;
	//1c-izhtc А.В. Пушин )
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФормуПоОрганизацииПериоду()
	
	Если (ТипЗнч(ДокументДвижений) = Тип("ДокументСсылка.ВводНачальныхОстатков")) Тогда
		Период = КонецМесяца(Объект.Дата) + 1;
	Иначе
		Период = Объект.Дата;
	КонецЕсли;
	
	Если Не Объект.ЕстьОрганизация Тогда
		
		ОрганизацияНабораХозрасчетный = Неопределено;
		
	Иначе
		
		ОрганизацияНабораХозрасчетный = Объект.Организация;
			
		ПериодФО = Период;
		Если ТипЗнч(ДокументДвижений) = Тип("ДокументСсылка.ОперацияБух") Тогда
			// При переходе с ОСН на УСН в проводках "Операций (БУ и НУ)",
			// введенных первым днем периода применения УСН при выполнении операций "Помощника по переходу на УСН",
			// требуется показывать суммы налогового учета по налогу на прибыль
			Если УчетУСН.ЭтоПервыйДеньПримененияУСНПослеУплатыНалогаНаПрибыль(Объект.Организация, Период) Тогда
				ПериодФО = НачалоДня(Период) - 1;
			КонецЕсли;
		КонецЕсли;
		
		ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрОрганизацияФункциональныхОпцийФормы(
			ЭтаФорма,
			Объект.Организация,
			ПериодФО);
		
		Если УчетнаяПолитика.ПлательщикНДФЛ(Объект.Организация, Период)
			И НЕ УчетнаяПолитика.ВестиУчетПоВидамДеятельностиИП(Объект.Организация, Период) Тогда
			ИПОсновнаяНоменклатурнаяГруппа = УчетнаяПолитика.ОсновнаяНоменклатурнаяГруппа(Объект.Организация, Период);
			ИПОсновнойХарактерДеятельности = УчетнаяПолитика.ОсновнойХарактерДеятельности(Объект.Организация, Период);
		КонецЕсли;
		
		РаздельныйУчетУСНТорговыйСбор = УчетнаяПолитика.ПрименяетсяУСНДоходы(Объект.Организация, Период)
			И УчетнаяПолитика.ПлательщикТорговогоСбораЗаПериод(Объект.Организация, НачалоГода(Период), КонецГода(Период));
			
	КонецЕсли;
	
	ПоляРазниц = Новый Массив;
	ПоляРазниц.Добавить(Элементы.ХозрасчетныйНадписьПР);
	ПоляРазниц.Добавить(Элементы.ХозрасчетныйНадписьВР);
	ПоляРазниц.Добавить(Элементы.ХозрасчетныйСуммаПРДт);
	ПоляРазниц.Добавить(Элементы.ХозрасчетныйСуммаВРДт);
	ПоляРазниц.Добавить(Элементы.ХозрасчетныйСуммаПРКт);
	ПоляРазниц.Добавить(Элементы.ХозрасчетныйСуммаВРКт);
	
	ПоляЗаголовков = Новый Структура;
	ПоляЗаголовков.Вставить("Дт", Элементы.ХозрасчетныйГруппаНалоговыйУчетДт);
	ПоляЗаголовков.Вставить("Кт", Элементы.ХозрасчетныйГруппаНалоговыйУчетКт);
	
	НалогНаПрибыльБухгалтерскийУчет.НастроитьПоляУчетаРазниц(ПоляРазниц, ПоляЗаголовков, Период, ОрганизацияНабораХозрасчетный);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	Если НЕ Объект.Проведен 
		 И НЕ Объект.РучнаяКорректировка Тогда
		
		 Если БухгалтерскийУчетКлиентСерверПереопределяемый.ЭтоРегламентнаяОперация(ДокументДвижений) Тогда
			//1c-izhtc (
			//ТекстПредупреждения = НСтр("ru='Регламентная операция не выполнена.
			//	|Для получения информации о движениях необходимо выполнить регламентную операцию.'");
			Возврат;
			//1c-izhtc )
		ИначеЕсли ТипЗнч(ДокументДвижений) = Тип("ДокументСсылка.ОперацияБух") Тогда
			//1c-izhtc (
			//ТекстПредупреждения = НСтр("ru='Операция помечена на удаление.
			//	|Для получения информации о движениях необходимо снять пометку на удаление'");
			Возврат;
			//1c-izhtc )
		Иначе
			ТекстПредупреждения = НСтр("ru='Документ не проведен.
				|Для получения информации о движениях необходимо провести документ.'");
		КонецЕсли;

		ПоказатьПредупреждение( , ТекстПредупреждения);

		Отказ = Истина;
		Возврат;

	КонецЕсли;

	Если ТребуетсяПоказатьОтложенныеРасчеты(ЭтотОбъект) Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПрочитатьСостояниеОтложенныхРасчетов", 1, Истина);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриПовторномОткрытии()

	ОбновитьДвиженияНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если Модифицированность Тогда
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru = 'Сохранить изменения движений?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, ,КодВозвратаДиалога.Да);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура РучнаяКорректировкаПриИзменении(Элемент)

	ВыполнитьДействияПриИзмененииРучнойКорректировки();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыХозрасчетный

&НаКлиенте
Процедура ХозрасчетныйПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Хозрасчетный.ТекущиеДанные;
	ИдСтроки      = Элементы.Хозрасчетный.ТекущаяСтрока;
	
	Если ИдСтроки <> ИдентификаторСтрокиПроводки Тогда
	
		ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
			"ХозрасчетныйСубконтоДт1", "ХозрасчетныйСубконтоДт2", "ХозрасчетныйСубконтоДт3");
		БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетДт, ЭтотОбъект, ПоляФормы, Неопределено, Истина, Ложь);
	
		ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
			"ХозрасчетныйСубконтоКт1", "ХозрасчетныйСубконтоКт2", "ХозрасчетныйСубконтоКт3");
		БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетКт, ЭтотОбъект, ПоляФормы, Неопределено, Истина, Ложь);
		
		ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "", Ложь);
		
		ИдентификаторСтрокиПроводки = ИдСтроки;
	
	КонецЕсли;
	
	// Сначала выполняем общие действия для всех регистров
	Подключаемый_ТаблицаРегистраПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование);
	
	Если НоваяСтрока Тогда
		ЗаполнитьНадписиВПроводке(ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСчетДтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета("Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоДт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоДт1ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоДт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоДт2ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоДт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСчетКтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета("Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоКт1ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоКт2ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ХозрасчетныйСубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУниверсальные

// Общая процедура для всех регистров. Устанавливает период и организацию в добавляемых строках.
//
&НаКлиенте
Процедура Подключаемый_ТаблицаРегистраПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Элемент.ТекущиеДанные.Свойство("Период") Тогда
		Элемент.ТекущиеДанные.Период = Объект.Дата;
	КонецЕсли;
	
	Если НоваяСтрока
			И НЕ Копирование
			И Элемент.ТекущиеДанные.Свойство("Организация")
			И ЗначениеЗаполнено(Объект.Организация) Тогда
		Элемент.ТекущиеДанные.Организация = Объект.Организация;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Свойство("НоменклатурнаяГруппа") И Элемент.ТекущиеДанные.Свойство("ХарактерДеятельности") Тогда
		Элемент.ТекущиеДанные.НоменклатурнаяГруппа = ИПОсновнаяНоменклатурнаяГруппа;
		Элемент.ТекущиеДанные.ХарактерДеятельности = ИПОсновнойХарактерДеятельности;
	КонецЕсли;
	
	Если НоваяСтрока И НЕ Копирование
		И Элемент.ТекущиеДанные.Свойство("Валюта")
		И Элемент.ТекущиеДанные.Свойство("ОтражениеВУСН") Тогда // Регистр накопления "Расходы при УСН".
		
		Элемент.ТекущиеДанные.Валюта        = ВалютаРегламентированногоУчета;
		Элемент.ТекущиеДанные.ОтражениеВУСН = ПредопределенноеЗначение("Перечисление.ОтражениеВУСН.Принимаются");
		
	КонецЕсли;
	
КонецПроцедуры

// Общая процедура для всех остаточных регистров накопления. Обеспечивает показ правильной картинки в строке.
//
&НаКлиенте
Процедура Подключаемый_ВидДвиженияПриИзменении(Элемент)
	
	Если ТипЗнч(ТекущийЭлемент) = Тип("ТаблицаФормы")
		И ТекущийЭлемент.ТекущиеДанные.Свойство("КартинкаСтроки") Тогда
		ТекущиеДанные = ТекущийЭлемент.ТекущиеДанные;
		Если ТекущиеДанные.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
			ТекущиеДанные.КартинкаСтроки = 2 + ?(ТекущиеДанные.Активность, 0, 1);
		Иначе
			ТекущиеДанные.КартинкаСтроки = 4 + ?(ТекущиеДанные.Активность, 0, 1);
		КонецЕсли;		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если НЕ Модифицированность Тогда
		Закрыть();
		Возврат;
	КонецЕсли;
	
	ИзмененаРучнаяКорректировка = Объект.ЕстьРучнаяКорректировка
		И Объект.РучнаяКорректировка <> РучнаяКорректировка;
		
	// бит_Финанс добавление кода. Начало: 11.11.2016
	//Если Объект.ЕстьРучнаяКорректировка
	//	И Объект.РучнаяКорректировка
	//	И НЕ РучнаяКорректировка Тогда
	бит_флЗадатьВопрос = (Объект.бит_ЕстьРучнаяКорректировкаФакта
							И Объект.бит_РучнаяКорректировкаФакта
							И НЕ ЭтотОбъект["бит_РучнаяКорректировкаФакта"])
						ИЛИ (Объект.ЕстьРучнаяКорректировка
							И Объект.РучнаяКорректировка
							И НЕ РучнаяКорректировка);
						
	ЭтотОбъект["бит_ИзмененаРучнаяКорректировкаФакта"] = Объект.бит_ЕстьРучнаяКорректировкаФакта
														И Объект.бит_РучнаяКорректировкаФакта <> ЭтотОбъект["бит_РучнаяКорректировкаФакта"];
	// бит_Финанс добавление кода конец }} 
	
	// бит_Финанс изменения кода. Начало: 11.11.2016  {{ 
	Если бит_флЗадатьВопрос Тогда
	// бит_Финанс изменения кода. Конец 11.11.2016  }} 	
		
		ТекстВопроса = НСтр("ru='Ручная корректировка движений документа отменена.
			|Восстановить движения, сформированные автоматически?'");
			
		Оповещение = Новый ОписаниеОповещения("ВопросВосстановитьДвиженияЗавершение", ЭтотОбъект, ИзмененаРучнаяКорректировка);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ЗаписатьДвиженияИЗакрыть(ИзмененаРучнаяКорректировка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДвижения(Команда)
	
	ОбновитьДвиженияНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура НастроитьСоставРегистров(Команда)

	СписокДвижений  = Новый СписокЗначений;
	СписокРегистров = Новый СписокЗначений;
	Для каждого СтрокаРегистра Из Регистры Цикл
		Если НЕ СтрокаРегистра.ПравоНаПросмотр Тогда
			Продолжить;
		КонецЕсли;
		СписокДвижений.Добавить(СтрокаРегистра.Имя, , СтрокаРегистра.ЕстьДвижения);
		СписокРегистров.Добавить(СтрокаРегистра.Имя, СтрокаРегистра.Синоним, СтрокаРегистра.Отображение);
	КонецЦикла;
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СписокДвижений",  СписокДвижений);
	СтруктураПараметров.Вставить("СписокРегистров", СписокРегистров);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОткрытьНастройкуЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Обработка.КорректировкаДвижений.Форма.ФормаНастройки", СтруктураПараметров, ЭтаФорма,,,,ОповещениеОЗакрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПерейтиВСписокРегистра(Команда)
	
	ИмяРегистра = СтрЗаменить(Команда.Имя, "ПерейтиВСписок", "");
	
	// Проверим, что таблица регистра добавлена на форму
	Если Элементы.Найти(ИмяРегистра) = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы[ИмяРегистра].ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	// установим отбор по организации документа (иначе в некоторых формах установится отбор по основной организации)
	Если Объект.ЕстьОрганизация Тогда
		ПараметрыОтбора = Новый Структура("Организация", Объект.Организация);
		ПараметрыФормы.Вставить("Отбор", ПараметрыОтбора); 
	КонецЕсли;
	
	// Подготовим ключ записи для позиционирования в форме списка
	ПараметрыТекущейЗаписи = Новый Структура;
	ПараметрыТекущейЗаписи.Вставить("ИмяРегистра", ИмяРегистра);
	ПараметрыТекущейЗаписи.Вставить("Регистратор", ДокументДвижений);
	Если Объект.ЕстьОрганизация Тогда
		ПараметрыТекущейЗаписи.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	Для Каждого ЭлементФормыТабПоля ИЗ Элементы[ИмяРегистра].ПодчиненныеЭлементы Цикл
		Если ТипЗнч(ЭлементФормыТабПоля) = Тип("ПолеФормы") Тогда
			ИмяСвойства = СтрЗаменить(ЭлементФормыТабПоля.Имя, ИмяРегистра, "");
			Если ИмяСвойства = "Регистратор" ИЛИ ИмяСвойства = "Организация" Тогда
				Продолжить;
			КонецЕсли;
			ПараметрыТекущейЗаписи.Вставить(ИмяСвойства, ТекущиеДанные[ИмяСвойства]);			
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыКлючаЗаписи = ПолучитьПараметрыКлючаЗаписи(ПараметрыТекущейЗаписи);
	Если ПараметрыКлючаЗаписи = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТипРегистра = ПараметрыКлючаЗаписи.ТипРегистра;
	ПараметрыФормы.Вставить("ТекущаяСтрока", ПараметрыКлючаЗаписи.КлючЗаписи);

	ОткрытьФорму(ТипРегистра + "." + ИмяРегистра + ".ФормаСписка", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();

	Для Сч = 1 По 3 Цикл

		///////////////////////
		// Субконто Дт
		
		// Не показываем субконто совсем, если его нет на счете, чтобы не занимать строку на экране,
		// в том числе и первое субконто, т.к. первой строкой идет счет, 
		// который есть всегда, то и колонка тоже будет отображаться всегда.
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();

		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйСубконтоДт" + Сч);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ХозрасчетныйНаборЗаписей.СубконтоДт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
		
		// Пустое субконто в виде "<...>"

		ЭлементУО = УсловноеОформление.Элементы.Добавить();

		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйСубконтоДт" + Сч);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ХозрасчетныйНаборЗаписей.СубконтоДт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ХозрасчетныйНаборЗаписей.СубконтоДт" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
	

		///////////////////////
		// Субконто Кт
		
		// Не показываем субконто совсем, если его нет на счете, чтобы не занимать строку на экране,
		// в том числе и первое субконто, т.к. первой строкой идет счет, 
		// который есть всегда, то и колонка тоже будет отображаться тоже всегда.

		ЭлементУО = УсловноеОформление.Элементы.Добавить();

		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйСубконтоКт" + Сч);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ХозрасчетныйНаборЗаписей.СубконтоКт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
		
		// Пустое субконто в виде "<...>"
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();

		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйСубконтоКт" + Сч);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ХозрасчетныйНаборЗаписей.СубконтоКт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ХозрасчетныйНаборЗаписей.СубконтоКт" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);

		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
		
	КонецЦикла;


	//////////////////////
	// ПодразделениеДт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйПодразделениеДт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ПодразделениеДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ПодразделениеДт", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУПодразделениеДт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ПодразделениеДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ПодразделениеДт", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
	//1c-izhtc А.В. Пушин )

	// ХозрасчетныйПодразделениеДт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйПодразделениеДт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ПодразделениеДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУПодразделениеДт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ПодразделениеДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	//1c-izhtc А.В. Пушин )

	
	//////////////////////
	// ПодразделениеКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйПодразделениеКт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ПодразделениеКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ПодразделениеКт", ВидСравненияКомпоновкиДанных.НеЗаполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУПодразделениеКт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ПодразделениеКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ПодразделениеКт", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
	//1c-izhtc А.В. Пушин )

	// ХозрасчетныйПодразделениеКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйПодразделениеКт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ПодразделениеКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУПодразделениеКт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ПодразделениеКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	//1c-izhtc А.В. Пушин )

	//////////////////////
	// Количество

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйНадписьКоличествоДт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйКоличествоДт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.КоличественныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ВалютныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУНадписьКоличествоДт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУКоличествоДт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУЗаписей.КоличественныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ВалютныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	//1c-izhtc А.В. Пушин )

	// ХозрасчетныйНадписьКоличествоКт, ХозрасчетныйКоличествоКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйНадписьКоличествоКт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйКоличествоКт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.КоличественныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ВалютныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУНадписьКоличествоКт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУКоличествоКт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.КоличественныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ВалютныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	//1c-izhtc А.В. Пушин )

	//////////////////////
	// Валюта

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйВалютаДт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйВалютнаяСуммаДт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ВалютныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУВалютаДт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУВалютнаяСуммаДт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ВалютныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	//1c-izhtc А.В. Пушин )

	// ХозрасчетныйВалютаКт, ХозрасчетныйВалютнаяСуммаКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйВалютаКт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ХозрасчетныйВалютнаяСуммаКт");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ХозрасчетныйНаборЗаписей.ВалютныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// КнигаУчетаДоходовИРасходовДоходТорговыйСбор
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КнигаУчетаДоходовИРасходовДоходТорговыйСбор");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РаздельныйУчетУСНТорговыйСбор", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);

	//1c-izhtc А.В. Пушин (СБ_КорректировкаРСБУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУВалютаКт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "КорректировкаРСБУВалютнаяСуммаКт");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СБ_КорректировкаРСБУНаборЗаписей.ВалютныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	//1c-izhtc А.В. Пушин )
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьНастройкуЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СписокРезультат = РезультатЗакрытия;
	
	Если ТипЗнч(СписокРезультат) = Тип("СписокЗначений") Тогда
		
		БылиИзменения = Ложь;
		Для каждого СтрокаРегистра Из Регистры Цикл
			СтрокаСписка = СписокРезультат.НайтиПоЗначению(СтрокаРегистра.Имя);
			Если СтрокаСписка = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если СтрокаРегистра.Отображение <> СтрокаСписка.Пометка Тогда
				БылиИзменения = Истина;
				СтрокаРегистра.Отображение = СтрокаСписка.Пометка;
			КонецЕсли;
		КонецЦикла;
		
		Если БылиИзменения Тогда
			
			ВыполнитьДействияПриИзмененииСоставаРегистров();
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокПараметров(Форма, ТекущиеДанные, ШаблонИмяПоляОбъекта, ИмяПоляСчетУчета)

	СписокПараметров = Новый Структура;
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);
		Если ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьОписаниеТиповДоговора().СодержитТип(ТипЗнч(ТекущиеДанные[ИмяПоля])) Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ТекущиеДанные[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущиеДанные[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ТекущиеДанные[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;
	СписокПараметров.Вставить("ОстаткиОбороты", "Кт");
	СписокПараметров.Вставить("Организация",    ТекущиеДанные["Организация"]);
	СписокПараметров.Вставить("СчетУчета",      ТекущиеДанные[ИмяПоляСчетУчета]);

	Возврат СписокПараметров;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, ДтКт = "", ОчищатьСвязанныеСубконто = Истина)
	
	ИдСтроки = Форма.Элементы.Хозрасчетный.ТекущаяСтрока;
	Если ИдСтроки <> Неопределено Тогда
		СтрокаТаблицы = Форма.ХозрасчетныйНаборЗаписей.НайтиПоИдентификатору(ИдСтроки);
		Если ДтКт <> "Кт" Тогда
			
			ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "СчетДт");
			
			Если ОчищатьСвязанныеСубконто Тогда
				ЗначенияСубконтоСтрокиТаблицы = СтрокаТаблицы;
			Иначе
				ЗначенияСубконтоСтрокиТаблицы = Новый Структура("СубконтоДт1,СубконтоДт2,СубконтоДт3");
				ЗаполнитьЗначенияСвойств(ЗначенияСубконтоСтрокиТаблицы, СтрокаТаблицы);
			КонецЕсли;
			
			БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, ЗначенияСубконтоСтрокиТаблицы, "СубконтоДт%Индекс%", "ХозрасчетныйСубконтоДт%Индекс%", ПараметрыДокумента);
				
		КонецЕсли;
		Если ДтКт <> "Дт" Тогда
			
			ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "СчетКт");
			
			Если ОчищатьСвязанныеСубконто Тогда
				ЗначенияСубконтоСтрокиТаблицы = СтрокаТаблицы;
			Иначе
				ЗначенияСубконтоСтрокиТаблицы = Новый Структура("СубконтоКт1,СубконтоКт2,СубконтоКт3");
				ЗаполнитьЗначенияСвойств(ЗначенияСубконтоСтрокиТаблицы, СтрокаТаблицы);
			КонецЕсли;
			
			БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, ЗначенияСубконтоСтрокиТаблицы, "СубконтоКт%Индекс%", "ХозрасчетныйСубконтоКт%Индекс%", ПараметрыДокумента);
				
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСчета(ДтКт)

	ТекущиеДанные = Элементы.Хозрасчетный.ТекущиеДанные;
	
	ПоляФормы = Новый Структура("Субконто1,Субконто2,Субконто3");
	ПоляФормы.Субконто1 = "ХозрасчетныйСубконто" + ДтКт + "1";
	ПоляФормы.Субконто2 = "ХозрасчетныйСубконто" + ДтКт + "2";
	ПоляФормы.Субконто3 = "ХозрасчетныйСубконто" + ДтКт + "3";
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные["Счет" + ДтКт], ЭтаФорма, ПоляФормы, Неопределено, Истина, Ложь);
	
	ПоляОбъекта = Новый Структура("Субконто1,Субконто2,Субконто3,Подразделение,Валютный,Количественный,Организация");
	ПоляОбъекта.Субконто1      = "Субконто" + ДтКт + "1";
	ПоляОбъекта.Субконто2      = "Субконто" + ДтКт + "2";
	ПоляОбъекта.Субконто3      = "Субконто" + ДтКт + "3";
	ПоляОбъекта.Подразделение  = "Подразделение" + ДтКт;
	ПоляОбъекта.Валютный       = "Валютный" + ДтКт;
	ПоляОбъекта.Количественный = "Количественный" + ДтКт;
	ПоляОбъекта.Организация    = Объект.Организация;
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(ТекущиеДанные["Счет" + ДтКт], ТекущиеДанные, ПоляОбъекта, Истина,, Ложь);
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтаФорма, ДтКт);
	
	ЗаполнитьНадписиВПроводке(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНачалоВыбораСубконто(ДтКт, Элемент, СтандартнаяОбработка)

	ТекущиеДанные      = Элементы.Хозрасчетный.ТекущиеДанные;
	ПараметрыДокумента = ПолучитьСписокПараметров(ЭтаФорма, ТекущиеДанные, "Субконто"+ДтКт+"%Индекс%", "Счет"+ДтКт);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

&НаСервере
Функция ИмяСобытияЖурналаРегистрации()

	Возврат НСтр("ru = 'Корректировка движений'", ОбщегоНазначения.КодОсновногоЯзыка());

КонецФункции
 

&НаСервере
Процедура ЗаписатьДвижения(Отказ)

	// бит_Финанс добавление кода. Начало: 11.10.2016 # 000000005227 {{
	бит_НадоПеретранслировать = Ложь;
	Если ЭтотОбъект["бит_РучнаяКорректировкаФакта"] И НЕ РучнаяКорректировка Тогда
		
		// Записать факт, трансляцию вызовет проведение документа (см. ниже)
		бит_ЗаписатьДвиженияПереопределяемый(Истина, Отказ);
		
	ИначеЕсли ЭтотОбъект["бит_РучнаяКорректировкаФакта"] И РучнаяКорректировка Тогда
		
		// Записать факт, штатно записать движения, вызвать трансляцию, 
		бит_ЗаписатьДвиженияПереопределяемый(Истина, Отказ);
		бит_НадоПеретранслировать = Истина;
		
	ИначеЕсли НЕ ЭтотОбъект["бит_РучнаяКорректировкаФакта"] И РучнаяКорректировка Тогда
		
		// штатно записать движения, вызвать трансляцию
		бит_ЗаписатьДвиженияПереопределяемый(Ложь, Отказ);
		бит_НадоПеретранслировать = Истина;
		
	ИначеЕсли НЕ ЭтотОбъект["бит_РучнаяКорректировкаФакта"] И НЕ РучнаяКорректировка Тогда	
		// Провести, все само
		бит_ЗаписатьДвиженияПереопределяемый(Ложь, Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	// бит_Финанс добавление кода конец }}  
	
	Если РучнаяКорректировка Тогда

		Попытка
			НачатьТранзакцию();
			ДокОбъект = ДокументДвижений.ПолучитьОбъект();
			Если НЕ ДокОбъект.РучнаяКорректировка Тогда
				ДокОбъект.РучнаяКорректировка = Истина;
				ДокОбъект.Записать(РежимЗаписиДокумента.Запись);
			КонецЕсли;

			Для каждого СтрокаРегистра Из Регистры Цикл
				
				Если НЕ СтрокаРегистра.ПравоНаИзменение Тогда
					Продолжить;
				КонецЕсли;
				
				Если СтрокаРегистра.ЕстьРеквизит Тогда
					ИмяРеквизита = СтрокаРегистра.Имя + "НаборЗаписей";
					НаборЗаписей = РеквизитФормыВЗначение(ИмяРеквизита);
					НаборЗаписей.Отбор.Регистратор.Установить(ДокументДвижений);
					НаборЗаписей.УстановитьАктивность(НЕ Объект.ПометкаУдаления);
					Если НЕ (СтрокаРегистра.ТипРегистра = "РегистрСведений" 
						И (Метаданные.РегистрыСведений[СтрокаРегистра.Имя].ПериодичностьРегистраСведений 
						= Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический)) Тогда
						Для каждого Запись Из НаборЗаписей Цикл
							Если Запись.Период = '00010101' Тогда
								Запись.Период = Объект.Дата;
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
					//БИТ АКриштопов 16022016 -->> Восстановление потерянного куска кода 06042016
					НаборЗаписей.ОбменДанными.Загрузка = Истина;
					//БИТ АКриштопов 16022016 <<--
					НаборЗаписей.Записать(Истина);
				КонецЕсли;
			КонецЦикла;
			// бит_Финанс добавление кода. Начало: 25.11.2016  {{
			Если бит_НадоПеретранслировать Тогда
				бит_Перетранслировать(Отказ);
			КонецЕсли; 
			// бит_Финанс добавление кода конец }}  
			ЗафиксироватьТранзакцию();
			Объект.РучнаяКорректировка = Истина;
		Исключение
			ОтменитьТранзакцию();
			ЗаписьЖурналаРегистрации(
				ИмяСобытияЖурналаРегистрации(), 
				УровеньЖурналаРегистрации.Ошибка,, 
				ДокументДвижений, 
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ВызватьИсключение ОписаниеОшибки();
		КонецПопытки;

	Иначе

		Попытка
			ДокОбъект = ДокументДвижений.ПолучитьОбъект();
			Если ДокОбъект.РучнаяКорректировка Тогда
				ДокОбъект.РучнаяКорректировка = Ложь;
			КонецЕсли;
			Если БухгалтерскийУчетКлиентСерверПереопределяемый.ЭтоРегламентнаяОперация(ДокументДвижений) Тогда
				ДокОбъект.ВыполнитьОперацию();
			Иначе
				ДокОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЕсли;
			Объект.РучнаяКорректировка = Истина;
		Исключение
			Если БухгалтерскийУчетКлиентСерверПереопределяемый.ЭтоРегламентнаяОперация(ДокументДвижений) Тогда
				ШаблонОшибки = НСтр("ru='Не удалось выполнить операцию по причине %1'") 
					+ Символы.ПС + НСтр("ru='Регламентная операция отменена.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибки, 
					ОписаниеОшибки());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументДвижений);
				ДокОбъект.ОтменитьОперацию();
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибки, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,, ДокументДвижений, 
					ТекстОшибки);
			Иначе
				ШаблонОшибки = НСтр("ru='Не удалось перепровести документ по причине %1'") 
					+ Символы.ПС + НСтр("ru='Проведение документа отменено.'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибки, 
					ОписаниеОшибки());
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументДвижений);
				ДокОбъект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					ШаблонОшибки, 
					ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
				ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,, ДокументДвижений, 
					ТекстОшибки);
			КонецЕсли;
			ВызватьИсключение;
		КонецПопытки;

		УстановитьДоступностьСубконто();

	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействияПриИзмененииСоставаРегистров()
	
	СоздатьРеквизитыФормы();
	СоздатьЭлементыФормы();
	
	УстановитьВозможностьРедактированияЗаписей();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДвиженияНаСервере()
	
	МетаданныеДокумента = ДокументДвижений.Метаданные();
	ЗаполнитьРеквизитыОбъекта(МетаданныеДокумента);

	Если НЕ Объект.Проведен 
		 //1c-izhtc (
		 //ОКЕЙ Первухин В.С. (ПервыйБИТ) Начало 2020-02-15 
		 // Если документ был с ручной корректировкой и после этого снят с проведения или помечен на удаление, 
		 // то движения не отображались. 
		 И Не Объект.РучнаяКорректировка
		 //ОКЕЙ Первухин В.С. (ПервыйБИТ) Конец 2020-02-15 
		 И НЕ МетаданныеДокумента.Имя = "ОперацияБух"
		 И НЕ МетаданныеДокумента.Имя = "РегламентнаяОперация" Тогда
		 //1c-izhtc )
		Возврат;
	КонецЕсли;
	
	НастроитьФормуПоОрганизацииПериоду();
	
	Элементы.РучнаяКорректировка.Видимость = Объект.ЕстьРучнаяКорректировка;
	ТолькоПросмотр = НЕ ЕстьПравоНаРедактирование;
	Элементы.КнопкаЗаписатьИЗакрыть.Доступность = Не ТолькоПросмотр;
	
	ЗаполнитьТаблицуРегистров(МетаданныеДокумента);
	УстановитьПривилегированныйРежим(Истина);
	РегистрыСДвижениями = ПроведениеСервер.ПолучитьМассивИспользуемыхРегистров(
		ДокументДвижений, МетаданныеДокумента.Движения);
	УстановитьПривилегированныйРежим(Ложь);
	УстановитьОтображениеВТаблицеРегистров(РегистрыСДвижениями);
	
	// бит_Финанс добавление кода. Начало: 06.10.2016  {{
	бит_СоздатьРеквизитыФормыНачало();   
	// бит_Финанс добавление кода конец }}  
	
	СоздатьРеквизитыФормы();
	ПрочитатьДвиженияДокумента(ДокументДвижений);
		
	// бит_Финанс добавление кода. Начало: 11.11.2016  {{
	бит_ЗаполнитьРеквизитыОбъектаЗавершение(ДокументДвижений.Метаданные());
	// бит_Финанс добавление кода конец }}  

	СоздатьЭлементыФормы();
	
	// бит_Финанс добавление кода. Начало: 07.10.2016  {{
	бит_УстановитьВозможностьРедактированияЗаписейФакта();
	// бит_Финанс добавление кода конец }} 

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	УстановитьВидимостьПриРучнойКорректировке();
	
	УстановитьВозможностьРедактированияЗаписей();
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Движения документа: %1'"), 
		Строка(ДокументДвижений));

	ОпределитьМоментАктуальностиОтложенныхРасчетов();

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуРегистров(МетаданныеДокумента)

	ВидРегистраОстатки = Метаданные.СвойстваОбъектов.ВидРегистраНакопления.Остатки;
	
	Регистры.Очистить();
	Для каждого МетаданныеРегистра Из МетаданныеДокумента.Движения Цикл
		
		Если Не ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(МетаданныеРегистра) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаРегистра     = Регистры.Добавить();
		СтрокаРегистра.Имя = МетаданныеРегистра.Имя;
		
		ПолноеИмя    = МетаданныеРегистра.ПолноеИмя();
		ПозицияТочки = СтрНайти(ПолноеИмя, ".");
		ТипРегистра  = Лев(ПолноеИмя, ПозицияТочки - 1);

		СтрокаРегистра.ТипРегистра      = ТипРегистра;
		СтрокаРегистра.Синоним          = МетаданныеРегистра.Синоним;
		СтрокаРегистра.ПравоНаПросмотр  = ПравоДоступа("Просмотр", МетаданныеРегистра);
		СтрокаРегистра.ПравоНаИзменение = ПравоДоступа("Изменение", МетаданныеРегистра);
		
		Если ТипРегистра = "РегистрНакопления" Тогда
			СтрокаРегистра.РегистрОстатков = МетаданныеРегистра.ВидРегистра = ВидРегистраОстатки;
		КонецЕсли;
		
	КонецЦикла;
	
	// Сначала показывается регистр бухгалтерии, затем регистры накопления, затем - сведений
	Регистры.Сортировать("ТипРегистра, Синоним");

КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыФормы()
	
	ПоказатьХозрасчетный = Ложь;
	ПоказатьКнигаУчетаДоходовИРасходов = Ложь;
	ПоказатьРасшифровкаКУДиР = Ложь;
	ПоказатьЖурналУчетаСчетовФактур = Ложь;
	ПоказатьКорректировкаРСБУ = Ложь;

	Для каждого СтрокаРегистра Из Регистры Цикл
		
		// бит_Финанс изменение кода. Начало. 31.08.2016{{
		ЭтоОборотыПоБюджетам = ?(СтрокаРегистра.ТипРегистра = "РегистрНакопления" И СтрокаРегистра.Имя = "бит_ОборотыПоБюджетам", Истина, Ложь);			
		// бит_Финанс изменение кода. Конец. 31.08.2016}}
		
		// бит_Финанс изменение кода. Начало. 15.12.2016{{
		ЭтоОборотыПоТипамПроводок = ?(СтрокаРегистра.ТипРегистра = "РегистрНакопления" И СтрокаРегистра.Имя = "бит_ОборотыПоТипамПроводок", Истина, Ложь);
        ЭтоОборотыПоСоответствиямЗаписейТрансляции = ?(СтрокаРегистра.ТипРегистра = "РегистрНакопления" И СтрокаРегистра.Имя = "бит_СоответствиеОборотовТрансляции", Истина, Ложь);
		// бит_Финанс изменение кода. Конец. 15.12.2016}}
		
		Если СтрокаРегистра.Имя = "Хозрасчетный" Тогда
			ПоказатьХозрасчетный = СтрокаРегистра.Отображение;
			Элементы.ГруппаХозрасчетный.Картинка = ?(АктивностьДвижений, 
				БиблиотекаКартинок.ЖурналПроводок, БиблиотекаКартинок.ЖурналПроводокНеактивный);
			Продолжить;
		ИначеЕсли СтрокаРегистра.Имя = "СБ_КорректировкаРСБУ" Тогда
			ПоказатьКорректировкаРСБУ = СтрокаРегистра.Отображение;
			Если ПоказатьКорректировкаРСБУ Тогда
				ИмяГруппы = "Группа" + СтрокаРегистра.Имя;
				ТекГруппа = Элементы.Найти(ИмяГруппы);
				ТекГруппа.Картинка = БиблиотекаКартинок.РегистрБухгалтерии;
				КартинкаАктивности = БиблиотекаКартинок.АктивностьПоРБ;
			КонецЕсли; 
			Продолжить;
		ИначеЕсли СтрокаРегистра.Имя = "КнигаУчетаДоходовИРасходов" Тогда
			ПоказатьКнигаУчетаДоходовИРасходов = СтрокаРегистра.Отображение;
			Элементы.ГруппаКнигаУчетаДоходовИРасходов.Картинка = ?(АктивностьДвижений, 
				БиблиотекаКартинок.РегистрНакопления, БиблиотекаКартинок.РегистрНакопленияНеактивный);
			Продолжить;
		ИначеЕсли СтрокаРегистра.Имя = "РасшифровкаКУДиР" Тогда
			ПоказатьРасшифровкаКУДиР = СтрокаРегистра.Отображение;
			Элементы.ГруппаРасшифровкаКУДиР.Картинка = ?(АктивностьДвижений, 
				БиблиотекаКартинок.РегистрСведений, БиблиотекаКартинок.РегистрСведенийНеактивный);
			Продолжить;
		ИначеЕсли СтрокаРегистра.Имя = "ЖурналУчетаСчетовФактур" Тогда
			ПоказатьЖурналУчетаСчетовФактур = СтрокаРегистра.Отображение;
			Элементы.ГруппаЖурналУчетаСчетовФактур.Картинка = ?(АктивностьДвижений, 
				БиблиотекаКартинок.РегистрСведений, БиблиотекаКартинок.РегистрСведенийНеактивный);
			Продолжить;
		КонецЕсли;
		
		ИмяГруппы = "Группа" + СтрокаРегистра.Имя;
		
		ТекГруппа = Элементы.Найти(ИмяГруппы);
		Если ТекГруппа = Неопределено 
			И СтрокаРегистра.Отображение Тогда

			//Найдем группу, перед которой будем вставлять новую группу
			СледующаяГруппа = Неопределено;
			Для каждого Строка Из Регистры Цикл
				Если Строка.ТипРегистра >= СтрокаРегистра.ТипРегистра
					И Строка.Синоним > СтрокаРегистра.Синоним
					И Строка.Отрисован Тогда
					СледующаяГруппа = Элементы["Группа" + Строка.Имя];
					Прервать;
				КонецЕсли;
			КонецЦикла;

			ТекГруппа = Элементы.Вставить(ИмяГруппы, Тип("ГруппаФормы"), Элементы.ПанельРегистров, СледующаяГруппа);
			
			ТекГруппа.Заголовок      = СтрокаРегистра.Синоним;
			СтрокаРегистра.Отрисован = Истина;

			Если СтрокаРегистра.ТипРегистра = "РегистрБухгалтерии" Тогда
				ТекГруппа.Картинка = БиблиотекаКартинок.РегистрБухгалтерии;
				КартинкаАктивности = БиблиотекаКартинок.АктивностьПоРБ;
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрНакопления" Тогда
				ТекГруппа.Картинка = БиблиотекаКартинок.РегистрНакопления;
				КартинкаАктивности = БиблиотекаКартинок.АктивностьПоРН;
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрСведений" Тогда
				ТекГруппа.Картинка = БиблиотекаКартинок.РегистрСведений;
				КартинкаАктивности = БиблиотекаКартинок.АктивностьПоРС;
			КонецЕсли;

			// На странице регистра создаем таблицу
			ИмяТаблицы = СтрокаРегистра.Имя;
			ТекТаблица = Элементы.Найти(ИмяТаблицы);
			Если ТекТаблица <> Неопределено Тогда
				Элементы.Удалить(ТекТаблица);
			КонецЕсли;
			ТекТаблица = Элементы.Добавить(ИмяТаблицы, Тип("ТаблицаФормы"), ТекГруппа);
			ПутьКДаннымТаблицы = СтрокаРегистра.Имя + "НаборЗаписей";
			ТекТаблица.ПутьКДанным = ПутьКДаннымТаблицы;
			// Назначаем общий обработчик
			ТекТаблица.УстановитьДействие("ПриНачалеРедактирования", "Подключаемый_ТаблицаРегистраПриНачалеРедактирования");
			ТекТаблица.КартинкаСтрок = КартинкаАктивности;
			Если СтрокаРегистра.ТипРегистра = "РегистрНакопления" 
				И СтрокаРегистра.РегистрОстатков Тогда
				ТекТаблица.ПутьКДаннымКартинкиСтроки = ПутьКДаннымТаблицы + ".КартинкаСтроки";
			Иначе
				ТекТаблица.ПутьКДаннымКартинкиСтроки = ПутьКДаннымТаблицы + ".Активность";
			КонецЕсли;
			
			ИмяКоманды   = ИмяТаблицы + "ПерейтиВСписок";
			НоваяКоманда = Команды.Добавить(ИмяКоманды);
			НоваяКоманда.Картинка  = БиблиотекаКартинок.НайтиВСписке;
			НоваяКоманда.Заголовок = НСтр("ru='Перейти в список движений'");
			НоваяКоманда.Подсказка = НСтр("ru='Перейти в список движений регистра'");
			НоваяКоманда.Действие  = "Подключаемый_ПерейтиВСписокРегистра";
			НоваяКнопка = Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ТекТаблица.КоманднаяПанель);
			НоваяКнопка.ИмяКоманды = ИмяКоманды;
			НоваяКнопка.ТолькоВоВсехДействиях = Истина;
			
			ТекГруппа.ПутьКДаннымЗаголовка = ПутьКДаннымТаблицы + ".КоличествоСтрок";
			
			Если СтрокаРегистра.ТипРегистра = "РегистрБухгалтерии" Тогда
				ТекГруппа.Картинка = ?(АктивностьДвижений, 
					БиблиотекаКартинок.ЖурналПроводок, БиблиотекаКартинок.ЖурналПроводокНеактивный);
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрНакопления" Тогда
				ТекГруппа.Картинка = ?(АктивностьДвижений, 
					БиблиотекаКартинок.РегистрНакопления, БиблиотекаКартинок.РегистрНакопленияНеактивный);
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрСведений" Тогда
				ТекГруппа.Картинка = ?(АктивностьДвижений, 
					БиблиотекаКартинок.РегистрСведений, БиблиотекаКартинок.РегистрСведенийНеактивный);
			КонецЕсли;
			
			РеквизитыНабораЗаписей = ЭтотОбъект[ПутьКДаннымТаблицы].Выгрузить(Новый Массив);

			// Некоторые колонки не показываем
			РеквизитыНабораЗаписей.Колонки.Удалить("Регистратор");
			РеквизитыНабораЗаписей.Колонки.Удалить("Активность");

			Если РеквизитыНабораЗаписей.Колонки.Найти("МоментВремени") <> Неопределено Тогда
				РеквизитыНабораЗаписей.Колонки.Удалить("МоментВремени");
			КонецЕсли;

			// бит_Финанс изменение кода. Начало. 06.06.2014{{
			//РеквизитыНабораЗаписей.Колонки.Удалить("Период");
			Если ?(СтрокаРегистра.ТипРегистра = "РегистрБухгалтерии" И Найти(СтрокаРегистра.Имя,"бит_")>0, Истина, Ложь)
				ИЛИ ?(СтрокаРегистра.ТипРегистра = "РегистрНакопления" И Найти(СтрокаРегистра.Имя,"бит_")>0, Истина, Ложь) Тогда
			
				//период для наших регистров не удаляем
			
			Иначе 
				Если РеквизитыНабораЗаписей.Колонки.Найти("Период") <> Неопределено Тогда
					РеквизитыНабораЗаписей.Колонки.Удалить("Период");
				КонецЕсли;
			КонецЕсли;
			// бит_Финанс изменение кода. Конец. 06.06.2014}}

			Если РеквизитыНабораЗаписей.Колонки.Найти("Организация") <> Неопределено Тогда
				РеквизитыНабораЗаписей.Колонки.Удалить("Организация");
			КонецЕсли;

			Если РеквизитыНабораЗаписей.Колонки.Найти("ИсходныйНомерСтроки") <> Неопределено Тогда
				РеквизитыНабораЗаписей.Колонки.Удалить("ИсходныйНомерСтроки");
			КонецЕсли;
			
			Если РеквизитыНабораЗаписей.Колонки.Найти("КартинкаСтроки") <> Неопределено Тогда
				РеквизитыНабораЗаписей.Колонки.Удалить("КартинкаСтроки");
			КонецЕсли;
			
			// бит_Финанс изменение кода. Начало. 27.06.2012{{
			// для регистра бухгалтерии колонки ВидСубконто<i> отобразить нельзя
			КоличествоКолонок = РеквизитыНабораЗаписей.Колонки.Количество();
			Для н = 1 ПО КоличествоКолонок - 1 Цикл
				й = КоличествоКолонок - н;
				ТекКолонка = РеквизитыНабораЗаписей.Колонки[й];
				Если Найти(ТекКолонка.Имя,"ВидСубконто") > 0 Тогда
					РеквизитыНабораЗаписей.Колонки.Удалить(й);
				КонецЕсли; 
			КонецЦикла;	
			ЭтоРББИТ = ?(СтрокаРегистра.ТипРегистра = "РегистрБухгалтерии" И Найти(СтрокаРегистра.Имя,"бит_")>0, Истина, Ложь);
			
			Если ЭтоРББИТ Тогда
				
				// Получим синоним  для РБ
				МетаРегистрБИТ = Метаданные.РегистрыБухгалтерии[СтрокаРегистра.Имя];
				
				ДанныеСинонима = Новый Структура("Заголовок");
				
				// Получим заголовок для страницы.
				бит_НазначениеСинонимовОбъектов.ВывестиЗаголовокФормы(МетаРегистрБИТ, ДанныеСинонима, Перечисления.бит_ВидыФормОбъекта.Списка);
				
				// Установим заголовок.
				ТекГруппа.Заголовок = ?(ЗначениеЗаполнено(ДанныеСинонима.Заголовок), ДанныеСинонима.Заголовок, МетаРегистрБИТ.Синоним);
				
				
			КонецЕсли; 
			
			РесурсыБИТ = Новый Массив;
			РесурсыБИТ.Добавить("Сумма");
			РесурсыБИТ.Добавить("СуммаУпр");
			РесурсыБИТ.Добавить("СуммаРегл");
			РесурсыБИТ.Добавить("СуммаСценарий");
			РесурсыБИТ.Добавить("СуммаМУ");
			// бит_Финанс изменение кода. Конец. 27.06.2012}}
			
			// Создаем колонки таблицы
			Для каждого КолонкаРеквизита Из РеквизитыНабораЗаписей.Колонки Цикл
				ИмяКолонки = СтрокаРегистра.Имя + КолонкаРеквизита.Имя;
				ТекКолонка = Элементы.Найти(ИмяКолонки);
				Если ТекКолонка = Неопределено Тогда
					// бит_Финанс изменение кода. Начало.
					//ТекКолонка = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТекТаблица);
					
					Если ЭтоРББИТ ИЛИ ЭтоОборотыПоТипамПроводок Тогда
						// колонки, специфичные для регистров бухгалтерии
						Если Найти(КолонкаРеквизита.Имя,"СубконтоДт") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаСубконтоДт"];
						ИначеЕсли Найти(КолонкаРеквизита.Имя, "СубконтоКт") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаСубконтоКт"];
						ИначеЕсли КолонкаРеквизита.Имя = "КоличествоДт" 
							ИЛИ КолонкаРеквизита.Имя = "ВалютаДт" 
							ИЛИ КолонкаРеквизита.Имя = "ВалютнаяСуммаДт" Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаКоличествоДт"];
						ИначеЕсли КолонкаРеквизита.Имя = "КоличествоКт" 
							ИЛИ КолонкаРеквизита.Имя = "ВалютаКт" 
							ИЛИ КолонкаРеквизита.Имя = "ВалютнаяСуммаКт" Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаКоличествоКт"];
						ИначеЕсли НЕ РесурсыБИТ.Найти(КолонкаРеквизита.Имя) = Неопределено Тогда	
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "Ресурсы"];
						Иначе	
							ТекРодитель = ТекТаблица;					
						КонецЕсли; 

					ИначеЕсли ЭтоОборотыПоСоответствиямЗаписейТрансляции Тогда
						// колонки, специфичные для регистров бухгалтерии
						Если Найти(КолонкаРеквизита.Имя,"СубконтоИсточникаДт") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаСубконтоИсточникаДт"];
						ИначеЕсли Найти(КолонкаРеквизита.Имя, "СубконтоИсточникаКт") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаСубконтоИсточникаКт"];
						ИначеЕсли Найти(КолонкаРеквизита.Имя,"СубконтоПриемникаДт") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаСубконтоПриемникаДт"];
						ИначеЕсли Найти(КолонкаРеквизита.Имя, "СубконтоПриемникаКт") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаСубконтоПриемникаКт"];	
						ИначеЕсли НЕ РесурсыБИТ.Найти(КолонкаРеквизита.Имя) = Неопределено Тогда	
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "Ресурсы"];
						ИначеЕсли Найти(КолонкаРеквизита.Имя, "НомерСтрокиИсточника") > 0 
							ИЛИ Найти(КолонкаРеквизита.Имя, "НомерСтрокиПриемника") > 0
							ИЛИ Найти(КолонкаРеквизита.Имя, "ВидПроводки") > 0 Тогда
							ТекРодитель = Элементы[СтрокаРегистра.Имя + "ГруппаРеквизиты"];
						Иначе	
							ТекРодитель = ТекТаблица;					
						КонецЕсли; 
					Иначе	
						ТекРодитель = ТекТаблица;
					КонецЕсли; 

					ТекКолонка = Элементы.Добавить(ИмяКолонки, Тип("ПолеФормы"), ТекРодитель);	
					// бит_Финанс изменение кода. Конец.										
				КонецЕсли;
				
				ТекКолонка.ПутьКДанным = ТекТаблица.ПутьКДанным + "." + КолонкаРеквизита.Имя;
				ТекКолонка.Заголовок   = КолонкаРеквизита.Заголовок;
				ТекКолонка.Вид = ВидПоляФормы.ПолеВвода;
				Если КолонкаРеквизита.Имя = "НомерСтроки" Тогда
					ТекКолонка.Ширина = 2;
				ИначеЕсли КолонкаРеквизита.Имя = "ВидДвижения" Тогда
					ТекКолонка.Ширина = 15;
				КонецЕсли;
				Если КолонкаРеквизита.Имя = "ВидДвижения"
					И СтрокаРегистра.ТипРегистра = "РегистрНакопления" 
					И СтрокаРегистра.РегистрОстатков Тогда
					ТекКолонка.УстановитьДействие("ПриИзменении", "Подключаемый_ВидДвиженияПриИзменении");
				КонецЕсли;
				
				Если СтрокаРегистра.Имя = "НДСРаздельныйУчет"
					И КолонкаРеквизита.ТипЗначения = Новый ОписаниеТипов("ПеречислениеСсылка.СпособыУчетаНДС") Тогда
					
					НовыйПараметр = Новый ПараметрВыбора("ОграничениеСпискаВыбора", "НеОграничивать");
					НовыйМассив = Новый Массив();
					НовыйМассив.Добавить(НовыйПараметр);
					НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив); 
					ТекКолонка.ПараметрыВыбора = НовыеПараметры;
					
				КонецЕсли;
				
				// бит_Финанс изменение кода. Начало. 27.06.2012{{
				Если ЭтоРББИТ ИЛИ ЭтоОборотыПоТипамПроводок Тогда
					// группы для полей регистров бухгалтерии
					Если КолонкаРеквизита.Имя = "СчетДт" Тогда
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаСубконтоДт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Субконто Дт";
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаКоличествоДт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Количество Дт";
					КонецЕсли; 
					Если КолонкаРеквизита.Имя = "СчетКт" Тогда
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаСубконтоКт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Субконто Кт";
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаКоличествоКт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Количество Кт";
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"Ресурсы", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Ресурсы";					
					КонецЕсли;
				КонецЕсли;	
				// бит_Финанс изменение кода. Конец. 27.06.2012}}
				
				// бит_Финанс изменение кода. Начало. 15.12.2016{{
				Если ЭтоОборотыПоСоответствиямЗаписейТрансляции Тогда
					// группы для полей регистра "Соответствие записец трансляции"
					Если КолонкаРеквизита.Имя = "СчетИсточникаДт" Тогда
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаСубконтоИсточникаДт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Субконто источника Дт";
					КонецЕсли; 
					Если КолонкаРеквизита.Имя = "СчетИсточникаКт" Тогда
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаСубконтоИсточникаКт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Субконто источника Кт";
						//НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"Ресурсы", Тип("ГруппаФормы"), ТекТаблица);
						//НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						//НоваяГруппа.Заголовок = "Ресурсы";					
					КонецЕсли;

					Если КолонкаРеквизита.Имя = "СчетПриемникаДт" Тогда
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаСубконтоПриемникаДт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Субконто приемника Дт";
					КонецЕсли; 
					Если КолонкаРеквизита.Имя = "СчетПриемникаКт" Тогда
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаСубконтоПриемникаКт", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Субконто приемника Кт";
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"Ресурсы", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Ресурсы";	
						НоваяГруппа = Элементы.Добавить(СтрокаРегистра.Имя+"ГруппаРеквизиты", Тип("ГруппаФормы"), ТекТаблица);
						НоваяГруппа.Вид = ВидГруппыФормы.ГруппаКолонок;
						НоваяГруппа.Заголовок = "Реквизиты";
					КонецЕсли;
					
				КонецЕсли;	
				// бит_Финанс изменение кода. Конец. 15.12.2016}}
				
			КонецЦикла;
			
			// бит_Финанс изменение кода. Начало. 15.12.2014{{
			Если ЭтоРББИТ Тогда
			
				// запрещаем редактировать проводки закрытого периода
				ТекТаблица.УстановитьДействие("ПередНачаломИзменения", "бит_ПередНачаломИзмененияРБ");
				ТекТаблица.УстановитьДействие("ПередУдалением", "бит_ПередУдалениемРБ");
				
				
			КонецЕсли; 
			// бит_Финанс изменение кода. Конец. 15.12.2014}}
			
			УстановитьСвязиПараметровВыбораКолонокСпискаРегистра(СтрокаРегистра.Имя);
			
		ИначеЕсли ТекГруппа <> Неопределено И НЕ СтрокаРегистра.Отображение Тогда
			
			Элементы.Удалить(ТекГруппа);
			СтрокаРегистра.Отрисован = Ложь;
			
		ИначеЕсли ТекГруппа <> Неопределено И СтрокаРегистра.Отображение Тогда
			
			Если СтрокаРегистра.ТипРегистра = "РегистрБухгалтерии" Тогда
				ТекГруппа.Картинка = ?(АктивностьДвижений, 
					БиблиотекаКартинок.ЖурналПроводок, БиблиотекаКартинок.ЖурналПроводокНеактивный);
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрНакопления" Тогда
				ТекГруппа.Картинка = ?(АктивностьДвижений, 
					БиблиотекаКартинок.РегистрНакопления, БиблиотекаКартинок.РегистрНакопленияНеактивный);
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрСведений" Тогда
				ТекГруппа.Картинка = ?(АктивностьДвижений, 
					БиблиотекаКартинок.РегистрСведений, БиблиотекаКартинок.РегистрСведенийНеактивный);
			КонецЕсли;
			
		КонецЕсли;
		
		// бит_Финанс добавление кода. Начало: 18.04.2017 BF-835 {{
		Если Объект.бит_ЕстьРучнаяКорректировкаФакта Тогда
			ГруппаФлажков = ЭтотОбъект.Элементы.Найти("бит_ГруппаРучнаяКорректировка");
			Если ГруппаФлажков = Неопределено Тогда
				ГруппаФлажков 					   = ЭтотОбъект.Элементы.Добавить("бит_ГруппаРучнаяКорректировка", Тип("ГруппаФормы"));
				ГруппаФлажков.Вид 				   = ВидГруппыФормы.ОбычнаяГруппа;
				ГруппаФлажков.Отображение 		   = ОтображениеОбычнойГруппы.Нет;
				ГруппаФлажков.Группировка	 	   = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
				ГруппаФлажков.Подсказка           = "";
				ГруппаФлажков.ОтображатьЗаголовок = Ложь;
				ЭтотОбъект.Элементы.Переместить(ГруппаФлажков,ЭтотОбъект,ЭтотОбъект.Элементы.Найти("ПанельРегистров")); 
				
				ФлагРучнаяКорректировка = ЭтотОбъект.Элементы.Найти("РучнаяКорректировка");
				ЭтотОбъект.Элементы.Переместить(ФлагРучнаяКорректировка, ГруппаФлажков);
			КонецЕсли; 
			Если ЭтотОбъект.Элементы.Найти("бит_РучнаяКорректировкаФакта") = Неопределено  Тогда
				ФлагРедактироватьФакт                    = ЭтотОбъект.Элементы.Добавить("бит_РучнаяКорректировкаФакта", Тип("ПолеФормы"),ГруппаФлажков);
				ФлагРедактироватьФакт.Вид                = ВидПоляФормы.ПолеФлажка;
				ФлагРедактироватьФакт.УстановитьДействие("ПриИзменении", "бит_Подключаемый_РучнаяКорректировкаФактаПриИзменении");
				ФлагРедактироватьФакт.Заголовок 		  = НСтр("ru = 'Редактировать фактические данные'");
				ФлагРедактироватьФакт.ПутьКДанным 		  = "бит_РучнаяКорректировкаФакта";
				ФлагРедактироватьФакт.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
				Если ЭтотОбъект["бит_РучнаяКорректировкаФактаПредупреждение"] Тогда
					ФлагРедактироватьФакт.ОтображениеПодсказки = ОтображениеПодсказки.ОтображатьСнизу;
					ФлагРедактироватьФакт.РасширеннаяПодсказка.Заголовок = "Необходимо снять флаг при отсутсвии движений по регистру накопления ""Обороты по бюджету""";
					ФлагРедактироватьФакт.РасширеннаяПодсказка.ЦветТекста = Новый Цвет(255,0,0);
				КонецЕсли; 
			КонецЕсли; 
		КонецЕсли;	 
		// бит_Финанс добавление кода конец }}  
		
		// бит_Финанс изменение кода. Начало. 07.09.2016{{
		Если ЭтоОборотыПоБюджетам = Истина И НЕ Элементы.Найти("бит_ОборотыПоБюджетам") = Неопределено Тогда
			
			// Скрываем ненужные колонки
			ИменаСкрыть = Новый Массив;
			ИменаСкрыть.Добавить("бит_ОборотыПоБюджетамУдалить_СуммаБезНДСРегл");
		    ИменаСкрыть.Добавить("бит_ОборотыПоБюджетамУдалить_СуммаБезНДССценарий");
			ИменаСкрыть.Добавить("бит_ОборотыПоБюджетамУдалить_СуммаБезНДСУпр");

			Для каждого ИмяСкрыть Из ИменаСкрыть Цикл
			
			    Если НЕ Элементы.Найти(ИмяСкрыть) = Неопределено Тогда
				
					 Элементы[ИмяСкрыть].Видимость = Ложь;
				
				КонецЕсли; 
			
			КонецЦикла; 
									
			// Программное создание функций редактирования
			Если Объект.бит_ЕстьРучнаяКорректировкаФакта Тогда
				Если ЭтотОбъект.Элементы.Найти("бит_РедактироватьОборотыПоБюджетам") = Неопределено Тогда
					КомандаРедактировать = ЭтотОбъект.Команды.Добавить("бит_РедактироватьОборотыПоБюджетам");
					КомандаРедактировать.Заголовок 			   	   =  НСтр("ru = 'Редактировать'");
					КомандаРедактировать.Действие 				   = "бит_Подключаемый_РедактироватьОборотыПоБюджетам";
					КомандаРедактировать.ИзменяетСохраняемыеДанные = Истина;
					
					КнопкаРедактировать = ЭтотОбъект.Элементы.Добавить("бит_РедактироватьОборотыПоБюджетам", Тип("КнопкаФормы"), ТекТаблица.КоманднаяПанель);
					КнопкаРедактировать.ИмяКоманды = "бит_РедактироватьОборотыПоБюджетам"; 
					КнопкаРедактировать.Заголовок  = НСтр("ru = 'Редактировать'");
				КонецЕсли; 
				
				// Подключение обработчика таблицы формы "Выбор".
				ТаблицаФормы = Элементы["бит_ОборотыПоБюджетам"];
				ТаблицаФормы.УстановитьДействие("Выбор", "бит_Подключаемый_ОборотыПоБюджетамВыбор");
			КонецЕсли; 
			
			// Управляем видимостью колонок дополнительных измерений
			Если ЗначениеЗаполнено(ИмяТаблицы) Тогда
				бит_МеханизмДопИзмерений.ОтобразитьДополнительныеИзмеренияВТабличномПоле_Управляемая(ЭтаФорма, ИмяТаблицы);
			КонецЕсли; 
		КонецЕсли; 
		// бит_Финанс изменение кода. Конец. 07.09.2016}}
		// +СБ Кузнецова С. 2016-08-22 Redmine 2528
		Если СтрокаРегистра.Имя = "бит_Дополнительный_2" Тогда
			//ОК+ Аверьянова 16.09.2016 исправления на доработку #2528 - не открываются проводки в доках без МСФО 
			Попытка //ОК-
				
			Элементы.Переместить(Элементы.бит_Дополнительный_2ВидДвиженияМСФО, Элементы.бит_Дополнительный_2ВидДвиженияМСФО.Родитель);
			
			Элементы.бит_Дополнительный_2ГруппаСубконтоДт.ОтображатьВШапке = Истина;
			
			Элементы.бит_Дополнительный_2СубконтоДт1.ОтображатьВШапке = Ложь;
			Элементы.бит_Дополнительный_2СубконтоДт2.ОтображатьВШапке = Ложь;
			Элементы.бит_Дополнительный_2СубконтоДт3.ОтображатьВШапке = Ложь;
			Элементы.бит_Дополнительный_2СубконтоДт4.ОтображатьВШапке = Ложь;
			
			Элементы.Переместить(Элементы.бит_Дополнительный_2ВалютаДт, Элементы.бит_Дополнительный_2ВалютаДт.Родитель);
			Элементы.Переместить(Элементы.бит_Дополнительный_2ВалютнаяСуммаДт, Элементы.бит_Дополнительный_2ВалютнаяСуммаДт.Родитель);
			
			Элементы.бит_Дополнительный_2ГруппаСубконтоКт.ОтображатьВШапке = Истина;
			
			Элементы.бит_Дополнительный_2СубконтоКт1.ОтображатьВШапке = Ложь;
			Элементы.бит_Дополнительный_2СубконтоКт2.ОтображатьВШапке = Ложь;
			Элементы.бит_Дополнительный_2СубконтоКт3.ОтображатьВШапке = Ложь;
			Элементы.бит_Дополнительный_2СубконтоКт4.ОтображатьВШапке = Ложь;
			
			Элементы.Переместить(Элементы.бит_Дополнительный_2ВалютаКт, Элементы.бит_Дополнительный_2ВалютаКт.Родитель);
			Элементы.Переместить(Элементы.бит_Дополнительный_2ВалютнаяСуммаКт, Элементы.бит_Дополнительный_2ВалютнаяСуммаКт.Родитель);
			
			Элементы.Переместить(Элементы.бит_Дополнительный_2СуммаУпр, Элементы.бит_Дополнительный_2СуммаУпр.Родитель);
			Элементы.Переместить(Элементы.бит_Дополнительный_2СуммаРегл, Элементы.бит_Дополнительный_2СуммаРегл.Родитель);
			
			ГруппаСодержание = Элементы.Вставить("бит_Дополнительный_2ГруппаСодержание", Тип("ГруппаФормы"), ТекТаблица, Элементы.бит_Дополнительный_2ВидДвиженияМСФО);
			ГруппаСодержание.Ширина = 5;
			
			Элементы.Переместить(Элементы.бит_Дополнительный_2Содержание, ГруппаСодержание);
			Элементы.Переместить(Элементы.бит_Дополнительный_2НомерЖурнала, ГруппаСодержание);
			Элементы.Переместить(Элементы.бит_Дополнительный_2ВидПроводки, ГруппаСодержание);
			
			Элементы.бит_Дополнительный_2ДатаФормирования.Видимость = Ложь;
			
			Элементы.бит_Дополнительный_2НомерСтроки.Ширина = 2;
			Элементы.бит_Дополнительный_2НомерСтроки.РастягиватьПоГоризонтали = Ложь;
			Элементы.бит_Дополнительный_2Период.Ширина = 10;
			Элементы.бит_Дополнительный_2СчетДт.Ширина = 10;
			Элементы.бит_Дополнительный_2СчетКт.Ширина = 10;
			Элементы.бит_Дополнительный_2ГруппаКоличествоДт.Ширина = 5;
			Элементы.бит_Дополнительный_2ГруппаКоличествоКт.Ширина = 5;
			Элементы.бит_Дополнительный_2Ресурсы.Ширина = 10;
			Элементы.бит_Дополнительный_2ВидДвиженияМСФО.Ширина = 15;
			Элементы.бит_Дополнительный_2Содержание.Ширина = 20;
			Элементы.бит_Дополнительный_2НомерЖурнала.Ширина = 20;
			Элементы.бит_Дополнительный_2ВидПроводки.Ширина = 20;
			//ОК+
			Исключение
			КонецПопытки;
			//ОК- 16.09.2016
		КонецЕсли;
		// -СБ Кузнецова С.
	КонецЦикла;
	
	Элементы.ГруппаХозрасчетный.Видимость = ПоказатьХозрасчетный;
	Элементы.ГруппаКнигаУчетаДоходовИРасходов.Видимость = ПоказатьКнигаУчетаДоходовИРасходов;
	Элементы.ГруппаРасшифровкаКУДиР.Видимость = ПоказатьРасшифровкаКУДиР;
	Элементы.ГруппаЖурналУчетаСчетовФактур.Видимость = ПоказатьЖурналУчетаСчетовФактур;
	Элементы.ГруппаСБ_КорректировкаРСБУ.Видимость = ПоказатьКорректировкаРСБУ;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьСубконто()

	Для Каждого Проводка Из ХозрасчетныйНаборЗаписей Цикл
		
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"СубконтоДт1", "СубконтоДт2", "СубконтоДт3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетДт, Проводка, ПоляОбъекта, Ложь);
		
		ПоляОбъекта = Новый Структура("Субконто1, Субконто2, Субконто3",
		"СубконтоКт1", "СубконтоКт2", "СубконтоКт3");
		БухгалтерскийУчетКлиентСервер.УстановитьДоступностьСубконто(Проводка.СчетКт, Проводка, ПоляОбъекта, Ложь);
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьВозможностьРедактированияЗаписей()
	
	// бит_Финанс добавление кода. Начало: 11.11.2016  {{
	СтраницыФакта = Новый Массив(); 
	Если Объект["бит_ЕстьРучнаяКорректировкаФакта"] Тогда
		СтраницыФакта.Добавить("Группабит_ОборотыПоБюджетам");
		СтраницыФакта.Добавить("Группабит_СоответствиеЗаписейТрансляции");
	КонецЕсли; 
	// бит_Финанс добавление кода конец }}  
	
	Элементы.КнопкаЗаписатьИЗакрыть.Видимость = Объект.ЕстьРучнаяКорректировка;
	Для Каждого СтраницаРегистра Из Элементы.ПанельРегистров.ПодчиненныеЭлементы Цикл
		
		// бит_Финанс добавление кода. Начало: 11.10.2016 # 000000005227 {{
		Если НЕ СтраницыФакта.Найти(СтраницаРегистра.Имя)= Неопределено Тогда
			Продолжить;
		КонецЕсли; 
		// бит_Финанс добавление кода конец }}	
	
		ИмяРегистра = СтрЗаменить(СтраницаРегистра.Имя, "Группа", "");
		
		ВозможностьРедактированияЗаписей = РучнаяКорректировка;
		Если ВозможностьРедактированияЗаписей Тогда
			ОписаниеРегистра = Регистры.НайтиСтроки(Новый Структура("Имя", ИмяРегистра));
			Если ОписаниеРегистра.Количество() <> 0 Тогда
				ВозможностьРедактированияЗаписей = ОписаниеРегистра[0].ПравоНаИзменение;
			КонецЕсли;
		КонецЕсли;
		
		СтраницаРегистра.ТолькоПросмотр = НЕ ВозможностьРедактированияЗаписей;
		ТаблицаРегистра = Элементы[ИмяРегистра];
		ТаблицаРегистра.ИзменятьСоставСтрок  = ВозможностьРедактированияЗаписей;
		ТаблицаРегистра.ИзменятьПорядокСтрок = ВозможностьРедактированияЗаписей;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтображениеВТаблицеРегистров(РегистрыСДвижениями)

	Для каждого СтрокаРегистра Из Регистры Цикл
		
		СтрокаРегистра.ЕстьДвижения = РегистрыСДвижениями.Найти(СтрокаРегистра.Имя) <> Неопределено;
		СтрокаРегистра.Отображение  = СтрокаРегистра.ЕстьДвижения И СтрокаРегистра.ПравоНаПросмотр;

	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура УстановитьСвязиПараметровВыбораКолонокСпискаРегистра(ИмяРегистра)
	
	ЭлементКонтрагент         = Элементы.Найти(ИмяРегистра + "Контрагент");
	ЭлементДоговорКонтрагента = Элементы.Найти(ИмяРегистра + "ДоговорКонтрагента");
	ЭлементПатент             = Элементы.Найти(ИмяРегистра + "Патент");
	
	Если ЭлементДоговорКонтрагента <> Неопределено Тогда
		СвязиПараметровДоговор = Новый Массив;
		СвязиПараметровДоговор.Добавить(Новый СвязьПараметраВыбора("Отбор.Организация", "Объект.Организация"));		
		Если ЭлементКонтрагент <> Неопределено Тогда			
			СвязиПараметровДоговор.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Элементы."+ИмяРегистра+".ТекущиеДанные.Контрагент"));
		КонецЕсли; 
		ЭлементДоговорКонтрагента.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровДоговор);
	КонецЕсли; 

	Если ЭлементПатент <> Неопределено Тогда
		СвязиПараметровПатент = Новый Массив;
		СвязиПараметровПатент.Добавить(Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Организация"));
		ЭлементПатент.СвязиПараметровВыбора = Новый ФиксированныйМассив(СвязиПараметровПатент);
	КонецЕсли; 
	
КонецПроцедуры 

&НаСервере
Процедура СоздатьРеквизитыФормы()

	ИменаРеквизитов = Новый Массив;
	Для каждого Реквизит Из ПолучитьРеквизиты() Цикл
		ИменаРеквизитов.Добавить(Реквизит.Имя);
	КонецЦикла;
	ДобавляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты   = Новый Массив;
	
	Для каждого СтрокаРегистра Из Регистры Цикл
		Если СтрокаРегистра.Имя = "Хозрасчетный"
			ИЛИ СтрокаРегистра.Имя = "КнигаУчетаДоходовИРасходов"
			ИЛИ СтрокаРегистра.Имя = "РасшифровкаКУДиР"
			ИЛИ СтрокаРегистра.Имя = "СБ_КорректировкаРСБУ"
			ИЛИ СтрокаРегистра.Имя = "ЖурналУчетаСчетовФактур" Тогда
			СтрокаРегистра.ЕстьРеквизит = Истина;
			Продолжить;
		КонецЕсли;
		ИмяРеквизита = СтрокаРегистра.Имя + "НаборЗаписей";
		Если (СтрокаРегистра.Отображение ИЛИ СтрокаРегистра.Записывать)
			И ИменаРеквизитов.Найти(ИмяРеквизита) = Неопределено Тогда
			ТипРеквизита  = Новый ОписаниеТипов(СтрокаРегистра.ТипРегистра + "НаборЗаписей." + СтрокаРегистра.Имя);
			НовыйРеквизит = Новый РеквизитФормы(ИмяРеквизита, ТипРеквизита, , , Истина);
			ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
			Если СтрокаРегистра.ТипРегистра = "РегистрНакопления" 
				И СтрокаРегистра.РегистрОстатков Тогда
				КартинкаСтроки = Новый РеквизитФормы("КартинкаСтроки", Новый ОписаниеТипов("Число"), ИмяРеквизита);
				ДобавляемыеРеквизиты.Добавить(КартинкаСтроки);
			КонецЕсли;
		ИначеЕсли НЕ (СтрокаРегистра.Отображение ИЛИ СтрокаРегистра.Записывать)
			И ИменаРеквизитов.Найти(ИмяРеквизита) <> Неопределено Тогда
			УдаляемыеРеквизиты.Добавить(ИмяРеквизита);
		КонецЕсли;
		СтрокаРегистра.ЕстьРеквизит = СтрокаРегистра.Отображение ИЛИ СтрокаРегистра.Записывать;
	КонецЦикла;
	
	Если ДобавляемыеРеквизиты.Количество() > 0 
		ИЛИ УдаляемыеРеквизиты.Количество() > 0 Тогда
		ИзменитьРеквизиты(ДобавляемыеРеквизиты, УдаляемыеРеквизиты);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьДвиженияДокумента(ДокументДвижений)
	
	АктивностьДвижений = Истина;
	
	Для каждого СтрокаРегистра Из Регистры Цикл
		Если СтрокаРегистра.Отображение Тогда
			ИмяРеквизита = СтрокаРегистра.Имя + "НаборЗаписей";
			НаборЗаписей = РеквизитФормыВЗначение(ИмяРеквизита);
			НаборЗаписей.Отбор.Регистратор.Установить(ДокументДвижений);
			// бит_Финанс изменение кода. Начало. 22.12.2014{{
			// НаборЗаписей.Прочитать();
			Попытка
				НаборЗаписей.Прочитать();
			Исключение
				СтрокаРегистра.Отображение = Ложь;
				Продолжить;
			КонецПопытки;
			// бит_Финанс изменение кода. Конец. 22.12.2014}}
			ЗначениеВРеквизитФормы(НаборЗаписей, ИмяРеквизита);
			Если СтрокаРегистра.ТипРегистра = "РегистрНакопления" 
				И СтрокаРегистра.РегистрОстатков Тогда
				Для каждого СтрокаЗаписи Из ЭтотОбъект[ИмяРеквизита] Цикл
					Если СтрокаЗаписи.ВидДвижения = ВидДвиженияНакопления.Приход Тогда
						СтрокаЗаписи.КартинкаСтроки = 2 + ?(СтрокаЗаписи.Активность, 0, 1);
					Иначе
						СтрокаЗаписи.КартинкаСтроки = 4 + ?(СтрокаЗаписи.Активность, 0, 1);
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			СтрокаРегистра.Записывать = ЭтотОбъект[ИмяРеквизита].Количество() > 0;
			Если СтрокаРегистра.Записывать Тогда
				АктивностьДвижений = АктивностьДвижений И ЭтотОбъект[ИмяРеквизита][0].Активность;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьДействияПриИзмененииРучнойКорректировки()
	
	Если РучнаяКорректировка = Объект.РучнаяКорректировка 
		// бит_Финанс изменения кода. Начало {{ 
		И ЭтотОбъект["бит_РучнаяКорректировкаФакта"] = Объект.бит_РучнаяКорректировкаФакта Тогда
		// бит_Финанс изменения кода. Конец }}  
		
		ОбновитьДвиженияНаСервере();
		ЭтаФорма.Модифицированность = Ложь;
	Иначе
		УстановитьВидимостьПриРучнойКорректировке();
		УстановитьВозможностьРедактированияЗаписей();
		// бит_Финанс добавление кода. Начало {{
		бит_УстановитьВозможностьРедактированияЗаписейФакта();
		// бит_Финанс добавление кода конец }}  
		ВключитьАктивностьДвиженийНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьПриРучнойКорректировке()
	
	Если РучнаяКорректировка Тогда
		
		СтрокиСОтображение = Регистры.НайтиСтроки(Новый Структура("Отображение", Истина));
		Если СтрокиСОтображение.Количество() = 0 Тогда
			
			Хозрасчетный = Регистры.НайтиСтроки(Новый Структура("Имя", "Хозрасчетный"));
			Если Хозрасчетный.Количество() > 0 Тогда
				Хозрасчетный[0].Отображение = Истина;
				Элементы.ГруппаХозрасчетный.Видимость = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.ИнформацияОПроведении.Видимость = ЗначениеЗаполнено(ИнформацияОПроведении)
												И РучнаяКорректировка = Ложь;

	МоментДокумента = Новый МоментВремени(Объект.Дата, ДокументДвижений);

	Элементы.ТребуетсяАктуализация.Видимость = НЕ РучнаяКорректировка
		И ИспользуетсяОтложенноеПроведение
		И МоментАктуальностиОтложенныхРасчетов <> Неопределено
		И МоментАктуальностиОтложенныхРасчетов.Сравнить(МоментДокумента) <= 0;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыКлючаЗаписи(ПараметрыЗаписи)
	
	ПараметрыКлючаЗаписи = Новый Структура("КлючЗаписи, ТипРегистра");
	
	ИмяРегистра = ПараметрыЗаписи.ИмяРегистра;
	
	Если НЕ Метаданные.РегистрыБухгалтерии.Найти(ИмяРегистра) = Неопределено Тогда
		
		ПараметрыКлючаЗаписи.ТипРегистра = "РегистрБухгалтерии";

		Если ПараметрыЗаписи.Свойство("Регистратор") И ПараметрыЗаписи.Свойство("НомерСтроки") Тогда
			ЗначениеКлючаЗаписи = Новый Структура;
			ЗначениеКлючаЗаписи.Вставить("Регистратор", ПараметрыЗаписи.Регистратор);
			ЗначениеКлючаЗаписи.Вставить("НомерСтроки", ПараметрыЗаписи.НомерСтроки);
			ПараметрыКлючаЗаписи.КлючЗаписи = РегистрыБухгалтерии[ИмяРегистра].СоздатьКлючЗаписи(ЗначениеКлючаЗаписи);	
		КонецЕсли;
		
	ИначеЕсли НЕ Метаданные.РегистрыНакопления.Найти(ИмяРегистра) = Неопределено Тогда
		
		ПараметрыКлючаЗаписи.ТипРегистра = "РегистрНакопления";
		
		Если ПараметрыЗаписи.Свойство("Регистратор") И ПараметрыЗаписи.Свойство("НомерСтроки") Тогда
			ЗначениеКлючаЗаписи = Новый Структура;
			ЗначениеКлючаЗаписи.Вставить("Регистратор", ПараметрыЗаписи.Регистратор);
			ЗначениеКлючаЗаписи.Вставить("НомерСтроки", ПараметрыЗаписи.НомерСтроки);
			ПараметрыКлючаЗаписи.КлючЗаписи = РегистрыНакопления[ИмяРегистра].СоздатьКлючЗаписи(ЗначениеКлючаЗаписи);	
		КонецЕсли;
		
	ИначеЕсли НЕ Метаданные.РегистрыСведений.Найти(ИмяРегистра) = Неопределено Тогда
		
		ПараметрыКлючаЗаписи.ТипРегистра = "РегистрСведений";
		
		ЗначениеКлючаЗаписи = Новый Структура;
		Если НЕ Метаданные.РегистрыСведений[ИмяРегистра].ПериодичностьРегистраСведений = Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический 
			И ПараметрыЗаписи.Свойство("Период") Тогда
			ЗначениеКлючаЗаписи.Вставить("Период", 		ПараметрыЗаписи.Период); 	
		КонецЕсли;
		Если Метаданные.РегистрыСведений[ИмяРегистра].РежимЗаписи = Метаданные.СвойстваОбъектов.РежимЗаписиРегистра.ПодчинениеРегистратору
			И ПараметрыЗаписи.Свойство("Регистратор") Тогда
			ЗначениеКлючаЗаписи.Вставить("Регистратор", ПараметрыЗаписи.Регистратор); 	
		КонецЕсли;
		Для Каждого ИзмерениеРегистра ИЗ Метаданные.РегистрыСведений[ИмяРегистра].Измерения Цикл
			Если ПараметрыЗаписи.Свойство(ИзмерениеРегистра.Имя) Тогда
				ЗначениеКлючаЗаписи.Вставить(ИзмерениеРегистра.Имя, ПараметрыЗаписи[ИзмерениеРегистра.Имя]); 		
			КонецЕсли;
		КонецЦикла;
		ПараметрыКлючаЗаписи.КлючЗаписи = РегистрыСведений[ИмяРегистра].СоздатьКлючЗаписи(ЗначениеКлючаЗаписи);
		
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ПараметрыКлючаЗаписи;

КонецФункции

&НаКлиенте
Процедура ЗаписатьДвиженияИЗакрыть(ИзмененаРучнаяКорректировка)
	
	Отказ = Ложь;
	ЗаписатьДвижения(Отказ);
	Если НЕ Отказ Тогда
		Если ИзмененаРучнаяКорректировка Тогда
			ОповеститьОбИзменении(ДокументДвижений);
		КонецЕсли;
		ЭтаФорма.Модифицированность = Ложь;
		Оповестить("ВыполненаЗаписьДокумента", Новый Структура("ДокументСсылка", ДокументДвижений));
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросВосстановитьДвиженияЗавершение(Ответ, ИзмененаРучнаяКорректировка) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьДвиженияИЗакрыть(ИзмененаРучнаяКорректировка
		// бит_Финанс изменения кода. Начало: 28.11.2016  {{ 
		ИЛИ ЭтотОбъект["бит_ИзмененаРучнаяКорректировкаФакта"])
		// бит_Финанс изменения кода. Конец 28.11.2016  }} 
	Иначе
		Если ИзмененаРучнаяКорректировка Тогда
			РучнаяКорректировка = Истина;
			УстановитьВидимостьПриРучнойКорректировке();
			УстановитьВозможностьРедактированияЗаписей();
		КонецЕсли; 
		// бит_Финанс добавление кода. Начало: 25.11.2016  {{
		Если ЭтотОбъект["бит_ИзмененаРучнаяКорректировкаФакта"] Тогда
			ЭтотОбъект["бит_РучнаяКорректировкаФакта"] = Истина;
			бит_УстановитьВидимостьПриРучнойКорректировкеФакта();
			бит_УстановитьВозможностьРедактированияЗаписейФакта();
		КонецЕсли;
		// бит_Финанс добавление кода конец }}  
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Отказ = Ложь;
		ЗаписатьДвижения(Отказ);
		Если НЕ Отказ Тогда
			Модифицированность = Ложь;
			Закрыть();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНадписиВПроводке(Проводка)
	
	Проводка.НадписьНУ = НСтр("ru = 'НУ:'");
	Проводка.НадписьПР = НСтр("ru = 'ПР:'");
	Проводка.НадписьВР = НСтр("ru = 'ВР:'");
	Проводка.НадписьКоличествоДт = ?(Проводка.КоличественныйДтДоступность, НСтр("ru = 'Кол.:'"), "");
	Проводка.НадписьКоличествоКт = ?(Проводка.КоличественныйКтДоступность, НСтр("ru = 'Кол.:'"), "");

КонецПроцедуры

&НаСервере
Процедура ВключитьАктивностьДвиженийНаСервере()
	
	Если РучнаяКорректировка И НЕ АктивностьДвижений Тогда
		
		АктивностьДвижений = Истина;
		
		Для каждого СтрокаРегистра Из Регистры Цикл
			Если НЕ СтрокаРегистра.ЕстьРеквизит Тогда
				Продолжить;
			КонецЕсли;
			
			ТекГруппа = Элементы["Группа" + СтрокаРегистра.Имя];
			Если СтрокаРегистра.ТипРегистра = "РегистрБухгалтерии" Тогда
				ТекГруппа.Картинка = БиблиотекаКартинок.ЖурналПроводок;
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрНакопления" Тогда
				ТекГруппа.Картинка = БиблиотекаКартинок.РегистрНакопления;
			ИначеЕсли СтрокаРегистра.ТипРегистра = "РегистрСведений" Тогда
				ТекГруппа.Картинка = БиблиотекаКартинок.РегистрСведений;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкрытьПриНеобходимостиАктуализацииНажатие(Элемент)

	// Пользователь хочет скрыть данные по актуализации, сбрасываем соответствующие признаки.
	МоментАктуальностиОтложенныхРасчетов = Неопределено;
	УстановитьВидимостьЭлементовОжиданияОтложенныеРасчеты();
	
КонецПроцедуры

&НаКлиенте
Процедура Актуализировать(Команда)
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОтложенныеРасчеты)
		И НЕ ЗаданиеВыполнено(ИдентификаторЗаданияОтложенныеРасчеты) Тогда
		// Задание запущено и еще не завершилось, продолжаем ожидание.
		Возврат;
	КонецЕсли;
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеОтложенныхРасчетов");
	
	ЗаданиеВыполнено = ЗапуститьОтложенныеРасчетыНаСервере();
	Если ЗаданиеВыполнено Тогда
		ПоказатьРезультатОтложенногоРасчета();
	Иначе
		ОжидатьВыполнениеОтложенныхРасчетов();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтменитьАктуализацию(Команда)
	
	Если ЗначениеЗаполнено(ИдентификаторЗаданияОтложенныеРасчеты) Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеОтложенныхРасчетов");
		ОтменитьВыполнениеЗадания(ИдентификаторЗаданияОтложенныеРасчеты);
		ИдентификаторЗаданияОтложенныеРасчеты = Неопределено;
	КонецЕсли;
	
	ОпределитьМоментАктуальностиОтложенныхРасчетов();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОтменитьВыполнениеЗадания(Знач ИдентификаторЗадания)

	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);

КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(Знач ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

#Область ОтложенныеРасчеты

&НаКлиенте
Процедура Подключаемый_ПрочитатьСостояниеОтложенныхРасчетов()

	ПрочитатьСостояниеОтложенныхРасчетов();

	Если ЗначениеЗаполнено(ИдентификаторЗаданияОтложенныеРасчеты) Тогда
		ОжидатьВыполнениеОтложенныхРасчетов();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПрочитатьСостояниеОтложенныхРасчетов()

	// Если используется отложенное проведение и проводится актуализация,
	// то поищем, возможно, фоновое задание для документа уже запущено ранее.
	Отбор = Новый Структура();
	Отбор.Вставить("Ключ", 		ДокументДвижений.УникальныйИдентификатор());
	Отбор.Вставить("Состояние", СостояниеФоновогоЗадания.Активно);
	
	МассивЗаданий = ФоновыеЗадания.ПолучитьФоновыеЗадания(Отбор);
	Если МассивЗаданий.Количество() > 0 Тогда
		ИдентификаторЗаданияОтложенныеРасчеты = МассивЗаданий[0].УникальныйИдентификатор;
	КонецЕсли;

	ОпределитьМоментАктуальностиОтложенныхРасчетов();

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ТребуетсяПоказатьОтложенныеРасчеты(Форма)

	Объект = Форма.Объект;

	Если НЕ Форма.ИспользуетсяОтложенноеПроведение 
		ИЛИ НЕ Форма.ДокументПоддерживаетВосстановлениеОтложенныхРасчетов
		ИЛИ Объект.РучнаяКорректировка Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;

КонецФункции

&НаСервере
Процедура ОпределитьМоментАктуальностиОтложенныхРасчетов()

	Если НЕ ТребуетсяПоказатьОтложенныеРасчеты(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;

	// Составим список договоров, которые встречаются в проводках документах,
	// для определения по ним даты актуальности расчетов.
	Договоры = Новый Массив;
	
	ТипДоговорыКонтрагентов = Тип("СправочникСсылка.ДоговорыКонтрагентов");
	
	МассивДтКт = Новый Массив(2);
	МассивДтКт[0] = "Дт";
	МассивДтКт[1] = "Кт";
	
	Для Каждого Проводка Из ХозрасчетныйНаборЗаписей Цикл
	
		Для Каждого ДтКт Из МассивДтКт Цикл
	
			Для НомерСубконто = 1 По 3 Цикл
		
				ЗначениеСубконто = Проводка["Субконто" + ДтКт + НомерСубконто];
			
				Если ЗначениеЗаполнено(ЗначениеСубконто)
					И ТипЗнч(ЗначениеСубконто) = ТипДоговорыКонтрагентов
					И Договоры.Найти(ЗначениеСубконто) = Неопределено Тогда
					Договоры.Добавить(ЗначениеСубконто);
				КонецЕсли;
			
			КонецЦикла;
		
		КонецЦикла;
		
	КонецЦикла;

	Если Договоры.Количество() > 0 Тогда
		МоментАктуальностиОтложенныхРасчетов = УчетВзаиморасчетовОтложенноеПроведение.МоментАктуальностиОтложенныхРасчетов(
			Объект.Организация,
			Объект.Дата,
			,
			Договоры);
			
		ТекстЗаголовка = "";
		Если МоментАктуальностиОтложенныхРасчетов <> Неопределено Тогда
			МассивСтрок = Новый Массив;
			МассивСтрок.Добавить(НСтр("ru = 'Проводки неактуальны с'") + " ");
			МассивСтрок.Добавить(Новый ФорматированнаяСтрока(Формат(МоментАктуальностиОтложенныхРасчетов.Дата, "ДЛФ=Д"), Новый Шрифт( , , Истина)));
			МассивСтрок.Добавить(".");

			ТекстЗаголовка = Новый ФорматированнаяСтрока(МассивСтрок);
		КонецЕсли;
		
		Элементы.ТекстПриНеобходимостиАктуализации.Заголовок = ТекстЗаголовка;
			
	Иначе
		МоментАктуальностиОтложенныхРасчетов = Неопределено;
	КонецЕсли;

	УстановитьВидимостьЭлементовОжиданияОтложенныеРасчеты();
		
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовОжиданияОтложенныеРасчеты()

	МоментДокумента = Новый МоментВремени(Объект.Дата, ДокументДвижений);
	
	ТребуетсяАктуализация = НЕ Объект.РучнаяКорректировка
		И МоментАктуальностиОтложенныхРасчетов <> Неопределено
		И МоментАктуальностиОтложенныхРасчетов.Сравнить(МоментДокумента) <= 0;

	ОжидаетсяВыполнение = ТребуетсяАктуализация И ЗначениеЗаполнено(ИдентификаторЗаданияОтложенныеРасчеты);

	// Пока выполняется отложенные расчеты не разрешаем делать ручную корректировку,
	// чтобы не создавать коллизий.
	Элементы.РучнаяКорректировка.Видимость 		= Объект.ЕстьРучнаяКорректировка И НЕ ОжидаетсяВыполнение;
	Элементы.ТребуетсяАктуализация.Видимость 	= ТребуетсяАктуализация И НЕ ОжидаетсяВыполнение;
	Элементы.ИдетАктуализация.Видимость			= ОжидаетсяВыполнение;

КонецПроцедуры

&НаСервере
Функция ЗапуститьОтложенныеРасчетыНаСервере()

	ЗаданиеВыполнено = Ложь;

	ИдентификаторЗаданияОтложенныеРасчеты = Неопределено;
	АдресХранилищаСОшибками = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
	
	ПараметрыРасчета = УчетВзаиморасчетовОтложенноеПроведение.НовыеПараметрыРасчета();
	ПараметрыРасчета.Организация                = Объект.Организация;
	ПараметрыРасчета.ДатаОкончания              = Объект.Дата;
	ПараметрыРасчета.Документ                   = ДокументДвижений;
	ПараметрыРасчета.АдресХранилищаСОшибками    = АдресХранилищаСОшибками;
	ПараметрыРасчета.СообщатьПрогрессВыполнения = Истина;
	
	РезультатЗадания = УчетВзаиморасчетовОтложенноеПроведение.ЗапуститьОтложенныеРасчетыВФоне(
		УникальныйИдентификатор, 
		ПараметрыРасчета,
		ДокументДвижений.УникальныйИдентификатор());

	АдресХранилищаОтложенныеРасчеты = РезультатЗадания.АдресРезультата;
	ИдентификаторЗаданияОтложенныеРасчеты = РезультатЗадания.ИдентификаторЗадания;
	
	Если РезультатЗадания.Статус <> "Выполняется" Тогда
		ЗаданиеВыполнено = Истина;
		ЗагрузитьДанныеПослеОтложенногоРасчета();
	Иначе
		УстановитьВидимостьЭлементовОжиданияОтложенныеРасчеты();
	КонецЕсли;
	
	Возврат ЗаданиеВыполнено;

КонецФункции

&НаКлиенте
Процедура ОжидатьВыполнениеОтложенныхРасчетов()

	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеОтложенныхРасчетов", 1, Истина);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеОтложенныхРасчетов()

	Попытка
		Если ЗначениеЗаполнено(ИдентификаторЗаданияОтложенныеРасчеты)
			И ЗаданиеВыполнено(ИдентификаторЗаданияОтложенныеРасчеты) Тогда 

			ЗагрузитьДанныеПослеОтложенногоРасчета();
			ПоказатьРезультатОтложенногоРасчета();
		
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеОтложенныхРасчетов", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		ИдентификаторЗаданияОтложенныеРасчеты = Неопределено;
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатОтложенногоРасчета()

	Если ЭтоАдресВременногоХранилища(АдресХранилищаСОшибками) Тогда
		ОбщегоНазначенияБПКлиент.ОткрытьФормуОшибокПерепроведения(ЭтотОбъект, АдресХранилищаСОшибками);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ЗагрузитьДанныеПослеОтложенногоРасчета()
	
	УчетВзаиморасчетовОтложенноеПроведение.ОбработатьРезультатВыполненияАктуализацияРасчетов(ЭтотОбъект);
	
	// Обновляем наборы записей на форме.
	ОбновитьДвиженияНаСервере();
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

// бит_Финанс изменение кода. Начало. 15.12.2014{{
#Область ИзмененияБИТ
	
&НаКлиенте
Процедура бит_ПередНачаломИзмененияРБ(Элемент, Отказ)

	// запрещаем редактировать спец-проводки механизма контроля закрытого периода
	Если Элемент.ТекущиеДанные.Свойство("ВидПроводки") И ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидПроводки) Тогда
	
		 Отказ = Истина;
	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура бит_ПередУдалениемРБ(Элемент, Отказ)

	// запрещаем редактировать спец-проводки механизма контроля закрытого периода
	Если Элемент.ТекущиеДанные.Свойство("ВидПроводки") И  ЗначениеЗаполнено(Элемент.ТекущиеДанные.ВидПроводки) Тогда
		
		 ТекстСообщения =  НСтр("ru = 'Проводки закрытого периода удалять запрещено!'");
		 бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		 Отказ = Истина;
	
	КонецЕсли; 
	
КонецПроцедуры

#Область бит_РедактированиеОборотовПоБюджетам

&НаСервере
Процедура бит_СоздатьРеквизитыФормыНачало()
	
	ИменаРеквизитов = Новый Массив;
	Для каждого Реквизит Из ПолучитьРеквизиты() Цикл
		ИменаРеквизитов.Добавить(Реквизит.Имя);
	КонецЦикла;
	ДобавляемыеРеквизиты = Новый Массив;

	Если ИменаРеквизитов.Найти("бит_РучнаяКорректировкаФакта") = Неопределено Тогда
		НовыйРеквизит = Новый РеквизитФормы("бит_РучнаяКорректировкаФакта", Новый ОписаниеТипов("Булево"), , , Истина);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);		
		
		НовыйРеквизит = Новый РеквизитФормы("бит_ИзмененаРучнаяКорректировкаФакта", Новый ОписаниеТипов("Булево"), , , Истина);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);
		
		НовыйРеквизит = Новый РеквизитФормы("бит_РучнаяКорректировкаФактаПредупреждение", Новый ОписаниеТипов("Булево"), , , Истина);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);		
		
		НовыйРеквизит = Новый РеквизитФормы("бит_АвтоматическийСборФакта", Новый ОписаниеТипов("Булево"), , , Истина);
		ДобавляемыеРеквизиты.Добавить(НовыйРеквизит);		

		ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	КонецЕсли; 
		
КонецПроцедуры

&НаКлиенте
Процедура бит_Подключаемый_ОборотыПоБюджетамВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если ЭтотОбъект["бит_РучнаяКорректировкаФакта"] Тогда
		СтандартнаяОбработка = Ложь;
		бит_ПерейтиККорректировкеФакта();
	КонецЕсли; 

КонецПроцедуры
 
&НаКлиенте 	
Процедура бит_Подключаемый_РедактироватьОборотыПоБюджетам(Команда)
	
	бит_ПерейтиККорректировкеФакта();
		
КонецПроцедуры

&НаКлиенте
Процедура бит_Подключаемый_РучнаяКорректировкаФактаПриИзменении(Элемент)
	
	Если ЭтотОбъект["бит_РучнаяКорректировкаФактаПредупреждение"] Тогда
		ЭтотОбъект["бит_РучнаяКорректировкаФакта"] = Ложь;
	КонецЕсли; 
	
	бит_ВыполнитьДействияПриИзмененииРучнойКорректировкиФакта();
	
КонецПроцедуры 

&НаКлиенте
Процедура бит_ПерейтиККорректировкеФакта()

	ТекущиеДанные = Элементы.бит_ОборотыПоБюджетам.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
	     ТекущаяСтрока = Неопределено;
	Иначе
		 ТекущаяСтрока = ТекущиеДанные.ПолучитьИдентификатор();
	КонецЕсли; 
	 
	АдресХранилища = "";
	бит_ПодготовитьПараметрыРедактированияФакта(АдресХранилища, ТекущаяСтрока);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресХранилища", АдресХранилища);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("бит_РедактироватьОборотыПоБюджетамНаСервереОкончание", ЭтотОбъект);
	РежимОткрытия      = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	ОткрытьФорму("Обработка.бит_КорректировкаФакта.Форма", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект,,,ОписаниеОповещения, РежимОткрытия);

КонецПроцедуры
 
&НаСервере
Процедура бит_ПодготовитьПараметрыРедактированияФакта(АдресХранилища, ТекущаяСтрока)  

	Таблицы = Новый Структура();
	Таблицы.Вставить("ТекущаяСтрока",                ТекущаяСтрока);
	Таблицы.Вставить("Регистратор",                  ДокументДвижений);
	Таблицы.Вставить("ОборотыПоБюджетам",            РеквизитФормыВЗначение("бит_ОборотыПоБюджетамНаборЗаписей").Выгрузить());
	Таблицы.Вставить("СоответствиеЗаписейТрансляции",РеквизитФормыВЗначение("бит_СоответствиеЗаписейТрансляцииНаборЗаписей").Выгрузить());
	
	АдресХранилища = ПоместитьВоВременноеХранилище(Таблицы, УникальныйИдентификатор);

КонецПроцедуры
 
&НаСервере
Процедура бит_РедактироватьОборотыПоБюджетамНаСервереОкончание(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("Успех") И Результат.Успех Тогда
		
		Таблицы = ПолучитьИзВременногоХранилища(Результат.АдресХранилища);

		ЗначениеВРеквизитФормы(Таблицы.СоответствиеЗаписейТрансляции, "бит_СоответствиеЗаписейТрансляцииНаборЗаписей");
		ЗначениеВРеквизитФормы(Таблицы.ОборотыПоБюджетам, 			  "бит_ОборотыПоБюджетамНаборЗаписей");

	КонецЕсли;

КонецПроцедуры // 

&НаСервере
Процедура бит_УстановитьВидимостьПриРучнойКорректировкеФакта()

	КнопкаФормы = ЭтотОбъект.Элементы.Найти("бит_РедактироватьОборотыПоБюджетам");
	Если НЕ КнопкаФормы = Неопределено Тогда
	     КнопкаФормы.Видимость = Объект.бит_ЕстьРучнаяКорректировкаФакта;
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Процедура бит_ЗаполнитьРеквизитыОбъектаЗавершение(МетаданныеДокумента)

	ВозможнаРучнаяКорректировкаФакта = Ложь;
	Если (РольДоступна("ПолныеПрава") ИЛИ РольДоступна("бит_ФинансистПолныеПрава")) И ПолучитьФункциональнуюОпцию("бит_ИспользоватьРучнуюКорректировкуФакта") Тогда		
		мдДвижения   					 = МетаданныеДокумента.Движения;
		ВозможнаРучнаяКорректировкаФакта = мдДвижения.Содержит(Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам);
	КонецЕсли; 
	
	Если ВозможнаРучнаяКорректировкаФакта Тогда		
		// У документов, где "в принципе" не поддеживается ручная правка, свой флаг не ставим
		Если МетаданныеДокумента.Реквизиты.Найти("РучнаяКорректировка") = Неопределено Тогда
			ВозможнаРучнаяКорректировкаФакта = Ложь;
		КонецЕсли; 
	КонецЕсли; 
	
	// Уточнение, выявленное при эксплуатации:
	// - может быть выключена ФО "ИспользоватьРучнуюКорректировкуДвижений".
	РучнаяКорректировкаФакта = бит_МеханизмДопИзмерений.ЕстьРучнаяКорректировкаФакта(ДокументДвижений);
	
	Если ВозможнаРучнаяКорректировкаФакта И НЕ РучнаяКорректировкаФакта Тогда
		// Если пользователи еще не успели ввести корректировки факта - нужно это предотвратить.
		ВозможнаРучнаяКорректировкаФакта = ПолучитьФункциональнуюОпцию("ИспользоватьРучнуюКорректировкуДвижений");
	КонецЕсли; 
	
	ЕстьРеквизитСоответствиеЗаписейТрансляции = Ложь;
	ЕстьСоответствиеЗаписейТрансляции         = Ложь;
	
	Если ВозможнаРучнаяКорректировкаФакта Тогда
		// Если есть записи в "СоответствиеЗаписейТрансляции", связанные с "ОборотыПоБюджетам" можно показать 
		
		ИменаРеквизитов = Новый Массив;
		Для каждого Реквизит Из ПолучитьРеквизиты() Цикл
			ИменаРеквизитов.Добавить(Реквизит.Имя);
		КонецЦикла;
		ВозможнаРучнаяКорректировкаФакта = НЕ ИменаРеквизитов.Найти("бит_СоответствиеЗаписейТрансляцииНаборЗаписей") = Неопределено;
		
	КонецЕсли; 
	
	Если ВозможнаРучнаяКорректировкаФакта Тогда
		// Гарантированно есть реквизит
		СоответствиеЗаписейТрансляции = РеквизитФормыВЗначение("бит_СоответствиеЗаписейТрансляцииНаборЗаписей").Выгрузить();

		Отбор = Новый Структура();
		Отбор.Вставить("Приемник", бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам));

		// Возмоны ситуации, когда в "приемник" пишут несколько "источников".
		// В такой ситуации в таблице "СоответствиеЗаписейТрансляции" повторяются номера строк "приемника". 
		// Это приводит к неопределенности при сортировке строк на "транслируемые" и "зависимые обороты".
		// Такую ситуацию мы не обрабатываем.
		КонтрольнаяТаблица = СоответствиеЗаписейТрансляции.Скопировать(Отбор);
		ЧислоСтрокДляКонтроля = КонтрольнаяТаблица.Количество();
		
		Если ЧислоСтрокДляКонтроля > 0 Тогда
			// Получим различные значения "Приемник и НомерСтрокиПриемника".
			КонтрольнаяТаблица.Свернуть("Приемник, НомерСтрокиПриемника");
			Если КонтрольнаяТаблица.Количество() = ЧислоСтрокДляКонтроля Тогда
				 ВозможнаРучнаяКорректировкаФакта = Истина;
			Иначе
				 ВозможнаРучнаяКорректировкаФакта = Ложь;
			КонецЕсли; 
		Иначе
			ВозможнаРучнаяКорректировкаФакта = Ложь;
		КонецЕсли; 
		
	КонецЕсли; 
		
	РучнаяКорректировкаФакта = бит_МеханизмДопИзмерений.ЕстьРучнаяКорректировкаФакта(ДокументДвижений);

	// Наличие флага имет высокий приоритет перед возможностью вносить корректировки:
	ЭтотОбъект["бит_РучнаяКорректировкаФактаПредупреждение"] = (НЕ ВозможнаРучнаяКорректировкаФакта И РучнаяКорректировкаФакта);
	ВозможнаРучнаяКорректировкаФакта = Макс(ВозможнаРучнаяКорректировкаФакта, РучнаяКорректировкаФакта);	 
	
	ЭтотОбъект["бит_АвтоматическийСборФакта"]  = ?(ВозможнаРучнаяКорректировкаФакта, Ложь, бит_ЭтоАвтоматическийСборФакта());
	Объект.бит_ЕстьРучнаяКорректировкаФакта    = ВозможнаРучнаяКорректировкаФакта;	
	Объект.бит_РучнаяКорректировкаФакта        = РучнаяКорректировкаФакта;
	ЭтотОбъект["бит_РучнаяКорректировкаФакта"] = РучнаяКорректировкаФакта;
	
КонецПроцедуры

&НаСервере
Функция бит_ЭтоАвтоматическийСборФакта()
	
	ДокументыДДС = Новый Массив(); 
	ДокументыДДС.Добавить(Метаданные.Документы.ПоступлениеНаРасчетныйСчет);
	ДокументыДДС.Добавить(Метаданные.Документы.ПриходныйКассовыйОрдер);
	ДокументыДДС.Добавить(Метаданные.Документы.СписаниеСРасчетногоСчета);
	ДокументыДДС.Добавить(Метаданные.Документы.РасходныйКассовыйОрдер);
	ЭтоДокументДДС = НЕ (ДокументыДДС.Найти(ДокументДвижений.Метаданные()) = Неопределено);

	Результат = Ложь;	
	Если ЭтоДокументДДС Тогда
		ДокументДвиженийШапка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументДвижений, "Организация, Дата");
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Организация", ДокументДвиженийШапка.Организация);
		
		НастройкиПрограммы = бит_ОбщегоНазначения.ПолучитьНастройкиПрограммы(ДокументДвиженийШапка.Дата, ,СтруктураПараметров, "Ошибки");
		Результат = НЕ бит_ОбщегоНазначения.ИзвлечьНастройкуПрограммы(НастройкиПрограммы,
							ПланыВидовХарактеристик.бит_НастройкиПрограммы.НеФормироватьДвиженияПоФактуАвтоматически);
	
	КонецЕсли; 
						
	Возврат Результат;	
	
КонецФункции
 
&НаСервере
Процедура бит_ЗаписатьСвойстваДокумента(ЗначениеСвойства)
	
	НаборЗаписей =  РегистрыСведений.бит_РучнаяКорректировкаФакта.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Объект.Установить(ДокументДвижений,Истина);
	
	Если ЗначениеСвойства = Ложь Тогда
		НаборЗаписей.Прочитать();
		Если НЕ НаборЗаписей.Количество() = 0 Тогда
			НаборЗаписей.Очистить();
			НаборЗаписей.Записать();
		КонецЕсли; 
	Иначе	
		Запись = НаборЗаписей.Добавить();
		Запись.Объект = ДокументДвижений;
		Запись.Значение = ЗначениеСвойства;
		НаборЗаписей.Записать();
	КонецЕсли; 
	
КонецПроцедуры

// Управляет видимостью кнопок "Записать и закрыть", "Редактировать ФАКТ", страницей регистра "Обороты по бюджету".
// 
&НаСервере
Процедура бит_УстановитьВозможностьРедактированияЗаписейФакта()
	
	Если НЕ Объект.бит_ЕстьРучнаяКорректировкаФакта Тогда
	    Возврат;	
	КонецЕсли; 
	
	Элементы.КнопкаЗаписатьИЗакрыть.Видимость = Объект.бит_ЕстьРучнаяКорректировкаФакта ИЛИ Объект.ЕстьРучнаяКорректировка;
	КнопкаРедактировать = Элементы.Найти("бит_РедактироватьОборотыПоБюджетам");
	Если НЕ КнопкаРедактировать = Неопределено Тогда
		КнопкаРедактировать.Видимость = ПолучитьФункциональнуюОпцию("бит_ИспользоватьРучнуюКорректировкуФакта") И ЭтотОбъект["бит_РучнаяКорректировкаФакта"];
	КонецЕсли; 
	
	СтраницыФакта = Новый Массив(); 
	СтраницыФакта.Добавить("Группабит_ОборотыПоБюджетам");
	СтраницыФакта.Добавить("Группабит_СоответствиеЗаписейТрансляции");
	
	Для каждого ИмяСтраницыРегистра Из СтраницыФакта Цикл
		СтраницаРегистра = Элементы.ПанельРегистров.ПодчиненныеЭлементы.Найти(ИмяСтраницыРегистра);
		Если НЕ СтраницаРегистра = Неопределено Тогда
			
			Если ЭтотОбъект["бит_РучнаяКорректировкаФакта"] Тогда
				//ВозможностьРедактированияЗаписей = ЭтотОбъект["бит_РучнаяКорректировкаФакта"];
				ВозможностьРедактированияЗаписей = Ложь;
			ИначеЕсли НЕ Объект.бит_ЕстьРучнаяКорректировкаФакта И ЭтотОбъект["бит_АвтоматическийСборФакта"] И РучнаяКорректировка Тогда	
				ВозможностьРедактированияЗаписей = РучнаяКорректировка;
			Иначе
				ВозможностьРедактированияЗаписей = Ложь;
			КонецЕсли; 
			
			// Меняем из спецализированой формы, 
			// если пользователь может осознанно редактировать факт
			СтраницаРегистра.ТолькоПросмотр =  НЕ ВозможностьРедактированияЗаписей;
			ИмяРегистра     = СтрЗаменить(СтраницаРегистра.Имя, "Группа", "");
			ТаблицаРегистра = Элементы[ИмяРегистра];
			ТаблицаРегистра.ИзменятьСоставСтрок  = ВозможностьРедактированияЗаписей;
			ТаблицаРегистра.ИзменятьПорядокСтрок = ВозможностьРедактированияЗаписей;
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры


&НаСервере
Процедура бит_ВыполнитьДействияПриИзмененииРучнойКорректировкиФакта()

	ВыполнитьДействияПриИзмененииРучнойКорректировки();
	
КонецПроцедуры
 
// бит_Финанс Записывает наборы записей, связанные со сбором факта    
// 
&НаСервере 
Процедура бит_ЗаписатьДвиженияСбораФакта(Отказ)
	
	// Вызывается только, когда бит_РучнаяКорректировкаФакта = Истина	
	Попытка
		
		ОбрабатываемыеНаборы = Новый Массив(); 
		ОбрабатываемыеНаборы.Добавить("бит_СоответствиеЗаписейТрансляции");
		ОбрабатываемыеНаборы.Добавить("бит_ОборотыПоБюджетам");
		
		Для каждого СтрокаРегистра Из Регистры Цикл
			Если СтрокаРегистра.ЕстьРеквизит 
				И НЕ ОбрабатываемыеНаборы.Найти(СтрокаРегистра.Имя) = Неопределено Тогда
				ИмяРеквизита = СтрокаРегистра.Имя + "НаборЗаписей";
				НаборЗаписей = РеквизитФормыВЗначение(ИмяРеквизита);
				НаборЗаписей.Отбор.Регистратор.Установить(ДокументДвижений);
				НаборЗаписей.УстановитьАктивность(НЕ Объект.ПометкаУдаления);
				Если НЕ (СтрокаРегистра.ТипРегистра = "РегистрСведений" 
					И (Метаданные.РегистрыСведений[СтрокаРегистра.Имя].ПериодичностьРегистраСведений 
					= Метаданные.СвойстваОбъектов.ПериодичностьРегистраСведений.Непериодический)) Тогда
					Для каждого Запись Из НаборЗаписей Цикл
						Если Запись.Период = '00010101' Тогда
							Запись.Период = Объект.Дата;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
				НаборЗаписей.Записать(Истина);
				ЗначениеВРеквизитФормы(НаборЗаписей,ИмяРеквизита); 
			КонецЕсли;
		КонецЦикла;
		
		Объект.бит_РучнаяКорректировкаФакта = Истина;
	Исключение
		Отказ = Истина;
		ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка,, 
		, 
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;

КонецПроцедуры

&НаСервере
Процедура бит_ЗаписатьДвиженияПереопределяемый(ЗаписатьФакт, Отказ)
	
	ИсточникМета = ДокументДвижений.Метаданные();
	Отказ = бит_ДатыЗапретаРедактирования.ВыполнитьПроверкуДатыЗапретаРедактирования(ИсточникМета, ДокументДвижений);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	НачатьТранзакцию();
	Попытка
		бит_ЗаписатьСвойстваДокумента(ЭтотОбъект["бит_РучнаяКорректировкаФакта"]);
		
		Если ЗаписатьФакт Тогда
			бит_ЗаписатьДвиженияСбораФакта(Отказ);
		КонецЕсли;
	Исключение
		ОтменитьТранзакцию();
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
	ЗафиксироватьТранзакцию();
		
КонецПроцедуры

&НаСервере
Процедура бит_Перетранслировать(Отказ)
	
	Попытка
		ДокОбъект = ДокументДвижений.ПолучитьОбъект();
		// бит_Финанс изменения кода. Начало: 24.04.2017 # BF-921 {{ 
		Если ТипЗнч(ДокументДвижений) = Тип("ДокументСсылка.РегламентнаяОперация") Тогда
			// бит_Финанс добавление кода. Начало: 25.04.2017 # BF-921 {{
			бит_КонтрольЗакрытогоПериода.бит_КонтрольЗакрытогоПериодаПередЗаписьюПередЗаписью(ДокОбъект, Отказ, РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный);
			бит_ОбщегоНазначения.ПередЗаписьюДокументаРучнаяКорректировкаФакта(ДокОбъект, Отказ, РежимЗаписиДокумента.Запись, РежимПроведенияДокумента.Неоперативный);
			// бит_Финанс добавление кода конец }}  

			бит_МеханизмТрансляции.бит_ТрансляцияОперацияПриЗаписиПриЗаписи(ДокОбъект, Отказ);
			бит_КонтрольЗакрытогоПериода.КонтрольПриОбработкеПроведения(ДокОбъект, Ложь, РежимЗаписиДокумента.Проведение, Истина);
		Иначе
			// бит_Финанс добавление кода. Начало: 25.04.2017 # BF-921 {{
			бит_КонтрольЗакрытогоПериода.бит_КонтрольЗакрытогоПериодаПередЗаписьюПередЗаписью(ДокОбъект, Отказ, РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			бит_ОбщегоНазначения.ПередЗаписьюДокументаРучнаяКорректировкаФакта(ДокОбъект, Отказ, РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
			// бит_Финанс добавление кода конец }}  

			бит_МеханизмТрансляции.бит_ОбработкаПроведенияТрансляцияОбработкаПроведения(ДокОбъект, Отказ, РежимПроведенияДокумента.Неоперативный);
			бит_КонтрольЗакрытогоПериода.бит_КонтрольЗакрытогоПериодаОбработкаПроведенияОбработкаПроведения(ДокОбъект, Отказ, РежимПроведенияДокумента.Неоперативный);
		КонецЕсли;
		// бит_Финанс изменения кода. Конец 24.04.2017 # BF-921 }} 
		 бит_ЗаписатьДвижения(ДокОбъект, Отказ);
	Исключение
		ШаблонОшибки = НСтр("ru='Не удалось выполнить трансляцию по причине %1'");
		ТекстОшибки  = СтрШаблон(ШаблонОшибки, ОписаниеОшибки());
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ДокументДвижений);
		
		ТекстОшибки = СтрШаблон(ШаблонОшибки, Строка(ДокументДвижений), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ЗаписьЖурналаРегистрации(ИмяСобытияЖурналаРегистрации(), УровеньЖурналаРегистрации.Ошибка,, ДокументДвижений, 
		ТекстОшибки);
	КонецПопытки;

	//УстановитьДоступностьСубконто();
	
КонецПроцедуры

&НаСервере
Процедура бит_ЗаписатьДвижения(ДокОбъект, Отказ)
	
	Попытка		
		Для каждого НаборЗаписей Из ДокОбъект.Движения Цикл
			Если НаборЗаписей.Записывать Тогда
				НаборЗаписей.Записать(Истина);
			КонецЕсли;
		КонецЦикла;
	Исключение
		ЗаписьЖурналаРегистрации(
		ИмяСобытияЖурналаРегистрации(), 
		УровеньЖурналаРегистрации.Ошибка,, 
		ДокументДвижений, 
		ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		ВызватьИсключение ОписаниеОшибки();
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция бит_ПолучитьПравилоТрансляции()

	Результат = Новый Структура("ФормироватьЗависимыеОбороты, ФормироватьПроводкиБюджетирования", Ложь, Ложь); 
	
	МассивИсточники = Новый Массив;
	МассивИсточники.Добавить(бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыБухгалтерии.Хозрасчетный));
	
	МассивПриеники = Новый Массив(); 
	МассивПриеники.Добавить(бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам));
		
	МетаданныеИсточник = ДокументДвижений.Метаданные();
	
	ОбъектСистемы        = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(МетаданныеИсточник);
	НастройкаРежима      = Перечисления.бит_РежимыТрансляции.OnLine;	
	ТрансляцияНеобходима = Истина;
	
	// Получим правила заполнения полей и правила трансляции.
	РезультатПравила = бит_МеханизмТрансляции.ПолучитьДоступныеПравилаТрансляцииПереопределяемый(
							Объект.Организация, ДокументДвижений,
							Объект.Дата, МассивИсточники, МассивПриеники);
	
	ВыборкаПравилоЗаполнения = РезультатПравила.Выбрать();
	Если ВыборкаПравилоЗаполнения.Следующий() Тогда
	
		Результат.ФормироватьЗависимыеОбороты       = ВыборкаПравилоЗаполнения.ПравилоЗаполнения.ФормироватьЗависимыеОбороты;
		Результат.ФормироватьПроводкиБюджетирования = ВыборкаПравилоЗаполнения.ПравилоЗаполнения.ФормироватьПроводкиБюджетирования;
	
	КонецЕсли; 

	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура бит_ЗаполнитьЗависимыеОбороты()

	// Оставим в наборе записей строки полученные трансляцией
	РНОборотыПоБюджетам = Справочники.бит_ОбъектыСистемы.НайтиПоРеквизиту("ИмяОбъектаПолное", "РегистрНакопления.бит_ОборотыПоБюджетам");
	СоответствиеЗаписей = ЭтотОбъект["бит_СоответствиеЗаписейТрансляцииНаборЗаписей"].Выгрузить();
	ОборотыПоБюджетам   = ЭтотОбъект["бит_ОборотыПоБюджетамНаборЗаписей"].Выгрузить();
	
	ЭтотОбъект["бит_ОборотыПоБюджетамНаборЗаписей"].Очистить();
	
	Отбор = Новый Структура();
	Отбор.Вставить("Приемник", РНОборотыПоБюджетам);
	
	НайденныеСтроки = СоответствиеЗаписей.НайтиСтроки(Отбор);
	Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
		Источник = ОборотыПоБюджетам.Найти(СтрокаТаблицы.НомерСтрокиПриемника,"НомерСтроки");
		Если НЕ Источник = Неопределено Тогда
			
			НоваяСтрока = ЭтотОбъект["бит_ОборотыПоБюджетамНаборЗаписей"].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Источник);

		КонецЕсли; 
	КонецЦикла; 

	бит_Бюджетирование.СформироватьЗависимыеОборотыПоНаборуЗаписей(ДокументДвижений, ЭтотОбъект["бит_ОборотыПоБюджетамНаборЗаписей"]);

КонецПроцедуры // ЗаполнитьЗависимыеОбороты()
 
#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура СБ_КорректировкаРСБУПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.СБ_КорректировкаРСБУ.ТекущиеДанные;
	ИдСтроки      = Элементы.СБ_КорректировкаРСБУ.ТекущаяСтрока;
	
	Если ИдСтроки <> ИдентификаторСтрокиПроводки Тогда
	
		ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
			"КорректировкаРСБУСубконтоДт1", "КорректировкаРСБУСубконтоДт2", "КорректировкаРСБУСубконтоДт3");
		БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетДт, ЭтаФорма, ПоляФормы, Неопределено, Истина, Ложь);
		ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Дт");
	
		ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
			"КорректировкаРСБУСубконтоКт1", "КорректировкаРСБУСубконтоКт2", "КорректировкаРСБУСубконтоКт3");
		БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетКт, ЭтаФорма, ПоляФормы, Неопределено, Истина, Ложь);
		ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Кт");
		
		ИдентификаторСтрокиПроводки = ИдСтроки;
	
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(Форма, ДтКт = "")
	
	ИдСтроки = Форма.Элементы.СБ_КорректировкаРСБУ.ТекущаяСтрока;
	Если ИдСтроки <> Неопределено Тогда
		СтрокаТаблицы = Форма.СБ_КорректировкаРСБУНаборЗаписей.НайтиПоИдентификатору(ИдСтроки);
		Если ДтКт <> "Кт" Тогда
			ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "СчетКт");
			БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "КорректировкаРСБУСубконтоКт%Индекс%", ПараметрыДокумента);
		КонецЕсли;
		Если ДтКт <> "Дт" Тогда
			ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "СчетДт");
			БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
				Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "КорректировкаРСБУСубконтоДт%Индекс%", ПараметрыДокумента);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура КорректировкаРСБУСчетДтПриИзменении(Элемент)
	ОбработатьИзменениеСчетаКорректировкаРСБУ("Дт");	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСчетКтПриИзменении(Элемент)
	ОбработатьИзменениеСчетаКорректировкаРСБУ("Кт");
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеСчетаКорректировкаРСБУ(ДтКт)

	ТекущиеДанные = Элементы.СБ_КорректировкаРСБУ.ТекущиеДанные;
	
	ПоляФормы = Новый Структура("Субконто1,Субконто2,Субконто3");
	ПоляФормы.Субконто1 = "КорректировкаРСБУСубконто" + ДтКт + "1";
	ПоляФормы.Субконто2 = "КорректировкаРСБУСубконто" + ДтКт + "2";
	ПоляФормы.Субконто3 = "КорректировкаРСБУСубконто" + ДтКт + "3";
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные["Счет" + ДтКт], ЭтаФорма, ПоляФормы, Неопределено, Истина, Ложь);
	
	ПоляОбъекта = Новый Структура("Субконто1,Субконто2,Субконто3,Подразделение,Валютный,Количественный,Организация");
	ПоляОбъекта.Субконто1      = "Субконто" + ДтКт + "1";
	ПоляОбъекта.Субконто2      = "Субконто" + ДтКт + "2";
	ПоляОбъекта.Субконто3      = "Субконто" + ДтКт + "3";
	ПоляОбъекта.Подразделение  = "Подразделение" + ДтКт;
	ПоляОбъекта.Валютный       = "Валютный" + ДтКт;
	ПоляОбъекта.Количественный = "Количественный" + ДтКт;
	ПоляОбъекта.Организация    = Объект.Организация;
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(ТекущиеДанные["Счет" + ДтКт], ТекущиеДанные, ПоляОбъекта, Истина,, Ложь,3);
	
	ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, ДтКт);
	
	//ЗаполнитьНадписиВПроводке(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоДт1ПриИзменении(Элемент)
		ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Дт");
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоДт2ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Дт");
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоДт3ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Дт");
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоКт1ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоКт2ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоКт3ПриИзменении(Элемент)
	ИзменитьПараметрыВыбораПолейСубконтоКорректировкаРСБУ(ЭтаФорма, "Кт");
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ(ДтКт, Элемент, СтандартнаяОбработка)

	ТекущиеДанные      = Элементы.СБ_КорректировкаРСБУ.ТекущиеДанные;
	ПараметрыДокумента = ПолучитьСписокПараметров(ЭтаФорма, ТекущиеДанные, "Субконто"+ДтКт+"%Индекс%", "Счет"+ДтКт);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, ПараметрыДокумента);
	Если СтандартнаяОбработка И СокрЛП(Элемент.ОграничениеТипа) = "Договор" Тогда
		 СтандартнаяОбработка = Ложь;
		 Форма = ПолучитьФорму("Справочник.ДоговорыКонтрагентов.ФормаВыбора",,Элемент,,ВариантОткрытияОкна.ОтдельноеОкно);
		 Форма.Список.Отбор.Элементы.Очистить();
		 		 ПараметрыОтбора = Новый Структура;
		 Если ПараметрыДокумента.Свойство("Организация") Тогда
			НовыйЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
			НовыйЭлементОтбора.ПравоеЗначение= ПараметрыДокумента.Организация;
			НовыйЭлементОтбора.Использование = ЗначениеЗаполнено(ПараметрыДокумента.Организация);	
		 КонецЕсли; 
		 Если ПараметрыДокумента.Свойство("Контрагент") Тогда
			НовыйЭлементОтбора = Форма.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			НовыйЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
			НовыйЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Владелец");
			НовыйЭлементОтбора.ПравоеЗначение= ПараметрыДокумента.Контрагент;
			НовыйЭлементОтбора.Использование = ЗначениеЗаполнено(ПараметрыДокумента.Контрагент);	
		 КонецЕсли;
		 
		 Форма.Открыть();
	КонецЕсли; 

КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоДт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ("Дт", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоДт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ("Дт", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоДт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ("Дт", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ("Кт", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ("Кт", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаРСБУСубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбработатьНачалоВыбораСубконтоКорректировкаРСБУ("Кт", Элемент, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура СБ_КорректировкаРСБУПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	// Сначала выполняем общие действия для всех регистров
	Подключаемый_ТаблицаРегистраПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование);
	
	Если НоваяСтрока Тогда
		ЗаполнитьНадписиВПроводке(Элементы.СБ_КорректировкаРСБУ.ТекущиеДанные);
	КонецЕсли;

КонецПроцедуры