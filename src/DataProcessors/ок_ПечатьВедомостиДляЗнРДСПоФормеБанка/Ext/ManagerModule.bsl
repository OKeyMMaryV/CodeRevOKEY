//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-06-04 (#ТП_БП05_ФР07)
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс 

Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-06-10 (#ТП_БП05_ФР07)
	ТаблицаОбъектовСМакетами = ПолучитьМакетыДокументов(МассивОбъектов);
	СтрокиСбербанк = ТаблицаОбъектовСМакетами.НайтиСтроки(Новый Структура("ИмяПечатнойФормы", "Ведомость_Сбербанк"));
	МассивОбъектовСбербанк =  ТаблицаОбъектовСМакетами.скопировать(СтрокиСбербанк).выгрузитьКолонку("Ссылка");
	СтрокиРосбанк = ТаблицаОбъектовСМакетами.НайтиСтроки(Новый Структура("ИмяПечатнойФормы", "Ведомость_Росбанк"));
	МассивОбъектовРосбанк =  ТаблицаОбъектовСМакетами.скопировать(СтрокиРосбанк).выгрузитьКолонку("Ссылка");
	СтрокиРайффайзенБанк = ТаблицаОбъектовСМакетами.НайтиСтроки(Новый Структура("ИмяПечатнойФормы", "Ведомость_Райффайзенбанк"));
	МассивОбъектовРайффайзенБанк =  ТаблицаОбъектовСМакетами.скопировать(СтрокиРайффайзенБанк).выгрузитьКолонку("Ссылка");
	СтрокиБезПечатнойФормы = ТаблицаОбъектовСМакетами.НайтиСтроки(Новый Структура("ИмяПечатнойФормы", "ПечатнаяФормаНеУстановлена"));
	МассивОбъектовБезПечатнойФормы =  ТаблицаОбъектовСМакетами.скопировать(СтрокиБезПечатнойФормы).выгрузитьКолонку("Ссылка");
	
	КоллекцияПечатныхФорм.очистить();
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-06-10 (#ТП_БП05_ФР07)
	
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-06-10 (#ТП_БП05_ФР07)
	//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВедомостьСбербанк") Тогда
	Если МассивОбъектовСбербанк.Количество() Тогда
	НоваяСтрокаКоллекции = КоллекцияПечатныхФорм.Добавить();
	НоваяСтрокаКоллекции.ИмяМакета = "ВедомостьСбербанк";
	НоваяСтрокаКоллекции.ИмяВРег = ВРег("ВедомостьСбербанк");
	НоваяСтрокаКоллекции.Экземпляров = 1;

	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-06-10 (#ТП_БП05_ФР07)
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ВедомостьСбербанк",
		"Ведомость сбербанк",
		ПечатьВедомостьСбербанк(МассивОбъектовСбербанк, ОбъектыПечати),
		,
		"Обработка.ок_ПечатьВедомостиДляЗнРДСПоФормеБанка.ПФ_ВедомостьСбербанк");
	КонецЕсли;
	
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-06-10 (#ТП_БП05_ФР07)
	//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВедомостьРосбанк") Тогда
	Если МассивОбъектовРосбанк.количество() Тогда
	НоваяСтрокаКоллекции = КоллекцияПечатныхФорм.Добавить();
	НоваяСтрокаКоллекции.ИмяМакета = "ВедомостьРосбанк";
	НоваяСтрокаКоллекции.ИмяВРег = ВРег("ВедомостьРосбанк");
	НоваяСтрокаКоллекции.Экземпляров = 1;

	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-06-10 (#ТП_БП05_ФР07)
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ВедомостьРосбанк",
		"Ведомость росбанк",
		ПечатьВедомостьРосбанк(МассивОбъектовРосбанк, ОбъектыПечати),
		,
		"Обработка.ок_ПечатьВедомостиДляЗнРДСПоФормеБанка.ПФ_ВедомостьРосбанк");
	КонецЕсли;
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-06-10 (#ТП_БП05_ФР07)
	//Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ВедомостьРайффайзенБанк") Тогда
	Если МассивОбъектовРайффайзенБанк.количество() Тогда
	НоваяСтрокаКоллекции = КоллекцияПечатныхФорм.Добавить();
	НоваяСтрокаКоллекции.ИмяМакета = "ВедомостьРайффайзенБанк";
	НоваяСтрокаКоллекции.ИмяВРег = ВРег("ВедомостьРайффайзенБанк");
	НоваяСтрокаКоллекции.Экземпляров = 1;

	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-06-10 (#ТП_БП05_ФР07)
		// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ВедомостьРайффайзенБанк",
		"Ведомость райффайзен банк",
		ПечатьВедомостьРайффайзенБанк(МассивОбъектов, ОбъектыПечати),
		,
		"Обработка.ок_ПечатьВедомостиДляЗнРДСПоФормеБанка.ПФ_ВедомостьРайффайзенБанк");
	КонецЕсли;
	
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-06-10 (#ТП_БП05_ФР07)
		Если МассивОбъектовБезПечатнойФормы.количество() Тогда
	НоваяСтрокаКоллекции = КоллекцияПечатныхФорм.Добавить();
	НоваяСтрокаКоллекции.ИмяМакета = "ПечатнаяФормаНеУстановлена";
	НоваяСтрокаКоллекции.ИмяВРег = ВРег("ПечатнаяФормаНеУстановлена");
	НоваяСтрокаКоллекции.Экземпляров = 1;

	// Формируем табличный документ и добавляем его в коллекцию печатных форм.
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм,
		"ПечатнаяФормаНеУстановлена",
		"Печатная форма не установлена",
		Новый ТабличныйДокумент,
		,
		"");
	КонецЕсли;

	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-06-10 (#ТП_БП05_ФР07)
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПечатьВедомостьСбербанк(МассивОбъектов, ОбъектыПечати) Экспорт
	ТабличныйДокумент = Новый ТабличныйДокумент;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫРАЗИТЬ(бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5 КАК Справочник.ФизическиеЛица).ОсновнойБанковскийСчет.НомерСчета КАК НомерСчета,
	|	ВЫРАЗИТЬ(бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5 КАК Справочник.ФизическиеЛица).Фамилия КАК Фамилия,
	|	ВЫРАЗИТЬ(бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5 КАК Справочник.ФизическиеЛица).Имя КАК Имя,
	|	ВЫРАЗИТЬ(бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5 КАК Справочник.ФизическиеЛица).Отчество КАК Отчество,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Сумма КАК Сумма,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.бит_ЗаявкаНаРасходованиеСредств.Распределение КАК бит_ЗаявкаНаРасходованиеСредствРаспределение
	|ГДЕ
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Ссылка В(&МассивОбъектов)
	|ИТОГИ ПО
	|	Ссылка";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаСсылка = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ПервыйДокумент   = Истина;
		
		Пока ВыборкаСсылка.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			
			Макет = ПолучитьМакет("ПФ_ВедомостьСбербанк");
			СтрокаТЧ = Макет.ПолучитьОбласть("СтрокаТЧ");
			
			
			ВыборкаДетальныеЗаписи = ВыборкаСсылка.Выбрать();
			
			Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
				СтрокаТЧ.Параметры.Заполнить(ВыборкаДетальныеЗаписи);
				ТабличныйДокумент.Вывести(СтрокаТЧ);
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьВедомостьРосбанк(МассивОбъектов, ОбъектыПечати) Экспорт
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_ЗаявкаНаРасходованиеСредств.Ссылка КАК Ссылка,
	|	бит_ЗаявкаНаРасходованиеСредств.Дата КАК ДатаДокумента,
	|	бит_ПлатежнаяПозиция.БанковскийСчетОрганизация.НомерСчета КАК СчетПлательщика,
	|	бит_ДополнительныеАналитики.ЗначениеАналитики КАК НомерВедомости,
	|	бит_ЗаявкаНаРасходованиеСредств.Организация КАК Организация
	|ИЗ
	|	Документ.бит_ЗаявкаНаРасходованиеСредств КАК бит_ЗаявкаНаРасходованиеСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.бит_ПлатежнаяПозиция КАК бит_ПлатежнаяПозиция
	|		ПО бит_ЗаявкаНаРасходованиеСредств.Ссылка = бит_ПлатежнаяПозиция.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ДополнительныеАналитики КАК бит_ДополнительныеАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.бит_ВидыДополнительныхАналитик КАК бит_ВидыДополнительныхАналитик
	|			ПО бит_ДополнительныеАналитики.Аналитика = бит_ВидыДополнительныхАналитик.Ссылка
	|				И (бит_ВидыДополнительныхАналитик.Код = ""Номер_ведомости"")
	|		ПО бит_ЗаявкаНаРасходованиеСредств.Ссылка = бит_ДополнительныеАналитики.Объект
	|ГДЕ
	|	бит_ЗаявкаНаРасходованиеСредств.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаШапка = РезультатЗапроса.Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Ссылка КАК Документ,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5 КАК ФизическоеЛицо,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5.ОсновнойБанковскийСчет.НомерСчета КАК НомерСчета,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Сумма КАК Сумма
	|ИЗ
	|	Документ.бит_ЗаявкаНаРасходованиеСредств.Распределение КАК бит_ЗаявкаНаРасходованиеСредствРаспределение
	|ГДЕ
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Ссылка В(&МассивОбъектов)";
	
	ВыборкаТЧ = Запрос.Выполнить().Выбрать();
	
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ПервыйДокумент   = Истина;
		
		Пока ВыборкаШапка.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			Макет = ПолучитьМакет("ПФ_ВедомостьРосбанк");
			
			Шапка = Макет.ПолучитьОбласть("Шапка");
			СтрокаТЧ = Макет.ПолучитьОбласть("СтрокаТЧ");
						
			Шапка.Параметры.Заполнить(ВыборкаШапка);
			ТабличныйДокумент.Вывести(Шапка);
			
			ВыборкаТЧ.Сбросить();
			ИтоговаяСумма = 0;
			Пока ВыборкаТЧ.НайтиСледующий(Новый Структура("Документ", ВыборкаШапка.Ссылка)) Цикл
				СтрокаТЧ.Параметры.Заполнить(ВыборкаТЧ);
				ИтоговаяСумма = ИтоговаяСумма + ВыборкаТЧ.Сумма;
				ТабличныйДокумент.Вывести(СтрокаТЧ);				
			КонецЦикла;
			Итого = Макет.ПолучитьОбласть("Итого");
			Итого.Параметры.ИтоговаяСумма = ИтоговаяСумма;
			Итого.Параметры.Заполнить(ВыборкаШапка);
			ТабличныйДокумент.Вывести(Итого);
			Подвал = Макет.ПолучитьОбласть("Подвал");
			ТабличныйДокумент.Вывести(Подвал);
		КонецЦикла;
	КонецЕсли; 	
	
	Возврат ТабличныйДокумент;
	
КонецФункции

Функция ПечатьВедомостьРайффайзенБанк(МассивОбъектов, ОбъектыПечати) Экспорт
	
	ТабличныйДокумент = Новый ТабличныйДокумент;	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_ЗаявкаНаРасходованиеСредств.Ссылка КАК Ссылка,
	|	бит_ЗаявкаНаРасходованиеСредств.Дата КАК ДатаДокумента,
	|	бит_ПлатежнаяПозиция.БанковскийСчетОрганизация.НомерСчета КАК СчетПлательщика,
	|	бит_ДополнительныеАналитики.ЗначениеАналитики КАК НомерВедомости
	|ИЗ
	|	Документ.бит_ЗаявкаНаРасходованиеСредств КАК бит_ЗаявкаНаРасходованиеСредств
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.бит_ПлатежнаяПозиция КАК бит_ПлатежнаяПозиция
	|		ПО бит_ЗаявкаНаРасходованиеСредств.Ссылка = бит_ПлатежнаяПозиция.ДокументОснование
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ДополнительныеАналитики КАК бит_ДополнительныеАналитики
	|			ЛЕВОЕ СОЕДИНЕНИЕ ПланВидовХарактеристик.бит_ВидыДополнительныхАналитик КАК бит_ВидыДополнительныхАналитик
	|			ПО бит_ДополнительныеАналитики.Аналитика = бит_ВидыДополнительныхАналитик.Ссылка
	|				И (бит_ВидыДополнительныхАналитик.Код = ""Номер_ведомости"")
	|		ПО бит_ЗаявкаНаРасходованиеСредств.Ссылка = бит_ДополнительныеАналитики.Объект
	|ГДЕ
	|	бит_ЗаявкаНаРасходованиеСредств.Ссылка В(&МассивОбъектов)";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаШапка = РезультатЗапроса.Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Ссылка КАК Документ,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5 КАК ФизическоеЛицо,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5.ОсновнойБанковскийСчет.НомерСчета КАК НомерСчета,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Сумма КАК Сумма,
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Аналитика_5.ДатаРождения КАК ДатаРождения
	|ИЗ
	|	Документ.бит_ЗаявкаНаРасходованиеСредств.Распределение КАК бит_ЗаявкаНаРасходованиеСредствРаспределение
	|ГДЕ
	|	бит_ЗаявкаНаРасходованиеСредствРаспределение.Ссылка В(&МассивОбъектов)";
	
	ВыборкаТЧ = Запрос.Выполнить().Выбрать();
	
	
	Если Не РезультатЗапроса.Пустой() Тогда
		ПервыйДокумент   = Истина;
		
		Пока ВыборкаШапка.Следующий() Цикл
			
			Если Не ПервыйДокумент Тогда
				ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
			КонецЕсли;
			
			ПервыйДокумент = Ложь;
			Макет = ПолучитьМакет("ПФ_ВедомостьРайффайзенБанк");
			
			Шапка = Макет.ПолучитьОбласть("Шапка");
			СтрокаТЧ = Макет.ПолучитьОбласть("СтрокаТЧ");
						
			Шапка.Параметры.Заполнить(ВыборкаШапка);
			Шапка.Параметры.ДатаДокумента = Формат(Шапка.Параметры.ДатаДокумента,"ДФ=dd.MM.yyyy");

			ТабличныйДокумент.Вывести(Шапка);
			
			ВыборкаТЧ.Сбросить();
			ИтоговаяСумма = 0;
			КоличествоСтрок = 0;
			Пока ВыборкаТЧ.НайтиСледующий(Новый Структура("Документ", ВыборкаШапка.Ссылка)) Цикл
				СтрокаТЧ.Параметры.Заполнить(ВыборкаТЧ);
				ИтоговаяСумма = ИтоговаяСумма + ВыборкаТЧ.Сумма;
				КоличествоСтрок = КоличествоСтрок + 1;
				ТабличныйДокумент.Вывести(СтрокаТЧ);				
			КонецЦикла;
			
			Итого = Макет.ПолучитьОбласть("Итого");
			Итого.Параметры.ИтоговаяСумма = ИтоговаяСумма;
			Итого.Параметры.КоличествоСтрок = КоличествоСтрок;
			ТабличныйДокумент.Вывести(Итого);
			
			Подвал = Макет.ПолучитьОбласть("Подвал");
			ТабличныйДокумент.Вывести(Подвал);
		КонецЦикла;
	КонецЕсли; 	
	
	Возврат ТабличныйДокумент;
	
КонецФункции  

//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-06-10 (#ТП_БП05_ФР07)
Функция ПолучитьМакетыДокументов(МассивОбъектов)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_ЗаявкаНаРасходованиеСредств.Ссылка КАК Ссылка,
	               |	ЕСТЬNULL(ДополнительныеСведения.Значение, ""ПечатнаяФормаНеУстановлена"") КАК ИмяПечатнойФормы
	               |ИЗ
	               |	Документ.бит_ЗаявкаНаРасходованиеСредств КАК бит_ЗаявкаНаРасходованиеСредств
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ДополнительныеСведения КАК ДополнительныеСведения
	               |		ПО бит_ЗаявкаНаРасходованиеСредств.СБ_БанковскийСчетКонтрагента = ДополнительныеСведения.Объект
	               |			И (ДополнительныеСведения.Свойство = &ИмяПечатнойФормы)
	               |ГДЕ
	               |	бит_ЗаявкаНаРасходованиеСредств.Ссылка В(&МассивОбъектов)";
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ИмяПечатнойФормы", ПланыВидовХарактеристик.ДополнительныеРеквизитыИСведения.НайтиПоРеквизиту("Имя", "ок_ИмяПечатнойФормы"));
	Возврат Запрос.Выполнить().Выгрузить();
конецфункции
//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-06-10 (#ТП_БП05_ФР07)

#КонецОбласти

#КонецЕсли
//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-06-04 (#ТП_БП05_ФР07)