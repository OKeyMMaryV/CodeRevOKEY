
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события "ПриСозданииНаСервере" формы.
// 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("Режим", Объект.Режим);
	
	ИмяДополнительногоПоля = "ДополнительноеПоле1";
	ИмяПоляПриемника       = "СтатьяОборотов";
	
	ШаблонВыражения = 
	"// добавление нового движения в приемник
	|Если ЗначениеЗаполнено(ДополнительныеДанные[""%ИмяДополнительногоПоля%""]) И НЕ Отказ  Тогда
	|	
	|	НоваяЗапись = НаборПриемник.Добавить();
	|	ЗаполнитьЗначенияСвойств(НоваяЗапись, ЗаписьПриемник, , ""ВидДвижения"");
	|	НоваяЗапись[""%ИмяПоляПриемника%""] = ДополнительныеДанные[""%ИмяДополнительногоПоля%""];
	|
	|	Если НЕ НоваяЗапись = Неопределено Тогда
	|		ЗаполнитьЗаписьСоответствияДвижений(НаборСоответствие, СтруктураШапкиДокумента, СтруктураПараметров, Ном, НомПриемник);
	|		НомПриемник = НомПриемник + 1;
	|	КонецЕсли;   
	|КонецЕсли;
	|";
					  
КонецПроцедуры
 
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОбновитьВыражениеРезультат();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИмяДополнительногоПоляПриИзменении(Элемент)
	
	ОбновитьВыражениеРезультат();
	
КонецПроцедуры

&НаКлиенте
Процедура ИмяПоляПриемникаПриИзменении(Элемент)
	
	ОбновитьВыражениеРезультат();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - действие команды "КомандаОК".
// 
&НаКлиенте
Процедура КомандаОК(Команда)
	
	ОбновитьВыражениеРезультат();
	Закрыть(ВыражениеРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция выполняет обновление результирующего выражения.
// 
&НаКлиенте
Процедура ОбновитьВыражениеРезультат()

	ВыражениеРезультат = ШаблонВыражения;
	ВыражениеРезультат = СтрЗаменить(ВыражениеРезультат, "%ИмяДополнительногоПоля%", ИмяДополнительногоПоля);
	ВыражениеРезультат = СтрЗаменить(ВыражениеРезультат, "%ИмяПоляПриемника%"      , ИмяПоляПриемника);
	
КонецПроцедуры // ОбновитьВыражениеРезультат()

#КонецОбласти


