
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
	МетаданныеОбъекта = Метаданные.Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений;
	
	// Вызов механизма защиты
	
	
	Если фОтказ Тогда
		Возврат;
	КонецЕсли;
	
	ВидОперацииИзменитьСтатус = Перечисления.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.ИзменитьСтатус;
	
	// Заполним вид операции
	Если Параметры.Свойство("ВидОперации") Тогда
	
		Объект.ВидОперации = Параметры.ВидОперации;
		
	КонецЕсли; 	
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидОперации) Тогда
	
		Объект.ВидОперации = Перечисления.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.ОтправитьОповещения;
	
	КонецЕсли; 
	
	УстановитьВидимость();
	
	// Инициализируем настройки компоновщика
	АдресСхемыКомпоновкиДанных = Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.ИнициализироватьКомпоновщик(Объект.Компоновщик,УникальныйИдентификатор);
	Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.УстановитьОтборыКомпоновщика(Объект.Компоновщик,Объект.ВидОперации);		
	
	Элементы.КомпоновщикПользовательскиеНастройки.РежимОтображения = РежимОтображенияНастроекКомпоновкиДанных.БыстрыйДоступ;	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если фОтказ Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ИзменениеВидаОперации();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды формы "ВыполнитьДействие".
// 
&НаКлиенте
Процедура ВыполнитьДействие(Команда)
	
	// Проверим заполнение обязательных реквизитов.
	флНетОшибок = ПроверитьЗаполнение();
	
	Если Объект.ВидОперации = ВидОперацииИзменитьСтатус И НЕ ЗначениеЗаполнено(Объект.Статус) Тогда
	
		ТекстСообщения = НСтр("ru = 'Поле ""%1%"" не заполнено!'");
		ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения,НСтр("ru = 'Статус'"));
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения,Объект,"Объект.Статус");
		флНетОшибок = Ложь;
		
	КонецЕсли; 
	
	Если флНетОшибок Тогда
		
		// Выполним требуемые действия
		ОбработатьЗаписиРегистраОповещений();
		ОповеститьОбИзменении(Тип("РегистрСведенийКлючЗаписи.бит_фн_Оповещения"));

	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура обрабатывает изменение вида операции.
// 
&НаСервере
Процедура ИзменениеВидаОперации()

	Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.УстановитьОтборыКомпоновщика(Объект.Компоновщик,Объект.ВидОперации);	

	УстановитьВидимость();
	
КонецПроцедуры // ИзменениеВидаОперации()

// Процедура выполняет действия с записями регистра сведений бит_фн_Оповещения.
// 
// Параметры:
//  Компоновщие                 - КомпоновщикНастроекСхемыКомпоновкиДанных.
//  АдресСхемыКомпоновкиДанных  - Строка.
//  ВидОперации                 - ПеречислениеСсылка.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.
// 
&НаСервере
Процедура ОбработатьЗаписиРегистраОповещений()
	
	Результат = Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.ВыполнитьЗапросПоНастройке(Объект.Компоновщик
	                                                                                           ,АдресСхемыКомпоновкиДанных
																							   ,Объект.ВидОперации);
																							   
	Если Объект.ВидОперации = Перечисления.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.ОтправитьОповещения Тогда
		
		Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.ОтправитьОповещения(Результат,"Все");
		
	ИначеЕсли Объект.ВидОперации = Перечисления.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.Удалить Тогда	
		
		Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.УдалитьОповещения(Результат,"Все");		
		
	ИначеЕсли Объект.ВидОперации = Перечисления.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.ИзменитьСтатус Тогда	
		
		Обработки.бит_фн_ОбработкаЗаписейРегистраОповещений.ИзменитьСтатусОповещений(Результат,Объект.Статус,"Все");				
		
	КонецЕсли; 																						   
	
КонецПроцедуры // ОбработатьЗаписиРегистраОповещений()

// Процедура устанавливает видимость/доступность элементов формы.
// 
&НаСервере
Процедура УстановитьВидимость()

	ЭтоИзменениеСтатуса = ?(Объект.ВидОперации = Перечисления.бит_фн_ВидыОперацийОбработкиЗаписейРегистраОповещений.ИзменитьСтатус,Истина,Ложь);
	
	Элементы.Статус.Видимость = ЭтоИзменениеСтатуса;

КонецПроцедуры // УстановитьВидимость()

#КонецОбласти
