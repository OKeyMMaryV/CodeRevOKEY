////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура производит загрузку данных в регистр сведений "бит_му_ДополнительныеСтавки"
// на основании данных, полученных с помощью объекта WS-ссылка "СБ_РегулярныеДанныеЦБРФ"
//
Процедура ЗагрузкаДанных(ВидСтавки, НачалоПериода, КонецПериода) Экспорт
	
	ПроксиСервер = WSСсылки.СБ_РегулярныеДанныеЦБРФ.СоздатьWSПрокси("http://web.cbr.ru/", "SecInfo", "SecInfoSoap");
	ТипWSПараметра = ПроксиСервер.ФабрикаXDTO.Пакеты.Получить("http://web.cbr.ru/").Получить("MosPrime");
	
	Дни = (КонецПериода - НачалоПериода) / 86400 + 1;
	
	Для День = 1 По Дни Цикл
		
		ТекущийДень = НачалоПериода + (День - 1) * 86400;
		
		Набор = РегистрыСведений.бит_му_ДополнительныеСтавки.СоздатьНаборЗаписей();
		Набор.Отбор.ВидСтавки.Установить(ВидСтавки);
		Набор.Отбор.Период.Установить(ТекущийДень);
		
		WSПараметр	   = ПроксиСервер.ФабрикаXDTO.Создать(ТипWSПараметра);
		WSПараметр.DateFrom	= ТекущийДень;
		WSПараметр.DateTo	= ТекущийДень;
		
		Попытка
			ЗначенияСтавки = ПроксиСервер.MosPrime(WSПараметр).MosPrimeResult.diffgram.MosPrime.MP;
		Исключение
			Сообщить("Данные для загрузки на дату " + Формат(ТекущийДень, "ДФ=dd.MM.yyyy") + " не обнаружены!");
			Продолжить;
		КонецПопытки;
		
		Запись = Набор.Добавить();
		Запись.Период 		= ТекущийДень;
		Запись.ВидСтавки 	= ВидСтавки;
		
		Если ВидСтавки = Справочники.бит_му_ВидыДополнительныхСтавок.СБ_MosPrimeRateOneMonth Тогда
			Запись.Ставка = ЗначенияСтавки.T1M;
		ИначеЕсли ВидСтавки = Справочники.бит_му_ВидыДополнительныхСтавок.СБ_MosPrimeRateThreeMonths Тогда
			Запись.Ставка = ЗначенияСтавки.T3M;
		КонецЕсли;
		
		Набор.Записать();
		
	КонецЦикла;
	
	
КонецПроцедуры // ЗагрузкаДанных()

