
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Настройки = ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекЗагрузить("ПодборОС", "");
	Если Настройки <> Неопределено Тогда
		Если Настройки.Свойство("ИсторияПоискаОС") Тогда
			Элементы.СтрокаПоиска.СписокВыбора.ЗагрузитьЗначения(Настройки.ИсторияПоискаОС);
		КонецЕсли;
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры.Заголовок) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.Заголовок;
	КонецЕсли;

	Если ЭтоАдресВременногоХранилища(Параметры.АдресОСВХранилище) Тогда
		ОС = ПолучитьИзВременногоХранилища(Параметры.АдресОСВХранилище);
		Объект.Корзина.Загрузить(ОС);
	КонецЕсли;
	
	Контрагент = Параметры.Контрагент;
	
	//ОКЕЙ Иванов А.Е.(СофтЛаб) Начало 2018-04-25 (#3010)
	Если Параметры.Свойство("МестонахождениеОС") 
		И ЗначениеЗаполнено(Параметры.МестонахождениеОС) Тогда
		Местонахождение = Параметры.МестонахождениеОС;
	КонецЕсли;	
	//ОКЕЙ Иванов А.Е.(СофтЛаб) Начало 2018-04-25 (#3010)
	
	СформироватьИнформационнуюНадпись(ЭтаФорма);

	ТекущаяГруппаОС = Справочники.ОсновныеСредства.ПустаяСсылка();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьОтборПоКонтрагенту(Контрагент);
	
	//ОКЕЙ Иванов А.Е.(СофтЛаб) Начало 2018-04-25 (#3010)
	Если ЗначениеЗаполнено(Местонахождение) Тогда
		УстановитьОтборПоМестонахождению(Местонахождение);
	КонецЕсли;	
	//ОКЕЙ Иванов А.Е.(СофтЛаб) Начало 2018-04-25 (#3010)
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)

	Если ЗавершениеРаботы И ДобавленыНовыеОС Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	Если НЕ ПеренестиВДокумент И ДобавленыНовыеОС Тогда

		Отказ = Истина;
		Оповещение = Новый ОписаниеОповещения("ВопросПеренестиВДокументЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Подобранные основные средства не перенесены в документ.
			|
			|Перенести?'"), РежимДиалогаВопрос.ДаНетОтмена);

	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)

	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;

	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("ИсторияПоискаОС", Элементы.СтрокаПоиска.СписокВыбора.ВыгрузитьЗначения());

	ОбщегоНазначенияВызовСервера.ХранилищеОбщихНастроекСохранить("ПодборОС", "", ПараметрыЗакрытия);

	Если ПеренестиВДокумент Тогда
		АдресОСВХранилище = ПоместитьОСВХранилище();
		Структура = Новый Структура("АдресОСВХранилище", АдресОСВХранилище);
		ПеренестиВДокумент = Истина;
		ОповеститьОВыборе(Структура);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаОчистка(Элемент, СтандартнаяОбработка)

	ПрименитьПоиск();

КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)

	ПрименитьПоиск();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыИерархияОС

&НаКлиенте
Процедура ИерархияОСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	УстановитьОтборПоИерархииОС(ВыбраннаяСтрока);

КонецПроцедуры

&НаКлиенте
Процедура ИерархияОСПриАктивизацииСтроки(Элемент)

	ТекущаяСтрока = Элементы.ИерархияОС.ТекущаяСтрока;
	Если ТекущаяСтрока <> ТекущаяГруппаОС Тогда
		ТекущаяГруппаОС = ТекущаяСтрока;
		УстановитьОтборПоИерархииОС(ТекущаяСтрока);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодборТаблицаОС

&НаКлиенте
Процедура ПодборТаблицаОСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-01-15 (#2950)
	//Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ЭтоГруппа Тогда
	//	СтруктураПараметрыОС = Новый Структура;
	//	СтруктураПараметрыОС.Вставить("ОсновноеСредство", Элемент.ТекущиеДанные.Ссылка);
	//	ДобавитьВКорзину(СтруктураПараметрыОС, 1);
	//КонецЕсли;
	ДобавитьВыбранныеОС(ВыбраннаяСтрока);
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-01-15 (#2950)

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКорзина

&НаКлиенте
Процедура КорзинаПриИзменении(Элемент)

	ЭтаФорма.Модифицированность = Истина;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)

	ПеренестиВДокумент = Истина;
	Закрыть(КодВозвратаДиалога.OK);

КонецПроцедуры

//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-01-15 (#2950)
&НаКлиенте
Процедура ок_ВыбратьВыделенныеСтроки(Команда)
	
	пВыделенныеСтроки = Элементы.ПодборТаблицаОС.ВыделенныеСтроки;
	
	Если пВыделенныеСтроки.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;                      
	
	ДобавитьВыбранныеОС(пВыделенныеСтроки);
	
КонецПроцедуры
//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-01-15 (#2950)

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьИнформационнуюНадпись(Форма)

	ШаблонНадписи = НСтр("ru = 'Всего подобрано %1 позиции(ия)'");
	Форма.ИнформационнаяНадпись = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонНадписи, Форма.Объект.Корзина.Количество());

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоИерархииОС(ГруппаОС)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ПодборТаблицаОС, "Ссылка",
		ГруппаОС, ЗначениеЗаполнено(ГруппаОС), ВидСравненияКомпоновкиДанных.ВИерархии);

КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоКонтрагенту(Контрагент)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ПодборТаблицаОС, "Контрагент",
		Контрагент, ЗначениеЗаполнено(Контрагент), ВидСравненияКомпоновкиДанных.Равно);

КонецПроцедуры

&НаКлиенте
Процедура ПрименитьПоиск()

	Использование = ЗначениеЗаполнено(СтрокаПоиска);

	ГруппаОтбора = ОтборыСписковКлиентСервер.СоздатьГруппуЭлементовОтбора(
		ЭтаФорма.ПодборТаблицаОС.Отбор.Элементы, "ПоискПоПодстроке",
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "Наименование", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "НаименованиеПолное", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораГруппыСписка(
		ГруппаОтбора, "Код", СтрокаПоиска,
		Использование, ВидСравненияКомпоновкиДанных.Содержит);

	СпискиВыбораКлиентСервер.ОбновитьСписокВыбора(Элементы.СтрокаПоиска.СписокВыбора, СтрокаПоиска);
	ОбновитьОтображениеДанных();

КонецПроцедуры

&НаСервере
Функция ПоместитьОСВХранилище()

	ОС = Объект.Корзина.Выгрузить();

	АдресОСВХранилище = ПоместитьВоВременноеХранилище(ОС, УникальныйИдентификатор);

	Возврат АдресОСВХранилище;

КонецФункции

&НаКлиенте
Процедура ДобавитьВКорзину(ПараметрыОС, Количество)

	РезультатПоиска = Объект.Корзина.НайтиСтроки(ПараметрыОС);
	Если РезультатПоиска.Количество() = 0 Тогда
		ТекущаяСтрока = Объект.Корзина.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, ПараметрыОС);

		ДобавленыНовыеОС = Истина;
	Иначе
		ТекущаяСтрока = РезультатПоиска[0];
	КонецЕсли;

	// Активизируем текущую строку табличной части
	Элементы.Корзина.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();

	СформироватьИнформационнуюНадпись(ЭтаФорма);

	ЭтаФорма.Модифицированность = Истина;

КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиВДокументЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		ДобавленыНовыеОС = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-01-15 (#2950)
&НаСервереБезКонтекста
Функция ПолучитьОтобранныеЗначенияВыбора(парСписокВыбранных)
	
	Результат = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОсновныеСредства.Ссылка
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.Ссылка В(&СписокВыбранных)
	|	И НЕ ОсновныеСредства.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("СписокВыбранных", парСписокВыбранных);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		Результат.Добавить(ВыборкаДетальныеЗаписи.Ссылка);	
	КонецЦикла;
	
	Возврат Результат;

КонецФункции

&НаКлиенте
Процедура ДобавитьВыбранныеОС(парВыбранныеСтроки)
	
	Если ТипЗнч(парВыбранныеСтроки)<>Тип("Массив") ИЛИ
		 парВыбранныеСтроки.Количество() = 0 
	Тогда 
		Возврат;
	КонецЕсли;
	
	ОтобранныеЗначения = ПолучитьОтобранныеЗначенияВыбора(парВыбранныеСтроки);
	
	Если ОтобранныеЗначения.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	СтруктураПараметрыОС = Новый Структура;
	Для Каждого пВыбраннаяСтрока ИЗ ОтобранныеЗначения Цикл 
		СтруктураПараметрыОС.Вставить("ОсновноеСредство", пВыбраннаяСтрока);
		ДобавитьВКорзину(СтруктураПараметрыОС, 1);		
	КонецЦикла;
	
КонецПроцедуры
//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-01-15 (#2950)

//ОКЕЙ Иванов А.Е.(СофтЛаб) Начало 2018-04-25 (#3010)
&НаКлиенте
Процедура УстановитьОтборПоМестонахождению(Местонахождение)

	ИзменитьЭлементОтбораСпискаЯвно(ПодборТаблицаОС, "Местонахождение",
		Местонахождение, ЗначениеЗаполнено(Местонахождение), ВидСравненияКомпоновкиДанных.Равно);
		
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьЭлементОтбораСпискаЯвно(Список, ИмяПоля, ПравоеЗначение = Неопределено, Использование = Ложь, ВидСравнения = Неопределено) Экспорт
	
	ОбщегоНазначенияКлиентСервер.УдалитьЭлементыГруппыОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор, 
		ИмяПоля);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор, 
		ИмяПоля, 
		ПравоеЗначение, 
		ВидСравнения, 
		, 
		Использование,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ
		); 
		
КонецПроцедуры	
//ОКЕЙ Иванов А.Е.(СофтЛаб) Конец 2018-04-25 (#3010)

#КонецОбласти