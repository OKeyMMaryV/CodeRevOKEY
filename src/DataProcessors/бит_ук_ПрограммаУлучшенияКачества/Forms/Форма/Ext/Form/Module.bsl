
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);

	Объект.УчастиеВПрограмме = Константы.бит_ук_УчаствоватьВПрограммеУлучшенияКачества.Получить();
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Объект.УчастиеВПрограмме Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВопросПринятьУчастиеЗавершение", ЭтаФорма);
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Хотите принять участие в программе улучшения качества БИТ.ФИНАНС?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
		
	КонецЕсли; 

КонецПроцедуры

// Процедура обработчик оповещения "ВопросПринятьУчастиеЗавершение".
//
// Параметры:
// Ответ - КодВозвратаДиалога
// ДополнительныеДанные - Структура.
//
&НаКлиенте 
Процедура ВопросПринятьУчастиеЗавершение(Ответ, ДополнительныеДанные) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		ПринятьУчастиеВПрограмме();
		
	Иначе
		
		Если ЭтотОбъект.Открыта() Тогда
            Закрыть();
        КонецЕсли;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОтправить(Команда)
	
	Объект.ТекстСообщения = "";
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение( НСтр("ru = 'Сообщение отправлено'"));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура управляет доступностью/видимостью элементов формы. 
//
&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	Элементы.ТекстСообщения.Доступность = Объект.УчастиеВПрограмме;
	Элементы.ФормаКомандаОтправить.Доступность = Объект.УчастиеВПрограмме;
	
КонецПроцедуры

// Процедура устанавливает участие в программе улучшения качества. 
//
&НаСервере
Процедура ПринятьУчастиеВПрограмме()
	
	Константы.бит_ук_УчаствоватьВПрограммеУлучшенияКачества.Установить(Истина);
	Объект.УчастиеВПрограмме = Истина;
	
	// Изменено значение общей переменной - сбрасываем повторно используемые значения.
	ОбновитьПовторноИспользуемыеЗначения();
	
	УправлениеЭлементамиФормы();
	
	
КонецПроцедуры

#КонецОбласти
