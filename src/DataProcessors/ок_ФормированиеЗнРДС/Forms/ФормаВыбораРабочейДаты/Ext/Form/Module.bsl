
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ТекущаяДата", ТекущаяДата);
	Параметры.Свойство("ПроизводственныйКалендарь", ПроизводственныйКалендарь);
	
	Если НЕ ЗначениеЗаполнено(ТекущаяДата) Тогда
		ТекущаяДата = ТекущаяДата();
	КонецЕсли; 
	
	Элементы.ДатаРасхода.НачалоПериодаОтображения = ТекущаяДата;
	
	ЦветаВидовДней = Новый ФиксированноеСоответствие(Справочники.ПроизводственныеКалендари.ЦветаОформленияВидовДнейПроизводственногоКалендаря());
	
	СписокВидовДня = Справочники.ПроизводственныеКалендари.СписокВидовДня();
	
 	ПреобразоватьДанныеПроизводственногоКалендаря(ПроизводственныйКалендарь, Год(ТекущаяДата));
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаРасходаПриВыводеПериода(Элемент, ОформлениеПериода)
	
	Для Каждого СтрокаОформленияПериода Из ОформлениеПериода.Даты Цикл
		ЦветОформленияДня = ЦветаВидовДней.Получить(ВидыДней.Получить(СтрокаОформленияПериода.Дата));
		Если ЦветОформленияДня = Неопределено Тогда
			ЦветОформленияДня = ОбщегоНазначенияКлиент.ЦветСтиля("ВидДняПроизводственногоКалендаряНеУказанЦвет");
		КонецЕсли;
		СтрокаОформленияПериода.ЦветТекста = ЦветОформленияДня;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаРасходаВыбор(Элемент, ВыбраннаяДата)
	
	Выбрать(Неопределено);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)

	ВидДня = ВидыДней.Получить(ДатаРасхода);
	Если ВидДня <> Неопределено 
		И ВидДня <> ПредопределенноеЗначение("Перечисление.ВидыДнейПроизводственногоКалендаря.Рабочий") Тогда
	
		ПоказатьПредупреждение(,"Выберите рабочий день оплаты!");
		Возврат;
		
	КонецЕсли;
	
	Закрыть(ДатаРасхода);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти


#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПреобразоватьДанныеПроизводственногоКалендаря(ПроизводственныйКалендарь, НомерГода)
	
	// Данные производственного календаря используются в форме 
	// в виде соответствий ВидыДней и ПереносыДней.
	// Процедура заполняет эти соответствия.
	
	ВидыДнейСоответствие = Новый Соответствие;
	ПереносыДнейСоответствие = Новый Соответствие;
	
	ДанныеПроизводственногоКалендаря_ТекущийГод = Справочники.ПроизводственныеКалендари.ДанныеПроизводственногоКалендаря(ПроизводственныйКалендарь, НомерГода);
	ДанныеПроизводственногоКалендаря_СледующийГод = Справочники.ПроизводственныеКалендари.ДанныеПроизводственногоКалендаря(ПроизводственныйКалендарь, НомерГода + 1);
	
	Для Каждого СтрокаТаблицы Из ДанныеПроизводственногоКалендаря_ТекущийГод Цикл
		ВидыДнейСоответствие.Вставить(СтрокаТаблицы.Дата, СтрокаТаблицы.ВидДня);
	КонецЦикла;
	
	Для Каждого СтрокаТаблицы Из ДанныеПроизводственногоКалендаря_СледующийГод Цикл
		ВидыДнейСоответствие.Вставить(СтрокаТаблицы.Дата, СтрокаТаблицы.ВидДня);
	КонецЦикла;
	
	ВидыДней = Новый ФиксированноеСоответствие(ВидыДнейСоответствие);
	
КонецПроцедуры

#КонецОбласти
