
#Область ОписаниеПеременных

// Хранит признак того, что форму обработки нужно открывать при запуске системы для текущего пользователя.
&НаКлиенте
Перем мПервоначальноеЗначениеПоказывать;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
	КоллекцияМакетов = Метаданные.Обработки.бит_ДополнительнаяИнформация.Макеты;
	Если КоллекцияМакетов.Количество() = 0 Тогда
		фОтказ = Истина;
		Возврат;
	КонецЕсли;
	
	фСодержаниеСформировано = Ложь;
	          	
	// Получим значение признака необходимости открывать форму при начале работы системы 
	// для текущего пользователя.
	фТекущийПользователь      = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь"); 	
	ПоказыватьПриНачалеРаботы = бит_УправлениеПользователямиСервер.ПолучитьНастройкуПользователя("ПоказыватьДополнительнуюИнформациюБит", фТекущийПользователь);

	ТекОбработка  = РеквизитФормыВЗначение("Объект");
	   		
	// Покажем первый макет в поле HTML документа.
	мТекущийМакет 	  = ТекОбработка.ПолучитьМакет("Главная");
	
	ТекстХТМЛ = мТекущийМакет.ПолучитьТекст();
	
	СсылкаКартинки = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.бит_БаннерКурсы, УникальныйИдентификатор);
	ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%src%", СсылкаКартинки);
	
	СсылкаКартинки = ПоместитьВоВременноеХранилище(БиблиотекаКартинок.бит_БаннерМобПриложение, УникальныйИдентификатор);
	ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%srcMob%", СсылкаКартинки);
	
	ЭтоERP = бит_ОбщегоНазначения.ЭтоСемействоERP();
	
	Если ЭтоERP Тогда
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%SectionNew_Begin%", "<!--");
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%SectionNew_End%", "-->");
		
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%Mobile2_Begin%", "");
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%Mobile2_End%", "");
	Иначе	
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%SectionNew_Begin%", "");
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%SectionNew_End%", "");
		
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%Mobile2_Begin%", "<!--");
		ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%Mobile2_End%", "-->");
	КонецЕсли; 
	
	ПолеHTMLДокумента = ТекстХТМЛ;
			
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если фОтказ Тогда	
		Отказ = Истина;
		Возврат; 	
	КонецЕсли;
	    			
	// Сохраняем первоначальное значение признака необходимости открывать форму при начале работы системы 
	// для текущего пользователя.
	мПервоначальноеЗначениеПоказывать = ПоказыватьПриНачалеРаботы;
		   	
КонецПроцедуры // ПриОткрытии()

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
	    Возврат;	
	КонецЕсли; 
	
	Если мПервоначальноеЗначениеПоказывать <> ПоказыватьПриНачалеРаботы Тогда		
		ПриЗакрытииСервер(); 		  	
	КонецЕсли;
	
КонецПроцедуры // ПриЗакрытии()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	Если ДанныеСобытия.Свойство("href") Тогда
		СтандартнаяОбработка = Ложь;
		ИмяОткрываемойСтраницы = ДанныеСобытия.href;
		Если ИмяОткрываемойСтраницы <> Неопределено Тогда
			ФайловаяСистемаКлиент.ЗапуститьПрограмму(ИмяОткрываемойСтраницы);
		КонецЕсли;       		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПерейтиНаГлавную(Команда)
	
	ПерейтиНаГлавнуюСтраницу();
	
КонецПроцедуры // ПерейтиНаГлавную()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
              
// Процедура выполняет действия на сервере, необходимые при закрытии формы.
// 
&НаСервере
Процедура ПриЗакрытииСервер()
	
	Настройка = ПланыВидовХарактеристик.бит_НастройкиПользователей.ПоказыватьДополнительнуюИнформациюБит;
	
	НаборЗаписей = РегистрыСведений.бит_НастройкиПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(фТекущийПользователь);
	НаборЗаписей.Отбор.Настройка.Установить(Настройка);
	НаборЗаписей.Прочитать();
	НаборЗаписей.Очистить();
	
	Запись = НаборЗаписей.Добавить();
	Запись.Пользователь 	 = фТекущийПользователь;
	Запись.Настройка 		 = Настройка;
	Запись.ЗначениеНастройки = ПоказыватьПриНачалеРаботы;
	
	НаборЗаписей.Записать();
		
КонецПроцедуры // ПриЗакрытииСервер()

&НаСервере
Процедура ПерейтиНаГлавнуюСтраницу()
	              	 
	ТекОбработка      = РеквизитФормыВЗначение("Объект");
	мТекущийМакет 	  = ТекОбработка.ПолучитьМакет("Главная");
	
	ТекстХТМЛ = мТекущийМакет.ПолучитьТекст();
	
	СсылкаКартинки = ПолучитьНавигационнуюСсылку(БиблиотекаКартинок.бит_БаннерКурсы);
	
	ТекстХТМЛ = СтрЗаменить(ТекстХТМЛ, "%src%", СсылкаКартинки);
	
	ПолеHTMLДокумента =  ТекстХТМЛ;
	
КонецПроцедуры // ПерейтиНаГлавнуюСтраницу()


#КонецОбласти


