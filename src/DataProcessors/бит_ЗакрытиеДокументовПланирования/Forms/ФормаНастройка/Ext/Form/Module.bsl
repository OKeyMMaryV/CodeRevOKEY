
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Инициализация схемы компановки данных
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	
	ДеревоЗначенийВидыДокументов = ДанныеФормыВЗначение(Параметры.ДеревоВидыДокументов, Тип("ДеревоЗначений"));
	
	МассивВидовДокументов = ОбработкаОбъект.ПолучитьМассивВидовДокументов(ДеревоЗначенийВидыДокументов);
	
	ОбработкаОбъект.ИнициализироватьКомпоновщик(Объект.КомпоновщикНастроек, МассивВидовДокументов);
	
	Если Параметры.Свойство("СтруктураНастроек") Тогда
		ОбработкаОбъект.РаспоковатьНастройкиИзСтруктурыМодуль(Объект, Параметры.СтруктураНастроек);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НастройкаПериода(Команда)
	
	ВыборПериода(Объект.Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = УпаковатьНастройкиВСтруктуру();
	
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура открывает форму выбора периода.
// 
// Параметры:
//  Период  - Стандартный период
// 
&НаКлиенте
Процедура ВыборПериода(Период)

	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Период;
	
	Оповещение = Новый ОписаниеОповещения("ПоказатьВыборПериода",ЭтаФорма);
	Диалог.Показать(Оповещение);
	
КонецПроцедуры // ВыборПериода()

// Процедура выбора периода.
// 
&НаКлиенте 
Процедура ПоказатьВыборПериода(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
	
		Объект.Период = РезультатВыбора;
	
	КонецЕсли; 
	
КонецПроцедуры // ПоказатьВыборПериода

// Процедура сохраняет настройки формы в хранилища общих настроек.
// 
&НаСервере
Функция УпаковатьНастройкиВСтруктуру()
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
	СтруктураНастроек = ОбработкаОбъект.УпаковатьНастройкиВСтруктуруМодуль(Объект);
	
	Возврат СтруктураНастроек;
	
КонецФункции // СохранитьНастройки()

#КонецОбласти