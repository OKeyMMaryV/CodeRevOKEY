#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Процедура ЗагрузитьСправочнуюИнформациюПриПереходеНаНовуюВерсию() Экспорт
	
	ЗагрузитьСправочнуюИнформациюИзМакета();
	
	// Регистрирует источник справочной информации, если такового нет.
	Если НЕ ЕстьИсточникДляСправочнойИнформации("бит_си_ИсточникСправочникСтатьи") Тогда
		бит_си_ОбщегоНазначения.ЗарегистрироватьОбработкуИсточника("бит_си_ИсточникСправочникСтатьи");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции
 
Процедура ЗагрузитьСправочнуюИнформациюИзМакета()
	
	Макет 			 = Обработки.бит_си_СправочнаяИнформация.ПолучитьМакет("СправочнаяИнформация");
	ТекстДляЗагрузки = Макет.ПолучитьТекст();
	
	Если СокрЛП(ТекстДляЗагрузки)="" Тогда
		Возврат;
	КонецЕсли;
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ТекстДляЗагрузки);
	ЧтениеXML.ПерейтиКСодержимому();
	ЧтениеXML.Прочитать();
	
	Попытка
		Пока ВозможноЧтениеДанных(ЧтениеXML) Цикл
			// Читаем очередное значение
			Данные = ПрочитатьXML(ЧтениеXML);
			Если Данные = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Данные);
		КонецЦикла;
		ЧтениеXML.Закрыть();
	Исключение
		ТекстОшибки = КраткоеПредставлениеОшибки(ОписаниеОшибки());		
		ВызватьИсключение (ТекстОшибки);
	КонецПопытки;
	
КонецПроцедуры

Функция ЕстьИсточникДляСправочнойИнформации(ИмяОбработки)

	ЕстьИсточник = Ложь;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_си_Источники.Ссылка,
	|	бит_си_Источники.Обработка
	|ИЗ
	|	Справочник.бит_си_Источники КАК бит_си_Источники";
				   
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл
		 ТекОбработка = Выборка.Обработка.Получить();
		 Если ТипЗнч(ТекОбработка) = Тип("Строка") Тогда
		 	Если ВРег(ТекОбработка) = ВРег(ИмяОбработки) Тогда
				  ЕстьИсточник = Истина;
				  Прервать;
			КонецЕсли; 
		 КонецЕсли;
	 КонецЦикла; 
	 
	Возврат ЕстьИсточник;
	
КонецФункции

Функция ВозможноЧтениеДанных(ЧтениеXML)
	
	ВозможноЧтение = Ложь;
	
	// Получаем тип данных XML, который может быть считан в данный момент.
	ТипXML = ПолучитьXMLТип(ЧтениеXML);
	Если ТипXML = Неопределено Тогда		
		ВозможноЧтение = Ложь;		
	ИначеЕсли ТипXML.ИмяТипа = "CatalogObject.бит_си_Статьи" Тогда
		ВозможноЧтение = Истина;
	КонецЕсли;
	
	Возврат ВозможноЧтение;
	
КонецФункции

#КонецОбласти

#КонецЕсли