
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ИмяФормы = ПараметрыВыполненияКоманды.Источник.ИмяФормы;
	ПараметрыФормы = ПодготовитьПараметрыДляФормы(ИмяФормы); 	
	Если ПараметрыФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.бит_ИмпортОбъектовИзExcel.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует структуру параметров для передачи в форму импорта объектов из Excel.
// 
// Параметры:
// 	ПараметрКоманды - СправочникСсылка - тип параметра команды
// 
// Возвращаемое значение:
// 	ПараметрыФормы - Структура
// 
&НаСервере
Функция ПодготовитьПараметрыДляФормы(ИмяФормы)
	
	Если Не бит_ОбщегоНазначения.ЕстьМеханизмЗагрузкиИзEXCEL() Тогда
		Возврат Неопределено;
	КонецЕсли; 
	
	МассивИмени = бит_СтрокиКлиентСервер.РазобратьСтрокуСРазделителями(ИмяФормы, ".");
	ВидОбъекта = МассивИмени[0];
	ИмяОбъекта = МассивИмени[1];
	
	НастройкаДляЗагрузки = бит_ОбменДанными_Excel.ПолучитьНастройкуЗагрузкиИзExcelДляОбъекта(ВидОбъекта, ИмяОбъекта);
	 	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаДляЗагрузки"	   , НастройкаДляЗагрузки);
	ПараметрыФормы.Вставить("ЗаполнитьШапкуПоНастройке", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции // ПодготовитьПараметрыДляФормы()

#КонецОбласти

