
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = ПодготовитьПараметрыДляФормы(ПараметрКоманды); 	
	Если ПараметрыФормы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму("Обработка.бит_ИмпортОбъектовИзExcel.Форма", ПараметрыФормы, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры // ОбработкаКоманды()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует структуру параметров для передачи в форму импорта объектов из Excel.
// 
// Параметры:
// 	ПараметрКоманды - СправочникСсылка - тип параметра команды
// 
// Возвращаемое значение:
// 	ПараметрыФормы - Структура
// 
&НаСервере
Функция ПодготовитьПараметрыДляФормы(ПараметрКоманды)
	
	Если Не бит_ОбщегоНазначения.ЕстьМеханизмЗагрузкиИзEXCEL() Тогда
		Возврат Неопределено;
	КонецЕсли; 	
	
	Если ТипЗнч(ПараметрКоманды) = Тип("СправочникСсылка.бит_НастройкиЗагрузкиИзExcel")
		И НЕ ПараметрКоманды.ЭтоГруппа Тогда
		
		НастройкаДляЗагрузки = ПараметрКоманды;
		
	Иначе
		
		МетаПараметр = ПараметрКоманды.Метаданные();
		
		ВидОбъекта = бит_ОбменДанными_Excel.ПолучитьВидОбъектаПоМетаданным(МетаПараметр);
		ИмяОбъекта = МетаПараметр.Имя;
		
		НастройкаДляЗагрузки = бит_ОбменДанными_Excel.ПолучитьНастройкуЗагрузкиИзExcelДляОбъекта(ВидОбъекта, ИмяОбъекта);
		
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаДляЗагрузки"	   , НастройкаДляЗагрузки);
	ПараметрыФормы.Вставить("ЗаполнитьШапкуПоНастройке", Истина);
	
	Возврат ПараметрыФормы;
	
КонецФункции // ПодготовитьПараметрыДляФормы()

#КонецОбласти

