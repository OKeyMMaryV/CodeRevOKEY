
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);

	МассивМетаОбъектов = Новый Массив;
	
	Попытка
	
		МассивМетаОбъектов  = ПолучитьСоставСтандартногоИнтерфейсаOData();
		фРежимСовместимости = Ложь;
	
	Исключение
		
		фРежимСовместимости = Истина;
		
	КонецПопытки;
	
	Элементы.ГруппаРежимСовместимости.Видимость = фРежимСовместимости;
	Элементы.КомандаВыполнить.Видимость = НЕ фРежимСовместимости;
	
	Если НЕ фРежимСовместимости Тогда
	
		 ОбновитьДерево(МассивМетаОбъектов);
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДеревообъектов

&НаКлиенте
Процедура ДеревоОбъектовВыполнятьПриИзменении(Элемент)
	
	 ТекущаяСтрока = Элементы.ДеревоОбъектов.ТекущиеДанные;
	 
	 КоллекцияПодч = ТекущаяСтрока.ПолучитьЭлементы();
	 
	 Для каждого СтрокаПодч Из КоллекцияПодч Цикл
	 
	 	СтрокаПодч.Выполнять = ТекущаяСтрока.Выполнять;
	 
	 КонецЦикла; 
	 
	 Модифицированность = Истина;
	 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаСервере
Процедура КомандаВыполнитьНаСервере()
	
	МассивМетаОбъектов = Новый Массив;
	
	ДеревоВрем = ДанныеФормыВЗначение(ДеревоОбъектов, Тип("ДеревоЗначений"));
	
	Для каждого СтрокаВерх Из ДеревоВрем.Строки Цикл
	
		Для каждого СтрокаОбъект Из СтрокаВерх.Строки Цикл
		
			Если СтрокаОбъект.Выполнять Тогда
			
				МетаОбъект = Метаданные[СтрокаОбъект.ИмяКоллекции][СтрокаОбъект.Имя];
				МассивМетаОбъектов.Добавить(МетаОбъект);
			
			КонецЕсли; 
		
		КонецЦикла; 
	
	КонецЦикла; 
	
	УстановитьСоставСтандартногоИнтерфейсаOData(МассивМетаОбъектов);
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыполнить(Команда)
	
	КомандаВыполнитьНаСервере();
	ТекстСообщения = НСтр("ru = 'Состав стандартного интерфейса OData установлен.'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура обновляет дерево объектов.
// 
&НаСервере
Процедура ОбновитьДерево(МассивМетаОбъектов)

	ДеревоОбъектов.ПолучитьЭлементы().Очистить();
	
	ДеревоВрем = Новый ДеревоЗначений;
	ДеревоВрем.Колонки.Добавить("Выполнять", Новый ОписаниеТипов("Булево"));
	ДеревоВрем.Колонки.Добавить("Имя");
	ДеревоВрем.Колонки.Добавить("Синоним");
	ДеревоВрем.Колонки.Добавить("Картинка");
	ДеревоВрем.Колонки.Добавить("ИмяКоллекции");
	
	ИменаКоллекций = Новый СписокЗначений;
	ИменаКоллекций.Добавить("Справочники"            , НСтр("ru = 'Справочники'"),,БиблиотекаКартинок.Справочник);
	ИменаКоллекций.Добавить("РегистрыСведений"       , НСтр("ru = 'Регистры сведений'"),,БиблиотекаКартинок.РегистрСведений);	
	ИменаКоллекций.Добавить("ПланыВидовХарактеристик", НСтр("ru = 'Планы видов характеристик'"),,БиблиотекаКартинок.Перечисление);
	ИменаКоллекций.Добавить("ПланыСчетов"            , НСтр("ru = 'Планы счетов'"),,БиблиотекаКартинок.Перечисление);	
	ИменаКоллекций.Добавить("Перечисления"           , НСтр("ru = 'Планы счетов'"),,БиблиотекаКартинок.Перечисление);
	
	Для каждого Эл Из ИменаКоллекций Цикл
	
		 СтрокаВерх = ДеревоВрем.Строки.Добавить();
		 СтрокаВерх.Имя = Эл.Значение;
		 СтрокаВерх.Синоним = Эл.Представление;
		 СтрокаВерх.ИмяКоллекции = Эл.Значение;
		 СтрокаВерх.Картинка = Эл.Картинка;
		 
		 флВсеИстина = Истина;
		 
		 Для каждого МетаОбъект Из Метаданные[Эл.Значение] Цикл
		 
		 	    СтрокаОбъект = СтрокаВерх.Строки.Добавить();
				СтрокаОбъект.Имя = МетаОбъект.Имя;
				СтрокаОбъект.Синоним = МетаОбъект.Синоним;
				СтрокаОбъект.ИмяКоллекции = СтрокаВерх.Имя;
				СтрокаОбъект.Выполнять = ?(НЕ МассивМетаОбъектов.Найти(МетаОбъект) = Неопределено, Истина, Ложь); 
				
				Если НЕ СтрокаОбъект.Выполнять Тогда
				
					флВсеИстина = Ложь;
				
				КонецЕсли; 
				
		 КонецЦикла; 
		 
		 СтрокаВерх.Выполнять = флВсеИстина;
		 СтрокаВерх.Строки.Сортировать("Синоним");
		 
		 
	КонецЦикла; 

	ЗначениеВДанныеФормы(ДеревоВрем, ДеревоОбъектов);
	
КонецПроцедуры // ОбновитьДерево()

#КонецОбласти

