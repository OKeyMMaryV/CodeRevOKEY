
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Ключ текущих настроек
	Перем КлючТекущихНастроек;
	
	КлючОбъекта = Параметры.КлючОбъекта;	
	КлючТекущихНастроек = Параметры.КлючТекущихНастроек;		
	
	// Список предопределенных
	СтандартныеВарианты = Параметры.СтандартныеНастройки; 
	
	// Список недоступных вариантов
	ОбновитьТаблицуВариантов(КлючТекущихНастроек);
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТаблицаСохраненныеВарианты

&НаКлиенте
Процедура ТаблицаСохраненныеВариантыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьВариант();
	
КонецПроцедуры // ТаблицаСохраненныеВариантыВыбор()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	
	ВыбратьВариант();
	
КонецПроцедуры // КомандаВыбрать()

&НаКлиенте
Процедура КомандаОбновить(Команда)
	
	ОбновитьТаблицуВариантов();
	
КонецПроцедуры // КомандаОбновить()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура обрабатывает выбор варианта.
// 
// 
&НаКлиенте
Процедура ВыбратьВариант()
	
	ТекущиеДанные = Элементы.ТаблицаСохраненныеВарианты.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено Тогда
		
		Закрыть();
		
	Иначе	
		
		ТекКлюч = ТекущиеДанные.КлючВарианта;
				
		ВыборВарианта = Новый ВыборНастроек(ТекКлюч);
		Закрыть(ВыборВарианта);
		
	КонецЕсли; 

КонецПроцедуры // ВыбратьВариант()

// Процедура обновляет таблицу сохраненных вариантов.
// 
// Параметры:
//  КлючТекущихНастроек - Строка (По умолчанию = Неопределено).
// 
&НаСервере
Процедура ОбновитьТаблицуВариантов(КлючТекущихНастроек = Неопределено)

	СтруктураДоступности        = бит_ОтчетыСервер.ПроверитьДоступностьВариантовНастроек(КлючОбъекта, Истина);
	фСписокНеДоступныхВариантов = СтруктураДоступности.СписокНеДоступныхВариантов;
	КлючВариантаПоУмолчанию     = СтруктураДоступности.ИспользуемыйПриОткрытииВариант;
	
	ИдТекущейСтроки = Неопределено;
	
	Запрос = Новый Запрос;	
	Запрос.УстановитьПараметр("КлючОбъекта", КлючОбъекта);
	Запрос.Текст = "
	|ВЫБРАТЬ
    |	бит_ВариантыОтчетов.Ссылка,
	|	бит_ВариантыОтчетов.Наименование,
	|	бит_ВариантыОтчетов.Ответственный,
    |	бит_ВариантыОтчетов.КлючВарианта,
    |	бит_ВариантыОтчетов.ТипВарианта
    |ИЗ
    |	Справочник.бит_ВариантыОтчетов КАК бит_ВариантыОтчетов
    |ГДЕ
    |	бит_ВариантыОтчетов.КлючОбъекта = &КлючОбъекта
    |
    |УПОРЯДОЧИТЬ ПО
    |	бит_ВариантыОтчетов.Наименование
    |АВТОУПОРЯДОЧИВАНИЕ
	|";
				   
	Результат = Запрос.Выполнить();
	
	Выборка = Результат.Выбрать();
	
	ТаблицаСохраненныеВарианты.Очистить();
	
	Пока Выборка.Следующий() Цикл
		
		Если фСписокНеДоступныхВариантов.НайтиПоЗначению(Выборка.КлючВарианта) <> Неопределено Тогда
			Продолжить;		
		КонецЕсли;
		
		НоваяСтрока = ТаблицаСохраненныеВарианты.Добавить();
		
		НоваяСтрока.СохраненныйВариант 		= Выборка.Ссылка;
		НоваяСтрока.КлючВарианта 			= Выборка.КлючВарианта;
		НоваяСтрока.Ответственный 			= Выборка.Ответственный;
		
		НоваяСтрока.ИспользоватьПриОткрытии = Выборка.КлючВарианта = КлючВариантаПоУмолчанию;
		
		Если Выборка.ТипВарианта = Перечисления.бит_ТипыВариантовОтчетов.Предопределенный Тогда
			НоваяСтрока.ЭтоПредопределенный  = Истина;
			СтандартныйВар = СтандартныеВарианты.НайтиПоЗначению(Выборка.КлючВарианта);
			Если СтандартныйВар <> Неопределено Тогда
				НоваяСтрока.Представление = СтандартныйВар.Представление;
			Иначе 
				НоваяСтрока.Представление =  Выборка.Наименование;
			КонецЕсли; 
		Иначе
			НоваяСтрока.Представление =  Выборка.Наименование;
		КонецЕсли;
		
		Если КлючТекущихНастроек = Выборка.КлючВарианта Тогда
			ИдТекущейСтроки = НоваяСтрока.ПолучитьИдентификатор();		
		КонецЕсли;
					 	  	
	КонецЦикла;
	
	Если ИдТекущейСтроки <> Неопределено Тогда
		Элементы.ТаблицаСохраненныеВарианты.ТекущаяСтрока = ИдТекущейСтроки;
	КонецЕсли;
	
КонецПроцедуры // ОбновитьТаблицуВариантов()

#КонецОбласти
