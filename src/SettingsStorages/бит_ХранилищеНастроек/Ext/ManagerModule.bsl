#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий
	
// Процедура содержит реализацию метода Загрузить(). 
// 	В данном обработчике необходимо получить настройки из некоторого объекта. 
// 	Например, из элемента справочника.
// 
Процедура ОбработкаЗагрузки(КлючОбъекта, КлючНастроек, Настройки, ОписаниеНастроек, Пользователь)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	бит_СохраненныеНастройки.Наименование,
 	|	бит_СохраненныеНастройки.ХранилищеНастроек
 	|ИЗ
 	|	Справочник.бит_СохраненныеНастройки КАК бит_СохраненныеНастройки
	|ГДЕ
	|	бит_СохраненныеНастройки.КлючОбъекта 	 = &КлючОбъекта
	|	И бит_СохраненныеНастройки.КлючНастройки = &КлючНастройки 
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КлючОбъекта"  , КлючОбъекта);
	Запрос.УстановитьПараметр("КлючНастройки", КлючНастроек);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ОписаниеНастроек.Представление 	= Выборка.Наименование;
		Настройки 						= Выборка.ХранилищеНастроек.Получить();
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры // ОбработкаЗагрузки()

// Процедура содержит реализацию метода Сохранить(). 
// 	В данном обработчике необходимо выполнить сохранение настройки в некоторый объект. 
// 	Например, в элемент справочника. 
// 
Процедура ОбработкаСохранения(КлючОбъекта, КлючНастроек, Настройка, ОписаниеНастроек, Пользователь)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
    |	бит_СохраненныеНастройки.Ссылка
 	|ИЗ
 	|	Справочник.бит_СохраненныеНастройки КАК бит_СохраненныеНастройки
 	|ГДЕ
 	|	бит_СохраненныеНастройки.КлючОбъекта 	 = &КлючОбъекта
 	|	И бит_СохраненныеНастройки.КлючНастройки = &КлючНастройки
	|";
		 
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КлючОбъекта"  , КлючОбъекта);
	Запрос.УстановитьПараметр("КлючНастройки", КлючНастроек);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		НастройкаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		
		Настройка.ДополнительныеСвойства.Вставить("КлючНастройки", КлючНастроек);  		
				
		НастройкаОбъект.ХранилищеНастроек = Новый ХранилищеЗначения(Настройка, Новый СжатиеДанных());
		Если ОписаниеНастроек <> Неопределено Тогда
			НастройкаОбъект.Наименование = ОписаниеНастроек.Представление;
		КонецЕсли;
		НастройкаОбъект.Записать();
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
		
КонецПроцедуры // ОбработкаСохранения()

// Процедура - содержит реализацию метода ПолучитьОписание(). 
// 	В данном обработчике необходимо заполнить описание настройки 
//  на основании свойств сохраненной настройки. 
// 	Например, из элемента справочника.
// 
Процедура ОбработкаПолученияОписания(КлючОбъекта, КлючНастроек, ОписаниеНастроек, Пользователь)
	
	ОписаниеНастроек.КлючОбъекта  = КлючОбъекта;
	ОписаниеНастроек.КлючНастроек = КлючНастроек;
	// 
	ТекстЗапроса = "
	|ВЫБРАТЬ
    |	бит_СохраненныеНастройки.Ссылка,
	|	бит_СохраненныеНастройки.Наименование
 	|ИЗ
 	|	Справочник.бит_СохраненныеНастройки КАК бит_СохраненныеНастройки
 	|ГДЕ
 	|	бит_СохраненныеНастройки.КлючОбъекта 	 = &КлючОбъекта
 	|	И бит_СохраненныеНастройки.КлючНастройки = &КлючНастройки
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КлючОбъекта",  КлючОбъекта);
	Запрос.УстановитьПараметр("КлючНастройки", КлючНастроек);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	УстановитьПривилегированныйРежим(Ложь);
	// 
	Если Выборка.Следующий() Тогда
		НастройкаОтчета = Выборка.Ссылка;
		Если Не НастройкаОтчета.Пустая() Тогда
			ОписаниеНастроек.Представление = Выборка.Наименование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПолученияОписания()

// Процедура содержит реализацию метода УстановитьОписание(). 
// 	В данном обработчике необходимо заполнить описание сохраненной настройки 
//  из переданного объекта ОписаниеНастроек. 
// 	Например, в элементе справочника.    
// 
Процедура ОбработкаУстановкиОписания(КлючОбъекта, КлючНастроек, ОписаниеНастроек, Пользователь)
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
    |	бит_СохраненныеНастройки.Ссылка
 	|ИЗ
 	|	Справочник.бит_СохраненныеНастройки КАК бит_СохраненныеНастройки
 	|ГДЕ
 	|	бит_СохраненныеНастройки.КлючОбъекта 	 = &КлючОбъекта
 	|	И бит_СохраненныеНастройки.КлючНастройки = &КлючНастройки
	|";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	Запрос.УстановитьПараметр("КлючОбъекта"  , КлючОбъекта);
	Запрос.УстановитьПараметр("КлючНастройки", КлючНастроек);
		
	УстановитьПривилегированныйРежим(Истина);
	 
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		Ссылка = Выборка.Ссылка;
		Если Не Ссылка.Пустая() Тогда
			НастройкаОбъект = Ссылка.ПолучитьОбъект();
			НастройкаОбъект.Наименование = ОписаниеНастроек.Представление;
			НастройкаОбъект.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры // ОбработкаУстановкиОписания()

#КонецОбласти

#КонецЕсли
