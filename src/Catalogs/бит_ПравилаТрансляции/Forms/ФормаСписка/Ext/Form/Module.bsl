
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.СписокПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма);
	
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.бит_ПравилаТрансляции);
	Элементы.ФормаКомандаКомпилироватьПравила.Видимость = МожноРедактировать;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОбновитИсполняемыйКод(Команда)
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() = 0 Тогда
		ТекстСообщения = Нстр("ru = 'Нет выделенных строк.'");
		ПоказатьПредупреждение(,ТекстСообщения,,Нстр("ru = 'Обновить исполняемый код'"));
	Иначе
		Оповещение = Новый ОписаниеОповещения("ВопросПередОбновлениемКодаЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'Будет выполнено обновление исполняемого кода выделенных элементов.
		|Продолжить?'");
		ПоказатьВопрос(Оповещение,ТекстВопроса,РежимДиалогаВопрос.ДаНет,,КодВозвратаДиалога.Да,Нстр("ru = 'Обновить исполняемый код'"));
	КонецЕсли; 
	
КонецПроцедуры

// Процедура обработчик оповещения "ВопросПередОбновлениемКодаЗавершение".
// 
// Параметры:
// Ответ - КодВозвратаДиалога
// ДополнительныеДанные - Структура.
// 
&НаКлиенте 
Процедура ВопросПередОбновлениемКодаЗавершение(Ответ, ДополнительныеДанные) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ДлительнаяОперация = ПараметрыОбновленияИсполняемогоКода();
		
		ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
		ПараметрыОжидания.ТекстСообщения = "Обновление исполняемого кода"; 
		ПараметрыОжидания.ВыводитьОкноОжидания = Истина;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриЗавершенииОбновленияКода", ЭтотОбъект);
		ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОписаниеОповещения, ПараметрыОжидания);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаНазначениеПравилТрансляции(Команда)
	Если Элементы.Список.ВыделенныеСтроки.Количество()>0 Тогда
		ЭлементыОтбора = Новый Структура;
		ЭлементыОтбора.Вставить("МассивПравил", Элементы.Список.ВыделенныеСтроки);
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЭлементыОтбора);
		ОткрытьФорму("Документ.бит_НазначениеПравилТрансляции.ФормаОбъекта", ПараметрыФормы,,ЭтотОбъект);
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Обраборка оповещения процедуры "ПриЗавершенииОбновленияКода".
//
// Параметры:
//  Результат				 - Структура - См. общий модуль ДлительныеОпрации.
//  ДополнительныеПараметры	 - Произвольный - произвольные данные, переданные в обработку оповещения. 
//
&НаКлиенте
Процедура ПриЗавершенииОбновленияКода(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Результат.Статус = "Ошибка" Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю("Ошибка при выполнении: " + Результат.КраткоеПредставлениеОшибки);
	Иначе	
		РезультатВыполнения = ПолучитьИзВременногоХранилища(Результат.АдресРезультата);
		Если РезультатВыполнения.Статус = "Ошибка" Тогда
			ОчиститьСообщения();
			Для каждого Элемент Из РезультатВыполнения.Ошибки Цикл
				ОбщегоНазначенияКлиент.СообщитьПользователю(Элемент.Значение, Элемент.Ключ);
			КонецЦикла;
		КонецЕсли;
		ПоказатьОповещениеПользователя("Обновление исполняемого кода",
			"e1cib/list/Справочник.бит_ПравилаТрансляции","Завершено.",БиблиотекаКартинок.Информация32);	
	КонецЕсли; 

КонецПроцедуры

&НаСервере
Функция ПараметрыОбновленияИсполняемогоКода()

	НаименованиеЗадания = НСтр("ru = 'Обновление исполняемого кода правил трансляции.'");
	ВыполняемыйМетод = "Справочники.бит_ПравилаТрансляции.ОбновитьИсполняемыйКод";
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	
	// Заполнение пераметов длительной операции.
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
	
	Возврат ДлительныеОперации.ВыполнитьВФоне(ВыполняемыйМетод, ВыделенныеСтроки, ПараметрыВыполнения);

КонецФункции

#КонецОбласти

