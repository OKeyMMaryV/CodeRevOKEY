#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Функция - Перечень валютных операций.
// 
// Возвращаемое значение:
//  нТаблица - ТаблицаЗначений - таблица с перечнем валютных операций.
//
Функция ПереченьВалютныхОпераций() Экспорт
	
	нТаблица = Новый ТаблицаЗначений;
	
	Макет = Справочники.бит_ВидыВалютныхОпераций.ПолучитьМакет("ПереченьВалютныхОпераций");
	СписокСтрок = Макет.Области.Найти("Перечень");
	
	Если СписокСтрок.ТипОбласти = ТипОбластиЯчеекТабличногоДокумента.Строки Тогда
		
		ОбластьВерх = СписокСтрок.Верх;
		ОбластьНиз = СписокСтрок.Низ;
		
		Сч = 1;
		Область = Макет.Область(ОбластьВерх - 1, Сч);
		ИмяКолонки = Область.Текст;
		
		Пока ЗначениеЗаполнено(ИмяКолонки) Цикл
			
			Если ИмяКолонки = "Код" Тогда
				нТаблица.Колонки.Добавить("Код", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(5)));
			ИначеЕсли ИмяКолонки = "Наименование" Тогда
				нТаблица.Колонки.Добавить("Наименование",Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0)));
			КонецЕсли;	
			
			Сч = Сч + 1;
			Область = Макет.Область(ОбластьВерх - 1, Сч);
			ИмяКолонки = Область.Текст;
			
		КонецЦикла;
		
		Для НомСтр = ОбластьВерх По ОбластьНиз Цикл
			
			Код = СокрП(Макет.Область(НомСтр, 1).Текст);
			
			Если СтрДлина(Код) = 2 Тогда
				Продолжить;
			КонецЕсли;
			
			СтрСписка = нТаблица.Добавить();
			Для Каждого Колонка Из нТаблица.Колонки Цикл
				
				СтрСписка[Колонка.Имя] = СокрП(Макет.Область(НомСтр, нТаблица.Колонки.Индекс(Колонка) + 1).Текст);
				
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	нТаблица.Сортировать(нТаблица.Колонки[0].Имя + " Возр");
	
	Возврат нТаблица;
	
КонецФункции

#КонецОбласти 	
	
#КонецЕсли