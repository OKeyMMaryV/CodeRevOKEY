
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НаличиеДвиженийПоТГ = НаличиеДвиженийПоТГ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
	Если НаличиеДвиженийПоТГ Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("ТГ заблокирована. Есть движения по ТГ в РС «История изменения товарных групп договоров».");
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЕстьОтсрочкаПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьОтсрочку(Команда)
	
	НаличиеДвиженийПоТГ = Ложь;
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ЕстьОтсрочка.ТолькоПросмотр	= НаличиеДвиженийПоТГ;
	Элементы.ЛимитОтсрочки.Доступность		= Объект.ЕстьОтсрочка И НЕ НаличиеДвиженийПоТГ;
	Элементы.ЕдиницаИзмерения.Доступность	= Объект.ЕстьОтсрочка И НЕ НаличиеДвиженийПоТГ;
	
	Элементы.ЛимитОтсрочки.АвтоОтметкаНезаполненного 	= Объект.ЕстьОтсрочка;
	Элементы.ЕдиницаИзмерения.АвтоОтметкаНезаполненного = Объект.ЕстьОтсрочка;
	Если НЕ Элементы.ЛимитОтсрочки.АвтоОтметкаНезаполненного Тогда
		Элементы.ЛимитОтсрочки.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	Если НЕ Элементы.ЕдиницаИзмерения.АвтоОтметкаНезаполненного Тогда
		Элементы.ЕдиницаИзмерения.ОтметкаНезаполненного = Ложь;
	КонецЕсли;
	
	Если НЕ Объект.ЕстьОтсрочка Тогда
	
		Если ЗначениеЗаполнено(Объект.ЛимитОтсрочки) Тогда
			Объект.ЛимитОтсрочки = 0;
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Объект.ЕдиницаИзмерения) Тогда
			Объект.ЕдиницаИзмерения = Неопределено;
		КонецЕсли; 
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция НаличиеДвиженийПоТГ()

	Если Объект.Ссылка.Пустая() Тогда
		Возврат Ложь;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	ок_ИсторияИзмененияТоварныхГруппДоговоров.ТоварнаяГруппа КАК ТоварнаяГруппа
		|ИЗ
		|	РегистрСведений.ок_ИсторияИзмененияТоварныхГруппДоговоров КАК ок_ИсторияИзмененияТоварныхГруппДоговоров
		|ГДЕ
		|	ок_ИсторияИзмененияТоварныхГруппДоговоров.ТоварнаяГруппа = &ТоварнаяГруппа";
	
	Запрос.УстановитьПараметр("ТоварнаяГруппа", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Возврат НЕ РезультатЗапроса.Пустой();
	
КонецФункции

#КонецОбласти 