
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.СписокПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект);
	
	// Определение назначения проформы	
	
	Если ПустаяСтрока(Параметры.Назначение) Тогда
		Назначение = Перечисления.бит_НазначенияПроформ.ФормаСбораДанных;
	Иначе
		Назначение = Перечисления.бит_НазначенияПроформ[Параметры.Назначение];
	КонецЕсли; 	
	
	// Проверка лицензии
	бит_ОбщегоНазначения.ПроверитьДоступностьПроформ(Назначение, Отказ);	
	
	// Настройка формы в зависимости от назначения.
	ЭтоФСД = ?(Назначение = Перечисления.бит_НазначенияПроформ.ПроизвольнаяФорма, Ложь, Истина);
	
	Если ЭтоФСД Тогда
		Заголовок =  НСтр("ru = 'Виды форм сбора данных'");
	Иначе	
		Заголовок =  НСтр("ru = 'Виды произвольных форм'");
	КонецЕсли; 
	
	Элементы.ФормаКомандаВыгрузитьШаблоныExcel.Видимость = ЭтоФСД;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(Список, "Назначение", Назначение, 
         ВидСравненияКомпоновкиДанных.Равно,, ЗначениеЗаполнено(Назначение)); 
		 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаВыгрузитьШаблоныExcel(Команда)
	
	МассивСтрок = Элементы.Список.ВыделенныеСтроки;
	Если МассивСтрок.Количество() = 0 Тогда
		
		ТекстСообщения = НСтр("ru = 'Не выбраны шаблоны для выгрузки.'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
	Иначе	
		
		ДанныеДляВыгрузки = ПодготовитьДанныеДляВыгрузки(МассивСтрок);
		Если ДанныеДляВыгрузки.МассивДанных.Количество() > 0 Тогда
			бит_ПроформыКлиентСервер.ВыгрузитьШаблоныПроформ(ДанныеДляВыгрузки);
		Иначе
			ТекстСообщения = НСтр("ru = 'Нет данных для выгрузки.'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьДанныеДляВыгрузки(МассивСтрок)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка
	|ИЗ
	|	Справочник.бит_ВидыПроформ КАК Таблица
	|ГДЕ
	|	Таблица.Ссылка В(&Список)
	|	И НЕ Таблица.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("Список", МассивСтрок);
	
	РезультатЗапроса 	   = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	МассивВидовПроформ     = Новый Массив;
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		МассивВидовПроформ.Добавить(ВыборкаДетальныеЗаписи.Ссылка);
	КонецЦикла;
		
	ДанныеДляВыгрузки = Справочники.бит_ВидыПроформ.ПодготовитьДанныеДляВыгрузкиШаблона(МассивВидовПроформ);
	
	Возврат ДанныеДляВыгрузки;
	
КонецФункции

#КонецОбласти