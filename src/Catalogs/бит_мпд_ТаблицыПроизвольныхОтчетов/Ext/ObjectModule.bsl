#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс
	
// Процедура меняет предыдущие координаты на текущие.
//
Процедура АктуализироватьПредыдущиеКоординаты() Экспорт
	
	ЭтотОбъект.ВерхПред = Верх;
	ЭтотОбъект.ЛевоПред = Лево;
	ЭтотОбъект.ВыводитьЗаголовкиКолонокПред = ВыводитьЗаголовкиКолонок;
	ЭтотОбъект.ВыводитьЗаголовкиСтрокПред   = ВыводитьЗаголовкиСтрок;

КонецПроцедуры // АктуализироватьПредыдущиеКоординаты()
	
#КонецОбласти 
	
#Область ОбработчикиСобытий
	
Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка Тогда			
		// В случае выполнения обмена данными не производить проверку.
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияНачалоСобытия(Отказ, ЭтотОбъект.ДополнительныеСвойства); 
	
	Если НЕ ЭтотОбъект.ПометкаУдаления = Ссылка.ПометкаУдаления Тогда 	
		Возврат; 	
	КонецЕсли; 
	
	// Проверка на пересечения таблиц в отчете
	
	// Проверка возможности обновления таблиц в макете
	МассивОписаний = Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПолучитьТаблицыОтчета(ЭтотОбъект.Владелец);
	
	// Скорректируем описание текущей таблицы с учетом внесенных изменений
	Сч = 0;
	Для каждого ОписаниеТаблицы Из МассивОписаний Цикл
	
		 Если ОписаниеТаблицы.Таблица = Ссылка Тогда
		 
		 	  ОписаниеТекТаблицы = Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПодготовитьОписаниеТаблицы(ЭтотОбъект, ОписаниеТаблицы.Ширина, ОписаниеТаблицы.Высота);
			  МассивОписаний[Сч] = ОписаниеТекТаблицы;
			  Прервать;
		 
		 КонецЕсли; 
		 
		 Сч = Сч + 1;
	КонецЦикла; 
	
	// Ситуация добавления новой таблицы
	Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
		
		ОписаниеТекТаблицы = Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПодготовитьОписаниеТаблицы(ЭтотОбъект, 0, 0);
		МассивОписаний.Добавить(ОписаниеТекТаблицы);
		
	КонецЕсли; 
	
	Если Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПроверитьПересечения(МассивОписаний, Истина) Тогда
		
		ТекстСообщения =  НСтр("ru = 'Запись изменений невозможна из-за пересечения таблиц!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения,,,Отказ);
		
	КонецЕсли; 

	// Проверка выхода за границу таблицы
	флВыход = Ложь;
	ЧислоВыводитьЗаголовкиКолонок = ?(ВыводитьЗаголовкиКолонок, 1, 0);
	ЧислоВыводитьЗаголовкиСтрок  = ?(ВыводитьЗаголовкиСтрок, 1, 0);
	
	Если ЭтотОбъект.Верх - ЧислоВыводитьЗаголовкиКолонок < 0  Тогда 	
		флВыход = Истина; 	
	КонецЕсли; 
	Если ЭтотОбъект.Лево - ЧислоВыводитьЗаголовкиСтрок < 0 Тогда  	
		флВыход = Истина; 	
	КонецЕсли; 
	
	Если флВыход Тогда	
		ТекстСообщения =  НСтр("ru = 'Запись изменений невозможна. Таблица выходит за границы макета!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения,,,Отказ);            	
	КонецЕсли;  	
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда			
		// В случае выполнения обмена данными не производить проверку.
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияПриЗаписи(Отказ, ЭтотОбъект.ДополнительныеСвойства, Метаданные().ПолноеИмя());
		
КонецПроцедуры // ПриЗаписи()

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Если НЕ ЭтотОбъект.ЭтоГруппа Тогда
		
		ЭтотОбъект.Лево = 2;
		ЭтотОбъект.Верх = 2;
		
		ЭтотОбъект.ВыводитьЗаголовкиКолонок = Истина;
		ЭтотОбъект.ВыводитьЗаголовкиСтрок = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры  	
		
#КонецОбласти
	
#КонецЕсли
