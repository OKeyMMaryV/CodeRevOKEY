
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);

	Если Отказ Тогда
		Возврат;
	КонецЕсли; 	
	
	Имена = Справочники.бит_ВидыСоответствийАналитик.ЗарезервированныеИмена();
	ЗарезервированныеИмена.Очистить();
	ЗарезервированныеИмена.ЗагрузитьЗначения(Имена.ВыгрузитьЗначения());
	
	// Запрет на редактирования соответствия если уже есть записи для данного вида соответствия.
	Использован 	= Справочники.бит_ВидыСоответствийАналитик.ЕстьЗаписи(Объект.Ссылка) ИЛИ Объект.Предопределенный;
	ЕстьОграничения = Использован ИЛИ Объект.Предопределенный;
	Элементы.Соотношение.ТолькоПросмотр = ЕстьОграничения;
	КоличествоАналитик = Справочники.бит_ВидыСоответствийАналитик.МаксКоличествоАналитик();
	
	Для Н = 1 По КоличествоАналитик Цикл
		Элементы["ЛеваяАналитика_"+Н].ТолькоПросмотр  = ЕстьОграничения;
		Элементы["ПраваяАналитика_"+Н].ТолькоПросмотр = ЕстьОграничения;		
	КонецЦикла; 
	
	Элементы.ГруппаИнформация.Видимость 		  = ЕстьОграничения;
	Элементы.Посмотреть.Видимость				  = ?(Объект.Предопределенный, Ложь, Использован);
    Элементы.ДекорацияНадписьИнформация.Заголовок = ?(Объект.Предопределенный
	                                                   ,  НСтр("ru = 'Изменение типов аналитик предопределенного вида соответствия невозможно.'")
	                                                   ,  НСтр("ru = 'Изменение типов аналитик невозможно. Вид соответствия уже используется в настройках.'"));
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	ПроверитьСпецсимволыИмени();
	Если ПустаяСтрока(Объект.Наименование) Тогда
		
		Объект.Наименование = Объект.Код;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.Код) Тогда
	
		Объект.Код = СтрЗаменить(ТРег(Объект.Наименование)," ","");
	    ПроверитьСпецсимволыИмени();		
	
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	АналитикаОбработкаВыбораНаСервере(ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДокументНазначениеСоответствияАналитик(Команда)
	
	Если Модифицированность Тогда
		
		Оповещение = Новый ОписаниеОповещения("СоздатьДокументНСАВопросЗавершение", ЭтотОбъект);
		ТекстВопроса =  НСтр("ru = 'Элемент будет записан. Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 15, КодВозвратаДиалога.Нет);
		
	Иначе
		
		бит_РаботаСДиалогамиКлиент.ОткрытьНовуюФормуДокументаНазначениеСоответствияАналитик(Объект.Ссылка);
	
	КонецЕсли;
	
КонецПроцедуры // СоздатьДокументРаспределениеБюджета()

// Процедура обработчик оповещения "СоздатьДокументРБВопросЗавершение".
//
// Параметры:
// Ответ 			    - КодВозвратаДиалога.
// ДополнительныеДанные - Структура.
//
&НаКлиенте 
Процедура СоздатьДокументНСАВопросЗавершение(Ответ, ДополнительныеДанные) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		флОК = Записать();
		
		Если флОК Тогда
			
			бит_РаботаСДиалогамиКлиент.ОткрытьНовуюФормуДокументаНазначениеСоответствияАналитик(Объект.Ссылка);
			
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры	// СоздатьДокументНСАВопросЗавершение

&НаКлиенте
Процедура ПосмотретьНажатие(Элемент)
	
	Отбор 		   = Новый Структура("ВидСоответствия", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", Отбор); 
	ОткрытьФорму("Документ.бит_НазначениеСоответствияАналитик.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура проверяет спецсимволы в имени вида соответствия. 
// 
&НаКлиенте
Процедура ПроверитьСпецсимволыИмени()
	
	Объект.Код = СтрЗаменить(Объект.Код," ","");
	СпецСимволы = " ,;:[]{}'""/\?!@#$%^&*+=<>~`|()1234567890№-";
	Объект.Код = бит_ОбщегоНазначенияКлиентСервер.ПроверитьСпецСимволы(Объект.Код,СпецСимволы,"Имя");
	
	Если НЕ ЗарезервированныеИмена.НайтиПоЗначению(Объект.Код) = Неопределено Тогда
	
		 ТекстСообщения =  НСтр("ru = 'Имя ""%1%"" зарезервировано для фиксированных соответствий.'");
		 ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Объект.Код);
		 бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		 
		 Объект.Код = "";
	
	КонецЕсли; 
	
КонецПроцедуры // ПроверитьСпецсимволыИмени()

// Процедура проверяет возможность использования выбранной аналитики. 
// 
&НаСервереБезКонтекста
Процедура АналитикаОбработкаВыбораНаСервере(Аналитика, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Аналитика) Тогда
	
		 МассивТипов = Аналитика.ТипЗначения.Типы();
		 
		 Если МассивТипов.Количество() > 1 Тогда
		 
		 	  ТекстСообщения =  НСтр("ru = 'Запрещено использовать аналитики с составными типами.'");
			  бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
			  СтандартнаяОбработка = Ложь;
		 
		 КонецЕсли; 
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти