#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий   

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда
		Возврат;	
	КонецЕсли; 
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);	
	КонецЕсли; 
	
	ЗаполнитьОбъектСистемы();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 	
		
	Если НЕ ЭтоГруппа Тогда
		ЗаполнитьОбъектСистемы();
	КонецЕсли; 
	
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда	
		Возврат;	
	КонецЕсли; 
	
	// Группы связанные с задачами и процессами запрещено редактировать, 
	// т.к. для процессов и задач логика переходов между состояними зафиксирована жестко в коде.
	ЗапрещенныеГруппы = Новый Массив;
	ЗапрещенныеГруппы.Добавить(Справочники.бит_СтатусыОбъектов.СостоянияЗадач);
	ЗапрещенныеГруппы.Добавить(Справочники.бит_СтатусыОбъектов.СостоянияПроцессов);
	
	Если Родитель <> Ссылка.Родитель Тогда		
		Если НЕ ЗапрещенныеГруппы.Найти(Родитель) = Неопределено  Тогда
			ТекстСообщения =  НСтр("ru = 'Изменение состава группы ""%1%"" запрещено.'");
			ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Родитель);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , ,Отказ); 
		КонецЕсли; 
		
		Если НЕ ЗапрещенныеГруппы.Найти(Ссылка.Родитель) = Неопределено Тогда
			ТекстСообщения =  НСтр("ru = 'Изменение состава группы ""%1%"" запрещено.'");
			ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Ссылка.Родитель);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , ,Отказ);
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры
	
Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли; 
		
КонецПроцедуры 

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Сохраняет оформление в хранилище значения. 
// 
Процедура СохранитьОформление(СтрОформление) Экспорт

	Оформление = Новый ХранилищеЗначения(СтрОформление);

КонецПроцедуры // СохранитьОформление()

// Восстанавливает настройки оформления из хранилища. 
// 
Функция ПолучитьОформление() Экспорт
	
	СтрОформление = Оформление.Получить();
	
	Возврат СтрОформление;
	
КонецФункции // ПолучитьОформление()

Процедура ЗаполнитьОбъектСистемы()
	
	Если ЗначениеЗаполнено(Родитель) Тогда
		Поставляемая = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Родитель, "Предопределенный");
		Если Поставляемая Тогда
			ОбъектСистемы = Справочники.бит_СтатусыОбъектов.ОбъектСистемыВГруппе(Родитель);
			Если ЗначениеЗаполнено(ОбъектСистемы) Тогда
				Объект = ОбъектСистемы;		
			КонецЕсли; 	
		КонецЕсли; 
	КонецЕсли; 
	
КонецПроцедуры
 
#КонецОбласти

#КонецЕсли
