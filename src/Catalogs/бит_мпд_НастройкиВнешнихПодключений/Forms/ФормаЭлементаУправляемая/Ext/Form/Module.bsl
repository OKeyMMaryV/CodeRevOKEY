
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события "ПриСозданииНаСервере" формы.
// 
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	УправлениеЭлементамиФормы(ЭтотОбъект);
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Процедура - обработчик события "ПриИзменении" переключателя "РасположениеБазы".
// 
&НаКлиенте
Процедура РасположениеБазыПриИзменении(Элемент)
	
	ИзменениеРасположенияБазы();
	
КонецПроцедуры

// Процедура - действие команды "КомандаПроверитьПодключение".
// 
&НаКлиенте
Процедура КомандаПроверитьПодключение(Команда)
	
	флУдалось = ПроверитьПодключение();
	
	Если флУдалось Тогда
		
		ТекстСообщения = НСтр("ru = 'Тестовое подключение успешно выполнено.'");
		
	Иначе	
		
		ТекстСообщения = НСтр("ru = 'Не удалось выполнить тестовое подключение!'");
		
	КонецЕсли; 
	
	ЗаголовокСообщения = НСтр("ru = 'Тестовое подключение'");
	
	ПоказатьПредупреждение(,ТекстСообщения,15,ЗаголовокСообщения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура устанавливает видимость доступность элементов формы.
// 
&НаСервереБезКонтекста
Процедура УправлениеЭлементамиФормы(Форма)

	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	ЭтоСервернаяБаза = ?(Объект.РасположениеБазы = Перечисления.бит_мпд_ВидыРасположенияИнформационныхБаз.Серверная, Истина, Ложь);
	ЭтоАвтоСервис    = ?(Объект.РасположениеБазы = Перечисления.бит_мпд_ВидыРасположенияИнформационныхБаз.RestСервис,Истина, Ложь);
	ИспользуетсяМДМ  = Константы.бит_мдм_Использовать.Получить();
	
	Элементы.ИмяСервера.Видимость = ЭтоСервернаяБаза ИЛИ ЭтоАвтоСервис;
	Элементы.ИмяБазы.Видимость    = ЭтоСервернаяБаза ИЛИ ЭтоАвтоСервис;
	Элементы.Путь.Видимость       = НЕ ЭтоСервернаяБаза И НЕ ЭтоАвтоСервис;
	
	Если Не ИспользуетсяМДМ Тогда
		RESTСервис = Элементы.РасположениеБазы.СписокВыбора.НайтиПоЗначению(ПредопределенноеЗначение("Перечисление.бит_мпд_ВидыРасположенияИнформационныхБаз.RestСервис"));
		Если Не RESTСервис = Неопределено Тогда
		    Элементы.РасположениеБазы.СписокВыбора.Удалить(RESTСервис);
		КонецЕсли;
	Иначе
		Элементы.РасположениеБазы.Подсказка = НСтр("ru = 'Данный вид подключения используется только для целей подсистемы MDM.'");
		Элементы.РасположениеБазы.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимость()

// Процедура обрабатывает изменение реквизита РасположениеБазы.
// 
&НаСервере
Процедура ИзменениеРасположенияБазы()

  УправлениеЭлементамиФормы(ЭтотОбъект);	

КонецПроцедуры // ИзменениеРасположенияБазы()

// Функция производит тестовое подключение.
// 
// Возвращаемое значение:
//  флУдалось - Булево.
// 
&НаСервере
Функция ПроверитьПодключение()

	флУдалось = Ложь;
	Если Объект.РасположениеБазы = Перечисления.бит_мпд_ВидыРасположенияИнформационныхБаз.RestСервис Тогда
		
		 флУдалось = Справочники.бит_мпд_НастройкиВнешнихПодключений.ТестовоеСоедениеREST(Объект, "Ошибки");
		
	Иначе	
		
		Подключение = Справочники.бит_мпд_НастройкиВнешнихПодключений.ПодключитьКОМ(Объект,"Ошибки");
		
		Если Подключение = Неопределено Тогда
			
			флУдалось = Ложь;
			
		Иначе	
			
			флУдалось = Истина;
			
		КонецЕсли; 
		
		Подключение = Неопределено;
		
	КонецЕсли; 
	
	Возврат флУдалось;
	
КонецФункции // ПроверитьПодключение()

#КонецОбласти

