#Если Сервер Или ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Функция формирует префикс для абсолютных ссылок.
// 
// Параметры:
//  НастройкаПодключения - СправочникСсылка.бит_мдм_НастройкиПодключения.
// 
// Возвращаемое значение:
//  АбсолютныйПрефикс - Строка.
// 
Функция ПолучитьАбсолютныйПрефикс(НастройкаПодключения) Экспорт
 
 АбсолютныйПрефикс = "http://%Сервер%/%База%/odata/standard.odata/";		 
 АбсолютныйПрефикс = СтрЗаменить(АбсолютныйПрефикс, "%Сервер%", НастройкаПодключения.ИмяСервера);
 АбсолютныйПрефикс = СтрЗаменить(АбсолютныйПрефикс, "%База%"  , НастройкаПодключения.ИмяБазы);
 
 Возврат АбсолютныйПрефикс;
 
КонецФункции // ПолучитьАбсолютныйПрефикс()

// Функция Формирует строку подключения.
// 
// Параметры:
// 
// НастройкаПодключения - СправочникСсылка.бит_НастройкиВнешнихПодключений
// 
// Возвращаемое значение:
//  СтрокаПодключения - Строка.
// 
Функция СформироватьСтрокуПодключения(НастройкаПодключения) Экспорт

	
	Если НастройкаПодключения.РасположениеБазы = Перечисления.бит_мпд_ВидыРасположенияИнформационныхБаз.Файловая Тогда
		
		СтрокаПодключения = "File="""
		                    +НастройкаПодключения.Путь
		                    +""";Usr="""
							+НастройкаПодключения.Пользователь
							+""";";
		
	Иначе	
		
		СтрокаПодключения = "Srvr="""
		                    +НастройкаПодключения.ИмяСервера
							+""";Ref="""
							+НастройкаПодключения.ИмяБазы
							+""";Usr="""
							+НастройкаПодключения.Пользователь
							+""";";
		
	КонецЕсли;      
	
	
	Если ЗначениеЗаполнено(НастройкаПодключения.Пароль) Тогда
	
		 СтрокаПодключения = СтрокаПодключения+"Pwd="""+НастройкаПодключения.Пароль+""";";
	
	КонецЕсли; 

	Возврат СтрокаПодключения;
	
КонецФункции // СформироватьСтрокуПодключения()
 
// Функция Выполняет подключение по COM.
// 
// Параметры:
// НастройкаПодключения - СправочникСсылка.бит_НастройкиВнешнихПодключений.
// РежимСообщений - Строка.
// 
// Возвращаемое значение:
//  Коннектор - COM.
// 
Функция ПодключитьКОМ(НастройкаПодключения, РежимСообщений = "Ошибки")  Экспорт
	
	Коннектор = Неопределено;
	
	РежимыВывода = бит_ОбщегоНазначения.ОпределитьРежимыВывода(РежимСообщений);	
	
	Если бит_ОбщегоНазначенияКлиентСервер.ЭтоВинОС() Тогда
		
		// На Windows пробуем подключиться по Com 
		СтрокаПодключения = СформироватьСтрокуПодключения(НастройкаПодключения);	
		
		ИмяКомОбъекта = ПолучитьИмяКомКоннектора();
		
		Попытка
			
			КомОбъект = Новый COMObject(ИмяКомОбъекта);
			
		Исключение
			
			КомОбъект = Неопределено;
			
			Если РежимыВывода.ВыводитьОшибки Тогда
				
				ТекстСообщения = НСтр("ru = 'Не удалось создать COM объект по имени ""%1%""!'");
				ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, ИмяКомОбъекта);
				бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
				
			КонецЕсли; 
			
		КонецПопытки;
		
		Если НЕ КомОбъект = Неопределено Тогда
			
			Попытка
				
				Коннектор=КомОбъект.Connect(СтрокаПодключения);
				
				Если РежимыВывода.ВыводитьИнформацию Тогда
					
					ТекстСообщения = НСтр("ru = 'Выполнено подключение по настройке ""%1%"".'");
					ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Строка(НастройкаПодключения));
					бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
					
				КонецЕсли; 
				
			Исключение
				
				Если РежимыВывода.ВыводитьОшибки Тогда
					
					ТекстСообщения = НСтр("ru = 'Не удалось выполнить подключение по настройке ""%1%""! Строка подключение %2%. Описание ошибки %3%.'");
					ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения
																									, НастройкаПодключения.Наименование
																									, СтрокаПодключения
																									, Строка(ОписаниеОшибки()));
					бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
					
				КонецЕсли; 
				
			КонецПопытки;
			
		КонецЕсли; 
		
	Иначе
		
		// Код выполняется не под Windows. Com - технология недоступна.
		Если РежимыВывода.ВыводитьОшибки Тогда
			
			ТекстСообщения =  НСтр("ru = 'COM технология доступна только на серверах под управлением ОС семейства Windows.'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
			
		КонецЕсли; 
		
	КонецЕсли; 
	
	Возврат Коннектор;
	
КонецФункции // ПодключитьКОМ()
 
// Функция Выполняет подключение по OLE.
// 
// Параметры:
// НастройкаПодключения - СправочникСсылка.бит_НастройкиВнешнихПодключений.
// РежимСообщений - Строка.
// 
// Возвращаемое значение:
//  Коннектор - ComObject.
// 
Функция ПодключитьОЛЕ(НастройкаПодключения, РежимСообщений = "Ошибки")  Экспорт
	
	Коннектор = Неопределено;
	
	РежимыВывода = бит_ОбщегоНазначения.ОпределитьРежимыВывода(РежимСообщений);
	
	Если бит_ОбщегоНазначенияКлиентСервер.ЭтоВинОС() Тогда	
		
		// На Windows пытаемся подключиться по OLE
		СтрокаПодключения = СформироватьСтрокуПодключения(НастройкаПодключения);	
		
		ИмяКомОбъекта = ПолучитьИмяОле();
		
		Попытка
			
			Соединение = Новый COMObject(ИмяКомОбъекта);
			
		Исключение
			
			Соединение = Неопределено;
			
			Если РежимыВывода.ВыводитьОшибки Тогда
				
				ТекстСообщения = НСтр("ru = 'Не удалось создать COM объект по имени ""%1%""!'");
				ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, ИмяКомОбъекта);
				бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
				
			КонецЕсли; 
			
		КонецПопытки;
		
		Если НЕ Соединение = Неопределено Тогда
			
			Попытка
				
				Соединение.Connect(СтрокаПодключения);
				Соединение.Visible = Ложь;
				
				Если РежимыВывода.ВыводитьИнформацию Тогда
					
					ТекстСообщения = НСтр("ru = 'Выполнено подключение по настройке ""%1%"".'");
					ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Строка(НастройкаПодключения));
					бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
					
				КонецЕсли; 
				
			Исключение
				
				Соединение = Неопределено;			
				
				Если РежимыВывода.ВыводитьОшибки Тогда
					
					ТекстСообщения = НСтр("ru = 'Не удалось выполнить подключение по настройке ""%1%""! Строка подключение %2%. Описание ошибки %3%.'");
					ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения
																								, НастройкаПодключения.Наименование
																								, СтрокаПодключения
																								, Строка(ОписаниеОшибки()));
					бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
					
				КонецЕсли; 
				
			КонецПопытки;
			
		КонецЕсли; 
		
	Иначе
	
		// Код выполняется не под Windows. OLE - технология недоступна.
		Если РежимыВывода.ВыводитьОшибки Тогда
			
			ТекстСообщения =  НСтр("ru = 'OLE технология доступна только на компьютерах под управлением ОС семейства Windows.'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
			
		КонецЕсли; 
	
	КонецЕсли;
	
	Возврат Соединение;
	
КонецФункции // ПодключитьОЛЕ()

// Функция выполняет тестовое подключение к удаленной информационной базе по REST-сервису.
// 
// Параметры:
// НастройкаПодключения - СправочникСсылка.бит_НастройкиВнешнихПодключений.
// РежимСообщений - Строка.
// 
// Возвращаемое значение:
//  флВыполнено - Булево.
// 
Функция ТестовоеСоедениеREST(НастройкаПодключения, РежимСообщений = "Ошибки")  Экспорт
	
	РежимыВывода = бит_ОбщегоНазначения.ОпределитьРежимыВывода(РежимСообщений);	
	
	ПризнакОбращения = бит_мпд_КлиентСервер.ПризнакОбращенияОДата();
	ТекстЗапросаМета =  ПризнакОбращения;
	
	Путь = НастройкаПодключения.ИмяСервера+"/"+НастройкаПодключения.ИмяБазы;
	
	СоединениеHTTP = Новый HTTPСоединение(Путь,,НастройкаПодключения.Пользователь,НастройкаПодключения.Пароль);
	
	ЗапросHTTP = Новый HTTPЗапрос(ТекстЗапросаМета);
	
	ОтветHTTP = СоединениеHTTP.Получить(ЗапросHTTP);
	
	флВыполнено = ?(ОтветHTTP.КодСостояния = 200, Истина, Ложь);
	
	Если флВыполнено Тогда
		
		Если РежимыВывода.ВыводитьИнформацию Тогда
			
			ТекстСообщения = НСтр("ru = 'Выполнено подключение по настройке ""%1%"".'");
			ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Строка(НастройкаПодключения));
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
			
		КонецЕсли; 
		
	Иначе	
		
		Если РежимыВывода.ВыводитьОшибки Тогда
			
			ТекстСообщения = НСтр("ru = 'Не удалось выполнить подключение по настройке ""%1%""! Код ошибки: %2%'");
			ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения
			, НастройкаПодключения.Наименование
			, ОтветHTTP.КодСостояния);
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);				 
			
		КонецЕсли; 
		
		
	КонецЕсли; 
	
	Возврат флВыполнено;
	
КонецФункции // ТестовоеСоедениеREST()

#КонецОбласти
 
#Область СлужебныеПроцедурыИФункции

// Функция возвращает имя компоненты ComConnector в зависимости от редакции платформы.
// 
// Возвращаемое значение:
//  ИмяКомОбъекта - Строка.
// 
Функция ПолучитьИмяКомКоннектора()

	ИмяКомОбъекта = "V%1%.ComConnector";
	ТекущаяРедакцияПлатформы = бит_ОбщегоНазначенияКлиентСервер.РедакцияПлатформы();
	ТекущаяРедакцияПлатформы = СтрЗаменить(ТекущаяРедакцияПлатформы,".","");
	ИмяКомОбъекта = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ИмяКомОбъекта, ТекущаяРедакцияПлатформы);
		
	Возврат ИмяКомОбъекта;
	
КонецФункции // ПолучитьИмяКомКоннектора()

// Функция возвращает имя компоненты Application в зависимости от редакции платформы.
// 
// Возвращаемое значение:
//  ИмяКомОбъекта - Строка.
// 
Функция ПолучитьИмяОле()

	ИмяКомОбъекта = "V%1%.Application";
	ТекущаяРедакцияПлатформы = бит_ОбщегоНазначенияКлиентСервер.РедакцияПлатформы();
	ТекущаяРедакцияПлатформы = СтрЗаменить(ТекущаяРедакцияПлатформы,".","");
	ИмяКомОбъекта = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ИмяКомОбъекта, ТекущаяРедакцияПлатформы);
		
	Возврат ИмяКомОбъекта;
	
КонецФункции // ПолучитьИмяОле()

#КонецОбласти

#КонецЕсли
