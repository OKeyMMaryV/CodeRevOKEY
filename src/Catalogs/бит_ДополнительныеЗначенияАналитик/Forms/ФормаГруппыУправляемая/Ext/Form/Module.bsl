
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Объект.Владелец) = Тип("ПланВидовХарактеристикСсылка.бит_ВидыДополнительныхАналитик") Тогда
		Элементы.Владелец.Заголовок = "Аналитика";
	Иначе	
		Элементы.Владелец.Заголовок = "Субконто";
	КонецЕсли;

	фКэшРеквизитовФормы = Новый Структура;
	фКэшРеквизитовФормы.Вставить("Владелец");
	
	ЗаполнитьТекущиеЗначенияРеквизитовФормы();
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВладелецПриИзменении(Элемент)
	
	ВладелецПриИзмененииСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура РодительНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Владелец", Объект.Владелец);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	
	ОткрытьФорму("Справочник.бит_ДополнительныеЗначенияАналитик.ФормаВыбораГруппы",ПараметрыФормы,Элемент);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область РаботаСКэшемРеквизитовФормы

// Процедура заполняет кэш реквизитов формы данными объекта.
// 
&НаСервере
Процедура ЗаполнитьТекущиеЗначенияРеквизитовФормы()
	
	Для каждого КлючИЗначение Из фКэшРеквизитовФормы Цикл
		
		фКэшРеквизитовФормы[КлючИЗначение.Ключ] = Объект[КлючИЗначение.Ключ];
		
	КонецЦикла; 
	
КонецПроцедуры // ЗаполнитьТекущиеЗначенияРеквизитовФормы()

// Процедура добавляет в кэш реквизитов текущее значение заданного реквизита.
// 
// Параметры 
// 	ИмяРеквизита - Строка
// 
&НаСервере
Процедура ДобавитьВКэш(ИмяРеквизита)
	
	фКэшРеквизитовФормы[ИмяРеквизита] = Объект[ИмяРеквизита];	
	
КонецПроцедуры // ДобавитьВКэш()

// Процедура извлекает из кэша и присваивает объекту значение заданного реквизита.
// 
// Параметры 
// 	ИмяРеквизита - Строка
// 
&НаСервере
Процедура ИзвлечьИзКэша(ИмяРеквизита)
	
	Объект[ИмяРеквизита] = фКэшРеквизитовФормы[ИмяРеквизита];
	
КонецПроцедуры // ИзвлечьИзКэша()

#КонецОбласти

&НаСервере
Процедура ВладелецПриИзмененииСервер()
	
	Если ЗначениеЗаполнено(Объект.Владелец) Тогда
		
		Если НЕ Объект.Владелец.ТипЗначения.СодержитТип(Тип("СправочникСсылка.бит_ДополнительныеЗначенияАналитик")) Тогда
			
			ТекстСообщения = НСтр("ru='Аналитика ""%1%"" не содержит в составе типов Справочник ""Дополнительные значения аналитик""!'");
			ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Объект.Владелец);
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
			
			ИзвлечьИзКэша("Владелец");
			
			Возврат;
			
		КонецЕсли; 
		
		Если ЗначениеЗаполнено(Объект.Родитель) 
			И Объект.Родитель.Владелец <> Объект.Владелец Тогда
		
			 Объект.Родитель = Справочники.бит_ДополнительныеЗначенияАналитик.ПустаяСсылка();
		
		КонецЕсли; 
		
	КонецЕсли;
	
	ДобавитьВКэш("Владелец");
	
КонецПроцедуры

#КонецОбласти
