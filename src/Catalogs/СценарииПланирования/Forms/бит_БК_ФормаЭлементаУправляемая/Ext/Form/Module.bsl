
#Область ОбработчикиСобытийФормы

&НаКлиенте 
Перем мКэшРеквизитовФормы; // Хранит текущие значения реквизитов формы. Клиент.

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//ОК Ванюков К. +// Вывод добавленных реквизитов // 2012-04-12
	ИжТиСи_СВД_Сервер.ОК_ВывестиРеквизиты(ЭтаФорма, "Справочник.СценарииПланирования.ФормаЭлементаУправляемая");
	//ОК Ванюков К. -
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	МетаданныеОбъекта = Объект.Ссылка.Метаданные();
	ВалютаЗаполнения = Объект.Валюта;
	
	ЗаполнитьКэшЗначений();
	
	// Если объект новый
	Если Параметры.Ключ.Пустая() Тогда
		
		// Если это копирование
		Если ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			
			фЗадатьВопросОКопированииКурсов = Параметры.ЗначениеКопирования.ИспользоватьКурсыСценария;					
			фКэшЗначений.Вставить("ЗначениеКопирования", Параметры.ЗначениеКопирования);
								 
		КонецЕсли;	 
	
	КонецЕсли;   
	
	// Вызов процедуры установки доступности элементов управления
	УстановитьДоступностьЭлементов();
	
	// Установка параметров динамических список на случай записи нового элемента.	
	УстановитьОтборыДинамическихСписков();
			 
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// Установка параметров динамических список на случай записи нового элемента.	
	УстановитьОтборыДинамическихСписков();
	
КонецПроцедуры // ПослеЗаписиНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
		
	// Зададим имена реквизитов, подлежащих кешированию.
	мКэшРеквизитовФормы = Новый Структура;
	мКэшРеквизитовФормы.Вставить("бит_Периодичность");
	                       	                                              		
	// Запомним текущие значения реквизитов формы.
	ЗаполнитьТекущиеЗначенияРеквизитовФормы();
	мКэшРеквизитовФормы.Вставить("ВалютаЗаполнения", ВалютаЗаполнения);

	//НедоступныйРежимОтображения = РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
	//СписокКурсыСценариевПланирования.Отбор.РежимОтображения = НедоступныйРежимОтображения;
	//СписокНастройкаКонтроляПоСценариям.Отбор.РежимОтображения = НедоступныйРежимОтображения;
	
	// Если это скопированный элемент с признаком использования курсов сценария, тогда
	Если фЗадатьВопросОКопированииКурсов Тогда
		
		
		Оповещение = Новый ОписаниеОповещения("ВопросОКопированииКурсовСценарияЗавершение", ЭтотОбъект, Отказ);
		ПоказатьВопрос(Оповещение, 
					  "Выполнить копирование курсов сценария? 
					  |Сценарий будет при этом записан."
					  ,РежимДиалогаВопрос.ДаНет
					  ,30
					  ,КодВозвратаДиалога.Да);
	
	КонецЕсли;
	
КонецПроцедуры // ПриОткрытии()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет кэш реквизитов формы данными объекта.
//
// Параметры:
//  Нет.
//
&НаКлиенте 
Процедура ЗаполнитьТекущиеЗначенияРеквизитовФормы()
	
	Для Каждого КлючИЗначение Из мКэшРеквизитовФормы Цикл
		мКэшРеквизитовФормы[КлючИЗначение.Ключ] = Объект[КлючИЗначение.Ключ];
	КонецЦикла; 
	
КонецПроцедуры // ЗаполнитьТекущиеЗначенияРеквизитовФормы()

// Процедура добавляет в кэш реквизитов текущее значение заданного реквизита.
//
// Параметры:
// 	ИмяРеквизита - Строка.
//
&НаКлиенте
Процедура ДобавитьВКэш(ИмяРеквизита)
	
	мКэшРеквизитовФормы[ИмяРеквизита] = Объект[ИмяРеквизита];	
	
КонецПроцедуры // ДобавитьВКэш()

// Процедура записывает данный объект
//
// Параметры:
//  Отказ - Булево
//  КопироватьКурсыСценария - Булево (По умолчанию = Ложь)
//
&НаКлиенте
Процедура ЗаписатьЭтотОбъектКлиент(Отказ, КопироватьКурсыСценария = Ложь)

	ЗаписатьЭтотОбъект(Отказ, КопироватьКурсыСценария);
	
	Если Не Отказ Тогда
		ОповеститьОбИзменении(Объект.Ссылка);
	КонецЕсли;

КонецПроцедуры // ЗаписатьЭтотОбъектКлиент()

// Процедура открывает форму настройки заполнения курсов сценария
//
// Параметры:
//	СпособЗаполнения - Число (Номер способа в соответствии с которым будет заполняться регистр)
//
&НаКлиенте
Процедура ОткрытьФормуНастройкиЗаполненияКурсов(СпособЗаполнения) 	
	
	Отказ = Ложь;
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Оповещение = Новый ОписаниеОповещения("ВопросОЗаписиЭлементаЗавершение", ЭтотОбъект, СпособЗаполнения);
		ПоказатьВопрос(Оповещение, "Элемент еще не записан. Записать?", РежимДиалогаВопрос.ОКОтмена);
		
	Иначе
		
		ОткрытиеФормыНастройкиЗаписиКурсов(СпособЗаполнения, Отказ);
		
	КонецЕсли;
	
КонецПроцедуры // ОткрытьФормуНастройкиЗаполненияКурсов()

// Процедура - обработчик оповещения "ВопросОЗаписиЭлементаЗавершение" 
//
&НаКлиенте
Процедура ВопросОЗаписиЭлементаЗавершение(Ответ, СпособЗаполнения) Экспорт
	
	Отказ = Ложь;
	
	Если Ответ = КодВозвратаДиалога.ОК Тогда
		ЗаписатьЭтотОбъектКлиент(Отказ);
	Иначе
		Отказ = Истина;
	КонецЕсли;

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытиеФормыНастройкиЗаписиКурсов(СпособЗаполнения, Отказ);
		
КонецПроцедуры // ВопросОЗаписиЭлементаЗавершение() 

// Процедура выполняет открытие формы настройки заполнения курсов сценария 
//
// Параметры:
//	СпособЗаполнения - Число (Номер способа в соответствии с которым будет заполняться регистр)
//
&НаКлиенте
Процедура ОткрытиеФормыНастройкиЗаписиКурсов(СпособЗаполнения, Отказ)
		
	Если НЕ Объект.ИспользоватьКурсыСценария Тогда
		бит_ОбщегоНазначения.СообщитьОбОшибке("Не установлен флаг ""Использовать курсы сценария""", Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаЗаполнения) Тогда
		бит_ОбщегоНазначения.СообщитьОбОшибке("Не указана ""Валюта заполнения""", Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанныеКурса = Элементы.СписокКурсыСценариевПланирования.ТекущиеДанные;
	Если ТекущиеДанныеКурса = Неопределено Тогда
		флДатаНачала    = ТекущаяДата();
		флНачальныйКурс = 1;
	Иначе
		флДатаНачала    = ТекущиеДанныеКурса.Период;
		флНачальныйКурс = ТекущиеДанныеКурса.Курс;
	КонецЕсли;
	
	ИмяФормыЗаполненияКурсов = "Справочник."+ фКэшЗначений.ИмяОбъекта + ".Форма.ФормаНастройкиЗаполненияКурсовУправляемая";
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Ключ"		  			  , Объект.Ссылка);
	ПараметрыФормы.Вставить("ВалютаЗаполнения"		  , ВалютаЗаполнения);
	ПараметрыФормы.Вставить("ДатаНачала"   			  , флДатаНачала);		
	ПараметрыФормы.Вставить("ДатаОкончания"	  		  , ТекущаяДата());
	ПараметрыФормы.Вставить("ДлинаИнтервала"  		  , 1);
	ПараметрыФормы.Вставить("КоличествоШагов" 		  , 1);
	ПараметрыФормы.Вставить("НачальныйКурс"			  , флНачальныйКурс);
	ПараметрыФормы.Вставить("СпособЗаполнения"		  , СпособЗаполнения);
	ОткрытьФорму(ИмяФормыЗаполненияКурсов, ПараметрыФормы, Элементы.СписокКурсыСценариевПланирования);

КонецПроцедуры // ОткрытиеФормыПомощникаЗаписиКурсов() 

// Процедура  - обработчик оповещения "ВопросКопированииКурсовСценарияЗавершение"
//             
&НаКлиенте    
Процедура ВопросОКопированииКурсовСценарияЗавершение(Ответ, Отказ) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаписатьЭтотОбъектКлиент(Отказ, Истина);
	КонецЕсли;
	
КонецПроцедуры // ВопросПроверкиЭлементаНаНаличиеВ_ИБЗавершение()

&НаКлиенте
Процедура КурсыКопироватьИзКурсовВалют(Команда)
	
	ОткрытьФормуНастройкиЗаполненияКурсов(1);
	
КонецПроцедуры // КурсыКопироватьИзКурсовВалют()

&НаКлиенте
Процедура КурсыЗаполнитьИспользуяЛинейноеЗаполнение(Команда)
	
	ОткрытьФормуНастройкиЗаполненияКурсов(2);
	
КонецПроцедуры // КурсыЗаполнитьИспользуяЛинейноеЗаполнение()

&НаКлиенте
Процедура КурсыЗаполнитьПоПроизвольномуАлгоритму(Команда)
	
	ОткрытьФормуНастройкиЗаполненияКурсов(3);
	
КонецПроцедуры //КурсыЗаполнитьПоПроизвольномуАлгоритму()

&НаКлиенте
Процедура бит_ПериодичностьПриИзменении(Элемент)
	
	
	Если Не ЗначениеЗаполнено(Объект.ПериодичностьКонтроля) 
		 ИЛИ Не Объект.бит_Периодичность = Объект.ПериодичностьКонтроля Тогда
	
		 Объект.ПериодичностьКонтроля = Объект.бит_Периодичность;
	
	КонецЕсли; 
	
	ДобавитьВКэш("бит_Периодичность");
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьКурсыСценарияПриИзменении(Элемент)
	
	УстановитьДоступностьЭлементов();
	
КонецПроцедуры // ИспользоватьКурсыСценарияПриИзменении()

&НаКлиенте
Процедура ВалютаЗаполненияПриИзменении(Элемент)
	
	Если ВалютаЗаполнения <> мКэшРеквизитовФормы.ВалютаЗаполнения Тогда
	 		
		УстановитьОтборыСпискаКурсовСценария();
		
		мКэшРеквизитовФормы.Вставить("ВалютаЗаполнения", ВалютаЗаполнения);
	
	КонецЕсли;
	
КонецПроцедуры // ВалютаЗаполненияПриИзменении()

&НаКлиенте
Процедура СписокКурсыСценариевПланированияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Отказ = Истина;
		ПоказатьПредупреждение(,НСтр("ru = 'Необходимо записать элемент!'"));
	
	КонецЕсли; 
	
КонецПроцедуры // СписокКурсыСценариевПланированияПередНачаломДобавления()

&НаКлиенте
Процедура СписокНастройкаКонтроляПоСценариямПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Отказ = Истина;
		ПоказатьПредупреждение(,НСтр("ru = 'Необходимо записать элемент!'"));
	
	КонецЕсли; 
	
КонецПроцедуры // СписокНастройкаКонтроляПоСценариямПередНачаломДобавления()

// Процедура заполняет кэш значений, необходимый при работе на клиенте.
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура ЗаполнитьКэшЗначений()
	
	фКэшЗначений = Новый Структура;
	МетаданныеОбъекта = Объект.Ссылка.Метаданные();
	
	фКэшЗначений.Вставить("ИмяОбъекта", МетаданныеОбъекта.Имя);
	фКэшЗначений.Вставить("ИмяТипаОбъекта", "СправочникОбъект." + МетаданныеОбъекта.Имя);
	
КонецПроцедуры // ЗаполнитьКэшЗначений()

// Процедура устанавливает доступность элементов управления формы
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьДоступностьЭлементов()

	// Выясняем, есть ли по сценарию движения в регистре бит_ОборотыПоБюджетам 
	СправочникОбъект = ДанныеФормыВЗначение(Объект, Тип(фКэшЗначений.ИмяТипаОбъекта));
	ДоступностьОбязательныхЭлементов = Не СправочникОбъект.ЕстьДвиженияПоСценарию();
	ДоступностьКонтроля              = Не СправочникОбъект.ЕстьДвиженияПоКонтролю();
		
	// Если движений нет, то делаем доступными обязательные реквизиты   
	Элементы.Валюта.ТолькоПросмотр 					  	= Не ДоступностьОбязательныхЭлементов;
	Элементы.бит_Периодичность.ТолькоПросмотр	  		= Не ДоступностьОбязательныхЭлементов;
	Элементы.ПродолжительностьИнтервала.ТолькоПросмотр  = Не ДоступностьОбязательныхЭлементов;
	Элементы.ИспользоватьКурсыСценария.ТолькоПросмотр 	= Не ДоступностьОбязательныхЭлементов;
	
	Элементы.СтраницаКурсыСценария.Доступность = Объект.ИспользоватьКурсыСценария;
	Элементы.СписокКурсыСценариевПланирования.ТолькоПросмотр = Не ДоступностьКонтроля;
	
	Элементы.ПериодичностьКонтроля.ТолькоПросмотр = НЕ ДоступностьКонтроля;
	Элементы.ПериодичностьКонтроля.Подсказка = ?(ДоступностьКонтроля
				, "Периодичность контроля"
				, "Изменение периодичности контроля запрещено, т.к. зафиксированы движения по исполнению в регистре ""Контрольные значения бюджетов"".");
		
КонецПроцедуры // УстановитьДоступностьЭлементов()

// Процедура устанавливает отборы списка курсов сценария.
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьОтборыСпискаКурсовСценария()
	
	СписокКурсыСценариевПланирования.Отбор.Элементы.Очистить();
	
	бит_РаботаСДиалогамиСервер.УстановитьЭлементОтбораСписка(СписокКурсыСценариевПланирования, "Сценарий", Объект.Ссылка);
	бит_РаботаСДиалогамиСервер.УстановитьЭлементОтбораСписка(СписокКурсыСценариевПланирования, "Валюта", ВалютаЗаполнения);
			
КонецПроцедуры // УстановитьОтборыСпискаКурсовСценария()

// Процедура устанавливает отборы списка настроек контроля по сценарию.
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьОтборыСпискаНастроекКонтроляСценария()
	
	СписокНастройкаКонтроляПоСценариям.Отбор.Элементы.Очистить();
	
	бит_РаботаСДиалогамиСервер.УстановитьЭлементОтбораСписка(СписокНастройкаКонтроляПоСценариям, "Сценарий", Объект.Ссылка);
		
КонецПроцедуры // УстановитьОтборыСпискаНастроекКонтроляСценария()

// Процедура устанавливает параметры динамических списков 
// (СписокКурсыСценариевПланирования и СписокНастройкаКонтроляПоСценариям)  
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьОтборыДинамическихСписков()

	УстановитьОтборыСпискаКурсовСценария();
	УстановитьОтборыСпискаНастроекКонтроляСценария();

КонецПроцедуры // УстановитьОтборыДинамическихСписков()

// Процедура записывает данный объект
//
// Параметры:
//  Отказ - Булево
//  КопироватьКурсыСценария - Булево (По умолчанию = Ложь)
//
&НаСервере
Процедура ЗаписатьЭтотОбъект(Отказ, КопироватьКурсыСценария = Ложь)

	СправочникОбъект = ДанныеФормыВЗначение(Объект, Тип(фКэшЗначений.ИмяТипаОбъекта));
	Отказ = Не бит_ОбщегоНазначения.ЗаписатьСправочник(СправочникОбъект, , "Нет");
          		
	Если Не Отказ Тогда
		
		Если КопироватьКурсыСценария Тогда			
			СправочникОбъект.СкопироватьКурсыСценария(фКэшЗначений.ЗначениеКопирования);	
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеВДанныеФормы(СправочникОбъект, Объект);
	
	УстановитьОтборыДинамическихСписков();

КонецПроцедуры // ЗаписатьЭтотОбъект()      

#КонецОбласти
