#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КопироватьНаСервере(ПараметрКоманды);
	ОповеститьОбИзменении(Тип("СправочникСсылка.Подразделения"));
	
КонецПроцедуры

&НаСервере
Процедура КопироватьНаСервере(ЭлементСправочника)
	
	ТаблицаСсылок = Новый ТаблицаЗначений(); 
	ТаблицаСсылок.Колонки.Добавить("ЧтоЗаменить");
	ТаблицаСсылок.Колонки.Добавить("НаЧтоЗаменить");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ЭлементСправочника);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Подразделения.Ссылка КАК Ссылка,
	|	Подразделения.Родитель,
	|	Подразделения.Ссылка = &Ссылка КАК ЭтоИсточник
	|ИЗ
	|	Справочник.Подразделения КАК Подразделения
	|ГДЕ
	|	Подразделения.Ссылка В ИЕРАРХИИ(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка ИЕРАРХИЯ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка 		 = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Значение = Справочники.Подразделения.СоздатьЭлемент();
		Значение = Выборка.Ссылка.Скопировать();
		
		Родитель = Выборка.Родитель;
		Если НЕ Выборка.ЭтоИсточник Тогда
			ПолучитьСсылку(Родитель, ТаблицаСсылок);
		КонецЕсли; 
		
		Ссылка = Выборка.Ссылка;
		ПолучитьСсылку(Ссылка, ТаблицаСсылок);

		Значение.Родитель = Родитель;
		Значение.УстановитьСсылкуНового(Ссылка);
		Значение.УстановитьНовыйКод();
		Значение.ОбменДанными.Загрузка = Истина;
		
		Значение.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСсылку(ЧтоЗаменить, ТаблицаСсылок)
		
	СтрокаСсылка = ТаблицаСсылок.Найти(ЧтоЗаменить,"ЧтоЗаменить");
	Если СтрокаСсылка = Неопределено Тогда
		НаЧтоЗаменить = Справочники.Подразделения.ПолучитьСсылку(Новый УникальныйИдентификатор());
		НоваяСтрока = ТаблицаСсылок.Добавить();
		НоваяСтрока.ЧтоЗаменить   = ЧтоЗаменить;
		НоваяСтрока.НаЧтоЗаменить = НаЧтоЗаменить;
		ЧтоЗаменить = НаЧтоЗаменить;
	Иначе
		ЧтоЗаменить = СтрокаСсылка.НаЧтоЗаменить;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти