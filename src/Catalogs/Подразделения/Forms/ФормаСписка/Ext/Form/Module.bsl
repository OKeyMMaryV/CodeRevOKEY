
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов.
	МожноРедактировать = ПравоДоступа("Редактирование", Метаданные.Справочники.Подразделения);
	Элементы.СписокКонтекстноеМенюИзменитьВыделенные.Видимость = МожноРедактировать;
	// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов.

	флРежимСинхронизации = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_СинхронизироватьПодразделенияРеглЦФО");
	
	Элементы.ФормаКомандаСинхронизировать.Видимость = флРежимСинхронизации;
	
    Элементы.Список.ИзменятьСоставСтрок = НЕ флРежимСинхронизации;
	Если Элементы.Найти("ФормаОбработкабит_ИмпортОбъектовИзExcelЗагрузитьИзExcelПоНастройке") <> Неопределено Тогда		
		Элементы.ФормаОбработкабит_ИмпортОбъектовИзExcelЗагрузитьИзExcelПоНастройке.Видимость = НЕ флРежимСинхронизации;		
	КонецЕсли; 	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СтандартныеПодсистемыОбработчикиКоманд

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список, Список);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандРаботыСExcel

&НаКлиенте
Процедура ЗагрузитьЧерезТабДок(Команда)
    
    ПараметрыФормы = Новый Структура;
    ПараметрыФормы.Вставить("РежимЗагрузки"	, "Справочник");
    ПараметрыФормы.Вставить("ИмяСправочника", "Подразделения");
    
    ОткрытьФорму("Обработка.бит_ЗагрузкаДанныхИзТабличногоДокумента.Форма"
                    , ПараметрыФормы, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);	
    
КонецПроцедуры // ЗагрузитьЧерезТабДок()

#КонецОбласти

&НаКлиенте
Процедура КомандаСинхронизировать(Команда)
	
	ТекстВопроса =  НСтр("ru = 'Будет выполнена синхронизация подразделений регл. и ЦФО. Продолжить?'");
	Оповещение = Новый ОписаниеОповещения("ВопросЗавершение", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 15, КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте 
Процедура ВопросЗавершение(Ответ, ДополнительныеДанные) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Состояние( НСтр("ru = 'Выполняется синхронизация...'"));
		КомандаСинхронизироватьНаСервере();
		ОповеститьОбИзменении(Тип("СправочникСсылка.Подразделения"));
		
	КонецЕсли; 
	
КонецПроцедуры	// ВопросЗавершение

&НаСервереБезКонтекста
Процедура КомандаСинхронизироватьНаСервере()
	
	бит_Бюджетирование.ПервоначальнаяСинхронизацияПодразделенийРеглЦФО();
	
КонецПроцедуры

#КонецОбласти
