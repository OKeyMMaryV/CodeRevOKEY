
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Макет = Справочники.бит_Языки.ПолучитьМакет("МакетПодбора");

	Макет.Параметры.Расшифровка = Истина; // Чтобы работала расшифровка.
	
	ПолеТабДок.Очистить();
	ПолеТабДок.Вывести(Макет);

	ПолеТабДок.ФиксацияСверху      = 3;

	ПолеТабДок.ОтображатьЗаголовки = Ложь;
	ПолеТабДок.ОтображатьСетку     = Ложь;
	ПолеТабДок.ТолькоПросмотр      = Истина;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеТабДокОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
      			
	Код = "";
	
	ПараметрыФормы = ОбработкаВыбораЭлемента(Код);
	Если ПараметрыФормы = Неопределено Тогда
		
		ТекстСообщения = Нстр("ru = 'В справочнике ""Языки (БИТ)"" уже существует элемент %1%'");
		ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Код);
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
 			
	Иначе
		
		ОткрытьФорму("Справочник.бит_Языки.ФормаОбъекта", ПараметрыФормы);
		
	КонецЕсли;
		
КонецПроцедуры // ПолеТабДокОбработкаРасшифровки()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция обрабатывает выбор.
// 
// Параметры:
//  Код  - Строка
//  
// Возвращаемое значение:
//   Структура или Неопределено
// 
&НаСервере
Функция ОбработкаВыбораЭлемента(Код)

	ПараметрыФормы = Неопределено;
		
	ТекущаяОбласть    = Элементы.ПолеТабДок.ТекущаяОбласть;
	
	ОбластьКод          = ПолеТабДок.Области.Код;
	ОбластьКодЯзыка     = ПолеТабДок.Области.КодЯзыка;
	ОбластьНаименование = ПолеТабДок.Области.Наименование;
	
	Код          = ПолеТабДок.Область(ТекущаяОбласть.Верх, ОбластьКод.Лево		   , ТекущаяОбласть.Низ, ОбластьКод.Право).Текст;
	КодЯзыка     = ПолеТабДок.Область(ТекущаяОбласть.Верх, ОбластьКодЯзыка.Лево	   , ТекущаяОбласть.Низ, ОбластьКодЯзыка.Право).Текст;
	Наименование = ПолеТабДок.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
	
	// Проверка наличия выбранного элемента в справочнике.
	Ссылка = Справочники.бит_Языки.НайтиПоРеквизиту("ЯзыкСтрана", Код); 
	Если Ссылка.Пустая() Тогда
		
		// Заполнение параметров формы нового элемента.
		СтруктураЗаполнения = Новый Структура("КодЯзыка, ЯзыкСтрана, Наименование", КодЯзыка, Код, Наименование);		
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", СтруктураЗаполнения);
		
	КонецЕсли;

	Возврат ПараметрыФормы;
	
КонецФункции // ОбработкаВыбораЭлемента()

#КонецОбласти