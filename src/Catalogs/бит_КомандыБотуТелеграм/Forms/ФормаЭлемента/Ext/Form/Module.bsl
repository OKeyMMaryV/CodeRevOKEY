
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИспользоватьВнешнийЗапрос = Объект.ИспользоватьВнешнийЗапрос;
	
	Если Параметры.Ключ.Пустая() Тогда
	
		Объект.ИсточникПолученияДанных = Объект.Ссылка;
	
	КонецЕсли; 

	УправлениеЭлементамиФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьВнешнийЗапросПриИзменении(Элемент)
	ИспользоватьВнешнийЗапросПриИзмененииСервер();	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьТолькоПериодПриИзменении(Элемент)
	ИспользованиеДат();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьИнтервалМеждуДатамиПриИзменении(Элемент)
	ИспользованиеДат();
КонецПроцедуры

&НаКлиенте
Процедура ИсточникПолученияДанныхПриИзменении(Элемент)
	ЗаполнитьПараметрыИзЗапросаНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаполнитьПараметрыИзЗапроса(Команда)
	
	Если ЗначениеЗаполнено(Объект.Запрос) Тогда
		Если Объект.Параметры.Количество() > 0 Тогда
			Текст = НСтр("ru = 'Таблица параметров не пуста. Очистить таблицу?'");
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПараметрыЗавершение", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, Текст, РежимДиалогаВопрос.ДаНет);
		Иначе
			ЗаполнитьПараметрыЗавершение(КодВозвратаДиалога.Да, Неопределено);
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры

// Процедура - Заполнить параметры завершение.
//
// Параметры:
//  Ответ					 - КодВозвратаДиалога - ответ на вопрос.
//  ДополнительныеПараметры	 - Структура - параметры.
//
&НаКлиенте
Процедура ЗаполнитьПараметрыЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
	
		Объект.Параметры.Очистить();
		
	КонецЕсли; 
	
	ЗаполнитьПараметрыИзЗапросаНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеЭлементамиФормы()

	ЭтаФорма.ТолькоПросмотр 					= Объект.Предопределенный;
	Элементы.ИсточникПолученияДанных.Видимость 	= ИспользоватьВнешнийЗапрос;
	Элементы.Запрос.Видимость 					= НЕ ИспользоватьВнешнийЗапрос;
	
КонецПроцедуры

&НаСервере
Процедура ИспользованиеДат()

	Если Объект.ИспользоватьТолькоПериод
		И Объект.ИспользоватьНачалоИКонецПериода Тогда
	
		Объект.ИспользоватьНачалоИКонецПериода = Ложь;
		Объект.ИспользоватьТолькоПериод = Ложь;
	
	КонецЕсли; 

КонецПроцедуры // ИспользованиеДат()

&НаСервере
Процедура ИспользоватьВнешнийЗапросПриИзмененииСервер()

	ИспользоватьВнешнийЗапрос = Объект.ИспользоватьВнешнийЗапрос;
	
	Если НЕ ИспользоватьВнешнийЗапрос Тогда
	
		Объект.ИсточникПолученияДанных = Объект.Ссылка;
		
	Иначе	
		Объект.ИсточникПолученияДанных = Неопределено;
	КонецЕсли; 
	
	УправлениеЭлементамиФормы();

КонецПроцедуры // ИспользоватьВнешнийЗапросПриИзмененииСервер()

&НаСервере
Процедура ЗаполнитьПараметрыИзЗапросаНаСервере()
	
	Если Объект.ИспользоватьВнешнийЗапрос Тогда 
		
		Если ТипЗнч(Объект.ИсточникПолученияДанных) = Тип("СправочникСсылка.бит_Запросы") Тогда
			
			Модуль = Вычислить("ОбщегоНазначения");
			Объект.Запрос = Модуль.ЗначениеРеквизитаОбъекта(Объект.ИсточникПолученияДанных, "ТекстЗапроса");
			Объект.Параметры.Очистить();
			
		ИначеЕсли ТипЗнч(Объект.ИсточникПолученияДанных) = Тип("СправочникСсылка.бит_КомандыБотуТелеграм") Тогда
			
			Модуль = Вычислить("ОбщегоНазначения");
			Объект.Запрос = Модуль.ЗначениеРеквизитаОбъекта(Объект.ИсточникПолученияДанных, "Запрос");
			Объект.Параметры.Очистить();
			
		КонецЕсли;	
		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Объект.Запрос) Тогда
		Возврат;	
	КонецЕсли; 
	
	Схема = Новый СхемаЗапроса;
	Схема.УстановитьТекстЗапроса(Объект.Запрос);
	
	ОписаниеПараметровЗапроса = Схема.НайтиПараметры();
	
	Для каждого СтрОписания Из ОписаниеПараметровЗапроса Цикл
		
		Если ЭтоТиповойПараметр(СтрОписания.Имя) Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаПараметров = Объект.Параметры.Добавить();
		СтрокаПараметров.Наименование = СтрОписания.Имя;
		
		ДоступныеТипы = СтрОписания.ТипЗначения.Типы();
		Если ДоступныеТипы.Количество()=0 Тогда
			СтрокаПараметров.Значение = ЗначениеВСтрокуВнутр("");
			
		Иначе	
			Массив = Новый Массив;
			Массив.Добавить(ДоступныеТипы.Получить(0));
			НовоеОписаниеТипов	= Новый ОписаниеТипов(Массив);
			
			СтрокаПараметров.Значение = НовоеОписаниеТипов.ПривестиЗначение(Неопределено);
		КонецЕсли;
		
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Функция ЭтоТиповойПараметр(ИмяПараметра)

	ЭтоТиповой = Ложь;
	
	Если ИмяПараметра = "Период"
		ИЛИ ИмяПараметра = "НачалоПериода"
		ИЛИ ИмяПараметра = "КонецПериода" Тогда
	
		ЭтоТиповой = Истина;
	
	КонецЕсли; 

	Возврат ЭтоТиповой;
	
КонецФункции

#КонецОбласти
