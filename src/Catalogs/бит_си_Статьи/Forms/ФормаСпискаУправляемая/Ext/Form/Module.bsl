
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.СписокПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма);

	ЭтаФорма.Элементы.Список.РежимВыбора = ЭтаФорма.Параметры.РежимВыбора;
	
	Элементы.ФормаСохранитьСтатьи.Видимость = (НЕ Метаданные.Обработки.Найти("бит_си_СохранениеСправочнойИнформации") = Неопределено);
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаСтатьяСправки" Тогда
		ТекущаяСтатья = ?(Элементы.Список.ТекущиеДанные = Неопределено, Неопределено, Элементы.Список.ТекущиеДанные.Ссылка);
		Если Параметр = ТекущаяСтатья Тогда
			ОбновитьОтображениеСтатьи();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТекстСтатьиРедактированиеПриИзменении(Элемент)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	ИначеЕсли Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ОбновитьРабочийТекстИзПоляРедактирования();
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры // ТекстСтатьиРедактированиеПриИзменении()

&НаКлиенте
Процедура ТекстСтатьиHTMLПриИзменении(Элемент)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда 
		Возврат;
	ИначеЕсли Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	СписокИзображений = ПолучитьСписокИзображенийВТексте(РабочийТекстHTML);
	
	ОбновитьТекстПоляРедактированияИзРабочегоТекста(СписокИзображений);
	
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры // ТекстСтатьиHTMLПриИзменении()

&НаКлиенте
Процедура ГруппаТекстСтатьиРедактированиеПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		ОткрытаяСтраницаГруппы = ТекущаяСтраница.Имя;
		Возврат;
	КонецЕсли;
	
	// Переделываем на смену страниц, чтобы пользователь мог работать сколько хотел.
	Если ОткрытаяСтраницаСтатьи = "ГруппаРедактирование" И Модифицированность Тогда 
		ОбновитьРабочийТекстИзПоляРедактирования();
	ИначеЕсли ОткрытаяСтраницаСтатьи = "ГруппаHTML" И Модифицированность Тогда 
		ОбновитьТекстПоляРедактированияИзРабочегоТекста(ПолучитьСписокИзображенийВТексте(РабочийТекстHTML));
	КонецЕсли;
	
	ОткрытаяСтраницаСтатьи = ТекущаяСтраница.Имя;
	
КонецПроцедуры // ГруппаТекстСтатьиРедактированиеПриСменеСтраницы()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	Если ПоследняяСсылка = Элемент.ТекущиеДанные.Ссылка Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЭтаФорма.Модифицированность И НЕ ПоследняяСсылка.Пустая() Тогда 
		ТекстСообщения 	   = "Текст статьи: " + Строка(ПоследняяСсылка)+" был изменен. Сохранить изменения?"; 
		ОписаниеОповещения = Новый ОписаниеОповещения("СписокПриАктивизацииСтрокиЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, , РежимДиалогаВопрос.ДаНет);
	КонецЕсли;	
	
КонецПроцедуры

// Завершение немодального вызова "СписокПриАктивизацииСтроки".
//
// Параметры:
//  Результат	 - КодВозвратаДиалога	 - реакция пользователя.
//  ДополнительныеПараметры		 - произвольный	 - Дополнительные параметры.
//
&НаКлиенте
Процедура СписокПриАктивизацииСтрокиЗавершение(Результат, ДополнительныеПараметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если ОткрытаяСтраницаСтатьи = "ГруппаРедактирование" И Модифицированность Тогда 
			ОбновитьРабочийТекстИзПоляРедактирования();
		ИначеЕсли ОткрытаяСтраницаСтатьи = "ГруппаHTML" И Модифицированность Тогда 
			ОбновитьТекстПоляРедактированияИзРабочегоТекста(ПолучитьСписокИзображенийВТексте(РабочийТекстHTML));
		КонецЕсли;
		ЗаписатьСтатьюНаСервере(ПоследняяСсылка, ПолучитьСписокИзображенийВТексте(РабочийТекстHTML));
		Оповестить("ИзмененаСтатьяСправки", ПоследняяСсылка, ЭтаФорма);
	КонецЕсли;
	ЭтаФорма.Модифицированность  = Ложь;
	ПоследняяСсылка = Элементы.Список.ТекущаяСтрока;
	
	ОтключитьОбработчикОжидания("ОбновитьОтображениеСтатьи");
	ПодключитьОбработчикОжидания("ОбновитьОтображениеСтатьи", 0.3, Истина);
	
КонецПроцедуры // СписокПриАктивизацииСтроки()

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ Элементы.Список.РежимВыбора Тогда
		Возврат;
	КонецЕсли;
	Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	ОповеститьОВыборе(Элементы.Список.ТекущиеДанные.Ссылка);
	
КонецПроцедуры // СписокВыбор()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыМестаИспользования

&НаКлиенте
Процедура МестаИспользованияИмяМеткиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ИменаКоллекций = Новый Массив();
	ИменаКоллекций.Добавить("Справочники");
	ИменаКоллекций.Добавить("Документы");
	ИменаКоллекций.Добавить("ЖурналыДокументов");
	ИменаКоллекций.Добавить("ПланыВидовХарактеристик");
	ИменаКоллекций.Добавить("ПланыСчетов");
	ИменаКоллекций.Добавить("ПланыВидовРасчета");
	ИменаКоллекций.Добавить("РегистрыСведений");
	ИменаКоллекций.Добавить("РегистрыНакопления");
	ИменаКоллекций.Добавить("РегистрыБухгалтерии");
	ИменаКоллекций.Добавить("РегистрыРасчета");
	ИменаКоллекций.Добавить("Отчеты");
	ИменаКоллекций.Добавить("Обработки");
	ИменаКоллекций.Добавить("БизнесПроцессы");
	ИменаКоллекций.Добавить("Задачи");
	ИменаКоллекций.Добавить("ПланыОбмена");
	ИменаКоллекций.Добавить("ОбщиеФормы");
	
	ФормаВыбора = ПолучитьФорму("Справочник.бит_си_Статьи.Форма.ДеревоМетаданныхУправляемая",
								Новый Структура("ИменаКоллекций,
												|ПоказыватьФормы,
												|ОповещатьОВыборе,
												|ВозвращаемоеЗначение",
												ИменаКоллекций,
												Истина,
												Истина,
												"ИмяОбъектаПолное"),
								Элемент);
	ФормаВыбора.Открыть();
	
КонецПроцедуры // МестаИспользованияИмяМеткиНачалоВыбора()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОтборыОбъектов

&НаКлиенте
Процедура ОтборыОбъектовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если Копирование = Истина Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если Элементы.ОтборыОбъектов.ТекущаяСтрока = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если Элементы.ОтборыОбъектов.ТекущиеДанные.ПолучитьРодителя() <> Неопределено Тогда
		Отказ = Истина;
		НоваяСтрока = Элементы.ОтборыОбъектов.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы().Добавить();
		Элементы.ОтборыОбъектов.ТекущаяСтрока = НоваяСтрока.ПолучитьИдентификатор();
		ОтборыОбъектовПриНачалеРедактирования(Элементы.ОтборыОбъектов, Истина, Ложь);
	КонецЕсли;
	
КонецПроцедуры // ОтборыОбъектовПередНачаломДобавления()

&НаКлиенте
Процедура ОтборыОбъектовПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		ТекущаяСтрока = Элементы.ОтборыОбъектов.ТекущиеДанные;
		Если ТекущаяСтрока.ПолучитьРодителя().Тип = "#Роль" Тогда
			ТекущаяСтрока.Тип		= ТекущаяСтрока.ПолучитьРодителя().Тип;
			ТекущаяСтрока.Объект	= "";
			Элемент.ПодчиненныеЭлементы.ОтборыОбъектовОбъект.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			Элемент.ПодчиненныеЭлементы.ОтборыОбъектовОбъект.ВыбиратьТип = Ложь;
		ИначеЕсли ТекущаяСтрока.ПолучитьРодителя().Тип = "#РежимЗапуска" Тогда
			ТекущаяСтрока.Тип		= ТекущаяСтрока.ПолучитьРодителя().Тип;
			ТекущаяСтрока.Объект	= "";
			Элемент.ПодчиненныеЭлементы.ОтборыОбъектовОбъект.ОграничениеТипа = Новый ОписаниеТипов("Строка");
			Элемент.ПодчиненныеЭлементы.ОтборыОбъектовОбъект.ВыбиратьТип = Ложь;
		Иначе
			ТекущаяСтрока.Тип		= ТекущаяСтрока.ПолучитьРодителя().Тип;
			ТекущаяСтрока.Объект	= Новый (Тип(СтрЗаменить(ТекущаяСтрока.Тип, ".", "Ссылка.")));
			Элемент.ПодчиненныеЭлементы.ОтборыОбъектовОбъект.ОграничениеТипа = Новый ОписаниеТипов(СтрЗаменить(ТекущаяСтрока.Тип, ".", "Ссылка."));
			Элемент.ПодчиненныеЭлементы.ОтборыОбъектовОбъект.ВыбиратьТип = Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ОтборыОбъектовПриНачалеРедактирования()

&НаКлиенте
Процедура ОтборыОбъектовПередНачаломИзменения(Элемент, Отказ)
	
	Если Элементы.ОтборыОбъектов.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ОтборыОбъектовПередНачаломИзменения()

&НаКлиенте
Процедура ОтборыОбъектовОбъектПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ОтборыОбъектов.ТекущиеДанные;
	ТекущаяСтрока = Элементы.ОтборыОбъектов.ТекущаяСтрока;
	НайденаяСтрока = Неопределено;
	СтрокиДляПроверки = ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
	Для Каждого ТекСтрока Из СтрокиДЛяПроверки Цикл
		Если НЕ ТекСтрока.ПолучитьИдентификатор() = ТекущаяСтрока И ТекСтрока.Объект = ТекущиеДанные.Объект Тогда
			НайденаяСтрока = ТекСтрока.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НЕ НайденаяСтрока = Неопределено Тогда
		СтрокиДляПроверки.Удалить(СтрокиДляПроверки.Индекс(ТекущиеДанные));
		Элементы.ОтборыОбъектов.ТекущаяСтрока = НайденаяСтрока;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры // ОтборыОбъектовОбъектПриИзменении()

&НаКлиенте
Процедура ОтборыОбъектовОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Элементы.ОтборыОбъектов.ТекущиеДанные.Тип = "#Роль" Тогда
		СтандартнаяОбработка = Ложь;
		ОтборыОбъектовОбъектНачалоВыбораРоли(Элемент);
	ИначеЕсли Элементы.ОтборыОбъектов.ТекущиеДанные.Тип = "#РежимЗапуска" Тогда
		СтандартнаяОбработка = Ложь;
		ОтборыОбъектовОбъектНачалоВыбораРежимаЗапуска(Элемент);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборыОбъектовОбъектНачалоВыбораРежимаЗапуска(Знач Элемент)
	
	Перем СписокРежимов, ТекРежим;
	
	СписокРежимов = Новый СписокЗначений();
	СписокРежимов.Добавить("Обычный", "Обычный");
	СписокРежимов.Добавить("Управляемый", "Управляемый");
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтборыОбъектовОбъектНачалоВыбораРежимаЗапускаЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокРежимов, Элемент, 
				СписокРежимов.НайтиПоЗначению(Элементы.ОтборыОбъектов.ТекущиеДанные.Объект));
КонецПроцедуры

// Завершение модального вызова ОтборыОбъектовОбъектНачалоВыбораРежимаЗапуска.
//
// Параметры:
//  Результат	 - Строка	 - результат выбора.
//  Дополнение	 - 	Произвольный - доп. параметры.
//
&НаКлиенте
Процедура ОтборыОбъектовОбъектНачалоВыбораРежимаЗапускаЗавершение(Результат, Дополнение) Экспорт 
	
	Если Результат = Неопределено Тогда
		Элементы.ОтборыОбъектов.ТекущиеДанные.Объект = Результат.Значение;
		ОтборыОбъектовОбъектПриИзменении(Дополнение.Элемент);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОтборыОбъектовОбъектНачалоВыбораРоли(Знач Элемент)
		
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтборыОбъектовОбъектНачалоВыбораРолиЗавершение", 
													ЭтотОбъект, Новый Структура("Элемент", Элемент));
	СписокРолей = ПолучитьСписокРолей();
	ПоказатьВыборИзСписка(ОписаниеОповещения, СписокРолей, Элемент, 
	СписокРолей.НайтиПоЗначению(Элементы.ОтборыОбъектов.ТекущиеДанные.Объект));
	
КонецПроцедуры

// Завершение модального вызова ОтборыОбъектовОбъектНачалоВыбораРоли.
//
// Параметры:
//  Результат	 - Строка	 - результат выбора
//  Дополнение	 - Произвольный - доп. параметры.
//
&НаКлиенте
Процедура ОтборыОбъектовОбъектНачалоВыбораРолиЗавершение(Результат, Дополнение) Экспорт
	
	Если НЕ Результат = Неопределено Тогда
		Элементы.ОтборыОбъектов.ТекущиеДанные.Объект = Результат.Значение;
		ОтборыОбъектовОбъектПриИзменении(Дополнение.Элемент);
	КонецЕсли;

КонецПроцедуры // ОтборыОбъектовОбъектНачалоВыбора()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСвязанныеОбъекты

&НаКлиенте
Процедура СвязанныеОбъектыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Копирование = Истина Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если Элементы.СвязанныеОбъекты.ТекущаяСтрока = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если Элементы.СвязанныеОбъекты.ТекущиеДанные.ПолучитьРодителя() <> Неопределено Тогда
		Отказ = Истина;
		НоваяСтрока = Элементы.СвязанныеОбъекты.ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы().Добавить();
		Элементы.СвязанныеОбъекты.ТекущаяСтрока = Элементы.СвязанныеОбъекты.ТекущаяСтрока + 1;
		СвязанныеОбъектыПриНачалеРедактирования(Элементы.СвязанныеОбъекты, Истина, Ложь);
	КонецЕсли;
КонецПроцедуры // СвязанныеОбъектыПередНачаломДобавления()

&НаКлиенте
Процедура СвязанныеОбъектыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	Если НоваяСтрока Тогда
		ТекущаяСтрока = Элементы.СвязанныеОбъекты.ТекущиеДанные;
		ТекущаяСтрока.Тип		= ТекущаяСтрока.ПолучитьРодителя().Тип;
		ТекущаяСтрока.Объект	= Новый (Тип(СтрЗаменить(ТекущаяСтрока.Тип, ".", "Ссылка.")));
		Элемент.ПодчиненныеЭлементы.СвязанныеОбъектыОбъект.ОграничениеТипа = Новый ОписаниеТипов(СтрЗаменить(ТекущаяСтрока.Тип, ".", "Ссылка."));
		Элемент.ПодчиненныеЭлементы.СвязанныеОбъектыОбъект.ВыбиратьТип = Ложь;
	КонецЕсли;
КонецПроцедуры // СвязанныеОбъектыПриНачалеРедактирования()

&НаКлиенте
Процедура СвязанныеОбъектыПередНачаломИзменения(Элемент, Отказ)
	Если Элементы.СвязанныеОбъекты.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры // СвязанныеОбъектыПередНачаломИзменения()

&НаКлиенте
Процедура СвязанныеОбъектыПередУдалением(Элемент, Отказ)
	Если Элементы.СвязанныеОбъекты.ТекущиеДанные.ПолучитьРодителя() = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
КонецПроцедуры // СвязанныеОбъектыПередУдалением()

&НаКлиенте
Процедура СвязанныеОбъектыОбъектПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.СвязанныеОбъекты.ТекущиеДанные;
	ТекущаяСтрока = Элементы.СвязанныеОбъекты.ТекущаяСтрока;
	НайденаяСтрока = Неопределено;
	СтрокиДляПроверки = ТекущиеДанные.ПолучитьРодителя().ПолучитьЭлементы();
	Для Каждого ТекСтрока Из СтрокиДляПроверки Цикл
		Если НЕ ТекСтрока.ПолучитьИдентификатор() = ТекущаяСтрока И ТекСтрока.Объект = ТекущиеДанные.Объект Тогда
			НайденаяСтрока = ТекСтрока.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если НЕ НайденаяСтрока = Неопределено Тогда
		СтрокиДляПроверки.Удалить(СтрокиДляПроверки.Индекс(ТекущиеДанные));
		Элементы.ОтборыОбъектов.ТекущаяСтрока = НайденаяСтрока;
	КонецЕсли;
	Модифицированность = Истина;
	
КонецПроцедуры // СвязанныеОбъектыОбъектПриИзменении()

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	Если ЭтаФорма.Модифицированность Тогда 
		ТекстСообщения = "Перед началом изменения необходимо записать "
					   + ?(Элементы.Список.ТекущиеДанные.ЭтоГруппа, "группу", "статью");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Элементы.Список.ТекущаяСтрока, , , Отказ); 			   
	КонецЕсли;
КонецПроцедуры // СписокПередНачаломИзменения()

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область ОбработчикиКомандЗагрузкиСохраненияСтатей

// Процедура обработчик - события "ЗагрузитьИзСправки" кнопки командной панели формы.
// 
&НаКлиенте
Процедура ЗагрузитьИзСправки(Команда)

	#Если Не ВебКлиент Тогда

	ВремИмя = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ВремИмя);
	КодВозврата = 0;
	ЗапуститьСистему("DESIGNER /DumpConfigFiles  """ + ВремИмя +"\"+ """ -Help", Истина, КодВозврата);
	
	Если НЕ КодВозврата = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не удалось выгрузить файлы справки.");
		Возврат;
	КонецЕсли;
	
	МассивФайлов = НайтиФайлы(ВремИмя, "*.htm");
	Если МассивФайлов.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Не обнаружено выгруженных файлов справки.");
		Возврат;
	КонецЕсли;
		
	ИмяФайлаАрхива = ПолучитьИмяВременногоФайла("zip");
	
	Архив = Новый ЗаписьZipФайла(ИмяФайлаАрхива, , , МетодСжатияZIP.Сжатие, УровеньСжатияZIP.Оптимальный);
	Архив.Добавить(ВремИмя + "\*.*",
				   РежимСохраненияПутейZIP.СохранятьОтносительныеПути,
				   РежимОбработкиПодкаталоговZIP.ОбрабатыватьРекурсивно);
	Архив.Записать();
	
	Попытка
		УдалитьФайлы(ВремИмя, "*.*");
	Исключение
		Ошибка = ОписаниеОшибки();
	КонецПопытки;
	
	АдресФайла = "";
	Если НЕ ПоместитьФайл(АдресФайла,
						  ИмяФайлаАрхива,
						  ,
						  Ложь,
						  ЭтаФорма.УникальныйИдентификатор) Тогда
		Возврат;
	КонецЕсли;
	
	УдалитьФайлы(ИмяФайлаАрхива);
	ЗагрузитьСправкуМакетНаСервере(АдресФайла);
	#Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("На веб клиенте не доступна возможность загрузки из справки.");
	#КонецЕсли
	
КонецПроцедуры // ЗагрузитьИзСправки()

// Процедура обработчик - события "ЗагрузитьИзФайла" кнопки командной панели формы.
// 
&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Фильтр = "Файлы разделов справки(*.xml)|*.xml|Упакованные файлы разделов справки (*.zip)|*.zip";
	ДиалогОткрытияФайла.Расширение = "zip";
	ДиалогОткрытияФайла.МножественныйВыбор = ложь;
			
	Если НЕ ДиалогОткрытияФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
		
	ВремФайл = ДиалогОткрытияФайла.ПолноеИмяФайла;
	Оповещение = Новый ОписаниеОповещения("ВопросОбОчисткеИнформацииПередЗагрузкойЗавершение", ЭтотОбъект, ДиалогОткрытияФайла);	
	ПоказатьВопрос(Оповещение,
				   "Очистить справочную информацию перед загрузкой?",
				   РежимДиалогаВопрос.ДаНетОтмена,
				   30,
				   КодВозвратаДиалога.Нет,
				   "Загрузка справочной информации");
				   	
КонецПроцедуры // ЗагрузитьИзФайла()

// Процедура - обработчик оповещения "ВопросОбОчисткеИнформацииПередЗагрузкойЗавершение". 
// 
&НаКлиенте
Процедура ВопросОбОчисткеИнформацииПередЗагрузкойЗавершение(Ответ, ДиалогОткрытияФайла) Экспорт

	Если Ответ = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	ИначеЕсли Ответ = КодВозвратаДиалога.Да Тогда
		ОчиститьСправкуНаСервере();
	КонецЕсли;
	Расширение = Прав(ДиалогОткрытияФайла.ПолноеИмяФайла,3);
	АдресФайлаВоВременномХранилище = "";
	Если Не ПоместитьФайл(АдресФайлаВоВременномХранилище,
						  ДиалогОткрытияФайла.ПолноеИмяФайла,
						  ДиалогОткрытияФайла.ПолноеИмяФайла,
						  Ложь,
						  ЭтаФорма.УникальныйИдентификатор) Тогда
		Возврат;
	КонецЕсли;
	ЗагрузитьСправкуНаСервере(АдресФайлаВоВременномХранилище, Расширение);
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Данные загружены"); 
	Элементы.Список.Обновить();

КонецПроцедуры // () 

// Процедура обработчик команды "СохранитьСтатьи".
// 
&НаКлиенте
Процедура СохранитьСтатьи(Команда)
	
	Попытка
		Форма = ПолучитьФорму("Обработка.бит_си_СохранениеСправочнойИнформации.Форма.ФормаУправляемая", , ЭтаФорма);
	Исключение
		Возврат;
	КонецПопытки;
	Форма.Открыть();
	
КонецПроцедуры // СохранитьСтатьи()

// Процедура обработчик - события "ЗагрузитьHTMLИзФайла" кнопки командной панели "ГруппаЗагрузитьСохранитьОтмена".
// 
&НаКлиенте
Процедура ЗагрузитьHTMLИзФайла(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Или 
		Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	Фильтр = "Web-страница (*.htm,*.html)|*.htm;*.html";
	ДиалогОткрытияФайла.Фильтр = Фильтр;
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = "Выберите файл";
	Если НЕ ДиалогОткрытияФайла.Выбрать() Тогда
		Возврат;
	КонецЕсли;
	
	ВремДок = Новый ТекстовыйДокумент();
	ВремДок.Прочитать(ДиалогОткрытияФайла.ПолноеИмяФайла);
	РабочийТекстHTML = ВремДок.ПолучитьТекст();
	
	ОбновитьТекстПоляРедактированияИзРабочегоТекста(ПолучитьСписокИзображенийВТексте(РабочийТекстHTML));

	Модифицированность = Истина;
	
КонецПроцедуры // ЗагрузитьHTMLИзФайла()

// Процедура обработчик - события "СохранитьСтатью" кнопки командной панели "ГруппаЗагрузитьСохранитьОтмена".
// 
&НаКлиенте
Процедура СохранитьСтатью(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ОткрытаяСтраницаСтатьи = "ГруппаРедактирование" И Модифицированность Тогда 
		ОбновитьРабочийТекстИзПоляРедактирования();
	ИначеЕсли ОткрытаяСтраницаСтатьи = "ГруппаHTML" И Модифицированность Тогда 
		ОбновитьТекстПоляРедактированияИзРабочегоТекста(ПолучитьСписокИзображенийВТексте(РабочийТекстHTML));
	КонецЕсли;
	ЗаписатьСтатьюНаСервере(Элементы.Список.ТекущиеДанные.Ссылка, ПолучитьСписокИзображенийВТексте(РабочийТекстHTML));
	Оповестить("ИзмененаСтатьяСправки", Элементы.Список.ТекущиеДанные.Ссылка, ЭтаФорма);
	
КонецПроцедуры // СохранитьСтатью()

// Процедура обработчик - события "ОтменитьЗаписть" кнопки командной панели "ГруппаЗагрузитьСохранитьОтмена".
// 
&НаКлиенте
Процедура ОтменитьЗапись(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено Или 
		Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	ЭтаФорма.Модифицированность = Ложь;
	СписокПриАктивизацииСтроки(Элементы.Список);
	
КонецПроцедуры // ОтменитьЗаписть()

#КонецОбласти

#Область ОбработчикиКомандДереваСтатей

// Процедура обработчик - события "ИзменитьПорядокСтатейВверх" кнопки командной панели "ГруппаКоманднаяПанельСписка".
// 
&НаКлиенте
Процедура ИзменитьПорядокСтатейВверх(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИзменитьПорядок(Элементы.Список.ТекущиеДанные.Ссылка, -1);
	Элементы.Список.Обновить();
КонецПроцедуры // ИзменитьПорядокСтатейВверх()

// Процедура обработчик - события "ИзменитьПорядокСтатейВниз" кнопки командной панели "ГруппаКоманднаяПанельСписка".
// 
&НаКлиенте
Процедура ИзменитьПорядокСтатейВниз(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИзменитьПорядок(Элементы.Список.ТекущиеДанные.Ссылка, 1);
	Элементы.Список.Обновить();
КонецПроцедуры // ИзменитьПорядокСтатейВниз()

#КонецОбласти

#Область ОбработчикиКомандРедактированияСтатьи

// Процедура обработчик - команды "ВставитьКартинкуИзБуфера" 
// кнопки командной панели "КоманднаяПанельФорматированногоДокумента"
// Вставляет в текст статьи картинку из буфера обмена.
// 
&НаКлиенте
Процедура ВставитьКартинкуИзБуфера(Команда)
	
	ВремДанныеРегистратора = "";
	ВремДанныеБиблиотеки = "";
	ПолучитьПараметрыБиблиотекиУтилитСервер(ВремДанныеРегистратора, ВремДанныеБиблиотеки);
	Если НЕ бит_Утилиты.ЕстьКартинкаВБуфереОбмена(ВремДанныеБиблиотеки, ВремДанныеРегистратора) Тогда
		Возврат;
	КонецЕсли;
	ВыбранноеИзображение = бит_Утилиты.ПолучитьКартинкуИзБуфераОбмена(ВремДанныеРегистратора,ВремДанныеБиблиотеки, "png");
	АдресВХранилище = ПоместитьВоВременноеХранилище(ВыбранноеИзображение, ЭтаФорма.УникальныйИдентификатор);
	ВставитьКартинкуСервер(АдресВХранилище);
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры // ВставитьКартинкуИзБуфера()

// Процедура обработчик - команды "ВставитьСсылкуНаСтатьюСправки" 
// кнопки командной панели "КоманднаяПанельФорматированногоДокумента"
// Вставляет в текст статьи ссылку на другую статью справки.
// 
&НаКлиенте
Процедура ВставитьСсылкуНаСтатьюСправки(Команда)
	
	ФормаСтатьи = ПолучитьФорму("Справочник.бит_си_Статьи.ФормаВыбора", Новый Структура("РежимВыбора", Истина), ЭтаФорма);
	РезСтруктура = ФормаСтатьи.ОткрытьМодально(30);
	Если НЕ РезСтруктура = Неопределено Тогда
		НачалоВыделения = "";
		КонецВыделения = "";
		Элементы.ТекстСтатьиРедактирование.ПолучитьГраницыВыделения(НачалоВыделения, КонецВыделения);
		
		ТекстДляВставки = "<a href=""ВнСсылка=" +
						  СокрЛП(РезСтруктура.УникальныйИдентификатор()) +
						  """> " +
						  ?(ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(НачалоВыделения) = ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(КонецВыделения),
						    РезСтруктура.НазваниеСтатьи,
							ТекстСтатьиРедактирование.ПолучитьТекст(НачалоВыделения, КонецВыделения)) +
						  " </a>";
		
		ТекстСтатьиРедактирование.Удалить(НачалоВыделения, КонецВыделения);
		
		ВставитьТекстВHTMLСервер(ТекстДляВставки, НачалоВыделения);
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ВставитьСсылкуНаСтатьюСправки()

// Процедура обработчик - команды "ВставитьСсылкуНаОбъект" 
// кнопки командной панели "КоманднаяПанельФорматированногоДокумента"
// Вставляет в текст статьи ссылку на форму элемента Справочника, Документа, форму Отчета, Обработки.
// 
&НаКлиенте
Процедура ВставитьСсылкуНаОбъект(Команда)
	
	ИменаКоллекций = Новый Массив;
	ИменаКоллекций.Добавить("Справочники");
	ИменаКоллекций.Добавить("Документы");
	ИменаКоллекций.Добавить("Отчеты");
	ИменаКоллекций.Добавить("Обработки");
	
	ФормаДерева = ПолучитьФорму("Справочник.бит_си_Статьи.Форма.ДеревоМетаданныхУправляемая", Новый Структура("ИменаКоллекций", ИменаКоллекций), ЭтаФорма);
	РезСтруктура = ФормаДерева.ОткрытьМодально(30);
	
	Если НЕ РезСтруктура = Неопределено Тогда
		НачалоВыделения = "";
		КонецВыделения = "";
		Элементы.ТекстСтатьиРедактирование.ПолучитьГраницыВыделения(НачалоВыделения, КонецВыделения);
		
		Если НЕ Найти("Справочники, Документы", РезСтруктура.ВидОбъекта) = 0 Тогда
			ФормаВыбора = ПолучитьФорму(РезСтруктура.ИмяОбъектаПолное + ".ФормаВыбора", , ЭтаФорма);
			ЗначениеВыбора = ФормаВыбора.ОткрытьМодально(30);
			Если ЗначениеВыбора <> Неопределено Тогда
				ТекстДляВставки = "<a href=""ОбъектКонфигурации=" +
								  РезСтруктура.ВидОбъекта +
								  "@" + РезСтруктура.ИмяОбъекта +
								  "@" + СокрЛП(ЗначениеВыбора.УникальныйИдентификатор()) +
								  """> " +
								  ?(ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(НачалоВыделения) = ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(КонецВыделения),
								    СокрЛП(ЗначениеВыбора),
									ТекстСтатьиРедактирование.ПолучитьТекст(НачалоВыделения, КонецВыделения)) +
								  " </a>";
			КонецЕсли; 
		Иначе	
			ТекстДляВставки = "<a href=""ОбъектКонфигурации=" +
							  РезСтруктура.ВидОбъекта +
							  "@" + РезСтруктура.ИмяОбъекта +
							  "@Форма" + """> " +
							  ?(ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(НачалоВыделения) = ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(КонецВыделения),
							    РезСтруктура.Синоним,
								ТекстСтатьиРедактирование.ПолучитьТекст(НачалоВыделения, КонецВыделения)) +
							  " </a>";
		КонецЕсли; 
		
		ТекстСтатьиРедактирование.Удалить(НачалоВыделения, КонецВыделения);
		
		ВставитьТекстВHTMLСервер(ТекстДляВставки, НачалоВыделения);
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ВставитьСсылкуНаОбъект()

&НаКлиенте
Процедура ВставитьСсылкуНаФормуСписка(Команда)
	
	ИменаКоллекций = Новый Массив;
	ИменаКоллекций.Добавить("Справочники");
	ИменаКоллекций.Добавить("Документы");
	ИменаКоллекций.Добавить("РегистрыСведений");
	
	ФормаДерева = ПолучитьФорму("Справочник.бит_си_Статьи.Форма.ДеревоМетаданныхУправляемая", Новый Структура("ИменаКоллекций", ИменаКоллекций), ЭтаФорма);
	РезСтруктура = ФормаДерева.ОткрытьМодально(30);
	
	Если РезСтруктура <> Неопределено Тогда
		НачалоВыделения = "";
		КонецВыделения = "";
		Элементы.ТекстСтатьиРедактирование.ПолучитьГраницыВыделения(НачалоВыделения, КонецВыделения);
		
		ТекстДляВставки = "<a href=""ОбъектКонфигурации=" +
						  РезСтруктура.ВидОбъекта +
						  "@" + РезСтруктура.ИмяОбъекта +
						  "@ФормаСписка" + """> " +
						  ?(ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(НачалоВыделения) = ТекстСтатьиРедактирование.ПолучитьПозициюПоЗакладке(КонецВыделения),
						    РезСтруктура.Синоним,
							ТекстСтатьиРедактирование.ПолучитьТекст(НачалоВыделения, КонецВыделения)) +
						  " </a>";
						  
		ТекстСтатьиРедактирование.Удалить(НачалоВыделения, КонецВыделения);
		
		ВставитьТекстВHTMLСервер(ТекстДляВставки, НачалоВыделения);
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ВставитьСсылкуНаФормуСписка

#КонецОбласти

#Область ОбработчикиКомандТаблицыФормыМетки

&НаКлиенте
Процедура Метки_РедактироватьМетку(Команда)
	
	Если Элементы.Метки.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	ОткрытьФорму("Справочник.бит_си_МеткиСтатей." + ?(Элементы.Метки.ТекущиеДанные.ЭтоГруппа, "ФормаГруппы", "ФормаОбъекта"),
				 Новый Структура("Ключ", Элементы.Метки.ТекущиеДанные.Метка),
				 ЭтаФорма);
				 
КонецПроцедуры // Метки_РедактироватьМетку()

&НаКлиенте
Процедура Метки_ОткрытьСправочник(Команда)
	
	ОткрытьФорму("Справочник.бит_си_МеткиСтатей.ФормаСписка", , ЭтаФорма);
				 
КонецПроцедуры // Метки_ОткрытьСправочник()

&НаКлиенте
Процедура Метки_Обновить(Команда)
	
	ЗаполнитьДеревоМеток(Элементы.Список.ТекущиеДанные.Ссылка);
	
	СтрокиДерева = Метки.ПолучитьЭлементы();
	Дерево = Элементы.Метки;
	Для Каждого ТекСтрока Из СтрокиДерева Цикл
		Узел = ТекСтрока.ПолучитьИдентификатор();
		Если Дерево.Развернут(Узел) Тогда
			Продолжить;
		КонецЕсли;
		Дерево.Развернуть(Узел);
	КонецЦикла;
	
КонецПроцедуры // Метки_Обновить()

#КонецОбласти

#Область ОбработкаПрочихКоманд

// Процедура обработчик - команды "Привязки" кнопки командной панели формы.
// 
&НаКлиенте
Процедура Привязки(Команда)
	ФормаУправляенияПривязками = ПолучитьФорму("Справочник.бит_си_Статьи.Форма.ФормаПривязкиСтатейУправляемая",
											   ,
											   ЭтаФорма);
	ФормаУправляенияПривязками.Открыть();										   
КонецПроцедуры // Привязки()

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура ПолучитьПараметрыБиблиотекиУтилитСервер(ВремДанныеРегистратора, ВремДанныеБиблиотеки)
	
	ВремДанныеРегистратора = ПоместитьВоВременноеХранилище(ПолучитьОбщийМакет(Метаданные.ОбщиеМакеты.бит_РегистраторВнешнихБиблиотек.Имя),Новый УникальныйИдентификатор);
	ВремДанныеБиблиотеки = ПоместитьВоВременноеХранилище(ПолучитьОбщийМакет(Метаданные.ОбщиеМакеты.бит_Утилиты.Имя),Новый УникальныйИдентификатор);
	
КонецПроцедуры // ПолучитьПараметрыБиблиотекиУтилитСервер()

&НаСервере
Процедура ВставитьКартинкуСервер(АдресВХранилище)
	
	КартинкаДляВставки = Новый Картинка(ПолучитьИзВременногоХранилища(АдресВХранилище));
	КартинкаДляВставки = КартинкаДляВставки.Преобразовать(ФорматКартинки.PNG);
	НачалоВыделения = "";
	КонецВыделения = "";
	Элементы.ТекстСтатьиРедактирование.ПолучитьГраницыВыделения(НачалоВыделения, КонецВыделения);
	ТекстСтатьиРедактирование.Удалить(НачалоВыделения, КонецВыделения);
	Элементы.ТекстСтатьиРедактирование.УстановитьГраницыВыделения(НачалоВыделения, НачалоВыделения);
	ТекстСтатьиРедактирование.Вставить(НачалоВыделения, КартинкаДляВставки, ТипЭлементаФорматированногоДокумента.Картинка);
	
	Модифицированность = Истина;
	
КонецПроцедуры // ВставитьКартинкуСервер()

&НаСервере
Процедура ВставитьТекстВHTMLСервер(ТекстДляВставки, ПозицияВставки)
	
	Элементы.ТекстСтатьиРедактирование.УстановитьГраницыВыделения(ПозицияВставки, ПозицияВставки);
		
	ТекстСтатьиРедактирование.Вставить(ПозицияВставки, "--СлужебныйТекстДляЗамены--", ТипЭлементаФорматированногоДокумента.Текст);
		
	ВремHTML = "";
	КартинкиПослеВставки = Новый Структура();
	ТекстСтатьиРедактирование.ПолучитьHTML(ВремHTML, КартинкиПослеВставки);
	ВремHTML = СтрЗаменить(ВремHTML, "--СлужебныйТекстДляЗамены--", ТекстДляВставки);
	ТекстСтатьиРедактирование.УстановитьHTML(ВремHTML, КартинкиПослеВставки);
		
КонецПроцедуры // ВставитьТекстВHTMLСервер()

&НаСервереБезКонтекста
Функция ПолучитьСписокРолей()
	
	СписокРолей = Новый СписокЗначений();
	Для Каждого ТекРоль Из Метаданные.Роли Цикл
		СписокРолей.Добавить(ТекРоль.ПолноеИмя(), ТекРоль.Представление());
	КонецЦикла;
	
	Возврат СписокРолей;
	
КонецФункции // ПолучитьСписокРолей()

&НаСервере
Процедура УстановитьВидимостьИДоступность(Ссылка = Неопределено)
	
	ВыбранЭлемент = НЕ (Ссылка = Неопределено ИЛИ Ссылка.ЭтоГруппа);
	ВыбранаГруппа = НЕ Ссылка = Неопределено И Ссылка.ЭтоГруппа;
	
	Элементы.ГруппаЗагрузитьСохранитьОтмена.Доступность = ?(Ссылка = Неопределено,
															Ложь,
															Истина);
															
	// Редактирование убивает вручную написанный HTML.
	Элементы.ГруппаРедактирование.Видимость					= Ложь;
	Элементы.ГруппаHTML.Видимость							= ВыбранЭлемент;
	Элементы.ГруппаПривязкиМетки.Видимость					= ВыбранЭлемент;
	Элементы.ГруппаДоступностиСвязанныхОбъектов.Видимость	= ВыбранЭлемент;
	Элементы.ГруппаПросмотр.Видимость						= ВыбранЭлемент;
	Элементы.НеВключатьВСодержание.Видимость				= ВыбранЭлемент;
	Элементы.ОписаниеРаздела.Видимость						= ВыбранаГруппа;
	Элементы.ЗаголовокСтатьи.Видимость						= ВыбранЭлемент;
	
	Элементы.НаименованиеРаздела.Заголовок = ?(ВыбранЭлемент,
											   "Идентификатор статьи",
											   "Наименование раздела");
												 
КонецПроцедуры // УстановитьВидимостьИДоступность()

#Область ПроцедурыИФункцииОбновленияОписанияСтатьи

&НаСервере
Процедура ЗаполнитьТаблицуМестИспользования(СтатьяСсылка)
	
	Если СтатьяСсылка.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	МестаИспользования.Очистить();
	МестаИспользования.Загрузить(СтатьяСсылка.МестаИспользования.Выгрузить());
	
КонецПроцедуры // ЗаполнитьТаблицуМестИспользования()

&НаСервере 
Процедура ЗаполнитьСтрокиДереваМеток(Строки, Родитель = Неопределено)
	
	Родитель = ?(Родитель = Неопределено,Метки,Родитель);
	Для Каждого СтрокаДерева Из Строки Цикл 
		Если СтрокаДерева.Ссылка.ЭтоГруппа И
			СтрокаДерева.Строки.Количество() = 0  Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = Родитель.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Метка = СтрокаДерева.Ссылка;
		НоваяСтрока.Использование = СтрокаДерева.Пометка;
		Если СтрокаДерева.Ссылка.ЭтоГруппа Тогда
			НоваяСтрока.Картинка = БиблиотекаКартинок.СправочникОбъект;
			НоваяСтрока.ЭтоГруппа = Истина;
		Иначе
			ВремКартинка = СтрокаДерева.Ссылка.Пиктограмма.Получить();
			Если ТипЗнч(ВремКартинка) = Тип("ДвоичныеДанные") Тогда
				ВремКартинка = Новый Картинка(ВремКартинка);
			КонецЕсли;
			Если НЕ ТипЗнч(ВремКартинка) = Тип("Картинка") ИЛИ ВремКартинка.Вид = ВидКартинки.Пустая Тогда
				ВремКартинка = БиблиотекаКартинок.бит_си_МеткаСтатьи;
			КонецЕсли;
			НоваяСтрока.Картинка = ВремКартинка;
		КонецЕсли;
		Если СтрокаДерева.Строки.Количество() > 0 Тогда
			ЗаполнитьСтрокиДереваМеток(СтрокаДерева.Строки, НоваяСтрока);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьСтрокиДереваМеток()

&НаСервере
Процедура ЗаполнитьДеревоМеток(Знач Статья, Знач УстановленныеМетки = Неопределено, Знач Обновлять = Истина)
	
	Если Статья.ЭтоГруппа Или Не Обновлять Тогда
		Возврат;
	КонецЕсли;
	Метки.ПолучитьЭлементы().Очистить();
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	УстановленныеМетки.Метка
	               |ПОМЕСТИТЬ УстановленныеМетки
	               |ИЗ
	               |	&УстановленныеМетки КАК УстановленныеМетки
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	МеткиСтатей.Наименование КАК Метка,
	               |	ЕСТЬNULL(УстановленныеМетки.Пометка, 0) КАК Пометка,
	               |	МеткиСтатей.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.бит_си_МеткиСтатей КАК МеткиСтатей
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	               |			УстановленныеМетки.Метка КАК Метка,
	               |			1 КАК Пометка
	               |		ИЗ
	               |			РегистрСведений.бит_си_УстановленныеМеткиСтатей КАК УстановленныеМетки
	               |		ГДЕ
	               |			УстановленныеМетки.Метка ССЫЛКА Справочник.бит_си_МеткиСтатей
	               |			И УстановленныеМетки.Статья = &Статья
	               |		
	               |		ОБЪЕДИНИТЬ
	               |		
	               |		ВЫБРАТЬ
	               |			УстановленныеМетки.Метка,
	               |			1
	               |		ИЗ
	               |			УстановленныеМетки КАК УстановленныеМетки) КАК УстановленныеМетки
	               |		ПО МеткиСтатей.Ссылка = УстановленныеМетки.Метка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Ссылка ИЕРАРХИЯ
	               |АВТОУПОРЯДОЧИВАНИЕ";
	Если НЕ ТипЗнч(УстановленныеМетки) = Тип("ТаблицаЗначений") Тогда
		УстановленныеМетки = Новый ТаблицаЗначений();
		УстановленныеМетки.Колонки.Добавить("Метка", Новый ОписаниеТипов("СправочникСсылка.бит_си_МеткиСтатей"));
	КонецЕсли;
	Запрос.УстановитьПараметр("Статья", Статья);
	Запрос.УстановитьПараметр("УстановленныеМетки", УстановленныеМетки);
	МеткиПер = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	ЗаполнитьСтрокиДереваМеток(МеткиПер.Строки);
	
КонецПроцедуры // ЗаполнитьДеревоМеток()

&НаСервере
Процедура ЗаполнитьДеревоОтборов(Статья)
	
	Если Статья.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
	ОтборыОбъектов.ПолучитьЭлементы().Очистить();
	ВыборкаИсточников = Справочники.бит_си_Источники.Выбрать();
	МассивДобавленных = Новый Массив();
	Пока ВыборкаИсточников.Следующий() Цикл
		Если НЕ ВыборкаИсточников.Обработка.Получить() = "бит_си_ИсточникСправочникСтатьи" Тогда
			Продолжить;
		КонецЕсли;
		ТекНастройки = ВыборкаИсточников.НастройкаПоУмолчанию.Получить();
		Если НЕ ТипЗнч(ТекНастройки) = Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ТекНастройки.Свойство("ОбъектыОтбора") Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ТекОбъект Из ТекНастройки.ОбъектыОтбора Цикл
			Если НЕ МассивДобавленных.Найти(ТекОбъект.МетаОбъект) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = ОтборыОбъектов.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Объект = ТекОбъект.Заголовок;
			НоваяСтрока.Тип = ТекОбъект.МетаОбъект;
			МассивДобавленных.Добавить(ТекОбъект.МетаОбъект);
		КонецЦикла;
	КонецЦикла;
	Если ОтборыОбъектов.ПолучитьЭлементы().Количество() = 0 Тогда
		Элементы.ОтборыОбъектов.Доступность = Ложь;
		Элементы.ОтборыОбъектов.КоманднаяПанель.Доступность = Ложь;
		Возврат;
	Иначе
		Элементы.ОтборыОбъектов.Доступность = Истина;
		Элементы.ОтборыОбъектов.КоманднаяПанель.Доступность = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	ОбъектыОтбора.Объект КАК Объект,
	               |	ОбъектыОтбора.Тип КАК Тип
	               |ИЗ
	               |	РегистрСведений.бит_си_СвязанныеОбъекты КАК ОбъектыОтбора
	               |ГДЕ
	               |	ОбъектыОтбора.Статья = &Статья
	               |	И ОбъектыОтбора.ДляОтборов";
	Запрос.УстановитьПараметр("Статья", Статья);
	ВыборкаОбъектов = Запрос.Выполнить().Выбрать();
	Пока ВыборкаОбъектов.Следующий() Цикл
		СтрокиДерева = ОтборыОбъектов.ПолучитьЭлементы();
		СтрокаТипа = Неопределено;
		Для Каждого ТекСтрока Из СтрокиДерева Цикл
			Если ТекСтрока.Тип = ВыборкаОбъектов.Тип Тогда
				СтрокаТипа = ТекСтрока;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаТипа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = СтрокаТипа.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Объект = ВыборкаОбъектов.Объект;
		НоваяСтрока.Тип = СтрокаТипа.Тип;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДеревоОтборов()

&НаСервере
Процедура ЗаполнитьДеревоСвязанныхОбъектов(Статья)
	
	Если Статья.ЭтоГруппа  Тогда
		Возврат;
	КонецЕсли;
	СвязанныеОбъекты.ПолучитьЭлементы().Очистить();
	ВыборкаИсточников = Справочники.бит_си_Источники.Выбрать();
	МассивДобавленных = Новый Массив();
	Пока ВыборкаИсточников.Следующий() Цикл
		Если НЕ ВыборкаИсточников.Обработка.Получить() = "бит_си_ИсточникСправочникСтатьи" Тогда
			Продолжить;
		КонецЕсли;
		ТекНастройки = ВыборкаИсточников.НастройкаПоУмолчанию.Получить();
		Если НЕ ТипЗнч(ТекНастройки) = Тип("Структура") Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ТекНастройки.Свойство("ОбъектыПривязки") Тогда
			Продолжить;
		КонецЕсли;
		Для Каждого ТекОбъект Из ТекНастройки.ОбъектыПривязки Цикл
			Если НЕ МассивДобавленных.Найти(ТекОбъект.МетаОбъект) = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			НоваяСтрока = СвязанныеОбъекты.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Объект = ТекОбъект.Заголовок;
			НоваяСтрока.Тип = ТекОбъект.МетаОбъект;
			МассивДобавленных.Добавить(ТекОбъект.МетаОбъект);
		КонецЦикла;
	КонецЦикла;
	Если ОтборыОбъектов.ПолучитьЭлементы().Количество() = 0 Тогда
		Элементы.СвязанныеОбъекты.Доступность = Ложь;
		Элементы.СвязанныеОбъекты.КоманднаяПанель.Доступность = Ложь;
		Возврат;
	Иначе
		Элементы.СвязанныеОбъекты.Доступность = Истина;
		Элементы.СвязанныеОбъекты.КоманднаяПанель.Доступность = Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = "ВЫБРАТЬ
	               |	СвязанныеОбъекты.Объект КАК Объект,
	               |	СвязанныеОбъекты.Тип КАК Тип
	               |ИЗ
	               |	РегистрСведений.бит_си_СвязанныеОбъекты КАК СвязанныеОбъекты
	               |ГДЕ
	               |	СвязанныеОбъекты.Статья = &Статья
	               |	И НЕ СвязанныеОбъекты.ДляОтборов";
	Запрос.УстановитьПараметр("Статья", Статья);
	ВыборкаОбъектов = Запрос.Выполнить().Выбрать();
	Пока ВыборкаОбъектов.Следующий() Цикл
		СтрокиДерева = СвязанныеОбъекты.ПолучитьЭлементы();
		СтрокаТипа = Неопределено;
		Для Каждого ТекСтрока Из СтрокиДерева Цикл
			Если ТекСтрока.Тип = ВыборкаОбъектов.Тип Тогда
				СтрокаТипа = ТекСтрока;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
		Если СтрокаТипа = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = СтрокаТипа.ПолучитьЭлементы().Добавить();
		НоваяСтрока.Объект = ВыборкаОбъектов.Объект;
		НоваяСтрока.Тип = СтрокаТипа.Тип;
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьДеревоСвязанныхОбъектов()

&НаСервере
Процедура ОбновитьТекстСтатьиНаСервере(СтатьяСсылка)
	
	СпрСпОб = СтатьяСсылка.ПолучитьОбъект();
	ВремСписокКартинок = Новый СписокЗначений();
	РабочийТекстHTML = СпрСпОб.ЗагрузитьИзображенияИзБД(ЭтаФорма.УникальныйИдентификатор, ВремСписокКартинок);
	
	ТекстHTML = РабочийТекстHTML;
	СтруктураКартинок = Новый Структура();
	НомерКартинки = 0;
	Для Каждого ТекКартинка Из ВремСписокКартинок Цикл
		НомерКартинки = НомерКартинки + 1;
		Попытка
			СтруктураКартинок.Вставить("image" + СокрЛП(НомерКартинки), Новый Картинка(ТекКартинка.Значение));
		Исключение
			Продолжить;
		КонецПопытки;
		ТекстHTML = СтрЗаменить(ТекстHTML, ТекКартинка.Представление, "image" + СокрЛП(НомерКартинки));
	КонецЦикла;
	ТекстСтатьиРедактирование.УстановитьHTML(ТекстHTML, СтруктураКартинок);
	
КонецПроцедуры // ОбновитьТекстСтатьиНаСервере()

&НаКлиенте
Процедура ОбновитьОтображениеСтатьи()
	
	ОбновитьОтображениеСтатьиСервер(?(Элементы.Список.ТекущиеДанные = Неопределено, Неопределено, Элементы.Список.ТекущиеДанные.Ссылка));
	
	Если Элементы.Список.ТекущиеДанные.ЭтоГруппа Тогда
		Возврат;
	КонецЕсли;
		
	СтрокиДерева = Метки.ПолучитьЭлементы();
	Дерево = Элементы.Метки;
	Для Каждого ТекСтрока Из СтрокиДерева Цикл
		Узел = ТекСтрока.ПолучитьИдентификатор();
		Если Дерево.Развернут(Узел) Тогда
			Продолжить;
		КонецЕсли;
		Дерево.Развернуть(Узел);
	КонецЦикла;
	
	СтрокиДерева = ОтборыОбъектов.ПолучитьЭлементы();
	Дерево = Элементы.ОтборыОбъектов;
	Для Каждого ТекСтрока Из СтрокиДерева Цикл
		Узел = ТекСтрока.ПолучитьИдентификатор();
		Если Дерево.Развернут(Узел) Тогда
			Продолжить;
		КонецЕсли;
		Дерево.Развернуть(Узел);
	КонецЦикла;
	
	СтрокиДерева = СвязанныеОбъекты.ПолучитьЭлементы();
	Дерево = Элементы.СвязанныеОбъекты;
	Для Каждого ТекСтрока Из СтрокиДерева Цикл
		Узел = ТекСтрока.ПолучитьИдентификатор();
		Если Дерево.Развернут(Узел) Тогда
			Продолжить;
		КонецЕсли;
		Дерево.Развернуть(Узел);
	КонецЦикла;
	
КонецПроцедуры // ОбновитьОтображениеСтатьи()

&НаСервере
Процедура ОбновитьОтображениеСтатьиСервер(Ссылка)
	
	УстановитьВидимостьИДоступность(Ссылка);
	
	Если Ссылка = Неопределено Тогда
		Возврат;
	КонецЕсли;

	НазваниеСтатьи        = Ссылка.НазваниеСтатьи;
	НаименованиеРаздела   = Ссылка.Наименование;
	ОписаниеРаздела       = Ссылка.ОписаниеРаздела;
	НеВключатьВСодержание = Ссылка.НеВключатьВСодержание;
	
	Если Ссылка.ЭтоГруппа Тогда
		РабочийТекстHTML = "<html><title>&nbsp;</title><body>&nbsp;</body></html>";
	Иначе
		ОбновитьТекстСтатьиНаСервере(Ссылка);
		ЗаполнитьДеревоМеток(Ссылка); 
		ЗаполнитьТаблицуМестИспользования(Ссылка);
		ЗаполнитьДеревоОтборов(Ссылка);
		ЗаполнитьДеревоСвязанныхОбъектов(Ссылка);
	КонецЕсли;
	
КонецПроцедуры // ОбновитьОтображениеСтатьиСервер()

#КонецОбласти

#Область ПроцедурыИФункцииРаботыСДеревомСтатей

&НаСервере
Процедура ИзменитьПорядок(ТекСсылка, Направление)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Статьи.Ссылка,
	                      |	Статьи.Порядок КАК Порядок
	                      |ИЗ
	                      |	Справочник.бит_си_Статьи КАК Статьи
	                      |ГДЕ
	                      |	Статьи.Родитель = &Родитель
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Порядок,
	                      |	Статьи.Наименование");
	Запрос.УстановитьПараметр("Родитель", ТекСсылка.Родитель);
	Выборка = Запрос.Выполнить().Выбрать();
	й = 1;
	ПредСсылка = "";
	Пока Выборка.Следующий() Цикл
		Если (Выборка.Ссылка = ТекСсылка И Направление = -1 И НЕ й = 1)
		 ИЛИ (ПредСсылка = ТекСсылка И Направление = 1) Тогда
			ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ТекОбъект.Порядок = й - 1;
			ТекОбъект.Записать();
			
			ТекОбъект = ПредСсылка.ПолучитьОбъект();
			ТекОбъект.Порядок = й;
			ТекОбъект.Записать();
		ИначеЕсли НЕ Выборка.Порядок = й Тогда
			ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ТекОбъект.Порядок = й;
			ТекОбъект.Записать();
		КонецЕсли;
		й = й + 1;
		ПредСсылка = Выборка.Ссылка;
	КонецЦикла;
	
КонецПроцедуры // ИзменитьПорядок()

#КонецОбласти

#Область ПроцедурыИФункцииОбработкиТекстаСтатьи

&НаКлиенте
Функция ПолучитьСписокИзображенийВТексте(Знач пТекстHTML)
	
	сзСсылки = Новый СписокЗначений;
	src_Открыт = Ложь;
	ВсегоСтрок = СтрЧислоСтрок(пТекстHTML);
	
	Для СчСтрок = 1 По ВсегоСтрок Цикл
		ТекСтрока = СтрПолучитьСтроку(пТекстHTML, СчСтрок);
		ВсегоСимволов = СтрДлина(ТекСтрока);
		
		IMG_Начало = Найти(ВРег(ТекСтрока),"<IMG");
		
		Если IMG_Начало <> 0 Тогда
			IMG_Начало = IMG_Начало + 4;
			IMG_Путь = "";
			
			Для СчСимвол = IMG_Начало По ВсегоСимволов Цикл
				ТекСимвол = Сред(ТекСтрока, СчСимвол, 1);
				
				Если (ТекСимвол = Символ(34) ИЛИ ТекСимвол = Символ(39)) И src_Открыт Тогда
					
					Если Лев(IMG_Путь, 5) = "e1cib" ИЛИ Лев(IMG_Путь, 6) = "e1c:// " Тогда
						ИмяДляЗамены = IMG_Путь;
					Иначе
						Попытка
							ИмяДляЗамены = "";
							//ПоместитьФайл(ИмяДляЗамены, _IMG_Путь, , Ложь, ЭтаФорма.УникальныйИдентификатор);
							НачатьПомещениеФайла(,ИмяДляЗамены, IMG_Путь, Ложь, ЭтаФорма.УникальныйИдентификатор);
						Исключение
							IMG_Путь = "";
							src_Открыт = Ложь;
							Описание = ОписаниеОшибки();
							Продолжить;
						КонецПопытки;
					КонецЕсли;
					
					СчСимвол = СчСимвол - (СтрДлина(IMG_Путь) - СтрДлина(ИмяДляЗамены));
					ТекСтрока = СтрЗаменить(ТекСтрока, IMG_Путь, ИмяДляЗамены);
					
					сзСсылки.Добавить(ИмяДляЗамены, IMG_Путь);
					
					IMG_Путь = "";
					src_Открыт = Ложь;
				КонецЕсли;
				
				Если НРег(Сред(ТекСтрока, СчСимвол, 5)) = ("src=" + Символ(34)) ИЛИ НРег(Сред(ТекСтрока, СчСимвол, 5)) = ("src=" + Символ(39)) Тогда
					СчСимвол = СчСимвол + 5;
					src_Открыт = Истина;
				КонецЕсли;
				
				Если src_Открыт Тогда
					IMG_Путь = IMG_Путь + Сред(ТекСтрока, СчСимвол, 1);
				КонецЕсли;
			КонецЦикла; // По строке, символы
		КонецЕсли; // встречается тег
	КонецЦикла;	// по тексту, строки

	Возврат сзСсылки;
	
КонецФункции // ПолучитьСписокИзображенийВТексте()

&НаСервере
Процедура ОбновитьРабочийТекстИзПоляРедактирования()
	
	СтруктураКартинок = Новый Структура();
	ТекстHTML = "";
	ТекстСтатьиРедактирование.ПолучитьHTML(ТекстHTML, СтруктураКартинок);
	Для Каждого ТекКартинка Из СтруктураКартинок Цикл
		АдресКартинки = ПоместитьВоВременноеХранилище(ТекКартинка.Значение.ПолучитьДвоичныеДанные(), ЭтаФорма.УникальныйИдентификатор);
		ТекстHTML = СтрЗаменить(ТекстHTML, ТекКартинка.Ключ, АдресКартинки);
	КонецЦикла;
	
	РабочийТекстHTML = ТекстHTML;
	
КонецПроцедуры // ОбновитьРабочийтекстИзПоляРедактирования()

&НаСервере
Процедура ОбновитьТекстПоляРедактированияИзРабочегоТекста(СписокКартинок)
	
	ТекстHTML = РабочийТекстHTML;
	СтруктураКартинок = Новый Структура();
	НомерКартинки = 0;
	Для Каждого ТекКартинка Из СписокКартинок Цикл
		НомерКартинки = НомерКартинки + 1;
		СтруктураКартинок.Вставить("image" + СокрЛП(НомерКартинки), Новый Картинка(ПолучитьИзВременногоХранилища(ТекКартинка.Значение)));
		ТекстHTML = СтрЗаменить(ТекстHTML, ТекКартинка.Представление, "image" + СокрЛП(НомерКартинки));
	КонецЦикла;
	
	ТекстСтатьиРедактирование.УстановитьHTML(ТекстHTML, СтруктураКартинок);
	
КонецПроцедуры // ОбновитьТекстПоляРедактированияИзРабочегоТекста()

&НаСервере
Процедура ЗаписатьСтатьюНаСервере(Ссылка, СписокИзображений)
	
	Если Не ЭтаФорма.Модифицированность Тогда 
		Возврат;
	КонецЕсли;
	
	ОбъектДляЗаписи = Ссылка.ПолучитьОбъект();
	ОбъектДляЗаписи.Наименование = НаименованиеРаздела;
	Если Ссылка.ЭтоГруппа Тогда 
		ОбъектДляЗаписи.ОписаниеРаздела = ОписаниеРаздела;
	КонецЕсли;
	ОбъектДляЗаписи.НеВключатьВСодержание = НеВключатьВСодержание;
	Если Ссылка.ЭтоГруппа Тогда 
		Попытка
			ОбъектДляЗаписи.Записать();
		Исключение
			Описание = ОписаниеОшибки();
		КонецПопытки;
		ЭтаФорма.Модифицированность = Ложь;
		Возврат;
	КонецЕсли;
	
	ОбъектДляЗаписи.НазваниеСтатьи = НазваниеСтатьи;
	ОбъектДляЗаписи.ТекстHTML = РабочийТекстHTML;
	
	СтруктураКартинок = Новый Структура();
	НомерКартинки = 0;
	Для Каждого ТекКартинка Из СписокИзображений Цикл
		НомерКартинки = НомерКартинки + 1;
		ДанныеКартинки = ПолучитьИзВременногоХранилища(ТекКартинка.Значение);
		СтруктураКартинок.Вставить("image" + СокрЛП(НомерКартинки), Новый Картинка(ДанныеКартинки));
		ОбъектДляЗаписи.ТекстHTML = СтрЗаменить(ОбъектДляЗаписи.ТекстHTML, ТекКартинка.Представление, "image" + СокрЛП(НомерКартинки));
		
		ТекКартинка.Представление	= "image" + СокрЛП(НомерКартинки);
		ТекКартинка.Значение		= ДанныеКартинки;
	КонецЦикла;
	
	ОбъектДляЗаписи.Изображения = Новый ХранилищеЗначения(СписокИзображений);
	
	ОбъектДляЗаписи.МестаИспользования.Очистить();
	ОбъектДляЗаписи.МестаИспользования.Загрузить(МестаИспользования.Выгрузить());
	Попытка
		ОбъектДляЗаписи.Записать();
	Исключение
		Описание = ОписаниеОшибки();
		ТекстСообщения = "При записи статьи произошла ошибка: " + Описание;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка); 
	КонецПопытки;
	
	НаборЗаписей = РегистрыСведений.бит_си_УстановленныеМеткиСтатей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Статья.Значение = Ссылка;
	НаборЗаписей.Отбор.Статья.Использование = Истина;
	УстановленныеМетки = УстановленныеМетки(Метки.ПолучитьЭлементы());
	Для Каждого ТекСтрока Из УстановленныеМетки Цикл
		Запись = НаборЗаписей.Добавить();
		Запись.Статья = Ссылка;
		Запись.Метка = ТекСтрока.Метка;
	КонецЦикла;
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Описание = ОписаниеОшибки();
		ТекстСообщения = "При записи меток произошла ошибка: " + Описание;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка); 
	КонецПопытки;
	
	НаборЗаписей = РегистрыСведений.бит_си_СвязанныеОбъекты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Статья.Значение = Ссылка;
	НаборЗаписей.Отбор.Статья.Использование = Истина;
	НаборЗаписей.Отбор.ДляОтборов.Значение = Истина;
	НаборЗаписей.Отбор.ДляОтборов.Использование = Истина;
	Для Каждого ТекТип Из ОтборыОбъектов.ПолучитьЭлементы() Цикл
		Для Каждого ТекСтрока Из ТекТип.ПолучитьЭлементы() Цикл
			Запись = НаборЗаписей.Добавить();
			Запись.Статья		= Ссылка;
			Запись.Объект		= ТекСтрока.Объект;
			Запись.Тип			= ТекТип.Тип;
			Запись.ДляОтборов	= Истина;
		КонецЦикла;
	КонецЦикла;
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Описание = ОписаниеОшибки();
		ТекстСообщения = "При критериев отбора произошла ошибка: " + Описание;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка); 
	КонецПопытки;
	
	НаборЗаписей = РегистрыСведений.бит_си_СвязанныеОбъекты.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Статья.Значение = Ссылка;
	НаборЗаписей.Отбор.Статья.Использование = Истина;
	НаборЗаписей.Отбор.ДляОтборов.Значение = Ложь;
	НаборЗаписей.Отбор.ДляОтборов.Использование = Истина;
	Для Каждого ТекТип Из СвязанныеОбъекты.ПолучитьЭлементы() Цикл
		Для Каждого ТекСтрока Из ТекТип.ПолучитьЭлементы() Цикл
			Запись = НаборЗаписей.Добавить();
			Запись.Статья		= Ссылка;
			Запись.Объект		= ТекСтрока.Объект;
			Запись.Тип			= ТекТип.Тип;
			Запись.ДляОтборов	= Ложь;
		КонецЦикла;
	КонецЦикла;
	Попытка
		НаборЗаписей.Записать();
	Исключение
		Описание = ОписаниеОшибки();
		ТекстСообщения = "При записи связанных объектов произошла ошибка: " + Описание;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Ссылка); 
	КонецПопытки;
		
	ЭтаФорма.Модифицированность = Ложь;
	
КонецПроцедуры // ЗаписатьСтатьюНаСервере()

&НаСервере
Функция УстановленныеМетки(Строки, ТабМетки = Неопределено)
	
	Если НЕ ТипЗнч(ТабМетки) = Тип("ТаблицаЗначений") Тогда
		ТабМетки = Новый ТаблицаЗначений();
		ТабМетки.Колонки.Добавить("Метка", Новый ОписаниеТипов("СправочникСсылка.бит_си_МеткиСтатей"));
	КонецЕсли;
	Для Каждого ТекСтрока Из Строки Цикл
		Если ТекСтрока.Метка.ЭтоГруппа Тогда
			УстановленныеМетки(ТекСтрока.ПолучитьЭлементы(), ТабМетки);
			Продолжить;
		КонецЕсли;
		Если ТекСтрока.Использование = Ложь Тогда
			Продолжить;
		КонецЕсли;
		Если НЕ ТабМетки.Найти(ТекСтрока.Метка, "Метка") = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		НоваяСтрока = ТабМетки.Добавить();
		НоваяСтрока.Метка = ТекСтрока.Метка;
	КонецЦикла;
	
	Возврат ТабМетки;
	
КонецФункции // УстановленныеМетки()

// Очищает справочник
// 
&НаСервереБезКонтекста
Процедура ОчиститьСправкуНаСервере()
	
	бит_си_ИсточникСправочникСтатьи.ОчиститьСправочнуюИнформацию();
	
КонецПроцедуры // ОчиститьСправкуНаСервере()

&НаСервереБезКонтекста
Процедура ЗагрузитьСправкуНаСервере(Адрес, Расширение)
	
	ПроверкаПройдена = Истина;
	Попытка
		ВремФайл = "";
		ИмяВременногоФайла = ПолучитьИмяВременногоФайла(Расширение);
		Данные = ПолучитьИзВременногоХранилища(Адрес);
		Данные.Записать(ИмяВременногоФайла);
		Файл = Новый Файл(ИмяВременногоФайла);
		ПроверкаПройдена = Файл.Существует();
	Исключение
		ПроверкаПройдена = Ложь;
	КонецПопытки;
	Если Не ПроверкаПройдена Тогда 
		Возврат;
	КонецЕсли;
	бит_си_ИсточникСправочникСтатьи.ЗагрузитьСправочнуюИнформациюИзФайла(ИмяВременногоФайла);
	
КонецПроцедуры // ЗагрузитьСправкуНаСервере()

&НаСервереБезКонтекста
Процедура ЗагрузитьСправкуМакетНаСервере(АдресФайла)
	
	СтруктураМетаданных = Новый Структура;
	СтруктураМетаданных.Вставить("Конфигурация"				, Новый Структура("Коллекция, Представление",
																			  "Конфигурация", "Конфигурация"));
	СтруктураМетаданных.Вставить("Подсистема"				, Новый Структура("Коллекция, Представление",
																			  "Подсистемы", "Подсистемы"));
	СтруктураМетаданных.Вставить("ОбщаяФорма"				, Новый Структура("Коллекция, Представление",
																			  "ОбщиеФормы", "Общие формы"));
	СтруктураМетаданных.Вставить("Справочник"				, Новый Структура("Коллекция, Представление",
																			  "Справочники", "Справочники"));
	СтруктураМетаданных.Вставить("Документ"					, Новый Структура("Коллекция, Представление",
																			  "Документы", "Документы"));
	СтруктураМетаданных.Вставить("ЖурналДокументов"			, Новый Структура("Коллекция, Представление",
																			  "ЖурналыДокументов", "Журналы документов"));
	СтруктураМетаданных.Вставить("Обработка"				, Новый Структура("Коллекция, Представление",
																			  "Обработки", "Обработки"));
	СтруктураМетаданных.Вставить("Отчет"					, Новый Структура("Коллекция, Представление",
																			  "Отчеты", "Отчеты"));
	СтруктураМетаданных.Вставить("РегистрСведений"			, Новый Структура("Коллекция, Представление",
																			  "РегистрыСведений", "Регистры сведений"));
	СтруктураМетаданных.Вставить("РегистрБухгалтерии"		, Новый Структура("Коллекция, Представление",
																			  "РегистрыБухгалтерии", "Регистры бухгалтерии"));
	СтруктураМетаданных.Вставить("РегистрНакопления"		, Новый Структура("Коллекция, Представление",
																			  "РегистрыНакопления", "Регистры накопления"));
	СтруктураМетаданных.Вставить("РегистрРасчета"			, Новый Структура("Коллекция, Представление",
																			  "РегистрыРасчета", "Регистры расчета"));
	СтруктураМетаданных.Вставить("ПланВидовХарактеристик"	, Новый Структура("Коллекция, Представление",
																			  "ПланыВидовХарактеристик", "Планы видов характеристик"));
	СтруктураМетаданных.Вставить("ПланВидовРасчета"			, Новый Структура("Коллекция, Представление",
																			  "ПланыВидовРасчета", "Планы видов расчета"));
	СтруктураМетаданных.Вставить("ПланСчетов"				, Новый Структура("Коллекция, Представление",
																			  "ПланыСчетов", "Планы счетов"));
	СтруктураМетаданных.Вставить("ПланОбмена"				, Новый Структура("Коллекция, Представление",
																			  "ПланыОбмена", "Планы обмена"));
	ДеревоСтатей = Новый ДеревоЗначений();
	ДеревоСтатей.Колонки.Добавить("Иерархия"			, Новый ОписаниеТипов("Строка"));
	ДеревоСтатей.Колонки.Добавить("Имя"					, Новый ОписаниеТипов("Строка"));
	ДеревоСтатей.Колонки.Добавить("Представление"		, Новый ОписаниеТипов("Строка"));
	ДеревоСтатей.Колонки.Добавить("Файл"				, Новый ОписаниеТипов("Строка"));
	ДеревоСтатей.Колонки.Добавить("СтруктураИерархии"	, Новый ОписаниеТипов("Строка"));
	
	ВремИмя = ПолучитьИмяВременногоФайла();
	ИмяФайлаАрхива = ПолучитьИмяВременногоФайла("zip");
	СоздатьКаталог(ВремИмя);
	
	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресФайла);
	ДвоичныеДанные.Записать(ИмяФайлаАрхива);
	
	Архив = Новый ЧтениеZipФайла(ИмяФайлаАрхива);
	Архив.ИзвлечьВсе(ВремИмя, РежимВосстановленияПутейФайловZIP.Восстанавливать);
	
	МассивФайлов = НайтиФайлы(ВремИмя, "*.htm");
	Для Каждого Файл Из МассивФайлов Цикл
		Если Файл.ЭтоКаталог() Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаРазбора     = Файл.ИмяБезРасширения + ".";
		СтруктураИерархии = "";
		Иерархия = "";
		КолВоТочек        = СтрЧислоВхождений(СтрокаРазбора, ".");
		Имя = "";
		Для Сч = 1 По КолВоТочек Цикл
			
			ЭтоОписаниеКонфигурации = (Сч = 2 И Имя = "Конфигурация");
			
			ПозТочки = Найти(СтрокаРазбора, ".");
			Имя = Лев(СтрокаРазбора, ПозТочки - 1);
			
			СтруктураИерархииСтар = СтруктураИерархии;
			СтруктураИерархии     = ?(СтруктураИерархии = "", Имя, СтруктураИерархии + "." + Имя);
			Если НЕ ((Сч = 3 ИЛИ (Сч = 2 И ЭтоОписаниеКонфигурации)) И (Имя = "Форма" ИЛИ Имя = "Справка")) Тогда
				Иерархия = ?(Иерархия = "", Имя, Иерархия + "." + Имя);
			КонецЕсли;
			РодительскаяСтрокаСтар  = ДеревоСтатей.Строки.Найти(СтруктураИерархииСтар, "СтруктураИерархии", Истина);
			РодительскаяСтрока      = ДеревоСтатей.Строки.Найти(СтруктураИерархии, "СтруктураИерархии", Истина);
			
			Если РодительскаяСтрокаСтар = Неопределено И РодительскаяСтрока = Неопределено Тогда
				СтрокаДерева = ДеревоСтатей.Строки.Добавить();
			ИначеЕсли НЕ РодительскаяСтрока = Неопределено Тогда 
				СтрокаРазбора = Прав(СтрокаРазбора, СтрДлина(СтрокаРазбора) - ПозТочки);
				Продолжить;
			Иначе 
				Если ((Сч = 2 И ЭтоОписаниеКонфигурации) ИЛИ Сч = 3 ИЛИ Сч = 5) И Имя = "Справка" Тогда
					РодительскаяСтрокаСтар.Файл = Файл.ПолноеИмя;
					Прервать;
				КонецЕсли;
				СтрокаДерева = РодительскаяСтрокаСтар.Строки.Добавить();
			КонецЕсли; 
			
			СтрокаДерева.Имя				= Имя;
			Если Сч = 1 Тогда
				СтрокаДерева.Имя			= СтруктураМетаданных[Имя].Коллекция;
				СтрокаДерева.Представление	= СтруктураМетаданных[Имя].Представление;
			ИначеЕсли Сч = 2 Тогда
				СтрокаДерева.Представление	= Метаданные[СтрокаДерева.Родитель.Имя][Имя].Представление();
			ИначеЕсли Сч = 4 И СтрокаДерева.Родитель.Имя = "Форма" Тогда
				ВремМетаОбъект = Метаданные[СтрокаДерева.Родитель.Родитель.Родитель.Имя];
				СтрокаДерева.Представление	= ВремМетаОбъект[СтрокаДерева.Родитель.Родитель.Имя].Представление() + ": " +
											  ВремМетаОбъект[СтрокаДерева.Родитель.Родитель.Имя].Формы[Имя].Представление();
			КонецЕсли;
			СтрокаДерева.Иерархия			= Иерархия;
			СтрокаДерева.СтруктураИерархии	= СтруктураИерархии;
			
			Если Сч = КолВоТочек - 1 Тогда
				СтрокаДерева.Файл = Файл.ПолноеИмя;
			КонецЕсли; 
			
			СтрокаРазбора = Прав(СтрокаРазбора, СтрДлина(СтрокаРазбора) - ПозТочки);
			
		КонецЦикла; 
	КонецЦикла; 
	
	ГруппаСС = Справочники.бит_си_Статьи.НайтиПоНаименованию("Справка 1С",Истина);
	Если НЕ ГруппаСС.ЭтоГруппа ИЛИ ГруппаСС.Пустая() Тогда
		ГруппаСС = Справочники.бит_си_Статьи.СоздатьГруппу();
		ГруппаСС.Наименование = "Справка 1С";
		ГруппаСС.ОписаниеРаздела = "Статьи встроенной справки конфигурации """ + СокрЛП(Метаданные.Синоним) + """";
		ГруппаСС.Записать();
		ГруппаСС = ГруппаСС.Ссылка;
	КонецЕсли;
	
	СоздатьСтатьиСправки(ДеревоСтатей, ГруппаСС);
	
	Для Каждого Файл Из МассивФайлов Цикл 
		Попытка
			УдалитьФайлы(Файл.ПолноеИмя);
		Исключение
			Продолжить;
		КонецПопытки;
	КонецЦикла;
	
КонецПроцедуры // ЗагрузитьСправкуМакетНаСервере()

&НаСервереБезКонтекста
Процедура СоздатьСтатьиСправки(СтрокаДерева, Группа)
	
	Для Каждого Объект Из СтрокаДерева.Строки Цикл
		Если НЕ Объект.Файл = "" Тогда
    		Статья = Справочники.бит_си_Статьи.НайтиПоНаименованию(Объект.Представление, Истина, Группа);
			Если Статья.ЭтоГруппа ИЛИ Статья.Пустая() Тогда
				Статья = Справочники.бит_си_Статьи.СоздатьЭлемент();
				Статья.Родитель = Группа;
				Статья.Наименование = Объект.Представление;
			Иначе
				Статья = Статья.ПолучитьОбъект();
			КонецЕсли;
			Статья.НазваниеСтатьи = Объект.Представление;
			Статья.МестаИспользования.Очистить();
			Место = Статья.МестаИспользования.Добавить();
			Место.ИмяМетки = Объект.Иерархия;
			Статья.Записать();
			ВремФайл = Новый Файл(Объект.Файл);
			ТекКаталог = ВремФайл.Путь;
			ВремФайл = Новый ТекстовыйДокумент();
			ВремФайл.Прочитать(Объект.Файл);
			Если Статья.СохранитьИзображенияВБД(ВремФайл.ПолучитьТекст(), ТекКаталог) Тогда
				Статья.Записать();
			КонецЕсли;
		Иначе
			ГруппаК = Справочники.бит_си_Статьи.НайтиПоНаименованию(Объект.Представление, Истина, Группа);
			Если НЕ ГруппаК.ЭтоГруппа ИЛИ ГруппаК.Пустая() Тогда
				ГруппаК = Справочники.бит_си_Статьи.СоздатьГруппу();
				ГруппаК.Родитель		= Группа;
				ГруппаК.Наименование	= Объект.Представление;
				ГруппаК.ОписаниеРаздела	= "";
				ГруппаК.Записать();
				ГруппаК = ГруппаК.Ссылка;
			КонецЕсли;
			СоздатьСтатьиСправки(Объект, ГруппаК);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // СоздатьСтатьиСправки()

#КонецОбласти

#КонецОбласти
