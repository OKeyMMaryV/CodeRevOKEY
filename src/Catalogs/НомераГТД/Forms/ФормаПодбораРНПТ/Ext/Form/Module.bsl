
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоказыватьТолькоОстатки = Истина;
	ЕстьПравоПодбора = Истина;
	Если ЗначениеЗаполнено(Параметры.ТекущийДокумент) Тогда
		ЕстьПравоПодбора = ПравоДоступа("Изменение", Параметры.ТекущийДокумент.Метаданные());
	КонецЕсли;

	Количество = Параметры.Количество;
	АдресТаблицыРНПТВХранилище = Параметры.АдресТаблицыРНПТВХранилище;
	
	ИмяТаблицы = Параметры.ИмяТаблицы;
	Остатки.Параметры.УстановитьЗначениеПараметра("Период",
		Новый Граница(КонецДня(Параметры.Период), ВидГраницы.Включая));
	Остатки.Параметры.УстановитьЗначениеПараметра("Номенклатура",        Параметры.Номенклатура);
	Остатки.Параметры.УстановитьЗначениеПараметра("Организация",         Параметры.Организация);
	Остатки.Параметры.УстановитьЗначениеПараметра("СтранаПроисхождения", Параметры.СтранаПроисхождения);
	Остатки.Параметры.УстановитьЗначениеПараметра("Комиссионер",         Параметры.Комиссионер);
	Остатки.Параметры.УстановитьЗначениеПараметра("Комитент",            Параметры.Комитент);
	Остатки.Параметры.УстановитьЗначениеПараметра("ВидЗапасов",          Параметры.ВидЗапасов);
	
	Остатки.Параметры.УстановитьЗначениеПараметра("ЭтоПоступление",      Параметры.ЭтоПоступление ИЛИ Параметры.ОткрыватьДляПросмотра);
	ТаблицаРНПТ = ПолучитьИзВременногоХранилища(ЭтотОбъект.АдресТаблицыРНПТВХранилище);
	
	Подбор.Очистить();
	Подбор.Загрузить(ТаблицаРНПТ);
	
	Если Параметры.ЭтоВозвратРозница Тогда
		Остатки.ТекстЗапроса = ТекстЗапросаВозвратТоваров();
		Остатки.Параметры.УстановитьЗначениеПараметра("ДатаРеализации",        Параметры.ДатаРеализации);
		Остатки.Параметры.УстановитьЗначениеПараметра("ДатаТекущегоДокумента", Параметры.ДатаТекущегоДокумента);
		Остатки.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент",       Параметры.ТекущийДокумент);
	
	КонецЕсли;
	
	Если Параметры.ЭтоВозвратПоставщику Тогда
		Остатки.ТекстЗапроса = ТекстЗапросаВозвратТоваровПоставщику();
		Остатки.Параметры.УстановитьЗначениеПараметра("ДатаТекущегоДокумента", Параметры.ДатаТекущегоДокумента);
		Остатки.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент",       Параметры.ТекущийДокумент);
		Остатки.Параметры.УстановитьЗначениеПараметра("ДокументРеализации",    Параметры.ДокументРеализации);
	КонецЕсли;
	
	Если Параметры.ЭтоВозвратКомиссия И Не Параметры.ВозвратКомиссияПоТекДокументу Тогда
		Остатки.ТекстЗапроса = ТекстЗапросаВозвратТоваровКомиссия();
		Остатки.Параметры.УстановитьЗначениеПараметра("ВыставленСФ",           Параметры.ВыставленСФ); 
		Остатки.Параметры.УстановитьЗначениеПараметра("ДокументРеализации",    Параметры.ДокументРеализации);
		Остатки.Параметры.УстановитьЗначениеПараметра("ДатаТекущегоДокумента", Параметры.ДатаТекущегоДокумента);
		Остатки.Параметры.УстановитьЗначениеПараметра("ТекущийДокумент",       Параметры.ТекущийДокумент);
		Остатки.Параметры.УстановитьЗначениеПараметра("СчетФактура",           Параметры.СчетФактура);
		Остатки.Параметры.УстановитьЗначениеПараметра("Покупатель",            Параметры.Покупатель);
	КонецЕсли;
		
	КоличествоПодобрано = Подбор.Итог("Количество");
	
	СвойстваНоменклатуры = 
		ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Параметры.Номенклатура, "ЕдиницаИзмерения, КодТНВЭД.ЕдиницаИзмерения");
	РазныеЕдиницыИзмерения = СвойстваНоменклатуры.ЕдиницаИзмерения <> СвойстваНоменклатуры.КодТНВЭДЕдиницаИзмерения;
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'РНПТ: %1'"),
		Параметры.Номенклатура);
		
	Элементы.ГруппаОстатки.Видимость  = НЕ (Параметры.ЭтоПоступление ИЛИ Параметры.ОткрыватьДляПросмотра);
	Элементы.ПодборДобавить.Видимость = Параметры.ЭтоПоступление;
	ЭтаФорма.ТолькоПросмотр = НЕ ЕстьПравоПодбора Или Параметры.ОткрыватьДляПросмотра;
	ЭтаФорма.Элементы.ПеренестиВДокумент.Доступность = ЕстьПравоПодбора;
	ЭтаФорма.Элементы.ОстаткиВыбрать.Доступность     = ЕстьПравоПодбора;
	ЭтаФорма.Элементы.Остатки.Доступность            = ЕстьПравоПодбора;
	ЭтаФорма.Элементы.Подбор.Доступность             = ЕстьПравоПодбора;
	
	Если Параметры.ОткрыватьДляПросмотра Тогда
		Элементы.ПеренестиВДокумент.Заголовок = НСтр("ru = 'ОК'");
	КонецЕсли;
	
	УстановитьЗаголовкиКолонок(СвойстваНоменклатуры);
	УстановитьЗначениеПараметраПоказыватьТолькоОстатки(ЭтотОбъект);
	УстановитьТекстПодсказки(ЭтотОбъект);
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ЕстьОшибки = Ложь;
	Если Подбор.Количество() <> 0
		И Модифицированность Тогда
		
		Если КоличествоПодобрано > Количество Тогда
			ЕстьОшибки = Истина;
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Список", 
			"Корректность",,,
			НСтр("ru = 'Подбор'"), 
			НСтр("ru = 'Подобранное количество РНПТ превышает количество, указанное в документе'"));
		ИначеЕсли КоличествоПодобрано < Количество Тогда
			ЕстьОшибки = Истина;
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
			"Список", 
			"Корректность",,,
			НСтр("ru = 'Подбор'"), 
			НСтр("ru = 'Подобранное количество РНПТ меньше количества, указанного в документе'"));
		КонецЕсли;
		
		Если ЕстьОшибки Тогда
			Если ПеренестиВДокумент Тогда
				ПеренестиВДокумент = Ложь;
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,,"Подбор",,Отказ);
			КонецЕсли;
		Иначе
			Если Не ПеренестиВДокумент Тогда
				Отказ = Истина;
				ТекстВопроса = НСтр("ru = 'Подобранные РНПТ не перенесены в документ.
					|
					|Перенести?'");
				Оповещение = Новый ОписаниеОповещения("ВопросПеренестиВДокументЗавершение", ЭтотОбъект);
				ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверим пустые строки.
	Если ПеренестиВДокумент Тогда
		НомерСтроки = 1;
		Для Каждого СтрокаТаблицы Из Подбор Цикл
			Префикс = "Подбор[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(НомерСтроки - 1, "ЧН=0; ЧГ="));
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.РНПТ) Тогда
				ЕстьОшибки = Истина;
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'РНПТ'"),
				НомерСтроки, НСтр("ru = 'Подбор'"));
				Поле = Префикс + "РНПТ";
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Количество) Тогда
				ЕстьОшибки = Истина;
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Количество'"),
				НомерСтроки, НСтр("ru = 'Подбор'"));
				Поле = Префикс + "Количество";
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
			КонецЕсли;
			Если РазныеЕдиницыИзмерения
					И НЕ ЗначениеЗаполнено(СтрокаТаблицы.КоличествоПрослеживаемости) Тогда
				ЕстьОшибки = Истина;
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Количество прослеживаемости'"),
				НомерСтроки, НСтр("ru = 'Подбор'"));
				Поле = Префикс + "КоличествоПрослеживаемости";
				ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения,, Поле,, Отказ);
			КонецЕсли;
			НомерСтроки = НомерСтроки + 1;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = ПриЗакрытииНаСервере();
	
	Если ПеренестиВДокумент Тогда
		ОповеститьОВыборе(СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПриЗакрытииНаСервере()
	
	СтруктураВозврата = Новый Структура();
	
	Если ПеренестиВДокумент Тогда
		АдресТаблицыРНПТВХранилище = ПоместитьРНПТВХранилище();
		СтруктураВозврата.Вставить("АдресТаблицыРНПТВХранилище", АдресТаблицыРНПТВХранилище);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПоказыватьТолькоОстаткиПриИзменении(Элемент)
	
	УстановитьЗначениеПараметраПоказыватьТолькоОстатки(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОстатки

&НаКлиенте
Процедура ОстаткиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаПодбора = Элемент.ТекущиеДанные;
	
	Если СтрокаПодбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КоличествоПодобрано = Подбор.Итог("Количество");
	
	Если СтрокаПодбора.Количество + КоличествоПодобрано > Количество Тогда
	
		КоличествоСкорректированное = Количество - КоличествоПодобрано;
		
		Если КоличествоСкорректированное = 0 Тогда
			ТекстСообщения = НСтр("ru='Все РНПТ подобраны.'");
			ПоказатьПредупреждение(, ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		СтрокиСРНПТ = Подбор.НайтиСтроки(Новый Структура("РНПТ", СтрокаПодбора.РНПТ));
		Если СтрокиСРНПТ.Количество() = 0 Тогда
			НоваяСтрока = Подбор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПодбора);
			НоваяСтрока.Количество = КоличествоСкорректированное;
			НоваяСтрока.КоличествоПрослеживаемости = КоличествоПрослеживаемости(
				СтрокаПодбора.Количество, СтрокаПодбора.КоличествоПрослеживаемости, КоличествоСкорректированное);
				
		Иначе
			СтрокиСРНПТ[0].Количество = СтрокиСРНПТ[0].Количество + КоличествоСкорректированное;
			СтрокиСРНПТ[0].КоличествоПрослеживаемости = СтрокиСРНПТ[0].КоличествоПрослеживаемости + КоличествоПрослеживаемости(
				СтрокаПодбора.Количество, СтрокаПодбора.КоличествоПрослеживаемости, КоличествоСкорректированное);
			
		КонецЕсли;
		
	Иначе
		
		СтрокиСРНПТ = Подбор.НайтиСтроки(Новый Структура("РНПТ", СтрокаПодбора.РНПТ));
		
		Если СтрокиСРНПТ.Количество() = 0 Тогда
			НоваяСтрока = Подбор.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаПодбора);
			
		Иначе
			СтрокиСРНПТ[0].Количество = СтрокиСРНПТ[0].Количество + СтрокаПодбора.Количество;
			СтрокиСРНПТ[0].КоличествоПрослеживаемости = СтрокиСРНПТ[0].КоличествоПрослеживаемости + СтрокаПодбора.КоличествоПрослеживаемости;
		КонецЕсли;
		
	КонецЕсли;
	
	КоличествоПодобрано = Подбор.Итог("Количество");
	УстановитьТекстПодсказки(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОстаткиВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	Если Элемент.ВыделенныеСтроки.Количество() = 1 Тогда
		ОстаткиВыбор(Элемент, Значение, , СтандартнаяОбработка);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПодбор

&НаКлиенте
Процедура ПодборПриИзменении(Элемент)
	
	КоличествоПодобрано = Подбор.Итог("Количество");
	УстановитьТекстПодсказки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	ПеренестиВДокумент = НЕ ЭтаФорма.ТолькоПросмотр;
	Закрыть(КодВозвратаДиалога.OK);

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	ПеренестиВДокумент = Ложь;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция КоличествоПрослеживаемости(ОстатокВУчете, ОстатокВПрослеживаемости, ПодобранноеКоличество)
	
	МассивКоэффициентов = Новый Массив;
	МассивКоэффициентов.Добавить(ОстатокВУчете - ПодобранноеКоличество);
	МассивКоэффициентов.Добавить(ПодобранноеКоличество);
	МассивСумм = ОбщегоНазначения.РаспределитьСуммуПропорциональноКоэффициентам(
		ОстатокВПрослеживаемости, МассивКоэффициентов, 11);
	
	Если Не ЗначениеЗаполнено(МассивСумм) Тогда
		Возврат 0;
	Иначе
		Возврат МассивСумм[1];
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ПоместитьРНПТВХранилище()
	
	Если НЕ РазныеЕдиницыИзмерения Тогда
		Для Каждого СтрокаРНПТ Из Подбор Цикл
			СтрокаРНПТ.КоличествоПрослеживаемости = СтрокаРНПТ.Количество;
		КонецЦикла;
	КонецЕсли;
	
	ТаблицаРНПТ = Подбор.Выгрузить();
	АдресТаблицыРНПТВХранилище = ПоместитьВоВременноеХранилище(ТаблицаРНПТ, УникальныйИдентификатор);
	
	Возврат АдресТаблицыРНПТВХранилище;
	
КонецФункции

&НаСервере
Процедура УстановитьЗаголовкиКолонок(СвойстваНоменклатуры)
	
	ТекстЗаголовкаКоличество = НСтр("ru = 'Количество'");
	ТекстЗаголовкаКоличествоПрослеживаемости = НСтр("ru = 'Количество прослеживаемости'");
	Если РазныеЕдиницыИзмерения Тогда
		ТекстЗаголовкаКоличество = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (%2)'"),
		ТекстЗаголовкаКоличество,
		СвойстваНоменклатуры.ЕдиницаИзмерения);
		ТекстЗаголовкаКоличествоПрослеживаемости = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (%2)'"),
		ТекстЗаголовкаКоличествоПрослеживаемости,
		СвойстваНоменклатуры.КодТНВЭДЕдиницаИзмерения);
	КонецЕсли;
	Элементы.ОстаткиКоличество.Заголовок = ТекстЗаголовкаКоличество;
	Элементы.ПодборКоличество.Заголовок  = ТекстЗаголовкаКоличество;
	Элементы.ОстаткиКоличествоПрослеживаемости.Заголовок  = ТекстЗаголовкаКоличествоПрослеживаемости;
	Элементы.ПодборКоличествоПрослеживаемости.Заголовок   = ТекстЗаголовкаКоличествоПрослеживаемости;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	// КоличествоПрослеживаемости в остатках
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОстаткиКоличествоПрослеживаемости");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РазныеЕдиницыИзмерения", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// КоличествоПрослеживаемости в подборе
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПодборКоличествоПрослеживаемости");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"РазныеЕдиницыИзмерения", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьЗначениеПараметраПоказыватьТолькоОстатки(Форма)

	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(
		Форма.Остатки, 
		"Количество", 
		0, 
		Форма.ПоказыватьТолькоОстатки, 
		ВидСравненияКомпоновкиДанных.Больше
		);

КонецПроцедуры

&НаКлиенте
Процедура ВопросПеренестиВДокументЗавершение(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос = КодВозвратаДиалога.Да Тогда
		ПеренестиВДокумент = Истина;
		Закрыть();
	ИначеЕсли ОтветНаВопрос = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		ПеренестиВДокумент = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьТекстПодсказки(Форма)
	
	Форма.НадписьПодобраноРНПТ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru='Подобрано %1 РНПТ из %2:'"), Форма.КоличествоПодобрано, Форма.Количество);
	
КонецПроцедуры

&НаСервере
Функция ТекстЗапросаВозвратТоваров()
	
	Возврат
	"ВЫБРАТЬ
	|	СправочникНомераГТД.Ссылка КАК РНПТ,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.СтранаПроисхождения, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтранаПроисхождения,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости
	|ИЗ
	|	Справочник.НомераГТД КАК СправочникНомераГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РеализацияМинусВозврат.РНПТ КАК РНПТ,
	|			РеализацияМинусВозврат.Организация КАК Организация,
	|			РеализацияМинусВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|			РеализацияМинусВозврат.Номенклатура КАК Номенклатура,
	|			СУММА(РеализацияМинусВозврат.Количество) КАК Количество,
	|			СУММА(РеализацияМинусВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости
	|		ИЗ
	|			(ВЫБРАТЬ
	|				РеализованныеТовары.Организация КАК Организация,
	|				РеализованныеТовары.Номенклатура КАК Номенклатура,
	|				РеализованныеТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|				РеализованныеТовары.Количество КАК Количество,
	|				РеализованныеТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|				РеализованныеТовары.РНПТ КАК РНПТ
	|			ИЗ
	|				РегистрНакопления.ПрослеживаемыеТовары КАК РеализованныеТовары
	|			ГДЕ
	|				РеализованныеТовары.Организация = &Организация
	|				И РеализованныеТовары.Номенклатура = &Номенклатура
	|				И РеализованныеТовары.СтранаПроисхождения = &СтранаПроисхождения
	|				И РеализованныеТовары.Комиссионер = &Комиссионер
	|				И РеализованныеТовары.Комитент = &Комитент
	|				И РеализованныеТовары.ВидЗапасов = &ВидЗапасов
	|				И &ДатаРеализации = НАЧАЛОПЕРИОДА(РеализованныеТовары.Период, ДЕНЬ)
	|				И РеализованныеТовары.Регистратор ССЫЛКА Документ.ОтчетОРозничныхПродажах
	|				И РеализованныеТовары.Регистратор <> &ТекущийДокумент
	|				И РеализованныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ВозвращенныеТовары.Организация,
	|				ВозвращенныеТовары.Номенклатура,
	|				ВозвращенныеТовары.СтранаПроисхождения,
	|				-ВозвращенныеТовары.Количество,
	|				-ВозвращенныеТовары.КоличествоПрослеживаемости,
	|				ВозвращенныеТовары.РНПТ
	|			ИЗ
	|				РегистрНакопления.ПрослеживаемыеТовары КАК ВозвращенныеТовары
	|			ГДЕ
	|				ВозвращенныеТовары.Организация = &Организация
	|				И ВозвращенныеТовары.Номенклатура = &Номенклатура
	|				И ВозвращенныеТовары.СтранаПроисхождения = &СтранаПроисхождения
	|				И ВозвращенныеТовары.Комиссионер = &Комиссионер
	|				И ВозвращенныеТовары.Комитент = &Комитент
	|				И ВозвращенныеТовары.ВидЗапасов = &ВидЗапасов
	|				И ВозвращенныеТовары.Период <= &ДатаТекущегоДокумента
	|				И ВозвращенныеТовары.Регистратор <> &ТекущийДокумент
	|				И ВозвращенныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				И ВозвращенныеТовары.ДатаРеализации = &ДатаРеализации) КАК РеализацияМинусВозврат
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РеализацияМинусВозврат.РНПТ,
	|			РеализацияМинусВозврат.СтранаПроисхождения,
	|			РеализацияМинусВозврат.Номенклатура,
	|			РеализацияМинусВозврат.Организация) КАК ПрослеживаемыеТоварыОстатки
	|		ПО СправочникНомераГТД.Ссылка = ПрослеживаемыеТоварыОстатки.РНПТ";
	
КонецФункции

&НаСервере
Функция ТекстЗапросаВозвратТоваровПоставщику()
	
	Возврат
	"ВЫБРАТЬ
	|	СправочникНомераГТД.Ссылка КАК РНПТ,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.Организация, ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)) КАК Организация,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.СтранаПроисхождения, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтранаПроисхождения,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(ПрослеживаемыеТоварыОстатки.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости
	|ИЗ
	|	Справочник.НомераГТД КАК СправочникНомераГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ПоступленияМинусВозврат.РНПТ КАК РНПТ,
	|			ПоступленияМинусВозврат.Организация КАК Организация,
	|			ПоступленияМинусВозврат.СтранаПроисхождения КАК СтранаПроисхождения,
	|			ПоступленияМинусВозврат.Номенклатура КАК Номенклатура,
	|			СУММА(ПоступленияМинусВозврат.Количество) КАК Количество,
	|			СУММА(ПоступленияМинусВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости
	|		ИЗ
	|			(ВЫБРАТЬ
	|				ЗакупленныеТовары.Организация КАК Организация,
	|				ЗакупленныеТовары.Номенклатура КАК Номенклатура,
	|				ЗакупленныеТовары.СтранаПроисхождения КАК СтранаПроисхождения,
	|				ЗакупленныеТовары.Количество КАК Количество,
	|				ЗакупленныеТовары.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|				ЗакупленныеТовары.РНПТ КАК РНПТ
	|			ИЗ
	|				РегистрНакопления.ПрослеживаемыеТовары КАК ЗакупленныеТовары
	|			ГДЕ
	|				ЗакупленныеТовары.Организация = &Организация
	|				И ЗакупленныеТовары.Номенклатура = &Номенклатура
	|				И ЗакупленныеТовары.СтранаПроисхождения = &СтранаПроисхождения
	|				И ЗакупленныеТовары.Комиссионер = &Комиссионер
	|				И ЗакупленныеТовары.Комитент = &Комитент
	|				И ЗакупленныеТовары.ВидЗапасов = &ВидЗапасов
	|				И ЗакупленныеТовары.Регистратор = &ДокументРеализации
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ВозвращенныеТовары.Организация,
	|				ВозвращенныеТовары.Номенклатура,
	|				ВозвращенныеТовары.СтранаПроисхождения,
	|				ВЫБОР
	|					КОГДА ВозвращенныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА ВозвращенныеТовары.Количество
	|					ИНАЧЕ -ВозвращенныеТовары.Количество
	|				КОНЕЦ,
	|				ВЫБОР
	|					КОГДА ВозвращенныеТовары.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА ВозвращенныеТовары.КоличествоПрослеживаемости
	|					ИНАЧЕ -ВозвращенныеТовары.КоличествоПрослеживаемости
	|				КОНЕЦ,
	|				ВозвращенныеТовары.РНПТ
	|			ИЗ
	|				РегистрНакопления.ПрослеживаемыеТовары КАК ВозвращенныеТовары
	|			ГДЕ
	|				ВозвращенныеТовары.Организация = &Организация
	|				И ВозвращенныеТовары.Номенклатура = &Номенклатура
	|				И ВозвращенныеТовары.СтранаПроисхождения = &СтранаПроисхождения
	|				И ВозвращенныеТовары.Комиссионер = &Комиссионер
	|				И ВозвращенныеТовары.Комитент = &Комитент
	|				И ВозвращенныеТовары.ВидЗапасов = &ВидЗапасов
	|				И ВозвращенныеТовары.Период <= &ДатаТекущегоДокумента
	|				И ВозвращенныеТовары.Регистратор <> &ТекущийДокумент
	|				И ВозвращенныеТовары.ОснованиеДляВозврата = &ДокументРеализации) КАК ПоступленияМинусВозврат
	|		
	|		СГРУППИРОВАТЬ ПО
	|			ПоступленияМинусВозврат.РНПТ,
	|			ПоступленияМинусВозврат.СтранаПроисхождения,
	|			ПоступленияМинусВозврат.Номенклатура,
	|			ПоступленияМинусВозврат.Организация) КАК ПрослеживаемыеТоварыОстатки
	|		ПО СправочникНомераГТД.Ссылка = ПрослеживаемыеТоварыОстатки.РНПТ";
	
КонецФункции

&НаСервере
Функция ТекстЗапросаВозвратТоваровКомиссия()
	
	Возврат
	"ВЫБРАТЬ
	|	ОтчетКомиссионераОПродажахТовары.Номенклатура КАК Номенклатура,
	|	ОтчетКомиссионераОПродажахПокупатели.Покупатель КАК Покупатель,
	|	ОтчетКомиссионераОПродажахПокупатели.СчетФактура КАК СчетФактура,
	|	ОтчетКомиссионераОПродажахПокупатели.ВыставленСФ КАК ВыставленСФ,
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.РНПТ КАК РНПТ,
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Количество КАК Количество,
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.КоличествоПрослеживаемости КАК КоличествоПрослеживаемости,
	|	ОтчетКомиссионераОПродажахТовары.Ссылка.Организация КАК Организация,
	|	ОтчетКомиссионераОПродажахТовары.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ РеализацияИВозврат
	|ИЗ
	|	Документ.ОтчетКомиссионераОПродажах.Покупатели КАК ОтчетКомиссионераОПродажахПокупатели
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Товары КАК ОтчетКомиссионераОПродажахТовары
	|			ЛЕВОЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.СведенияПрослеживаемости КАК ОтчетКомиссионераОПродажахСведенияПрослеживаемости
	|			ПО ОтчетКомиссионераОПродажахТовары.Ссылка = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Ссылка
	|				И ОтчетКомиссионераОПродажахТовары.ИдентификаторСтроки = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.ИдентификаторСтроки
	|		ПО ОтчетКомиссионераОПродажахПокупатели.Ссылка = ОтчетКомиссионераОПродажахТовары.Ссылка
	|			И ОтчетКомиссионераОПродажахПокупатели.КлючСтроки = ОтчетКомиссионераОПродажахТовары.КлючСтроки
	|ГДЕ
	|	ОтчетКомиссионераОПродажахПокупатели.Ссылка = &ДокументРеализации
	|	И ОтчетКомиссионераОПродажахПокупатели.Покупатель = &Покупатель
	|	И ОтчетКомиссионераОПродажахТовары.Номенклатура = &Номенклатура
	|	И ОтчетКомиссионераОПродажахТовары.СтранаПроисхождения = &СтранаПроисхождения
	|	И ВЫБОР
	|			КОГДА ОтчетКомиссионераОПродажахПокупатели.ВыставленСФ
	|				ТОГДА ОтчетКомиссионераОПродажахПокупатели.СчетФактура В (&СчетФактура)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ОтчетКомиссионераОПродажахПокупатели.ВыставленСФ = &ВыставленСФ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОтчетКомиссионераОПродажахТоварыВозвращенные.Номенклатура,
	|	ОтчетКомиссионераОПродажахВозвраты.Покупатель,
	|	ОтчетКомиссионераОПродажахВозвратыИсходныеДокументы.ИсходныйДокумент,
	|	ОтчетКомиссионераОПродажахВозвраты.ВыставленСФ,
	|	ОтчетКомиссионераОПродажахСведенияПрослеживаемости.РНПТ,
	|	-ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Количество,
	|	-ОтчетКомиссионераОПродажахСведенияПрослеживаемости.КоличествоПрослеживаемости,
	|	ВложенныйЗапрос.Организация,
	|	ОтчетКомиссионераОПродажахТоварыВозвращенные.СтранаПроисхождения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПрослеживаемыеТовары.Регистратор КАК Регистратор,
	|		ПрослеживаемыеТовары.Организация КАК Организация
	|	ИЗ
	|		РегистрНакопления.ПрослеживаемыеТовары КАК ПрослеживаемыеТовары
	|	ГДЕ
	|		ПрослеживаемыеТовары.Организация = &Организация
	|		И ПрослеживаемыеТовары.Номенклатура = &Номенклатура
	|		И ПрослеживаемыеТовары.СтранаПроисхождения = &СтранаПроисхождения
	|		И ПрослеживаемыеТовары.Комиссионер = &Комиссионер
	|		И ПрослеживаемыеТовары.Комитент = &Комитент
	|		И ПрослеживаемыеТовары.ВидЗапасов = &ВидЗапасов
	|		И ПрослеживаемыеТовары.Период <= &ДатаТекущегоДокумента
	|		И ПрослеживаемыеТовары.Регистратор <> &ТекущийДокумент
	|		И ПрослеживаемыеТовары.ОснованиеДляВозврата = &ДокументРеализации) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.ТоварыВозвращенные КАК ОтчетКомиссионераОПродажахТоварыВозвращенные
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.ВозвратыИсходныеДокументы КАК ОтчетКомиссионераОПродажахВозвратыИсходныеДокументы
	|			ПО ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка = ОтчетКомиссионераОПродажахВозвратыИсходныеДокументы.Ссылка
	|				И ОтчетКомиссионераОПродажахТоварыВозвращенные.КлючСтроки = ОтчетКомиссионераОПродажахВозвратыИсходныеДокументы.КлючСтроки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.СведенияПрослеживаемости КАК ОтчетКомиссионераОПродажахСведенияПрослеживаемости
	|			ПО ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.Ссылка
	|				И ОтчетКомиссионераОПродажахТоварыВозвращенные.ИдентификаторСтроки = ОтчетКомиссионераОПродажахСведенияПрослеживаемости.ИдентификаторСтроки
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ОтчетКомиссионераОПродажах.Возвраты КАК ОтчетКомиссионераОПродажахВозвраты
	|			ПО ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка = ОтчетКомиссионераОПродажахВозвраты.Ссылка
	|				И ОтчетКомиссионераОПродажахТоварыВозвращенные.КлючСтроки = ОтчетКомиссионераОПродажахВозвраты.КлючСтроки
	|		ПО ВложенныйЗапрос.Регистратор = ОтчетКомиссионераОПродажахТоварыВозвращенные.Ссылка
	|ГДЕ
	|	ОтчетКомиссионераОПродажахТоварыВозвращенные.Номенклатура = &Номенклатура
	|	И ОтчетКомиссионераОПродажахТоварыВозвращенные.ДокументРеализации = &ДокументРеализации
	|	И ОтчетКомиссионераОПродажахТоварыВозвращенные.СтранаПроисхождения = &СтранаПроисхождения
	|	И ВЫБОР
	|			КОГДА &ВыставленСФ
	|				ТОГДА ОтчетКомиссионераОПродажахВозвратыИсходныеДокументы.ИсходныйДокумент В (&СчетФактура)
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И ОтчетКомиссионераОПродажахВозвраты.ВыставленСФ = &ВыставленСФ
	|	И ОтчетКомиссионераОПродажахВозвраты.Покупатель = &Покупатель
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РеализацияИВозврат.Номенклатура КАК Номенклатура,
	|	РеализацияИВозврат.РНПТ КАК РНПТ,
	|	СУММА(РеализацияИВозврат.Количество) КАК Количество,
	|	СУММА(РеализацияИВозврат.КоличествоПрослеживаемости) КАК КоличествоПрослеживаемости,
	|	РеализацияИВозврат.Организация КАК Организация,
	|	РеализацияИВозврат.СтранаПроисхождения КАК СтранаПроисхождения
	|ПОМЕСТИТЬ РеализацияМинусВозвратИтого
	|ИЗ
	|	РеализацияИВозврат КАК РеализацияИВозврат
	|
	|СГРУППИРОВАТЬ ПО
	|	РеализацияИВозврат.Номенклатура,
	|	РеализацияИВозврат.РНПТ,
	|	РеализацияИВозврат.Организация,
	|	РеализацияИВозврат.СтранаПроисхождения
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(РеализацияМинусВозвратИтого.Номенклатура, ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК Номенклатура,
	|	ЕСТЬNULL(РеализацияМинусВозвратИтого.Количество, 0) КАК Количество,
	|	ЕСТЬNULL(РеализацияМинусВозвратИтого.КоличествоПрослеживаемости, 0) КАК КоличествоПрослеживаемости,
	|	НомераГТД.Ссылка КАК РНПТ,
	|	ЕСТЬNULL(РеализацияМинусВозвратИтого.СтранаПроисхождения, ЗНАЧЕНИЕ(Справочник.СтраныМира.ПустаяСсылка)) КАК СтранаПроисхождения,
	|	РеализацияМинусВозвратИтого.Организация КАК Организация
	|ИЗ
	|	Справочник.НомераГТД КАК НомераГТД
	|		ЛЕВОЕ СОЕДИНЕНИЕ РеализацияМинусВозвратИтого КАК РеализацияМинусВозвратИтого
	|		ПО (РеализацияМинусВозвратИтого.РНПТ = НомераГТД.Ссылка)";
	
КонецФункции

#КонецОбласти
