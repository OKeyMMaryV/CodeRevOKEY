
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбработатьПараметрыАвтозаполненияНаСервере();
	ЗаполнитьТабличныеЧастиНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выполнить_(Команда)
	
	СформироватьСтруктуруАвтозаполненияНаСервере();
	
	ВозвратПараметры = Новый Структура("ОчищатьДвижения, ПараметрыАвтозаполнения");
	ВозвратПараметры.ОчищатьДвижения = ОчиститьИмеющиесяСтрокиШаблона;
	ВозвратПараметры.ПараметрыАвтозаполнения = ПолучитьПараметрыАвтозаполнения() ;
	
	Закрыть(ВозвратПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура Закрыть_(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает параметры автозаполнения.
// 
&НаКлиенте
Функция ПолучитьПараметрыАвтозаполнения()
	
	ПараметрыАвтозаполнения = ЗначенияРеквизитовДвижений;
	
	Возврат ПараметрыАвтозаполнения;
	
КонецФункции

// Обрабатывает параметры переданные формой-владельцем.
// 
&НаСервере
Процедура ОбработатьПараметрыАвтозаполненияНаСервере()
	
	Если Параметры.Свойство("Регистр") Тогда
		Регистр = Параметры.Регистр;
	КонецЕсли;
	
	Если Параметры.Свойство("Документ") Тогда
		Документ = Параметры.Документ;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидПроформы") Тогда
		ВидПроформы = Параметры.ВидПроформы;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Документ) Тогда
		Если Документ.ТипЗначенияОбъекта.Получить() = Тип("ДокументСсылка.бит_Проформы") и ЗначениеЗаполнено(ВидПроформы) Тогда
			ЭтоПроформа = Истина;
			МетаданныеПроформы 				= Справочники.бит_ВидыПроформ.ЭмулироватьМетаданные(ВидПроформы);
			МетаданныеПроформыСтруктурой 	= Справочники.бит_ВидыПроформ.ЭмулироватьМетаданные(ВидПроформы, "Структура");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Заполняет список табличных частей по метаданным документа.
// 
&НаСервере
Процедура ЗаполнитьТабличныеЧастиНаСервере()
	
    СписокТабЧастей.Очистить();
	
	Если не ЗначениеЗаполнено(Документ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоПроформа Тогда
		ТабЧасти = МетаданныеПроформы.ТабличныеЧасти;
	Иначе	
		ТабЧасти = Метаданные.Документы[Документ.ИмяОбъекта].ТабличныеЧасти;
	КонецЕсли;
	
	Для Каждого ТабЧасть Из ТабЧасти Цикл
		
		Если ЭтоПроформа Тогда
			ТабЧасть = ТабЧасть.Значение;
		КонецЕсли;
		
		СписокТабЧастей.Добавить(ТабЧасть.Имя, ТабЧасть.Синоним, Истина);
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует структуру автозаполнения шаблона движений.
// 
&НаСервере
Процедура СформироватьСтруктуруАвтозаполненияНаСервере()
	
	// Получим список реквизитов для регистра
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	бит_ВидыРеквизитовДвижений.Наименование,
	|	бит_ВидыРеквизитовДвижений.ТипыЗначений,
	|	бит_ВидыРеквизитовДвижений.Ссылка
	|ИЗ
	|	Справочник.бит_ВидыРеквизитовДвижений КАК бит_ВидыРеквизитовДвижений
	|ГДЕ
	|	бит_ВидыРеквизитовДвижений.Родитель = &Регистр
	|	И (НЕ бит_ВидыРеквизитовДвижений.ЭтоГруппа)");
	
	Запрос.УстановитьПараметр("Регистр", Регистр);
	
	ТабСписокРеквизитов 	= Запрос.Выполнить().Выгрузить();
	
	Если ЭтоПроформа Тогда
		МетаданныеДокумента = МетаданныеПроформы;
	Иначе	
		МетаданныеДокумента = Метаданные.Документы[Документ.ИмяОбъекта];
	КонецЕсли;
	
	СчетчикНомеровДвижений 	= 0;
	
	Для Каждого СтрокаТабЧасть Из СписокТабЧастей Цикл
		
		Если не СтрокаТабЧасть.Пометка Тогда
			Продолжить;
		КонецЕсли;
		
		СчетчикНомеровДвижений = СчетчикНомеровДвижений + 1;
		
		ИмяТабЧасти = СтрокаТабЧасть.Значение;
		
		ЗаполнитьСтруктуруДляТабличнойЧасти(ИмяТабЧасти, МетаданныеДокумента, ТабСписокРеквизитов, СчетчикНомеровДвижений);
		
	КонецЦикла;
	
КонецПроцедуры

// Формирует структуру автозаполнения шаблона движений для выбранной табчасти.
// 
&НаСервере
Процедура ЗаполнитьСтруктуруДляТабличнойЧасти(ИмяТабЧасти, МетаданныеДокумента, ТабСписокРеквизитов, НомерСтроки)
	
	// Первый проход: заполним реквизиты, если их имена полностью совпадают с реквизитами ТЧ.
	
	Если ЭтоПроформа Тогда
		
		РеквизитыТабЧасти = ПолучитьРеквизитыТабЧастиПроформы(ИмяТабЧасти);
		
		Если РеквизитыТабЧасти = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
	Иначе
		
		РеквизитыТабЧасти = МетаданныеДокумента.ТабличныеЧасти[ИмяТабЧасти].Реквизиты;
		
	КонецЕсли;
	
	НовСтрока = ЗначенияРеквизитовДвижений.Добавить();
	НовСтрока.НомерДвижения = НомерСтроки;
	НовСтрока.Реквизит 		= ТабСписокРеквизитов.Найти("Таблица", "Наименование").Ссылка;
	НовСтрока.Значение		= ИмяТабЧасти;
	
	Для Каждого РеквизитДвижения Из ТабСписокРеквизитов Цикл
		
		НаименованиеДляПоиска = РеквизитДвижения.Наименование;
		
		Если (не ЭтоПроформа и РеквизитыТабчасти.Найти(НаименованиеДляПоиска) <> Неопределено)
			или (ЭтоПроформа и РеквизитыТабЧасти.Свойство(НаименованиеДляПоиска)) Тогда
			
			НовСтрока = ЗначенияРеквизитовДвижений.Добавить();
			НовСтрока.НомерДвижения = НомерСтроки;
			НовСтрока.Реквизит 		= РеквизитДвижения.Ссылка;
			НовСтрока.Значение		= "#Т." + РеквизитДвижения.Наименование;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Второй проход: заполним реквизиты, если их имена полностью совпадают с реквизитами шапки.
	
	Если ЭтоПроформа Тогда
		РеквизитыШапки = МетаданныеПроформыСтруктурой.Реквизиты;
	Иначе
		РеквизитыШапки = МетаданныеДокумента.Реквизиты;
	КонецЕсли;
	
	Для Каждого РеквизитДвижения Из ТабСписокРеквизитов Цикл
		
		НаименованиеДляПоиска = РеквизитДвижения.Наименование;
		
		Если РеквизитДвижения.Наименование = "Период" Тогда
			
			Если не РеквизитЗаполнен(РеквизитДвижения.Ссылка, НомерСтроки) Тогда
				
				НовСтрока = ЗначенияРеквизитовДвижений.Добавить();
				НовСтрока.НомерДвижения = НомерСтроки;
				НовСтрока.Реквизит 		= РеквизитДвижения.Ссылка;
				НовСтрока.Значение		= "#Р.Дата";
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если (не ЭтоПроформа и РеквизитыШапки.Найти(НаименованиеДляПоиска) <> Неопределено)
			или (ЭтоПроформа и РеквизитыШапки.Свойство(НаименованиеДляПоиска)) Тогда
			
			Если РеквизитЗаполнен(РеквизитДвижения.Ссылка, НомерСтроки) Тогда
				Продолжить;
			КонецЕсли;
			
			НовСтрока = ЗначенияРеквизитовДвижений.Добавить();
			НовСтрока.НомерДвижения = НомерСтроки;
			НовСтрока.Реквизит 		= РеквизитДвижения.Ссылка;
			НовСтрока.Значение		= "#Р." + РеквизитДвижения.Наименование;
			
		КонецЕсли;
		
	КонецЦикла;
	
	// Третий проход: подбор по типу данных в реквизитах табличной части.
	
	РеквизитыТабЧасти = РеквизитыТабЧасти;
	
	Для Каждого РеквизитДвижения Из ТабСписокРеквизитов Цикл
		
		Если Найти(РеквизитДвижения.Наименование, "Субконто") > 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТипДляПоиска = РеквизитДвижения.ТипыЗначений.Получить();
		
		Если ТипДляПоиска = Тип("Строка") 
				или ТипДляПоиска.СодержитТип(Тип("Число")) 
				или ТипДляПоиска.СодержитТип(Тип("Булево")) Тогда
				
			Продолжить; // Примитивные типы не заполняются по соответствию типов.
			
		КонецЕсли;
		
		Для Каждого РеквизитТабЧасти Из РеквизитыТабЧасти Цикл
			
			Если РеквизитЗаполнен(РеквизитДвижения.Ссылка, НомерСтроки) Тогда
				Продолжить;
			КонецЕсли;
			
			Если ЭтоПроформа Тогда
				РеквизитТабЧасти = РеквизитТабЧасти.Значение;
			КонецЕсли;
			
			Если ТипЗнч(РеквизитТабЧасти.Тип.ПривестиЗначение()) = Тип("Строка") Тогда
				Продолжить;
			КонецЕсли;
			
			Если ТипДляПоиска.СодержитТип(ТипЗнч(РеквизитТабЧасти.Тип.ПривестиЗначение())) Тогда
				
				НовСтрока = ЗначенияРеквизитовДвижений.Добавить();
				НовСтрока.НомерДвижения = НомерСтроки;
				НовСтрока.Реквизит 		= РеквизитДвижения.Ссылка;
				НовСтрока.Значение		= "#Т." + РеквизитТабЧасти.Имя;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

// Определяет заполнен ли указанный реквизит в указанной строке.
// 
&НаСервере
Функция РеквизитЗаполнен(РеквизитСсылка, НомерСтроки)
	
	Отбор = Новый Структура("Реквизит, НомерДвижения", РеквизитСсылка, НомерСтроки);
	
	МассивНайденных = ЗначенияРеквизитовДвижений.НайтиСтроки(Отбор);
	
	Если МассивНайденных.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат ЗначениеЗаполнено(МассивНайденных[0].Значение);
	
КонецФункции

// Возвращает реквизиты табчасти проформы.
// 
&НаСервере
Функция ПолучитьРеквизитыТабЧастиПроформы(ИмяТабЧасти)
	
	Для Каждого ТабЧасть Из МетаданныеПроформыСтруктурой.ТабличныеЧасти Цикл
		
		Если ТабЧасть.Значение.Имя = ИмяТабЧасти Тогда
			Возврат ТабЧасть.Значение.Реквизиты;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
