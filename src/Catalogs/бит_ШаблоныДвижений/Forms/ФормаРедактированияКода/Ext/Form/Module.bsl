
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
    Если Параметры.Свойство("КодИнициализации") Тогда
		КодИнициализации = Параметры.КодИнициализации;
    КонецЕсли;
    
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сохранить(Команда)
    
    Закрыть(Новый Структура("ИмяТаблицы, КодИнициализации", ИмяТаблицы, КодИнициализации));
    
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФорму(Команда)
    
    Закрыть(Неопределено);
    
КонецПроцедуры

&НаКлиенте
Процедура КонструкторЗапроса(Команда)
	
	#Если ТолстыйКлиентОбычноеПриложение или ТолстыйКлиентУправляемоеПриложение Тогда
		
		ВыделенныйТекст = Элементы.КодИнициализации.ВыделенныйТекст;
			
		КонструкторЗапроса = Новый КонструкторЗапроса;
		
		Если СокрЛП(ВыделенныйТекст) <> "" Тогда
			КонструкторЗапроса.Текст = УбратьДобавитьПалки(ВыделенныйТекст);
		КонецЕсли;	
		
		Оповещение = Новый ОписаниеОповещения("КонструкторЗапросаЗавершениеРедактирования", ЭтотОбъект);
		КонструкторЗапроса.Показать(Оповещение);
	
	#Иначе
		
		ТекстСообщения = НСтр("ru='Работа с конструктором запроса доступна только в толстом клиенте!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
	#КонецЕсли
	
КонецПроцедуры

// Обработчик завершения закрытия диалога конструктора запроса.
//  См. Синтакс-помощник: КонструкторЗапроса.Показать().
//
&НаКлиенте
Процедура КонструкторЗапросаЗавершениеРедактирования(ИзмененныйТекстЗапроса, ДополнительныеДанные) Экспорт
	
	Если ИзмененныйТекстЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.КодИнициализации.ВыделенныйТекст = бит_СтрокиКлиентСервер.УбратьДобавитьПалки(ИзмененныйТекстЗапроса, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция возвращает текст с добавлением/удаление разделителя "|".
// 
// Параметры:
//  Текст    - Строка.
//  Добавить - Булево.
// 
// Возвращаемое значение:
//  ТекстНов - Строка.
// 
&НаКлиенте
Функция УбратьДобавитьПалки(Текст, Добавить = Ложь)
	
	ТекстНов = Текст;
	
	Если Добавить Тогда
		
		ТекстНов = """" + ТекстНов + """";
		
		ТекстНов = СтрЗаменить(ТекстНов, Символы.ПС, Символы.ПС + "|");
		
	Иначе	
		
		ТекстНов = СтрЗаменить(ТекстНов, """", "");
		ТекстНов = СтрЗаменить(ТекстНов, "|", "");
		
	КонецЕсли;
	
	Возврат ТекстНов;
	
КонецФункции

#КонецОбласти