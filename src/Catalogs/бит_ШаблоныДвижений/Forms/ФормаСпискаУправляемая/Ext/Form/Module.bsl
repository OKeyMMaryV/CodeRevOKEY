
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Стандартные действия при создании на сервере.
  	бит_РаботаСДиалогамиСервер.СписокПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма);
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВключитьШаблонВКомплект(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	бит_МеханизмХозяйственныхОперацийКлиент.ВключитьШаблонВКомплектИнтерактивно(ТекущаяСтрока.Ссылка, ТекущаяСтрока.ВидДокумента);
	
КонецПроцедуры // ВключитьШаблонВКомплект()

&НаКлиенте
Процедура ПоказатьИнформациюОШаблоне(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если ТекущаяСтрока = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// Откроем форму информации о шаблоне
	СтруктураИнформации = ПодготовитьСтруктуруИнфоНаСервере(ТекущаяСтрока.Ссылка);
	ПоказатьИнформациюОШаблонеКлиент(СтруктураИнформации);
	
КонецПроцедуры // ПоказатьИнформациюОШаблоне()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Открывает форму информации о шаблоне движений.
// 
&НаКлиенте
Процедура ПоказатьИнформациюОШаблонеКлиент(СтруктураИнформации)
	
	ПараметрыФормыИнфо = Новый Структура("СтруктураИнфо", СтруктураИнформации);		
	ОткрытьФорму("Справочник.бит_ШаблоныДвижений.Форма.ФормаИнформацииОШаблонеУправлемая", ПараметрыФормыИнфо);
	
КонецПроцедуры // ПоказатьИнформациюОШаблонеКлиент()

// Возвращает структуру, содержащую основные сведения о шаблоне движений.
// 
&НаСервере
Функция ПодготовитьСтруктуруИнфоНаСервере(Ссылка)
	
	Объект = Ссылка;
	
	СтруктураИнфо = Новый Структура("Документ, Регистр, Автор, ДатаСоздания, Редактор,
	|ДатаПоследнейРедакции, Идентификатор, СписокКомплектов, СписокОрганизаций");
	
	СтруктураИнфо.Документ 				= Объект.ВидДокумента.Наименование;
	СтруктураИнфо.Регистр 				= Объект.Регистр.Наименование;
	СтруктураИнфо.Автор 				= Строка(Объект.Автор);
	СтруктураИнфо.ДатаСоздания 			= Объект.ДатаСоздания;
	СтруктураИнфо.Редактор 				= Строка(Объект.ПоследнийРедактор);
	СтруктураИнфо.ДатаПоследнейРедакции = Объект.ДатаПоследнегоРедактирования;
	СтруктураИнфо.Идентификатор 		= Объект.Идентификатор;
	
	// Подготовим список комплектов, в которые включен шаблон.
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	|	бит_КомплектыДвиженийШаблоныДвижений.Ссылка КАК Комплект
	|ИЗ
	|	Справочник.бит_КомплектыДвижений.ШаблоныДвижений КАК бит_КомплектыДвиженийШаблоныДвижений
	|ГДЕ
	|	бит_КомплектыДвиженийШаблоныДвижений.ШаблонДвижения = &ШаблонДвижения
	|
	|СГРУППИРОВАТЬ ПО
	|	бит_КомплектыДвиженийШаблоныДвижений.Ссылка");
	
	Запрос.УстановитьПараметр("ШаблонДвижения", Объект);
	
	СписокКомплектов = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Комплект");
	
	СтруктураИнфо.СписокКомплектов = СписокКомплектов;
	
	Возврат СтруктураИнфо;
	
КонецФункции // ПодготовитьСтруктуруИнфоНаСервере()

#КонецОбласти

