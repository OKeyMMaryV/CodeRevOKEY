//ОКЕЙ Вдовиченко Г.В(СофтЛаб) 2019-09-20 

Функция СписокВыбораДляФормыДокумента(Ссылка) Экспорт
	
	ПричиныОтклонения = Новый СписокЗначений;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	МАКСИМУМ(Данные.Пометка) КАК Пометка,
		|	Данные.Значение КАК Значение
		|ИЗ
		|	(ВЫБРАТЬ
		|		ИСТИНА КАК Пометка,
		|		ДанныеДокумента.Причина КАК Значение
		|	ИЗ
		|		Документ.ЭлектронныйДокументВходящий.ок_ПричиныОтклонения КАК ДанныеДокумента
		|	ГДЕ
		|		ДанныеДокумента.Причина <> ЗНАЧЕНИЕ(Справочник.ок_ПричиныОтклоненияДО.ПустаяСсылка)
		|		И ДанныеДокумента.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ЛОЖЬ,
		|		ДанныеСправочника.Ссылка
		|	ИЗ
		|		Справочник.ок_ПричиныОтклоненияДО КАК ДанныеСправочника
		|	ГДЕ
		|		НЕ ДанныеСправочника.ПометкаУдаления) КАК Данные
		|
		|СГРУППИРОВАТЬ ПО
		|	Данные.Значение";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		
		ПричиныОтклонения.Добавить(Выборка.Значение, Строка(Выборка.Значение), Выборка.Пометка);
		
	КонецЦикла;
	
	Возврат ПричиныОтклонения;
				
КонецФункции	
