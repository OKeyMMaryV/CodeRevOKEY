
#Область ОписаниеПеременных

// Надо ли формировать автоматически НаименованиеПолное, или нет.
&НаКлиенте
Перем мФормироватьНаименованиеПолноеАвтоматически; 
// Надо ли формировать автоматически НаименованиеПолное для табличного поля "СтатьиБюджета", или нет.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов.
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов.
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
	Элементы.Владелец.ТолькоПросмотр = ЗначениеЗаполнено(Объект.Владелец);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьФлагФормироватьНаименованиеПолноеАвтоматически();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПараметрыОповещения = Новый Структура("Бюджет", Объект.Владелец);
	Оповестить("ЗаписьСтатьиБюджета", ПараметрыОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)

	СформироватьНаименованиеПолноеАвтоматически();

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеПриИзменении(Элемент)

	УстановитьФлагФормироватьНаименованиеПолноеАвтоматически();

КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ВыбранноеЗначение <> Неопределено Тогда
		Объект.НаименованиеПолное = ВыбранноеЗначение;
		мФормироватьНаименованиеПолноеАвтоматически = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПолноеАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	Элемент.СписокВыбора.Добавить(Объект.Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяОборотовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = ПолучитьПараметрыФормыВыбора(Объект.Владелец);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Объект.СтатьяОборотов);
	ОткрытьФорму("Справочник.бит_СтатьиОборотов.ФормаВыбора", ПараметрыФормы, Элементы.СтатьяОборотов,,,,, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяОборотовПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.СтатьяОборотов) Тогда
		ЗаполнитьРеквизитыПоДаннымСтатьиОборотов();
	КонецЕсли; 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура проверяет, совпадало ли ранее полное наименование с наименованием,
// и присваивает соответствующее значение переменной мФормироватьНаименованиеПолноеАвтоматически.
// 
// Параметры:
//  Нет.
// 
&НаКлиенте
Процедура УстановитьФлагФормироватьНаименованиеПолноеАвтоматически()

	Если ПустаяСтрока(Объект.НаименованиеПолное) 
	 ИЛИ Объект.НаименованиеПолное = Объект.Наименование Тогда
		мФормироватьНаименованиеПолноеАвтоматически = Истина;
	Иначе
		мФормироватьНаименованиеПолноеАвтоматически = Ложь;
	КонецЕсли;

КонецПроцедуры

// Процедура проверяет, необходимо ли формировать полное наименование автоматически или нет,
// и, если необходимо, формирует его.
// 
// Параметры:
//  Нет.
// 
&НаКлиенте
Процедура СформироватьНаименованиеПолноеАвтоматически()

	Если мФормироватьНаименованиеПолноеАвтоматически Тогда
		Объект.НаименованиеПолное = Объект.Наименование;
	КонецЕсли;

КонецПроцедуры

// Процедура заполняет необходимые реквизиты по данным статьи оборота.
// 
&НаСервере
Процедура ЗаполнитьРеквизитыПоДаннымСтатьиОборотов()

	СписокРеквизитовСтатьи = "Наименование, НаименованиеПолное, Кодификатор, РасходДоход";
	Если ЗначениеЗаполнено(Объект.СтатьяОборотов) Тогда
		РеквизитыСтатьи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СтатьяОборотов, СписокРеквизитовСтатьи);
	Иначе	
		РеквизитыСтатьи = Новый Структура(СписокРеквизитовСтатьи); 
	КонецЕсли; 
	
	ЗаполнитьЗначенияСвойств(Объект, РеквизитыСтатьи);
	
	Если Объект.Коэффициент = 0 Тогда
		Объект.Коэффициент = ?(РеквизитыСтатьи.РасходДоход = Перечисления.бит_РасходДоход.Расходование, -1, 1);
	КонецЕсли;
     
КонецПроцедуры

// Функция формирует параметры формы выбора.
// 
// Возвращаемое значение:
//   Структура   - параметры.
// 
&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбора(Бюджет)
	
	ТипБюджета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Бюджет, "ТипБюджета");
	Возврат бит_ОбщегоНазначения.ПолучитьПараметрыФормыВыбораСтатейОборотов(ТипБюджета); 	
	
КонецФункции

#КонецОбласти
