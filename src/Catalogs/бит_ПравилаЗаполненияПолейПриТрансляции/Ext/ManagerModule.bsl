#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
// Функция - получает таблицу реквизитов для данного регистра.
//
// Параметры:
//  ОбъектСистемы	 - СправочникСсылкабит_ОбъектыСистемы - объект системы.
// 
// Возвращаемое значение:
//  ТаблицаРеквизитов - ТаблицаЗначений.
//
Функция ЗаполнитьТаблицуРеквизитов(ОбъектСистемы)  Экспорт

	ТаблицаРеквизитов = Новый ТаблицаЗначений;
	Если ОбъектСистемы.ВидОбъекта = Перечисления.бит_ВидыОбъектовСистемы.РегистрБухгалтерии Тогда
		ТаблицаРеквизитов = бит_МеханизмТрансляции.ПолучитьРеквизитыРегистраБухгалтерии(ОбъектСистемы.ИмяОбъекта,Истина,Ложь);
	ИначеЕсли ОбъектСистемы.ВидОбъекта = Перечисления.бит_ВидыОбъектовСистемы.РегистрНакопления Тогда	
	    ТаблицаРеквизитов = бит_МеханизмТрансляции.ПолучитьРеквизитыРегистраНакопления(ОбъектСистемы.ИмяОбъекта);
	КонецЕсли; 
	
	Возврат ТаблицаРеквизитов;
	
КонецФункции // ЗаполнитьТаблицуРеквизитов()

// Функция - возвращает список "фиксированных" (хранящихся в отдельных регистрах) соответствий.
// 
// Возвращаемое значение:
//  ФиксВидыСоотв - СписокЗначений.
//
Функция ФиксированныеСоответствия() Экспорт
	
	ФиксВидыСоотв = Новый СписокЗначений;
	
	Если бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_СинхронизироватьПодразделенияРеглЦФО") Тогда
	
		ФиксВидыСоотв.Добавить("Использовать_СинхронизациюПодразделенияЦФО", "Синхронизация Подразделения --> ЦФО");
	
	КонецЕсли; 
	
	флЕстьРегистр = ?(НЕ Метаданные.РегистрыСведений.Найти("бит_СтатьиОборотов_СтатьиРегл") = Неопределено, Истина, Ложь);
	
	Если флЕстьРегистр Тогда
		
		ФиксВидыСоотв.Добавить("Использовать_Соответствие_Статьи_СтатьиРегл", "Статьи регл. --> Статьи оборотов");
		
	КонецЕсли; 
	
	флЕстьРегистр = ?(НЕ Метаданные.РегистрыСведений.Найти("бит_СтатьиОборотов_НоменклатурныеГруппы") = Неопределено, Истина, Ложь);
	
	Если флЕстьРегистр Тогда
		
		ФиксВидыСоотв.Добавить("Использовать_Соответствие_Статьи_НомГруппы",  "Ном. группы --> Статьи оборотов");
		
	КонецЕсли; 
	
	флЕстьРегистр = ?(НЕ Метаданные.РегистрыСведений.Найти("бит_ЦФО_Подразделения") = Неопределено, Истина, Ложь);		
	
	Если флЕстьРегистр Тогда
		
		ФиксВидыСоотв.Добавить("Использовать_Соответствие_ЦФО_Подразделения", "Подразделения --> ЦФО");	
		
	КонецЕсли; 
	
	Возврат ФиксВидыСоотв;
	
КонецФункции // ФиксированныеСоответствия	

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Процедура заполняет реквизит РежимИспользованияСоответствий в справочнике ПравилаЗаполненияПоТрансляции
// Также выполняется "перекомпиляция" кода правил заполнения.
// 
Процедура УстановитьРежимИспользованияСоответствий() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	бит_ПравилаЗаполненияПолейПриТрансляции.Ссылка,
	|	бит_ПравилаЗаполненияПолейПриТрансляции.Использовать_Соответствие_Статьи_СтатьиРегл,
	|	бит_ПравилаЗаполненияПолейПриТрансляции.Использовать_Соответствие_Статьи_НомГруппы,
	|	бит_ПравилаЗаполненияПолейПриТрансляции.Использовать_Соответствие_ЦФО_Подразделения
	|ИЗ
	|	Справочник.бит_ПравилаЗаполненияПолейПриТрансляции КАК бит_ПравилаЗаполненияПолейПриТрансляции
	|ГДЕ
	|	НЕ бит_ПравилаЗаполненияПолейПриТрансляции.ЭтоГруппа";
				   
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		 ТекущийОбъект = Выборка.Ссылка.ПолучитьОбъект();
		 Если Выборка.Использовать_Соответствие_Статьи_СтатьиРегл = Истина 
		  ИЛИ Выборка.Использовать_Соответствие_Статьи_НомГруппы = Истина 
		  ИЛИ Выборка.Использовать_Соответствие_ЦФО_Подразделения = Истина Тогда
			 
			 ТекущийОбъект.РежимИспользованияСоответствий = Перечисления.бит_РежимыИспользованияСоответствийПриТрансляции.Стандартный;
		 Иначе
			 ТекущийОбъект.РежимИспользованияСоответствий = Перечисления.бит_РежимыИспользованияСоответствийПриТрансляции.НеИспользовать;
		 КонецЕсли; 
		 ТекущийОбъект.ГенерироватьКод(); 
		 ОбновлениеИнформационнойБазы.ЗаписатьДанные(ТекущийОбъект);
	КонецЦикла; 
	
КонецПроцедуры  // УстановитьРежимИспользованияСоответствий

#КонецОбласти 

#КонецЕсли
