
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
	// ++ БП 
	Если ЗначениеЗаполнено(Объект.Аналитика) Тогда
		МассивТипов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Аналитика, "ТипЗначения").Типы();
		бит_рлс_Основной.ПроверитьБитыеСсылкиВОбъекте(РеквизитФормыВЗначение("Объект"), МассивТипов, Отказ);
		Если Отказ Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли; 
	// -- БП 
	
	УправлениеФормой(ЭтотОбъект);
	УстановитьУсловноеОфрмление();
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВидПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура АналитикаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекстСообщения = "";
	АналитикаОбработкаВыбораНаСервере(ВыбранноеЗначение, ТекстСообщения, СтандартнаяОбработка);
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
		ПоказатьПредупреждение(,ТекстСообщения, , Нстр("ru = 'Профиль распределения.'"));
	КонецЕсли; 
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрофиль

&НаКлиенте
Процедура ПрофильПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		КолВоСтрок = Объект.Профиль.Количество();
		Если Не КолВоСтрок = 1 Тогда
			МаксНомерПериода = 0;
			// Поиск максимального номера периода.
			Для Каждого ТекСтрока Из Объект.Профиль Цикл
				Если МаксНомерПериода < ТекСтрока.НомерПериода Тогда
					МаксНомерПериода = ТекСтрока.НомерПериода;
				КонецЕсли;
			КонецЦикла;
			// Установим номер периода.
			Элемент.ТекущиеДанные.НомерПериода = МаксНомерПериода + 1;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаРавномерно(Команда)
	
	бит_БюджетированиеКлиент.РавномерныйПрофиль(Объект.Профиль);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаПодбор(Команда)
	
	  ОбъектПодбора = Неопределено;
	  Если ТипЗнч(фОбъектПодбора) = Тип("Структура") Тогда
		  ОбъектПодбора = фОбъектПодбора;
	  Иначе	
		  ПараметрыПодбора = ПодготовитьПараметрыПодбора();
		  Если ПараметрыПодбора.ОбъектыПодбора.Количество() > 0 Тогда
		  	   ОбъектПодбора = ПараметрыПодбора.ОбъектыПодбора[0];
		  КонецЕсли; 
	  КонецЕсли; 
	
	  Если НЕ ОбъектПодбора = Неопределено Тогда
		  
		  ИмяФормыПодбора = ОбъектПодбора.ИмяКласса+"."+ОбъектПодбора.ИмяОбъекта+".ФормаВыбора";
		  
		  ПараметрыФормы = Новый Структура("РежимВыбора, МножественныйВыбор, ЗакрыватьПриВыборе", Истина, Ложь, Ложь);
		  ОткрытьФорму(ИмяФормыПодбора,ПараметрыФормы,ЭтаФорма,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	  КонецЕсли; 
	  
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция АналитикаОбработкаВыбораНаСервере(ВыбранноеЗначение, ТекстСообщения, СтандартнаяОбработка)

	// Проверка на примитивные типы
	МассивТипов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ВыбранноеЗначение, "ТипЗначения").Типы();
	Для каждого ТекущийТип Из МассивТипов Цикл
		Если НЕ ОбщегоНазначения.ЭтоСсылка(ТекущийТип) Тогда
			Разделитель    = ?(ПустаяСтрока(ТекстСообщения), "", Символы.ПС);
			ТекстСообщения = ТекстСообщения + Разделитель + СтрШаблон(НСтр("ru = '- %1'"), ТекущийТип);
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ПустаяСтрока(ТекстСообщения) Тогда
		ТекстСообщения = Нстр("ru = 'Аналитика содежит неподдерживаемые типы:'") + Символы.ПС + ТекстСообщения; 
	КонецЕсли; 
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Процедура УправлениеФормой(Форма)

	Объект 	 = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ЭтоПрофильАналитики = Объект.Вид = ПредопределенноеЗначение("Перечисление.бит_ВидыПрофилейРаспределения.ПоАналитике");
	
	Элементы.Аналитика.Видимость 				= ЭтоПрофильАналитики;
	Элементы.ПрофильЗначениеАналитики.Видимость = ЭтоПрофильАналитики;
	Элементы.ПрофильНомерПериода.Видимость 		= НЕ ЭтоПрофильАналитики;
	Элементы.ПрофильКомандаПодбор.Видимость 	= ЭтоПрофильАналитики;
	
КонецПроцедуры // УправлениеЭлементамиФормы()

// Функция готовит параметры для подбора аналитик.
//
// Возвращаемое значение:
//  РезДанные - Структура.
//
&НаСервере
Функция ПодготовитьПараметрыПодбора()

	РезДанные = Новый Структура;
	РезДанные.Вставить("ОбъектыПодбора", Новый Массив);
	
	Если ЗначениеЗаполнено(Объект.Аналитика) Тогда
		  МассивТипов = Объект.Аналитика.ТипЗначения.Типы();
		  Для каждого ТекТип Из МассивТипов Цикл
		  	   МетаОбъект = Метаданные.НайтиПоТипу(ТекТип);
			   Если НЕ МетаОбъект = Неопределено Тогда
			   	    Имена = бит_ОбщегоНазначенияКлиентСервер.РазобратьПолноеИмяОбъекта(МетаОбъект.ПолноеИмя());
					РезДанные.ОбъектыПодбора.Добавить(Имена);
			   КонецЕсли; 
		  КонецЦикла; 
	КонецЕсли; 

	Возврат РезДанные;
	
КонецФункции // ПодготовитьПараметрыПодбора()

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтрОтбор = Новый Структура;
		СтрОтбор.Вставить("ЗначениеАналитики", ВыбранноеЗначение);
		
		НайденныеСтроки = Объект.Профиль.НайтиСтроки(СтрОтбор);
		
		Если НайденныеСтроки.Количество() > 0 Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Значение %1 уже подобрано.'"), ВыбранноеЗначение);
			ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения); 
		Иначе	
			НоваяСтрока = Объект.Профиль.Добавить();
			НоваяСтрока.ЗначениеАналитики = ВыбранноеЗначение;
			Модифицированность = Истина;
		КонецЕсли; 
	КонецЕсли; 	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОфрмление()

	УсловноеОформление.Элементы.Очистить();

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрофильЗначениеАналитики");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Аналитика", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.бит_ТекстНедоступнойНадписи);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<Не заполнена аналитика>'"));
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

КонецПроцедуры
  
#КонецОбласти