#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(Вид) Тогда
		Вид = Перечисления.бит_ВидыПрофилейРаспределения.ПоПериодам;
	КонецЕсли; 
	Ответственный = бит_УправлениеПользователямиСервер.ЗначениеПоУмолчаниюТиповой("ОсновнойОтветственный");
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если Вид = Перечисления.бит_ВидыПрофилейРаспределения.ПоПериодам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Аналитика");
	КонецЕсли;  
	
	ПроверитьПрофиль(Отказ); 
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);

КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли; 	
	
	бит_ук_СлужебныйСервер.РегистрацияНачалоСобытия(Отказ, ДополнительныеСвойства);
	
	Если Вид = Перечисления.бит_ВидыПрофилейРаспределения.ПоПериодам Тогда
		Аналитика = Неопределено;
		Для каждого СтрокаТаблицы Из Профиль Цикл
			СтрокаТаблицы.ЗначениеАналитики = Неопределено;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()
	
Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияПриЗаписи(Отказ, ДополнительныеСвойства, Метаданные().ПолноеИмя());
		
КонецПроцедуры // ПриЗаписи()

Процедура ПриКопировании(ОбъектКопирования)
	
	Ответственный = бит_УправлениеПользователямиСервер.ЗначениеПоУмолчаниюТиповой("ОсновнойОтветственный");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьПрофиль(Отказ)

	Если Профиль.Итог("Процент") <> 100 Тогда
		ТекстСообщения = Нстр("ru = 'Сумма процентов по профилю должна равняться 100.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,,Отказ); 
	КонецЕсли; 
	
	// Проверка на дубли номеров.
	Если Вид = Перечисления.бит_ВидыПрофилейРаспределения.ПоПериодам Тогда
		
		ШаблонСообщения = НСтр("ru = 'Номера периодов повторяются.'") ;
		Для Каждого ТекСтрока Из Профиль Цикл
			НайденнаяСтрока = Профиль.Найти(ТекСтрока.НомерПериода, "НомерПериода");
			Если НайденнаяСтрока <> Неопределено И ТекСтрока.НомерСтроки <> НайденнаяСтрока.НомерСтроки Тогда
				
				Поле 		   = СтрШаблон("Профиль[%1].НомерПериода", Формат(ТекСтрока.НомерСтроки - 1, "ЧН=0; ЧГ="));
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("КОЛОНКА","КОРРЕКТНОСТЬ",
									Нстр("ru = 'Номер периода'"), ТекСтрока.НомерСтроки, Нстр("ru = 'Профиль'"), ШаблонСообщения);
									
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли; 

КонецПроцедуры
 
#КонецОбласти

#КонецЕсли
