
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("КонтрагентСсылка", КонтрагентСсылка);
	
	Если Не ЗначениеЗаполнено(КонтрагентСсылка) Тогда
		ТекстСообщения = НСтр("ru = 'Форма не может быть открыта для пустого контрагента'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, , , , Отказ);
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.Свойство("КредитныйЛимит", КредитныйЛимит) Тогда
		КредитныйЛимит = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтрагентСсылка, "ок_КредитныйЛимит");
	КонецЕсли;
	
	Если Не Параметры.Свойство("Рейтинг", Рейтинг) Тогда
		Рейтинг = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(КонтрагентСсылка, "ок_Рейтинг");
	КонецЕсли;
	
	Если Параметры.Свойство("ЗаголовокФормы") Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = Параметры.ЗаголовокФормы;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отмена(Команда)
	
	Если Модифицированность Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ОтменаЗавершение", ЭтотОбъект), 
						"Введенные даннные не будут сохранены", 
						РежимДиалогаВопрос.ОКОтмена);
		
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтменаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Успешно = ЗаписатьДанныеНаСервере();
	
	Если Успешно Тогда
		
		Закрыть(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаписатьДанныеНаСервере()
	
	СниматьПривилегированныйРежим = Ложь;
	
	Если Не ПривилегированныйРежим() Тогда
		УстановитьПривилегированныйРежим(Истина);
		СниматьПривилегированныйРежим = Истина;
	КонецЕсли;
	
	Успешно = Ложь;
	
	Попытка
		
		ОбъектКонтрагент = КонтрагентСсылка.ПолучитьОбъект();
		ОбъектКонтрагент.ОбменДанными.Загрузка = Истина;
		
		ОбъектКонтрагент.ок_Рейтинг = Рейтинг;
		ОбъектКонтрагент.ок_КредитныйЛимит = КредитныйЛимит;
		ОбъектКонтрагент.Записать();
		
		Успешно = Истина;
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
	    ОбщегоНазначения.СообщитьПользователю(ИнформацияОбОшибке.Описание);
		
	КонецПопытки;
	
	Если СниматьПривилегированныйРежим Тогда
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
	Возврат Успешно;
	
КонецФункции

#КонецОбласти
