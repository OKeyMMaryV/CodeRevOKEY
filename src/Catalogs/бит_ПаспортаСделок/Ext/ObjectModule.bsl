#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
	
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
		Если ДанныеЗаполнения.Свойство("ДоговорКонтрагента") Тогда
		
			Справочники.бит_ПаспортаСделок.ЗаполнитьОбъектПоДоговоруКонтрагента(ЭтотОбъект);
		КонецЕсли; 
	КонецЕсли;
	
	ВидРегистрацииДоговора = Перечисления.бит_ВидыРегистрацийДоговоров.УникальныйНомерДоговора;
    Ответственный = бит_УправлениеПользователямиСервер.ЗначениеПоУмолчаниюТиповой("ОсновнойОтветственный", бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь"));
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДоговорКонтрагента.ВалютаВзаиморасчетов <> ВалютаДоговора Тогда
		
		ТекстСообщения = Нстр("ru = 'Валюта договора не совпадает с валютой справочника!'");
		бит_ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, "ВалютаДоговора",,Отказ);
	
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.ЭтоНовый Тогда
		Справочники.бит_ПаспортаСделок.УстановитьСостояние(Ссылка, ТекущаяДата(), Перечисления.бит_СостоянияПаспортовСделок.Записан);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
