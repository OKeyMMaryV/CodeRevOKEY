#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЭтоГруппа Тогда
		Возврат;	
	КонецЕсли; 
	
	Если ЭтоНовый() Тогда
		Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
			ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВалютаДокумента) Тогда
		ВалютаДокумента    = Константы.ВалютаРегламентированногоУчета.Получить();
		КурсДокумента 	   = 1;
		КратностьДокумента = 1;	
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ЭтоГруппа Тогда
		Возврат;
	КонецЕсли; 
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если НЕ ФормироватьДвиженияПоБюджету Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Сценарий");
	КонецЕсли; 	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли; 
	
	Если ЭтоГруппа Тогда
		Возврат;	
	КонецЕсли; 
	
	бит_ук_СлужебныйСервер.РегистрацияНачалоСобытия(Отказ, ДополнительныеСвойства); 
	
	Если ПустаяСтрока(Наименование) Тогда
		ПараметрыАвтоНаименования = Справочники.бит_ЭкземплярыОтчетов.НовыеПараметрыСформироватьАвтонаименование();
		ЗаполнитьЗначенияСвойств(ПараметрыАвтоНаименования, ЭтотОбъект);
		Наименование = Справочники.бит_ЭкземплярыОтчетов.СформироватьАвтоНаименование(ПараметрыАвтоНаименования);
	КонецЕсли; 
	
	Если ПометкаУдаления <> Ссылка.ПометкаУдаления Тогда
		
		// Синхронизируем пометку экземпляра и пометку версий.
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ЭкземплярОтчета", Ссылка);
		Запрос.УстановитьПараметр("ПометкаУдаления", ПометкаУдаления);
		
		Запрос.Текст = "ВЫБРАТЬ
		|	бит_ВерсияОтчета.Ссылка
		|ИЗ
		|	Документ.бит_ВерсияОтчета КАК бит_ВерсияОтчета
		|ГДЕ
		|	бит_ВерсияОтчета.ЭкземплярОтчета = &ЭкземплярОтчета
		|	И бит_ВерсияОтчета.ПометкаУдаления <> &ПометкаУдаления";
		
		Результат = Запрос.Выполнить();
		
		Выборка = Результат.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			бит_ОбщегоНазначения.ИзменитьПометкуНаУдалениеУОбъекта(Выборка.Ссылка, ПометкаУдаления,,"Ошибки",Новый Структура("ОшибкуПодробно"));
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()
		
Процедура ПриКопировании(ОбъектКопирования)
	
	АктуальнаяВерсия = Документы.бит_ВерсияОтчета.ПустаяСсылка();
	
КонецПроцедуры // ПриКопировании()
	
#КонецОбласти

#КонецЕсли
