
#Область ОписаниеПеременных

&НаКлиенте
Перем мПредИмя; // Хранит имя переменной до его изменения

&НаКлиенте
Перем мЗначенияПараметров; // Хранит соответствие параметров и их значений для отладки.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПрочитатьТипыПараметров(ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
	
	
	// Вызов механизма защиты
	
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Заполнение параметров
	Если Параметры.Ключ.Пустая() Тогда
		Если ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения) Тогда
			Если Параметры.ЗначенияЗаполнения.Свойство("ПараметрыФункции") Тогда				
				СпрОбъект = РеквизитФормыВЗначение("Объект");
				Для Каждого ТекущийПараметр Из Параметры.ЗначенияЗаполнения.ПараметрыФункции Цикл					
					НовыйПараметр = СпрОбъект.ПараметрыФункции.Добавить();		
					НовыйПараметр.Наименование = ТекущийПараметр.Ключ; 		
					ЗаполнитьЗначенияСвойств(НовыйПараметр, ТекущийПараметр.Значение);		
				КонецЦикла;
				
				ЗначениеВДанныеФормы(СпрОбъект, Объект);
				ПрочитатьТипыПараметров(СпрОбъект);
			Иначе
				ДобавитьПараметры();
			КонецЕсли;
		Иначе
			ДобавитьПараметры(); 	
		КонецЕсли;
	КонецЕсли;
	
	ПолучитьНазваниеПодпрограммыСПараметрамиСервер();
	
	// Установить видимость реквизитов и заголовков колонок.
	УправлениеЭлементамиФормы();	
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	мЗначенияПараметров = Новый Соответствие;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// Сохраним указанные типы параметров в объекте.
	Для Каждого ТекущийПараметр Из Объект.ПараметрыФункции Цикл
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Наименование", ТекущийПараметр.Наименование);
		
		МассивСтрок = ТекущийОбъект.ПараметрыФункции.НайтиСтроки(СтруктураОтбора);
		
		Если НЕ МассивСтрок.Количество() = 0 Тогда
			МассивСтрок[0].ТипПараметра = Новый ХранилищеЗначения(ТекущийПараметр.фТипПараметра);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	ПрочитатьТипыПараметров(ТекущийОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	ПолучитьНазваниеПодпрограммыСПараметрамиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтекстПриИзменении(Элемент)
	
	КонтекстПриИзмененииСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПараметрыФункции

&НаКлиенте
Процедура ПараметрыФункцииПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ПараметрыФункции.ТекущиеДанные;
	
	Если НоваяСтрока Тогда
		ТекущиеДанные.Наименование = "П" + Строка(ТекущиеДанные.НомерСтроки);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыФункцииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ПолучитьНазваниеПодпрограммыСПараметрамиСервер();
	
КонецПроцедуры

&НаКлиенте
Процедура ПараметрыФункцииПослеУдаления(Элемент)
	
	ПолучитьНазваниеПодпрограммыСПараметрамиСервер();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПеременные

&НаКлиенте
Процедура ПеременныеПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Переменные.ТекущиеДанные;
	
	мПредИмя = ТекущиеДанные.Имя;
	
КонецПроцедуры

&НаКлиенте
Процедура ПеременныеИмяПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Переменные.ТекущиеДанные;
	
	Если НЕ ПустаяСтрока(мПредИмя) И НЕ ПустаяСтрока(ТекущиеДанные.Имя) Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПеременныеИмяПриИзмененииЗавершение", ЭтотОбъект, ТекущиеДанные);
		ТекстВопроса = НСтр("ru = 'Выполнить замену в коде имени ""%1%"" на имя ""%2%""?'");
		ТекстВопроса = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстВопроса, мПредИмя, ТекущиеДанные.Имя);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 15, КодВозвратаДиалога.Да);
	
	Иначе
		
		мПредИмя = ТекущиеДанные.Имя;
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура обработчик оповещения "ПеременныеИмяПриИзмененииЗавершение".
// 
// Параметры:
// Ответ - КодВозвратаДиалога.
// ТекущиеДанные - Текущие данные таблицы "Переменные".
// 
&НаКлиенте
Процедура ПеременныеИмяПриИзмененииЗавершение(Ответ, ТекущиеДанные) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Объект.КодПодпрограммы = СтрЗаменить(Объект.КодПодпрограммы, "."+мПредИмя, "."+ТекущиеДанные.Имя);
		
	КонецЕсли; 
	
	мПредИмя = ТекущиеДанные.Имя;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВвестиУсловноеВыражение(Команда)
	
	ОткрытьКонструкторУсловногоВыражения();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПоле(Команда)
	
	ОткрытьФормуВыбораПоля();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОписание(Команда)
	
	Обработчик = Новый ОписаниеОповещения("ДобавитьОписаниеЗавершение", ЭтотОбъект);
	ПоказатьВводСтроки(Обработчик, Объект.Описание, Нстр("ru = 'Описание подпрограммы'"),, Истина);
	
КонецПроцедуры

// Процедура обработчик оповещения "ДобавитьОписаниеЗавершение".
// 
// Параметры:
// Текст - Строка.
// ПараметрыИсточника - Структура.
// 
&НаКлиенте
Процедура ДобавитьОписаниеЗавершение(Текст, ПараметрыИсточника) Экспорт
	
	Если Текст <> Неопределено Тогда
		
		Объект.Описание = Текст;
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьЗапрос(Команда)
	

	#Если ТолстыйКлиентОбычноеПриложение ИЛИ ТолстыйКлиентУправляемоеПриложение Тогда
		КонструкторЗапроса = Новый КонструкторЗапроса;
		
		Если СокрЛП(Элементы.КодПодпрограммы.ВыделенныйТекст) <> "" Тогда
			КонструкторЗапроса.Текст = бит_СтрокиКлиентСервер.УбратьДобавитьПалки(Элементы.КодПодпрограммы.ВыделенныйТекст);
		КонецЕсли;	
		
		Оповещение = Новый ОписаниеОповещения("КонструкторЗапросаЗавершениеРедактирования", ЭтотОбъект);
		КонструкторЗапроса.Показать(Оповещение);		
	#Иначе
		
		ТекстСообщения = НСтр("ru='Работа с конструктором запроса доступна только в толстом клиенте.'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
	#КонецЕсли
	
КонецПроцедуры

// Обработчик завершения закрытия диалога конструктора запроса.
//  См. Синтакс-помощник: КонструкторЗапроса.Показать().
//
&НаКлиенте
Процедура КонструкторЗапросаЗавершениеРедактирования(ИзмененныйТекстЗапроса, ДополнительныеДанные) Экспорт
	
	Если ИзмененныйТекстЗапроса = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.КодПодпрограммы.ВыделенныйТекст = бит_СтрокиКлиентСервер.УбратьДобавитьПалки(ИзмененныйТекстЗапроса, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьПараметры(Команда)
	
	СтруктураТаблиц = Новый Структура;
	УпаковатьТаблицу("ПараметрыФункции",СтруктураТаблиц);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПараметрыФункции", СтруктураТаблиц.ПараметрыФункции);
	ПараметрыФормы.Вставить("ЗначенияПараметров", мЗначенияПараметров);
	
	Обработчик = Новый ОписаниеОповещения("ЗадатьПараметрыЗавершение", ЭтотОбъект);
    Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
    ОткрытьФорму("Справочник.бит_ПользовательскиеФункции.Форма.ФормаПараметров",ПараметрыФормы,ЭтаФорма,,,, Обработчик, Режим);

КонецПроцедуры

// Процедура обработчик оповещения "ЗадатьПараметрыЗавершение".
// 
// Параметры:
// НаборПараметров - Структура.
// Параметры - Структура.
// 
&НаКлиенте
Процедура ЗадатьПараметрыЗавершение(НаборПараметров, Параметры) Экспорт
	
	Если НЕ НаборПараметров = Неопределено Тогда
		мЗначенияПараметров = НаборПараметров;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьКод(Команда)
	
	ВыполнитьКодСервер(мЗначенияПараметров);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура управляет видимостью/доступностью элементов формы.
// 
&НаСервере
Процедура УправлениеЭлементамиФормы()
	
	ТолькоПросмотр = Объект.Предопределенный;
	
	Элементы.ГруппаКоманднаяПанель.Доступность = НЕ Объект.Предопределенный;
	
	флКонтекстЗависимыхОборотов = ?(Объект.Контекст = Перечисления.бит_КонтекстыВыполненияПользовательскихФункций.ЗависимыеОбороты, Истина, Ложь);
	флКонтекстЗагрузкаЭксель    = ?(Объект.Контекст = Перечисления.бит_КонтекстыВыполненияПользовательскихФункций.ЗагрузкаЭксель, Истина, Ложь);
	флКонтекстФинансовыеДоговора= ?(Объект.Контекст = Перечисления.бит_КонтекстыВыполненияПользовательскихФункций.ФинансовыеДоговора, Истина, Ложь);
	
	Элементы.ПараметрыФункции.Доступность = НЕ флКонтекстЗависимыхОборотов И НЕ флКонтекстФинансовыеДоговора;
	
	// При загрузке из Эксель контекст зависит от того в каком правиле пытаемся выполнить загрузку, 
	// в общем случае проверка невозможна.
	Элементы.ВыполнитьКод.Доступность = НЕ флКонтекстЗагрузкаЭксель;
	Элементы.ЗадатьПараметры.Доступность = НЕ флКонтекстЗагрузкаЭксель;
	
КонецПроцедуры

// Процедура добавляет параметры, необходимые для вычисления функции в данном контексте.
// 
&НаСервере
Процедура ДобавитьПараметры()

	Справочники.бит_ПользовательскиеФункции.ЗаполнитьПараметры(Объект); 

	ПолучитьНазваниеПодпрограммыСПараметрамиСервер();
	
КонецПроцедуры // ДобавитьПараметры()

// Процедура устанавливает заголовок функции в соответствии с внесенными изменениями.
// 
&НаСервере
Процедура ПолучитьНазваниеПодпрограммыСПараметрамиСервер()
	
	СтруктураЗаголовка = Справочники.бит_ПользовательскиеФункции.ПолучитьНазваниеПодпрограммыСПараметрами(Объект);
	НазваниеПодпрограммы = СтруктураЗаголовка.НазваниеСПараметрами;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьТипыПараметров(СпрОбъект)
	
	Для Каждого ТекущийПараметр Из СпрОбъект.ПараметрыФункции Цикл
		
		ОписаниеТипаПараметра = ТекущийПараметр.ТипПараметра.Получить(); 
		
		Если ОписаниеТипаПараметра = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("Наименование", ТекущийПараметр.Наименование);
		
		МассивСтрок = Объект.ПараметрыФункции.НайтиСтроки(СтруктураОтбора);
		
		Если НЕ МассивСтрок.Количество() = 0 Тогда
			
			МассивСтрок[0].фТипПараметра = ОписаниеТипаПараметра;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Процедура преобразует реквизит управляемой формы ДанныеФормыКоллекция в массив структур.
// 
// Параметры:
//  СтрТаблиц  - Структура
//  ИмяТаблицы - Строка
// 
&НаСервере
Процедура УпаковатьТаблицу(ИмяТаблицы, СтрТаблиц)
	
	МассивРеквизитов = Объект[ИмяТаблицы].Выгрузить().Колонки;
	ИменаРеквизитов  = Новый Структура;
	Для каждого Реквизит Из МассивРеквизитов Цикл
		
		ИменаРеквизитов.Вставить(Реквизит.Имя,Реквизит.ТипЗначения);
		
	КонецЦикла;
	СтрТаблиц.Вставить(ИмяТаблицы, бит_ОбщегоНазначенияКлиентСервер.УпаковатьДанныеФормыКоллекция(Объект[ИмяТаблицы],ИменаРеквизитов));
	СтрТаблиц.Вставить(ИмяТаблицы+"_Колонки", ИменаРеквизитов);
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКодСервер(ЗначенияПараметров)
	
	СтрПеременные = Новый Структура;
	
	Для Каждого СтрокаТаблицы Из Объект.Переменные Цикл
		
		Если НЕ ПустаяСтрока(СтрокаТаблицы.Имя) И НЕ СтрПеременные.Свойство(СтрокаТаблицы.Имя) Тогда
			
			СтрПеременные.Вставить(СтрокаТаблицы.Имя, СтрокаТаблицы.Значение);
			
		КонецЕсли; 
	
	КонецЦикла; 
	
	// Если контекст зависимые обороты его нужно сэмулировать.
	ЗаполнитьСценарий = Ложь;
	Если Объект.Контекст = ПредопределенноеЗначение("Перечисление.бит_КонтекстыВыполненияПользовательскихФункций.ЗависимыеОбороты") Тогда
		
		ЭмулироватьКонтекстЗависимыхОборотов(ЗначенияПараметров);
		ЗаполнитьСценарий = Истина;
	
	КонецЕсли; 
	
	ТабПараметров = Новый ТаблицаЗначений;
	ТабПараметров.Колонки.Добавить("Номер");
	ТабПараметров.Колонки.Добавить("Параметр");
	ТабПараметров.Колонки.Добавить("Значение");
	
	Ном = 1;
	Для Каждого КлючЗначение Из ЗначенияПараметров Цикл
		НоваяСтрока = ТабПараметров.Добавить();
		НоваяСтрока.Номер = Ном;
		НоваяСтрока.Параметр = КлючЗначение.Ключ;
		НоваяСтрока.Значение = КлючЗначение.Значение;
		
		Ном = Ном + 1;
	КонецЦикла;
	
	// Чтобы текст функции не изменился сформируем его копию.
	ТекстФункции = Строка(Объект.КодПодпрограммы);
	
	// Изменение кода. Начало. 10.05.2018{{
	Если ЗаполнитьСценарий Тогда
		ТекстФункции = СтрЗаменить(ТекстФункции, "Сценарий", "П"+Ном);
		ТекстФункции = СтрЗаменить(ТекстФункции, "Переменные.П"+Ном, "Переменные.Сценарий");
	КонецЕсли; 
	// Изменение кода. Конец. 10.05.2018}}
	
	Результат = бит_ОбщегоНазначения.ВыполнитьПользовательскуюФункциюСПараметрами(ТекстФункции
																					, ТабПараметров
																					, СтрПеременные);  
	
	ОбщегоНазначения.СообщитьПользователю(Строка(Результат)); 
	
КонецПроцедуры

// Процедура создает контекст ЗависимыхОборотов для проверки функции.
// 
&НаСервере
Процедура ЭмулироватьКонтекстЗависимыхОборотов(ЗначенияПараметров)

	СтрНастройкиРасчета = Новый Структура;
	
	МетаРесурсы = Метаданные.РегистрыСведений.бит_ЗависимыеОбороты.Ресурсы;
	
	Для Каждого мета Из МетаРесурсы Цикл
		
		Имя = мета.Имя;
		СтрНастройкиРасчета.Вставить(Имя, мета.Тип.ПривестиЗначение());
		
	КонецЦикла; 
	
	
	СтрИсточник = Новый Структура;
	СтрПриемник = Новый Структура;
	
	Исключения = Новый Массив;
	Исключения.Добавить("КомментарийРасчета");
	
	МетаРеквизиты = Метаданные.Документы.бит_БюджетнаяОперация.ТабличныеЧасти.ЗависимыеОбороты.Реквизиты;
	
	Для каждого метаРеквизит Из МетаРеквизиты Цикл
		
		ИмяРеквизита = метаРеквизит.Имя;
		Если НЕ Исключения.Найти(ИмяРеквизита) = Неопределено Тогда
			
			Продолжить;
			
		КонецЕсли; 
		
		СтрИсточник.Вставить(метаРеквизит.Имя, метаРеквизит.Тип.ПривестиЗначение());
		СтрПриемник.Вставить(метаРеквизит.Имя, метаРеквизит.Тип.ПривестиЗначение());
		
	КонецЦикла; 
	
	ЗначенияПараметров = Новый Соответствие;
	ЗначенияПараметров.Вставить("НастройкаРасчета", СтрНастройкиРасчета);
	ЗначенияПараметров.Вставить("СтрокаИсточник", СтрИсточник);
	ЗначенияПараметров.Вставить("СтрокаПриемник", СтрПриемник);
	
КонецПроцедуры // ЭмулироватьКонтекстЗависимыхОборотов()

// Процедура выполняет действия при изменении контекста выполнения функции.
&НаСервере
Процедура КонтекстПриИзмененииСервер()
	
	УправлениеЭлементамиФормы();
	
	ДобавитьПараметры();
	
КонецПроцедуры

// Функция открывает помошник для выбора поля.
// 
&НаКлиенте
Процедура ОткрытьФормуВыбораПоля()
	
	ПараметрыФормы = Новый Структура;
	
	Если Объект.Контекст = ПредопределенноеЗначение("Перечисление.бит_КонтекстыВыполненияПользовательскихФункций.ЗависимыеОбороты") Тогда
		
		ПараметрыФормы.Вставить("Режим","ФункцияЗависимыхОборотов");
		
	ИначеЕсли Объект.Контекст = ПредопределенноеЗначение("Перечисление.бит_КонтекстыВыполненияПользовательскихФункций.ФинансовыеДоговора") Тогда
		
		ПараметрыФормы.Вставить("Режим","ФункцияФинансовыхДоговоров");	
		
	КонецЕсли; 
	
	МассивИменПеременных = Новый Массив;
	
	Для Каждого ТекСтрока Из Объект.Переменные Цикл
		МассивИменПеременных.Добавить(ТекСтрока.Имя);
	КонецЦикла;
	
	ПараметрыФормы.Вставить("Переменные",МассивИменПеременных);
	
	Обработчик = Новый ОписаниеОповещения("ОткрытьФормуВыбораПоляЗавершение", ЭтотОбъект);
    Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
    ОткрытьФорму("ОбщаяФорма.бит_ФормаВыбораПоляУправляемая",ПараметрыФормы,ЭтаФорма,,,, Обработчик, Режим);
	
КонецПроцедуры // ОткрытьФормуВыбораПоля()

// Процедура обработчик оповещения "ОткрытьФормуВыбораПоляЗавершение".
// 
// Параметры:
// РезСтруктура - Структура.
// Параметры - Структура.
// 
&НаКлиенте
Процедура ОткрытьФормуВыбораПоляЗавершение(РезСтруктура, Параметры) Экспорт
	
	Если ТипЗнч(РезСтруктура) = Тип("Структура") Тогда
		
		Элементы.КодПодпрограммы.ВыделенныйТекст = РезСтруктура.Имя;
		Модифицированность = Истина;
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура открывает форму мастера для ввода условного выражения.
// 
&НаКлиенте
Процедура ОткрытьКонструкторУсловногоВыражения()

	ПараметрыФормы = Новый Структура;
	
	Если Объект.Контекст = ПредопределенноеЗначение("Перечисление.бит_КонтекстыВыполненияПользовательскихФункций.ЗависимыеОбороты") Тогда
		
		ПараметрыФормы.Вставить("Режим","ФункцияЗависимыхОборотов");
		
	КонецЕсли; 
	
	СписокПеременных = Новый СписокЗначений;
	
	Для Каждого Элемент Из Объект.Переменные Цикл
	
		СписокПеременных.Добавить(Элемент.Имя);
	
	КонецЦикла; 
	
	ПараметрыФормы.Вставить("Переменные",СписокПеременных);
	
	Обработчик = Новый ОписаниеОповещения("ОткрытьКонструкторУсловногоВыраженияЗавершение", ЭтотОбъект);
    Режим = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
    ОткрытьФорму("Обработка.бит_КонструкторВыражений.Форма.ФормаУсловныйОператорУправляемая",ПараметрыФормы,ЭтаФорма,,,, Обработчик, Режим);

КонецПроцедуры // ОткрытьКонструкторУсловногоВыражения()

// Процедура обработчик оповещения "ОткрытьКонструкторУсловногоВыраженияЗавершение".
// 
// Параметры:
// РезВыражение - Строка.
// Параметры - Структура.
// 
&НаКлиенте
Процедура ОткрытьКонструкторУсловногоВыраженияЗавершение(РезВыражение, Параметры) Экспорт
	
	Если ТипЗнч(РезВыражение) = Тип("Строка") Тогда
		
		Элементы.КодПодпрограммы.ВыделенныйТекст = РезВыражение;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
