#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область СлужебныйПрограммныйИнтерфейс

// Функция - Сформировать таблицы получателей
//
// Параметры:
//  МассивОповещений - Справочникссылка - Оповещение для которого формируется массив получателей.
// 
// Возвращаемое значение:
//  Соответствие - Оповещение.
//
Функция СформироватьТаблицыПолучателей(МассивОповещений) Экспорт

	РезСоответствие = Новый Соответствие;
	
	МассивПолучателей = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОповещений",МассивОповещений);
	Запрос.УстановитьПараметр("ТипГруппыПользователей",Тип("СправочникСсылка.ГруппыПользователей"));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_фн_ОповещенияПолучатели.Ссылка КАК Оповещение,
	|	бит_фн_ОповещенияПолучатели.Пользователь КАК Получатель,
	|	бит_фн_ОповещенияПолучатели.УсловиеВхождения КАК УсловиеВхождения,
	|	бит_фн_ОповещенияПолучатели.ПользовательскоеУсловие КАК ПользовательскоеУсловие,
	|	ТИПЗНАЧЕНИЯ(бит_фн_ОповещенияПолучатели.Пользователь) КАК ТипЗаписи,
	|	бит_фн_ОповещенияПолучатели.ВидКонтактнойИнформации КАК ВидКонтактнойИнформации,
	|	ВЫБОР
	|		КОГДА бит_фн_ОповещенияПолучатели.ШаблонСообщения = ЗНАЧЕНИЕ(Справочник.бит_фн_ШаблоныСообщений.ПустаяСсылка)
	|			ТОГДА бит_фн_ОповещенияПолучатели.Ссылка.ШаблонСообщения
	|		ИНАЧЕ бит_фн_ОповещенияПолучатели.ШаблонСообщения
	|	КОНЕЦ КАК ШаблонСообщения,
	|	ВЫБОР
	|		КОГДА бит_фн_ОповещенияПолучатели.НастройкаДоставки = ЗНАЧЕНИЕ(Справочник.бит_фн_НастройкиДоставкиОповещений.ПустаяСсылка)
	|			ТОГДА бит_фн_ОповещенияПолучатели.Ссылка.НастройкаДоставки
	|		ИНАЧЕ бит_фн_ОповещенияПолучатели.НастройкаДоставки
	|	КОНЕЦ КАК НастройкаДоставки,
	|	бит_фн_ОповещенияПолучатели.СоздаватьОбсуждение КАК СоздаватьОбсуждение
	|ИЗ
	|	Справочник.бит_фн_Оповещения.Получатели КАК бит_фн_ОповещенияПолучатели
	|ГДЕ
	|	бит_фн_ОповещенияПолучатели.Ссылка В(&МассивОповещений)
	|	И ТИПЗНАЧЕНИЯ(бит_фн_ОповещенияПолучатели.Пользователь) <> &ТипГруппыПользователей
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	бит_фн_ОповещенияПолучатели.Ссылка,
	|	ГруппыПользователейПользователиГруппы.Пользователь,
	|	бит_фн_ОповещенияПолучатели.УсловиеВхождения,
	|	бит_фн_ОповещенияПолучатели.ПользовательскоеУсловие,
	|	ТИПЗНАЧЕНИЯ(ГруппыПользователейПользователиГруппы.Пользователь),
	|	бит_фн_ОповещенияПолучатели.ВидКонтактнойИнформации,
	|	ВЫБОР
	|		КОГДА бит_фн_ОповещенияПолучатели.ШаблонСообщения = ЗНАЧЕНИЕ(Справочник.бит_фн_ШаблоныСообщений.ПустаяСсылка)
	|			ТОГДА бит_фн_ОповещенияПолучатели.Ссылка.ШаблонСообщения
	|		ИНАЧЕ бит_фн_ОповещенияПолучатели.ШаблонСообщения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА бит_фн_ОповещенияПолучатели.НастройкаДоставки = ЗНАЧЕНИЕ(Справочник.бит_фн_НастройкиДоставкиОповещений.ПустаяСсылка)
	|			ТОГДА бит_фн_ОповещенияПолучатели.Ссылка.НастройкаДоставки
	|		ИНАЧЕ бит_фн_ОповещенияПолучатели.НастройкаДоставки
	|	КОНЕЦ,
	|	бит_фн_ОповещенияПолучатели.СоздаватьОбсуждение
	|ИЗ
	|	Справочник.бит_фн_Оповещения.Получатели КАК бит_фн_ОповещенияПолучатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ #ИмяТабЧасти КАК ГруппыПользователейПользователиГруппы
	|		ПО бит_фн_ОповещенияПолучатели.Пользователь = ГруппыПользователейПользователиГруппы.Ссылка
	|ГДЕ
	|	бит_фн_ОповещенияПолучатели.Ссылка В(&МассивОповещений)
	|	И ТИПЗНАЧЕНИЯ(бит_фн_ОповещенияПолучатели.Пользователь) = &ТипГруппыПользователей
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	бит_фн_ОповещенияПолучатели.Ссылка,
	|	ВсеПользователи.Пользователь,
	|	бит_фн_ОповещенияПолучатели.УсловиеВхождения,
	|	бит_фн_ОповещенияПолучатели.ПользовательскоеУсловие,
	|	ТИПЗНАЧЕНИЯ(ВсеПользователи.Пользователь),
	|	бит_фн_ОповещенияПолучатели.ВидКонтактнойИнформации,
	|	ВЫБОР
	|		КОГДА бит_фн_ОповещенияПолучатели.ШаблонСообщения = ЗНАЧЕНИЕ(Справочник.бит_фн_ШаблоныСообщений.ПустаяСсылка)
	|			ТОГДА бит_фн_ОповещенияПолучатели.Ссылка.ШаблонСообщения
	|		ИНАЧЕ бит_фн_ОповещенияПолучатели.ШаблонСообщения
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА бит_фн_ОповещенияПолучатели.НастройкаДоставки = ЗНАЧЕНИЕ(Справочник.бит_фн_НастройкиДоставкиОповещений.ПустаяСсылка)
	|			ТОГДА бит_фн_ОповещенияПолучатели.Ссылка.НастройкаДоставки
	|		ИНАЧЕ бит_фн_ОповещенияПолучатели.НастройкаДоставки
	|	КОНЕЦ,
	|	бит_фн_ОповещенияПолучатели.СоздаватьОбсуждение
	|ИЗ
	|	Справочник.бит_фн_Оповещения.Получатели КАК бит_фн_ОповещенияПолучатели
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи) КАК ГруппаВсеПользователи,
	|			Пользователи.Ссылка КАК Пользователь
	|		ИЗ
	|			Справочник.Пользователи КАК Пользователи
	|		ГДЕ
	|			НЕ Пользователи.Недействителен) КАК ВсеПользователи
	|		ПО бит_фн_ОповещенияПолучатели.Пользователь = ВсеПользователи.ГруппаВсеПользователи
	|ГДЕ
	|	бит_фн_ОповещенияПолучатели.Ссылка В(&МассивОповещений)
	|	И бит_фн_ОповещенияПолучатели.Пользователь = ЗНАЧЕНИЕ(Справочник.ГруппыПользователей.ВсеПользователи)
	|
	|УПОРЯДОЧИТЬ ПО
	|	бит_фн_ОповещенияПолучатели.УсловиеВхождения УБЫВ
	|ИТОГИ ПО
	|	Оповещение";
	
	
	ИмяТабЧасти  = бит_ОбщегоНазначения.ИмяТабЧастиГруппыПользователей();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "#ИмяТабЧасти", "Справочник.ГруппыПользователей." + ИмяТабЧасти);	//Справочник.ГруппыПользователей.%ИмяТабЧасти%
	
	Результат = Запрос.Выполнить();
	
	ВыборкаОповещение = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	Пока ВыборкаОповещение.Следующий() Цикл
		
		Выборка 		  = ВыборкаОповещение.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		ТаблицаПолучатели = НоваяТаблицыПолучателей();
		
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = ТаблицаПолучатели.Добавить();
			НоваяСтрока.Получатель               = Выборка.Получатель;
			НоваяСтрока.ТипЗаписи                = Выборка.ТипЗаписи;
			НоваяСтрока.ВидКонтактнойИнформации  = Выборка.ВидКонтактнойИнформации;
			НоваяСтрока.ШаблонСообщения          = Выборка.ШаблонСообщения;
			НоваяСтрока.НастройкаДоставки        = Выборка.НастройкаДоставки;
			НоваяСтрока.УсловиеВхождения         = Выборка.УсловиеВхождения;
			НоваяСтрока.ПользовательскоеУсловие  = Выборка.ПользовательскоеУсловие;
			НоваяСтрока.СоздаватьОбсуждение  	 = Выборка.СоздаватьОбсуждение;
		КонецЦикла; 
		
		Если ТаблицаПолучатели.Количество() > 0 Тогда
			РезСоответствие.Вставить(ВыборкаОповещение.Оповещение,ТаблицаПолучатели);
		КонецЕсли; 
	КонецЦикла; 

	Возврат РезСоответствие;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция НоваяТаблицыПолучателей()
		
	ТаблицаПолучатели = Новый ТаблицаЗначений;
	ТаблицаПолучатели.Колонки.Добавить("Получатель"             );
	ТаблицаПолучатели.Колонки.Добавить("ТипЗаписи"              ,Новый ОписаниеТипов("Тип"));
	ТаблицаПолучатели.Колонки.Добавить("ВидКонтактнойИнформации",Новый ОписаниеТипов("СправочникСсылка.ВидыКонтактнойИнформации"));
	ТаблицаПолучатели.Колонки.Добавить("ШаблонСообщения"        ,Новый ОписаниеТипов("СправочникСсылка.бит_фн_ШаблоныСообщений"));
	ТаблицаПолучатели.Колонки.Добавить("НастройкаДоставки"      ,Новый ОписаниеТипов("СправочникСсылка.бит_фн_НастройкиДоставкиОповещений"));		  
	ТаблицаПолучатели.Колонки.Добавить("УсловиеВхождения"       ,Новый ОписаниеТипов("Строка"));
	ТаблицаПолучатели.Колонки.Добавить("ПользовательскоеУсловие",Новый ОписаниеТипов("СправочникСсылка.бит_ПользовательскиеУсловия"));
	ТаблицаПолучатели.Колонки.Добавить("СоздаватьОбсуждение",	 Новый Описаниетипов("Булево"));
	Возврат ТаблицаПолучатели;

КонецФункции // СформироватьТаблицыПолучателей()

#КонецОбласти
 
#КонецЕсли
