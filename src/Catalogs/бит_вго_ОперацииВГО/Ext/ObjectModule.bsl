#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат;	
	Иначе
		Если ДанныеЗаполнения.Свойство("Parent") Тогда
			ТекущаяГруппа = ДанныеЗаполнения.Parent;
		ИначеЕсли ДанныеЗаполнения.Свойство("Родитель") Тогда
			ТекущаяГруппа = ДанныеЗаполнения.Родитель;
		Иначе
			Возврат;	
		КонецЕсли;	
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущаяГруппа) Тогда
		
		ВидОперации = ТекущаяГруппа.ВидОперации;
		ТипОперации = ТекущаяГруппа.ТипОперации;
	
	КонецЕсли;
	
КонецПроцедуры // ОбработкаЗаполнения()

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если // Не ЭтоГруппа И
		(ВидОперации = Перечисления.бит_вго_ВидыОперацийВГО.ОперацияПокупателя
			ИЛИ ВидОперации = Перечисления.бит_вго_ВидыОперацийВГО.ОперацияПоставщика) Тогда
		ПроверяемыеРеквизиты.Добавить("ТипОперации"); 
	КонецЕсли;		
		
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		// В случае выполнения обмена данными не производить проверку.
		Возврат;
	КонецЕсли; 
	
	бит_ук_СлужебныйСервер.РегистрацияНачалоСобытия(Отказ, ДополнительныеСвойства);

	Если Не ЭтоНовый() И Не ПометкаУдаления = Ссылка.ПометкаУдаления Тогда
		// В случае установки или снятия пометки удаления не производить проверку.
		Возврат;
	КонецЕсли; 

	Если ЗначениеЗаполнено(Родитель) 
		И (ВидОперации <> Родитель.ВидОперации 
			ИЛИ (ЗначениеЗаполнено(Родитель.ТипОперации) И ТипОперации <> Родитель.ТипОперации)) Тогда
			
		ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(
						Нстр("ru = 'Невозможно записать элемент в группе %1%. Вид операции и тип операции должны совпадать.'")
						, Родитель);
						
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
			
		Отказ = Истина;
	
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда			
		// В случае выполнения обмена данными не производить проверку.
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияПриЗаписи(Отказ, ДополнительныеСвойства, Метаданные().ПолноеИмя());
		
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли
