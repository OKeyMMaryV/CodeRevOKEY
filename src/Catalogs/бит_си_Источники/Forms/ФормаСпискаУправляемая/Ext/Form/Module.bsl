
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.СписокПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма);
	
	ЭтаФорма.Элементы.Список.РежимВыбора = ЭтаФорма.Параметры.РежимВыбора;
	
КонецПроцедуры // ПриСозданииНаСервере(()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПорядокВверх(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИзменитьПорядок(Элементы.Список.ТекущаяСтрока, -1);
	ЭтаФорма.Элементы.Список.Обновить();
КонецПроцедуры // ИзменитьПорядокВверх()

&НаКлиенте
Процедура ИзменитьПорядокВниз(Команда)
	Если Элементы.Список.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ИзменитьПорядок(Элементы.Список.ТекущаяСтрока, 1);
	ЭтаФорма.Элементы.Список.Обновить();
КонецПроцедуры // ИзменитьПорядокВниз()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
// Изменяет порядок источника, переданного в качестве параметра, в указанном направлении.
// 
// Параметры 
// 	ТекСсылка         	 -	СправочникСсылка.бит_си  Источник справочной информации.
// 	Направление        	 -	Число                    Направление изменения порядка: -1 - Вверх; 1 - Вниз.
// 
Процедура ИзменитьПорядок(ТекСсылка, Направление)
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	Источники.Ссылка,
	                      |	Источники.Порядок КАК Порядок
	                      |ИЗ
	                      |	Справочник.бит_си_Источники КАК Источники
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	Порядок,
	                      |	Источники.Наименование");
	Выборка = Запрос.Выполнить().Выбрать();
	Сч = 1;
	ПредСсылка = "";
	Пока Выборка.Следующий() Цикл
		Если (Выборка.Ссылка = ТекСсылка И Направление = -1 И НЕ Сч = 1)
		 ИЛИ (ПредСсылка = ТекСсылка И Направление = 1) Тогда
			ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ТекОбъект.Порядок = Сч - 1;
			ТекОбъект.Записать();
			
			ТекОбъект = ПредСсылка.ПолучитьОбъект();
			ТекОбъект.Порядок = Сч;
			ТекОбъект.Записать();
		ИначеЕсли НЕ Выборка.Порядок = Сч Тогда
			ТекОбъект = Выборка.Ссылка.ПолучитьОбъект();
			ТекОбъект.Порядок = Сч;
			ТекОбъект.Записать();
		КонецЕсли;
		Сч = Сч + 1;
		ПредСсылка = Выборка.Ссылка;
	КонецЦикла;
КонецПроцедуры // ИзменитьПорядок()

#КонецОбласти
