
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);

	Если Не Объект.Ссылка.Пустая() Тогда 
		ПутьКПиктограмме = ПолучитьНавигационнуюСсылку(Объект.Ссылка, "Пиктограмма");
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекПиктограмма = Новый Картинка();
		
	Если  ЗначениеЗаполнено(ПутьКПиктограмме) Тогда
		Попытка
			ТекПиктограмма = Новый Картинка(ПолучитьИзВременногоХранилища(ПутьКПиктограмме));
		Исключение
		КонецПопытки;
	КонецЕсли;
    
    ТекущийОбъект.Пиктограмма = Новый ХранилищеЗначения(ТекПиктограмма);
	
КонецПроцедуры // ПередЗаписьюНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьКартинкуИзВстроенных(Команда)
	
	СписокКартинок = ПолучитьСписокОбщихКартинок();
	Выбор = СписокКартинок.ВыбратьЭлемент();
    
    Если Выбор <> Неопределено Тогда
		ПутьКПиктограмме = ПоместитьВоВременноеХранилище(Выбор.Картинка.ПолучитьДвоичныеДанные(),
														 ЭтаФорма.УникальныйИдентификатор);
		ЭтаФорма.Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры // ВыбратьКартинкуИзВстроенных()

&НаКлиенте
Процедура ВыбратьКартинкуИзФайла(Команда)
	
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.Заголовок = "Выберите файл с изображением";
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	ДиалогОткрытияФайла.ПредварительныйПросмотр = Истина;
	ДиалогОткрытияФайла.Фильтр = ПолучитьФильтрИзображений();
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		ПутьКПиктограмме = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ДиалогОткрытияФайла.ПолноеИмяФайла),
															   ЭтаФорма.УникальныйИдентификатор);
		ЭтаФорма.Модифицированность = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
КонецПроцедуры // ВыбратьКартинкуИзФайла()

&НаКлиенте
Процедура ОчиститьКартинку(Команда)
	
	ПутьКПиктограмме = "";
	ЭтаФорма.Модифицированность = Истина;
	
КонецПроцедуры // ОчиститьКартинку()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Получает строку-фильтр изображений для использования в диалогах открытия/сохранения файлов.
//  
// Возвращаемое значение:
// 	Строка - Строка содержащая фильтр файлов изображений, как он может        
// 	         использоваться в диалогах открытия/сохранения файлов.             
// 
&НаКлиенте
Функция ПолучитьФильтрИзображений()

	Возврат "Все картинки (*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf)"
		  + "|*.bmp;*.dib;*.rle;*.jpg;*.jpeg;*.tif;*.gif;*.png;*.ico;*.wmf;*.emf|" 
	      + "Формат bmp (*.bmp;*.dib;*.rle)|*.bmp;*.dib;*.rle|"
	      + "Формат jpeg (*.jpg;*.jpeg)|*.jpg;*.jpeg|"
	      + "Формат tiff (*.tif)|*.tif|"
	      + "Формат gif (*.gif)|*.gif|"
	      + "Формат png (*.png)|*.png|"
	      + "Формат icon (*.ico)|*.ico|"
	      + "Формат метафайл (*.wmf;*.emf)|*.wmf;*.emf|";

КонецФункции // ПолучитьФильтрИзображений()

// Получает список общих картинок.
//
// Возвращаемое значение:
// 	СписокЗначений - Список общих картинок.
//
&НаСервереБезКонтекста
Функция ПолучитьСписокОбщихКартинок()
    
    СписокКартинок = Новый СписокЗначений();
	Для Каждого ТекКартинка Из Метаданные.ОбщиеКартинки Цикл
		СписокКартинок.Добавить(ТекКартинка.Имя,ТекКартинка.Представление(),,БиблиотекаКартинок[ТекКартинка.Имя]);
    КонецЦикла;
    
	Возврат СписокКартинок;
    
КонецФункции // ПолучитьСписокОбщихКартинок()

#КонецОбласти
