#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	ПарамРБ = бит_РегламентныеЗакрытия.ПолучитьПараметрыРегистраБухгалтерии(РегистрБухгалтерии);
	МаксКоличествоРесурсовНастройки = Справочники.бит_НастройкиЗакрытия.ПолучитьМаксимальноеКоличествоРесурсовНастройки();
	
	// Если это регистр бюджетирования, тогда
	// Проверим заполненность имени показателя ЦФО.	
	Если ПарамРБ.ЭтоРегистрБюдж Тогда
		ПроверяемыеРеквизиты.Добавить("ИмяПоказателяЦФО");
	КонецЕсли;
	
	// Проверка табличной части закрытие
	Для каждого СтрокаИмена Из ИменаРесурсов Цикл
	
		ПроверяемыеРеквизиты.Добавить("Закрытие.ИмяПоказателя"+СтрокаИмена.ИмяРеквизита);
	
	КонецЦикла; 
	
	// Проверим правильность заполнения "Закрытия" по строчно.
	ПроверитьТабличнойЧастиЗакрытиеПоСтрочно(Отказ);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
		
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияНачалоСобытия(Отказ, ДополнительныеСвойства);
		
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли;
	
	// Проверка на идентичность имен в строке
	ПоляТаблицы = Новый Массив;
	ПоляТаблицы.Добавить("ИмяПоказателяВалютнаяСумма");
	ПоляТаблицы.Добавить("ИмяПоказателяСумма1");
	ПоляТаблицы.Добавить("ИмяПоказателяСумма2");
	ПоляТаблицы.Добавить("ИмяПоказателяСумма3");
	ПоляТаблицы.Добавить("ИмяПоказателяКоличество");
	
	Ресурсы = Новый Массив;
	Ресурсы.Добавить("Сумма");
	Ресурсы.Добавить("СуммаРегл");
	Ресурсы.Добавить("СуммаУпр");
	
	Для каждого Строка Из Закрытие Цикл
		Для каждого Значение Из Ресурсы Цикл
			Сч=0;
			Для каждого Имя Из ПоляТаблицы Цикл
				Если Строка[Имя] = Значение Тогда
					Сч=Сч+1;
				КонецЕсли; 
			КонецЦикла; 
			
			Если Сч > 1 Тогда
				ТекстСообщения =  НСтр("ru = 'Присутствуют дублируемые значения полей имен.'");
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , ,Отказ); 
				Прервать;
			КонецЕсли; 
		КонецЦикла;
		
		Если Сч > 1 Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
	// Проверка на наличие суммы в строках
	Для каждого СтрТаб Из Закрытие Цикл
		
		Если СтрТаб.ВидЗакрытия = Перечисления.бит_ВидыЗакрытия.Простое Тогда
			Продолжить;
		КонецЕсли; 
		
		Тыц=0;
		Для каждого тСтр Из ПоляТаблицы Цикл
			Если СтрТаб[тСтр] = "Сумма" Тогда
				Прервать;
			Иначе	
				Тыц=Тыц+1;
			КонецЕсли;	
		КонецЦикла; 
		
		Если Тыц=ПоляТаблицы.Количество() Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'В строке № %1 нет ни одного значения с именем ""Сумма"".'"),
								СтрТаб.НомерСтроки);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , ,Отказ); 
			Прервать;
		КонецЕсли; 
		
		Если Тыц=ПоляТаблицы.Количество() Тогда
			Прервать;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура проверяет по строчно правильность заполнения строк табличной части "Закрытие".
// 
// Параметры: 
//  Отказ	  - флаг отказа в проведении.
// 
Процедура ПроверитьТабличнойЧастиЗакрытиеПоСтрочно(Отказ)

	СтрокиПростогоЗакрытия = Закрытие.НайтиСтроки(Новый Структура("ВидЗакрытия", Перечисления.бит_ВидыЗакрытия.Простое));
	
	Для каждого СтрокаТЧ Из Закрытие Цикл

		СтрИндекс = Формат(СтрокаТЧ.НомерСтроки-1,"ЧН=0; ЧГ="); 
		Если СтрокаТЧ.ВидЗакрытия = Перечисления.бит_ВидыЗакрытия.Простое И НЕ ЗначениеЗаполнено(СтрокаТЧ.СчетЗакрытия) Тогда
			
			СтрокаСообщения =  НСтр("ru = 'В строке номер ""%1%"" табличной части ""Закрытие"" не заполнено значение реквизита ""Счет закрытия"".'");
			СтрокаСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(СтрокаСообщения, СтрокаТЧ.НомерСтроки);
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(СтрокаСообщения, ЭтотОбъект, "Закрытие["+СтрИндекс+"].СчетЗакрытия");
			Отказ = Истина;
			
		ИначеЕсли СтрокаТЧ.ВидЗакрытия = Перечисления.бит_ВидыЗакрытия.Распределение И НЕ ЗначениеЗаполнено(СтрокаТЧ.База) Тогда	
			
			СтрокаСообщения =  НСтр("ru = 'В строке номер ""%1%"" табличной части ""Закрытие"" не заполнено заполнено значение реквизита ""База"".'");
			СтрокаСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(СтрокаСообщения, СтрокаТЧ.НомерСтроки);
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(СтрокаСообщения, ЭтотОбъект, "Закрытие["+СтрИндекс+"].База");
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЦикла; // Закрытие
	
КонецПроцедуры // ПроверитьТабличнойЧастиЗакрытиеПоСтрочно()

#КонецОбласти

#КонецЕсли
