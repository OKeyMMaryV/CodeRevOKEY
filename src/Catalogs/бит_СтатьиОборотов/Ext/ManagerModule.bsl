#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Функция - получает настройки статьи оборотов.
//
// Параметры:
//  СтатьяОборотов	 - СправочникСсылкабит_СтатьиОборотов - статья.
// 
// Возвращаемое значение:
//  Настройки - Структура.
//
Функция ПолучитьНастройки(СтатьяОборотов) Экспорт
	
	МетаОбъект = Метаданные.Справочники.бит_СтатьиОборотов;
	
	Настройки = Новый Структура;
	Настройки.Вставить("Измерения", Новый Структура);
	Настройки.Вставить("Учет_Сумма", Ложь);
	Настройки.Вставить("Учет_Количество", Ложь);
	Настройки.Вставить("ЭтоКоэффициент", Ложь);
	Настройки.Вставить("СтавкаНДС", Неопределено);
	
	Если ЗначениеЗаполнено(СтатьяОборотов) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка", СтатьяОборотов);
		Запрос.Текст =  
		"ВЫБРАТЬ
		|	*
		|ИЗ
		|	Справочник.бит_СтатьиОборотов КАК бит_СтатьиОборотов
		|ГДЕ
		|	бит_СтатьиОборотов.Ссылка = &Ссылка";
		
		Результат = Запрос.Выполнить();
		Выборка = Результат.Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			Настройки.Учет_Сумма 	  = Выборка.Учет_Сумма;
			Настройки.Учет_Количество = Выборка.Учет_Количество;
			Настройки.СтавкаНДС 	  = Выборка.СтавкаНДС;
			
			НастройкиИзмерений = бит_Бюджетирование.НастройкиИзмеренийБюджетирования();		
			Для каждого КиЗ Из НастройкиИзмерений Цикл
				
				ИмяАналитики 	   = КиЗ.Ключ;
				НастройкиАналитики = НовыеНастройкиАналитики(ИмяАналитики);
				
				НастройкиАналитики.Учет 		= ЗначениеИзВыборки("Учет_" + ИмяАналитики, Ложь, Результат, Выборка);
				НастройкиАналитики.Обязательный = ЗначениеИзВыборки("Обязательный_" + ИмяАналитики, Ложь, Результат, Выборка);
				ИмяТипаПоУмолчанию 		        = ЗначениеИзВыборки("ИмяТипаПоУмолчанию_" + ИмяАналитики, "", Результат, Выборка); 
				
				Если ПустаяСтрока(ИмяТипаПоУмолчанию) Тогда
					ИмяТипа   = Новый Массив(); 
					Настройка = КиЗ.Значение;
					Если ТипЗнч(Настройка) = Тип("Структура") И Настройка.ЗначениеПоУмолчанию <> Неопределено Тогда
						ИмяТипа.Добавить(ТипЗнч(Настройка.ЗначениеПоУмолчанию));
					КонецЕсли; 
				Иначе					
					ИмяТипа = СтрЗаменить(НастройкиАналитики.ИмяТипаПоУмолчанию,".","Ссылка.");
				КонецЕсли; 
				
				НастройкиАналитики.ИмяТипаПоУмолчанию = ИмяТипаПоУмолчанию;
				НастройкиАналитики.ТипПоУмолчанию 	= Новый ОписаниеТипов(ИмяТипа);
				
				Настройки.Измерения.Вставить(ИмяАналитики, НастройкиАналитики);
				
			КонецЦикла; // по измерениям
		КонецЕсли; 
	КонецЕсли; 
	
	Возврат Настройки;
	
КонецФункции // ПолучитьНастройки()
 
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

//  Процедура заполняет поля предопределенных статей оборотов.  
//
Процедура НастроитьПредопределенныеСтатьиОборотов() Экспорт

	// ++ БП 
	Совместимо = Истина;
	// -- БП 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗначениеИзВыборки(Поле, ЗначениеПоУмолчанию, ПоляВыборки, Выборка)

	Если НЕ ПоляВыборки.Колонки.Найти(Поле) = Неопределено Тогда
		ЗначениеИзВыборки = Выборка[Поле];
	Иначе
		ЗначениеИзВыборки = ЗначениеПоУмолчанию;
	КонецЕсли; 	

	Возврат ЗначениеИзВыборки;
	
КонецФункции // ЗначениеИзВыборки()

Функция НовыеНастройкиАналитики(ИмяАналитики)
	
	НастройкиАналитики = Новый Структура; 
	НастройкиАналитики.Вставить("Имя",				  ИмяАналитики);
	НастройкиАналитики.Вставить("Учет",				  Ложь);
	НастройкиАналитики.Вставить("Обязательный",		  Ложь);
	НастройкиАналитики.Вставить("ИмяТипаПоУмолчанию", "");
	НастройкиАналитики.Вставить("ТипПоУмолчанию", 	  Неопределено);

	Возврат НастройкиАналитики;
	
КонецФункции

#КонецОбласти

//{ bit SVKushnirenko Bit 02.03.2017 #2701
Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	//ОК+ Аверьянова 13.12.17 АТ-1620043 --бухгалтеру форма с отбором открываться не должна
	//Если РольДоступна("Guest") Тогда
	 Если РольДоступна("Guest") и Не РольДоступна("ДобавлениеИзменениеДанныхБухгалтерии") Тогда
     //ОК-
		
		СтандартнаяОбработка = Ложь;
		ВыбраннаяФорма = "бит_БК_ФормаВыбораGuest";
	КонецЕсли;
	
КонецПроцедуры
//} bit SVKushnirenko Bit 02.03.2017 #2701

#КонецЕсли
