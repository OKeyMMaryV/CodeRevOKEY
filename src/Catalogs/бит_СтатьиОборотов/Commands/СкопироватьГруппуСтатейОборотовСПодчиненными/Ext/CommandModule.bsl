#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	КопироватьНаСервере(ПараметрКоманды);
	ОповеститьОбИзменении(Тип("СправочникСсылка.бит_СтатьиОборотов"));
	
КонецПроцедуры

&НаСервере
Процедура КопироватьНаСервере(ЭлементСправочника)
	
	ТаблицаСсылок = Новый ТаблицаЗначений(); 
	ТаблицаСсылок.Колонки.Добавить("ЧтоЗаменить");
	ТаблицаСсылок.Колонки.Добавить("НаЧтоЗаменить");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ЭлементСправочника);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_СтатьиОборотов.Ссылка КАК Ссылка,
	|	бит_СтатьиОборотов.Родитель,
	|	бит_СтатьиОборотов.Ссылка = &Ссылка КАК ЭтоИсточник
	|ИЗ
	|	Справочник.бит_СтатьиОборотов КАК бит_СтатьиОборотов
	|ГДЕ
	|	бит_СтатьиОборотов.Ссылка В ИЕРАРХИИ(&Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка ИЕРАРХИЯ";
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка 		 = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Значение = Выборка.Ссылка.Скопировать();
		
		Родитель = Выборка.Родитель;
		Если НЕ Выборка.ЭтоИсточник Тогда
			ПолучитьСсылку(Родитель, ТаблицаСсылок);
		КонецЕсли; 
		
		Ссылка = Выборка.Ссылка;
		ПолучитьСсылку(Ссылка, ТаблицаСсылок);

		Значение.Родитель = Родитель;
		Значение.УстановитьСсылкуНового(Ссылка);
		Значение.УстановитьНовыйКод();
		Значение.ОбменДанными.Загрузка = Истина;
		
		Значение.Записать();
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСсылку(ЧтоЗаменить, ТаблицаСсылок)
		
	СтрокаСсылка = ТаблицаСсылок.Найти(ЧтоЗаменить,"ЧтоЗаменить");
	Если СтрокаСсылка = Неопределено Тогда
		НаЧтоЗаменить = Справочники.бит_СтатьиОборотов.ПолучитьСсылку(Новый УникальныйИдентификатор());
		НоваяСтрока = ТаблицаСсылок.Добавить();
		НоваяСтрока.ЧтоЗаменить   = ЧтоЗаменить;
		НоваяСтрока.НаЧтоЗаменить = НаЧтоЗаменить;
		ЧтоЗаменить = НаЧтоЗаменить;
	Иначе
		ЧтоЗаменить = СтрокаСсылка.НаЧтоЗаменить;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
