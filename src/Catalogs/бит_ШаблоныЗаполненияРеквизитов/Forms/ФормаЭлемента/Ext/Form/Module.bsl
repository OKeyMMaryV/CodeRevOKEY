
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект, Объект);
		 	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ЭтоНовый = Параметры.Ключ.Пустая();
	Если ЭтоНовый И Не ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		Объект.ТекстШаблона = "{{ПорядковыйНомер}}";
	КонецЕсли;
	
	// Временные значения по умолчанию
	ОбъектСистемыПоУмолчанию = Справочники.бит_ОбъектыСистемы.НайтиПоРеквизиту("ИмяОбъектаПолное", "Документ.бит_ПроектДоговора", Справочники.бит_ОбъектыСистемы.Документы);
	Если ОбъектСистемыПоУмолчанию <> Неопределено Тогда
		Объект.ОбъектСистемы = ОбъектСистемыПоУмолчанию;	
	КонецЕсли;
	Объект.ИмяРеквизита = "НомерДоговора";
		
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПроверитьДоступностьИзмененияШаблона();
	
КонецПроцедуры // ПриЧтенииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВставитьШаблон(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ОбъектСистемы", Объект.ОбъектСистемы);
	ПараметрыФормы.Вставить("ШаблонСсылка" , Объект.Ссылка);
	ПараметрыФормы.Вставить("ТипШаблона"   , Объект.ТипШаблона);
	
	Оповещение = Новый ОписаниеОповещения("ВыборШаблонаЗавершение", ЭтотОбъект);
    ОткрытьФорму("Справочник.бит_ШаблоныЗаполненияРеквизитов.Форма.ФормаШаблона", ПараметрыФормы,,,,,Оповещение);
	
КонецПроцедуры // ВставитьШаблон()

// Процедура обработчик оповещения "ВыборШаблонаЗавершение".
// 
// Параметры:
// СтрокаРезультат - Произвольный.
// ДополнительныеДанные - Структура.
// 
&НаКлиенте 
Процедура ВыборШаблонаЗавершение(СтрокаРезультат, ДополнительныеДанные) Экспорт

	Если СтрокаРезультат <> Неопределено Тогда
		
		Элементы.ТекстШаблона.ВыделенныйТекст = СтрокаРезультат;
		Модифицированность 				= Истина;
		ОбновитьОтображениеДанных();
		
	КонецЕсли;                               
	
КонецПроцедуры	// ВыборШаблонаЗавершение

&НаКлиенте
Процедура ПроверитьШаблон(Команда)
	
	Если Модифицированность Тогда
		ТекстСообщения = Нстр("ru = 'Шаблон не записан. Проверка не выполнена.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли; 
	
	Результат = ПроверитьШаблонСервер(Объект.Ссылка);	
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(Результат);		 
	
КонецПроцедуры // ПроверитьШаблон()

&НаКлиенте
Процедура НазначитьШаблон(Команда)
	
	Если Модифицированность Тогда
		ТекстСообщения = Нстр("ru = 'Сохраните шаблон перед назначением.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Шаблон", Объект.Ссылка);
	ОткрытьФорму("РегистрСведений.бит_НазначениеШаблоновЗаполненияРеквизитов.ФормаЗаписи", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПроверитьДоступностьИзмененияШаблона()

	Если Объект.ТипШаблона = Перечисления.бит_ТипыШаблоновЗаполненияРеквизитов.Номер Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Шаблон", Объект.Ссылка);
		Запрос.Текст = "
		|ВЫБРАТЬ ПЕРВЫЕ 1
		|	НазначениеШаблонов.Шаблон
		|ИЗ
		|	РегистрСведений.бит_НазначениеШаблоновЗаполненияРеквизитов КАК НазначениеШаблонов
		|ГДЕ
		|	НазначениеШаблонов.Шаблон = &Шаблон
		|";
		Результат = Запрос.Выполнить();
		// РедактированиеДоступно = Результат.Пустой();
		
		Если Не Результат.Пустой() Тогда
			
			Элементы.ТипШаблона.ТолькоПросмотр    = Истина; 			
			Элементы.ОбъектСистемы.ТолькоПросмотр = Истина;
			Элементы.ИмяРеквизита.ТолькоПросмотр  = Истина;
		    Элементы.Периодичность.ТолькоПросмотр = Истина;
			Элементы.ТекстШаблона.ТолькоПросмотр  = Истина;
			
			Элементы.ГруппаЗапрещеноИзменение.Видимость = Истина;
			
		КонецЕсли;
				
	КонецЕсли;
		
КонецПроцедуры // ПроверитьДоступностьИзмененияШаблона()

&НаСервереБезКонтекста
Функция ПроверитьШаблонСервер(ШаблонСсылка)
	
	Результат = Справочники.бит_ШаблоныЗаполненияРеквизитов.ПроверитьШаблон(ШаблонСсылка);
	
	Возврат Результат;		 
	
КонецФункции // ПроверитьШаблонСервер()

#КонецОбласти

