#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ЭтотОбъект.Алгоритм = "Итог";
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда 
		
		
		Если ДанныеЗаполнения.Свойство("Вид") 
		     И ДанныеЗаполнения.Свойство("Владелец") Тогда
		
		  ЭтотОбъект.КодСортировки = Справочники.бит_мпд_ЭлементыТаблицПроизвольныхОтчетов.МаксимальныйКодСортировки(ДанныеЗаполнения.Владелец, ДанныеЗаполнения.Вид)+10;
	    КонецЕсли;
	
	    Если ДанныеЗаполнения.Свойство("ВидЯчейки") Тогда
		
			 ЭтотОбъект.ВидЯчейки = ДанныеЗаполнения.ВидЯчейки;
		
		КонецЕсли; 
		
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ВидЯчейки) Тогда
		
		ЭтотОбъект.ВидЯчейки = Перечисления.бит_ВидыЯчеекПроизвольногоОтчета.Значение;
		
	КонецЕсли; 

	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда			
		// В случае выполнения обмена данными не производить проверку.
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияНачалоСобытия(Отказ, ЭтотОбъект.ДополнительныеСвойства); 
	
	Если НЕ ЭтотОбъект.ПометкаУдаления = Ссылка.ПометкаУдаления Тогда	
		Возврат; 	
	КонецЕсли; 
	
	Если ЭтоНовый() Тогда
		
		// Проверим, что добавление элемента не приведет к пересечению таблиц.
		МассивОписаний = Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПолучитьТаблицыОтчета(ЭтотОбъект.Владелец);
		
		// Скорректируем описание текущей таблицы с учетом внесенных изменений.
		Сч = 0;
		Для каждого ОписаниеТаблицы Из МассивОписаний Цикл
			
			Если ОписаниеТаблицы.Таблица = ЭтотОбъект.Таблица Тогда
				
				ТекШирина = ОписаниеТаблицы.Ширина;
				ТекВысота = ОписаниеТаблицы.Высота;
				
				Если ЭтотОбъект.Вид = Перечисления.бит_мпд_ВидыЭлементовТаблицПроизвольныхОтчетов.Колонка Тогда
					
					ТекШирина = ТекШирина + 1;
					
				КонецЕсли; 
				
				Если ЭтотОбъект.Вид = Перечисления.бит_мпд_ВидыЭлементовТаблицПроизвольныхОтчетов.Строка Тогда
					
					ТекВысота = ТекВысота + 1;
					
				КонецЕсли; 
				
				ОписаниеТекТаблицы = Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПодготовитьОписаниеТаблицы(ЭтотОбъект.Таблица, ТекШирина, ТекВысота);
				МассивОписаний[Сч] = ОписаниеТекТаблицы;
				Прервать;
				
			КонецЕсли; 
			
			Сч = Сч + 1;
		КонецЦикла; 
		
		Если Справочники.бит_мпд_ТаблицыПроизвольныхОтчетов.ПроверитьПересечения(МассивОписаний, Истина) Тогда
			
			ТекстСообщения =  НСтр("ru = 'Запись изменений невозможна из-за пересечения таблиц!'");
			бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения,,,Отказ);
			
		КонецЕсли; 
		
	КонецЕсли; // ЭтоНовый()
	
КонецПроцедуры // ПередЗаписью()
	
Процедура ПриЗаписи(Отказ)
		
	Если ОбменДанными.Загрузка Тогда			
		// В случае выполнения обмена данными не производить проверку.
		Возврат;			
	КонецЕсли; 
		
	бит_ук_СлужебныйСервер.РегистрацияПриЗаписи(Отказ, ЭтотОбъект.ДополнительныеСвойства, Метаданные().ПолноеИмя());
		
КонецПроцедуры // ПриЗаписи()

#КонецОбласти
	
#КонецЕсли
