
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДата());	
	
	МетаданныеОбъекта = Метаданные.Задачи.бит_уп_Задача;
	
	// Добавление кнопки "Раскрасить по статусам"
	// Также добавляются процедуры: К-РаскраситьПоСтатусам(), С-ОформитьСписокДокументовПоСтатусам().
	бит_РаботаСДиалогамиСервер.ДобавитьКнопкуРаскраситьПоСтатусам(Элементы, Команды, КоманднаяПанель,
																  МетаданныеОбъекта);
	ОформитьСписокДокументовПоСтатусам(Элементы.РаскраситьПоСтатусам.Пометка, Истина);
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыСписок

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("СтруктураЗадачи", ПолучитьПараметрыОтбораСписка());
	
	ОткрытьФорму("Задача.бит_уп_Задача.Форма.ФормаСозданияЗадачиУправляемая", ПараметрыФормы, ЭтаФорма, Истина);
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура назначается динамически действию кнопки командной панели 
// КоманднаяПанель.РаскраситьПоСтатусам
// (обработчик события "Нажатие" кнопки "РаскраситьПоСтатусам").
// 
&НаКлиенте
Процедура Подключаемый_РаскраситьПоСтатусам()
	
	Элементы.РаскраситьПоСтатусам.Пометка = Не Элементы.РаскраситьПоСтатусам.Пометка;
	ОформитьСписокДокументовПоСтатусам(Элементы.РаскраситьПоСтатусам.Пометка);	
		
КонецПроцедуры // РаскраситьПоСтатусам()

&НаКлиенте
Процедура КомандаЗадачаНаОсновании(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		
		РежимОкна  = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;				
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ЗадачаОснование", ТекущаяСтрока.Ссылка);
		ОткрытьФорму("Задача.бит_уп_Задача.Форма.ФормаСозданияЗадачиУправляемая",ПараметрыФормы, ЭтаФорма,,,,, РежимОкна);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОткрытьИсториюИзменений(Команда)
	
	ТекущаяСтрока = Элементы.Список.ТекущиеДанные;
	
	Если НЕ ТекущаяСтрока = Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Задача", ТекущаяСтрока.Ссылка);
		ОткрытьФорму("РегистрСведений.бит_ИсторияИзмененияСтатусовОбъектов.Форма.ФормаИсторияСостоянийЗадач",ПараметрыФормы, ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры
     
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура обновляет оформление списка документов по статусам.
// 
// Параметры:
//  ТолькоОчистить - Булево 
// 
&НаСервере
Процедура ОформитьСписокДокументовПоСтатусам(ПометкаКн, ЭтоОткрытие = Ложь)

	Если ЭтоОткрытие И Не ПометкаКн Тогда
		Возврат;	
	КонецЕсли;

	Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата", ТекущаяДата());	
	
	МетаданныеОбъекта = Метаданные.Задачи.бит_уп_Задача;
	
	МасОбъектов = ?(ПометкаКн, бит_РаботаСДиалогамиСервер.ПолучитьМассивОбъектов(МетаданныеОбъекта), Новый Массив);
	бит_РаботаСДиалогамиСервер.ОформитьСписокДокументовПоСтатусам(МасОбъектов, ПометкаКн, ЭтаФорма.УсловноеОформление, "Список", "Состояние");
	
	Если Не ЭтоОткрытие Тогда
		// Сохранение значения пометки
		РегистрыСведений.бит_СохраненныеЗначения.СохранитьЗнч(бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь")
																	,МетаданныеОбъекта
																	,"РаскраситьПоСтатусам_Пометка"
																	,ПометкаКн);
	КонецЕсли;
															
КонецПроцедуры // ОформитьСписокДокументовПоСтатусам()

// Функция получает необходимые элементы отбора в списке.
// 
// Параметры:
// 	нет
// 
// Возвращаемое значение:
// 	СтруктураОтбора - структура
// 
&НаСервере
Функция ПолучитьПараметрыОтбораСписка()
	
	СтруктураОтбора = Новый Структура;
	
	ОтборСписка = ЭтаФорма.Список.Отбор;
	
	ПолеПредмет = Новый ПолеКомпоновкиДанных("Предмет");
	
	Для Каждого ТекЭлемент Из ОтборСписка.Элементы Цикл
		
		Если ТекЭлемент.ЛевоеЗначение = ПолеПредмет Тогда
			СтруктураОтбора.Вставить("Предмет", ТекЭлемент.ПравоеЗначение);
			
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктураОтбора;
	
КонецФункции

#КонецОбласти
