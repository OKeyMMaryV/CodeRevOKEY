
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КлючВарианта", "Основной");
	
	ФормаОтчета = ПолучитьФорму("Отчет.бит_КонтрольныеЗначенияБюджетов.Форма"
									,ПараметрыФормы
									,ПараметрыВыполненияКоманды.Источник
									,ПараметрыВыполненияКоманды.Источник.УникальныйИдентификатор
									,ПараметрыВыполненияКоманды.Окно);
				
	ФормаОтчета.Отчет.Период.ДатаНачала	   = Дата("00010101");
	ФормаОтчета.Отчет.Период.ДатаОкончания = Дата("00010101");
				
	КомпоновщикНастроек   = ФормаОтчета.Отчет.КомпоновщикНастроек;
	СтруктураПараметров   = ПолучитьПараметрыВОтчет(ПараметрКоманды, КомпоновщикНастроек);
	НастройкиКомпоновщика = СтруктураПараметров.НастройкиКомпоновщика;
	НастройкиКомпоновщика.Отбор.Элементы.Очистить();
	
	бит_ОбщегоНазначенияКлиентСервер.УстановитьОтборУСписка(НастройкиКомпоновщика.Отбор, 
	                                                         Новый ПолеКомпоновкиДанных("Сценарий"), 
															 СтруктураПараметров.Сценарий);
															 
	бит_ОбщегоНазначенияКлиентСервер.УстановитьОтборУСписка(НастройкиКомпоновщика.Отбор, 
															 Новый ПолеКомпоновкиДанных("СтатьяОборотов"), 
															 СтруктураПараметров.СтатьиОборотов, 
															 ВидСравненияКомпоновкиДанных.ВСписке);
															
	бит_ОбщегоНазначенияКлиентСервер.УстановитьОтборУСписка(НастройкиКомпоновщика.Отбор, 
	                                                         Новый ПолеКомпоновкиДанных("РежимКонтроля"), 
															 СтруктураПараметров.РежимКонтроля);
	
	ФормаОтчета.Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиКомпоновщика);
	ФормаОтчета.Открыть();
	ФормаОтчета.СкомпоноватьРезультат();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПолучитьПараметрыВОтчет(ПараметрКоманды, КомпоновщикНастроек)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ПараметрКоманды);
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	ГрафикПлатежей.СтатьяОборотов КАК СтатьяОборотов
		|ИЗ
		|	Документ.бит_ДополнительныеУсловияПоДоговору.ГрафикПлатежей КАК ГрафикПлатежей
		|ГДЕ
		|	ГрафикПлатежей.Ссылка = &Ссылка
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	ГрафикНачислений.СтатьяОборотовНачисления
		|ИЗ
		|	Документ.бит_ДополнительныеУсловияПоДоговору.ГрафикНачислений КАК ГрафикНачислений
		|ГДЕ
		|	ГрафикНачислений.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДополнительныеУсловияПоДоговору.Сценарий КАК Сценарий
		|ИЗ
		|	Документ.бит_ДополнительныеУсловияПоДоговору КАК ДополнительныеУсловияПоДоговору
		|ГДЕ
		|	ДополнительныеУсловияПоДоговору.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	Выборка = РезультатЗапроса[0].Выбрать();
	
	СписокСтатей = Новый СписокЗначений;
	Пока Выборка.Следующий() Цикл
		СписокСтатей.Добавить(Выборка.СтатьяОборотов)
	КонецЦикла;
	
	ВыборкаСценарий = РезультатЗапроса[1].Выбрать();
	ВыборкаСценарий.Следующий();
	
	Структура = Новый Структура;
	Структура.Вставить("Сценарий",              ВыборкаСценарий.Сценарий);
	Структура.Вставить("НастройкиКомпоновщика", КомпоновщикНастроек.ПолучитьНастройки());
	Структура.Вставить("СтатьиОборотов",        СписокСтатей);
	Структура.Вставить("РежимКонтроля",         Перечисления.бит_РежимыКонтроляПоБюджету.ГрафикиДоговоров);
	
	Возврат Структура;
	
КонецФункции

#КонецОбласти


