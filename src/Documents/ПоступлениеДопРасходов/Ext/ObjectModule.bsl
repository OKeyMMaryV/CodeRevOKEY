#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Признак для передачи в счет-фактуру для того, чтобы не устанавливать статус повторно.
Перем УстановленСтатусДокумента;

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура")
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения);
	Иначе
		СуммаВключаетНДС = Ложь;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения, Истина);
	
	Если ПустаяСтрока(Содержание) Тогда
		Содержание	= НСтр("ru = 'Доп. расходы'");
	КонецЕсли;

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	НомерВходящегоДокумента = "";
	ДатаВходящегоДокумента  = '00010101';
	
	ЗачетАвансов.Очистить();
	
	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
	ВалютаДокумента, Дата);
	
	КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;

	//Начало_бит_Магомедов_02.08.2013 
	бит_ДокументыСобственныхКонтрагентов.Очистить();
	//Конец_бит_Магомедов_02.08.2013
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-11-01 (#3111)
	//Если Дата >= Дата('20190101') Тогда 
	//	СтавкаИзменилась = Ложь;
	//	Если СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18") Тогда 
	//		СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
	//		СтавкаИзменилась = Истина;
	//	ИначеЕсли СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118") Тогда 
	//		СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120");
	//		СтавкаИзменилась = Истина;
	//	КонецЕсли;
	//	Если СтавкаИзменилась Тогда 
	//		Для каждого СтрокаТаблицы Из Товары Цикл
	//			СтрокаТаблицы.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
	//														СтрокаТаблицы.Сумма,
	//														СуммаВключаетНДС,
	//														УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС));
	//		КонецЦикла;
	//		
	//		СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
	//														Сумма,
	//														СуммаВключаетНДС,
	//														УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС));

	//	КонецЕсли;
	//КонецЕсли;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-11-01 (#3111)
	
	//ОКЕЙ Вдовиченко Г.В(СофтЛаб) Начало 2019-08-05 (#3364)
	ОчищатьДанныеФВБ = ок_ОбменСКонтрагентамиВнутренний.ПолучитьПризнакОчисткиДанныхФВБВУчетномДокументеПриКопировании(ОбъектКопирования);
	//ОКЕЙ Вдовиченко Г.В(СофтЛаб) Конец 2019-08-05 (#3364)
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-08-13 (#3067)
	Для Каждого СтрТовары Из Товары Цикл
		
		//ОКЕЙ Вдовиченко Г.В(СофтЛаб) Начало 2019-08-05 (#3364)
		Если Не ОчищатьДанныеФВБ Тогда
			Продолжить;
		КонецЕсли;	
		//ОКЕЙ Вдовиченко Г.В(СофтЛаб) Конец 2019-08-05 (#3364)
	
		СтрТовары.ок_Период 			= Неопределено;	
		СтрТовары.ок_ЦФО 				= Неопределено;	
		СтрТовары.ок_СтатьяОборотов 	= Неопределено;	
		СтрТовары.ок_Проект	 			= Неопределено;			
		СтрТовары.ок_Аналитика_2 		= Неопределено;			
		
	КонецЦикла;	
	ОК_ID_Разноска = Неопределено;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-08-13 (#3067)
	
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2019-10-19 (#3443)
	ок_ВыгруженВAXAPTA = Ложь;
	ок_ВыгруженВAXAPTA_Актуальный = Ложь;
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2019-10-19 (#3443)
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	Если УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата) Тогда
		НДСВключенВСтоимость = Ложь;
	КонецЕсли;
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);

	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);
	
	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект, "СчетФактураПолученный");
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	Документы.КорректировкаПоступления.ОбновитьРеквизитыСвязанныхДокументовКорректировки(ЭтотОбъект, Отказ);
	
	Если НЕ УчетнаяПолитика.ПлательщикНДФЛ(Организация, Дата) Тогда
		Для каждого СтрокаТовары Из Товары Цикл
			Если ЗначениеЗаполнено(СтрокаТовары.СчетУчетаНУ) 
				И НЕ Документы.ПоступлениеДопРасходов.СвойстваСчетаНУ(СтрокаТовары.СчетУчетаНУ).СтатьяЗатратНУДоступность Тогда
				
				СтрокаТовары.СтатьяЗатратНУ = Неопределено;
			КонецЕсли; 
		КонецЦикла; 
	КонецЕсли; 

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСтатусДокумента();
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		УчетНДСПереопределяемый.СинхронизироватьРеквизитыСчетаФактурыПолученного(ЭтотОбъект, НЕ УстановленСтатусДокумента);
	КонецЕсли;
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-19 (#3049)
	Если Не Отказ
		И ДополнительныеСвойства.Свойство("ок_ФВБ")
		И ДополнительныеСвойства.Свойство("Аналитика")
		Тогда
		бит_БК_Общий.ПроверитьИДобавитьЗаписьПоАналитике1(Ссылка,ДополнительныеСвойства.Аналитика,ДополнительныеСвойства.ок_ФВБ);
	КонецЕсли;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-19 (#3049)	
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата);

	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если НЕ ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если Товары.Итог("Сумма") <> 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Сумма");
	КонецЕсли;
	Если Сумма = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("СпособРаспределения");
		МассивНепроверяемыхРеквизитов.Добавить("СтавкаНДС");
	КонецЕсли;

	ОтражатьВНалоговомУчете = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Организация, Дата);

	// Проверка заполнения табличной части "Товары"
	Если СпособРаспределения = Перечисления.СпособыРаспределенияДопРасходов.ПоСумме Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СуммаТовара");
	КонецЕсли;

	
	// Табличная часть "Зачет авансов"
	Если СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	ИначеЕсли ЗачетАвансов.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки с документом аванса!'");
		Поле = "ПорядокУчетаРасчетов";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , Поле, Отказ);
	КонецЕсли;
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-25 (#3049)
	Если НЕ ДополнительныеСвойства.Свойство("ок_ФВБ") ИЛИ
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-24 (#3192)
		//ЗначениеЗаполнено(ДополнительныеСвойства.ок_ФВБ)
		//Заменено на:
		НЕ ЗначениеЗаполнено(ДополнительныеСвойства.ок_ФВБ)
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-24 (#3192)
		Тогда
		
		//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-09-17 (#3758)
		//пСтрокаПроверяемыхРеквизитов = "Товары.ок_Период;Товары.ок_ЦФО;Товары.ок_СтатьяОборотов;Товары.ок_Проект;Товары.ок_Аналитика_2;";
		пСтрокаПроверяемыхРеквизитов = "Товары.ок_Период;Товары.ок_ЦФО;Товары.ок_Проект;Товары.ок_Аналитика_2;";
		//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-09-17 (#3758)
		пНепроверяемыеАналитики = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(пСтрокаПроверяемыхРеквизитов,";");
		Для Каждого пЭлНепроверяемый Из пНепроверяемыеАналитики Цикл
			МассивНепроверяемыхРеквизитов.Добавить(пЭлНепроверяемый);
		КонецЦикла;
		
	КонецЕсли;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-25 (#3049)	
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	РеквизитыЗаСсылками = Документы.ПоступлениеДопРасходов.РеквизитыЗаСсылками();
	СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);

	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2019-10-19 (#3443)
	ОК_ОбщегоНазначения.ВыполнитьПроверкуОднозначногоСоответствияСтатьиОборотовИСтавкиНДСКСчету(ЭтотОбъект, ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы"), Отказ);
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2019-10-19 (#3443)
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ПоступлениеДопРасходов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ (АНАЛИЗ ОСТАТКОВ И Т.П.)

	// Таблица взаиморасчетов
	ТаблицаВзаиморасчетов = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ЗачетАвансовТаблицаАвансов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);

	// Распределение суммы доп.расхода:
	ДопРасходыТаблица = Документы.ПоступлениеДопРасходов.РаспределениеСуммыДопРасходов(
		ПараметрыПроведения.ДопРасходыРеквизиты,
		ПараметрыПроведения.ДопРасходыТаблица);
	ТаблицаРасходовУСН = Документы.ПоступлениеДопРасходов.РаспределениеСуммыДопРасходовУСН(
		ПараметрыПроведения.ДопРасходыРеквизиты,
		ПараметрыПроведения.ПоступлениеРасходовУСНТаблицаРасходов);

	СтруктураТаблицДокумента = Новый Структура;
	СтруктураТаблицДокумента.Вставить("ТаблицаТовары", ДопРасходыТаблица);
	
	// Таблицы документа с корректировкой сумм по курсу авансов
	СтруктураТаблицДокумента = УчетДоходовРасходов.ПодготовитьТаблицыПоступленияПоКурсуАвансов(
		СтруктураТаблицДокумента,
		ТаблицаВзаиморасчетов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты);

	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура("ТаблицаРасчетов", ТаблицаВзаиморасчетов);
	
	// Учет доходов и расходов ИП
	// Распределение суммы доп.расхода:
	ТаблицаРасходовИП = Документы.ПоступлениеДопРасходов.РаспределениеСуммыДопРасходов(
		ПараметрыПроведения.ПоступлениеМПЗИПРеквизиты,
		ПараметрыПроведения.ПоступлениеМПЗИПТаблицаТоваров,
		Истина);
	
	ТаблицаТоваровИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ТаблицаРасходовИП,
		ПараметрыПроведения.ПоступлениеМПЗИПРеквизиты);
		
	ТаблицаОстатковОСНМАИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОстатковОСНМА(
		ТаблицаТоваровИП,
		ПараметрыПроведения.ПоступлениеМПЗИПРеквизиты);
		
	СтруктураТаблицМПЗ = Новый Структура("ТаблицаТоваров", ТаблицаТоваровИП);
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ

	// Зачет аванса
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(
		ТаблицаВзаиморасчетов,
		ПараметрыПроведения.ЗачетАвансовРеквизиты,
		Движения, Отказ);

	// Увеличение стоимости товаров
	УчетТоваров.СформироватьДвиженияИзменениеСебестоимостиОстатковТоваров(
		СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.ДопРасходыРеквизиты, Движения, Отказ);
		
	// Учет НДС
	УчетНДС.СформироватьДвиженияПоступлениеДопРасходовОтПоставщика(
		СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.ДопРасходыРеквизиты,
		Движения, Отказ);
		
	УчетНДСРаздельный.СформироватьДвиженияПоступлениеДопРасходовОтПоставщика(
		СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.ДопРасходыРеквизиты,
		Движения, Отказ);
		
	//Регистр "Рублевые суммы документов в валюте"
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаТовары, 
		ПараметрыПроведения.ДопРасходыРеквизиты, Движения, Отказ);	

	// УСН
	СуммаСторноРасхода = 0;
	УчетУСН.ПоступлениеРасходовУСН(ТаблицаРасходовУСН, 
		ПараметрыПроведения.ПоступлениеРасходовУСНРеквизиты, СуммаСторноРасхода, Движения, Отказ);

	Если НЕ Отказ И Движения.РасходыПриУСН.Количество()>0 Тогда
		Движения.РасходыПриУСН.Записать(Истина);
		Движения.РасходыПриУСН.Записывать = Ложь;
	КонецЕсли; 
	
	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);
	
	// Учет доходов и расходов ИП
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеМПЗ(СтруктураТаблицМПЗ,
		ТаблицаВзаиморасчетов, ТаблицаОстатковОСНМАИП, ПараметрыПроведения.ПоступлениеМПЗИПРеквизиты, Движения, Отказ);
	
	// Переоценка валютных остатков - после формирования проводок всеми другими механизмами
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.Реквизиты,
		Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(
		ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты,
		Движения, Отказ);
		
	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(
		ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты,
		Движения, Отказ);

	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);
		
	// Регистрация в последовательности
	РаботаСПоследовательностями.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение, ПараметрыПроведения.ТаблицаОприходованныеТовары);

	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
		
	//1c-izhtc alena 07.08.2015(
	//Движения.Записать();
	//
	//ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");	
	//УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ, НЕ УстановленСтатусДокумента);
	//1c-izhtc alena 07.08.2015)
		
	//БИТ Тртилек 23072013 выполним проверку по связанным документам
	Если УстановитьВидимостьРеализацияУСобственногоЮЛ() Тогда
		ИтоговаяСумма = бит_ДокументыСобственныхКонтрагентов.Итог("бит_СуммаБезНДС");
		
		Для Каждого СвязанныйДокумент Из бит_ДокументыСобственныхКонтрагентов Цикл
			
			Если СвязанныйДокумент.ДатаДокументаРеализацииПередачи > Дата Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен не буден, т.к. у связанного документа " + СвязанныйДокумент.ДокументРеализацииПередачи + " дата больше даты данного документа");
				Отказ = Истина;	
				Возврат;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СвязанныйДокумент.бит_СуммаБезНДС) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен не будет, т.к. в реализации у собственного юрлица в строке с документом: " + СвязанныйДокумент.ДокументРеализацииПередачи + " не заполнена сумма без НДС");
				Отказ = Истина;	
				Возврат;
			КонецЕсли;
			
		КонецЦикла;
		
		Если НЕ ИтоговаяСумма = СуммаДокумента - УчетНДСПереопределяемый.ПолучитьНДСДокумента(ЭтотОбъект) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен не буден, т.к. сумма по связанным документам не равна сумме документа");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	///БИТ Тртилек	
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-23 (#3192)
	//ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");	
	//УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ, НЕ УстановленСтатусДокумента);	
	Если ДополнительныеСвойства.Свойство("ок_ФВБ") Тогда 
		Если НЕ ДополнительныеСвойства.Свойство("ДополнительныеАналитики") Тогда 
			ДополнительныеСвойства.Вставить("ДополнительныеАналитики", Новый Соответствие);
		КонецЕсли;
		
		ДополнительныеСвойства.ДополнительныеАналитики.Вставить("NЗАЯВКИ", ДополнительныеСвойства.ок_ФВБ);
		
	КонецЕсли;		
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-23 (#3192)
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-18 (#3049)
	Если ПараметрыПроведения.Свойство("ДопРасходыТаблица") И 
		 ПараметрыПроведения.ДопРасходыТаблица<>Неопределено И
		 ПараметрыПроведения.ДопРасходыТаблица.Количество()>0
	Тогда 
		
		стрТаблицыДляПроведенияПоБК = Новый Структура;
		
		пИменаКолонокТаблиц = "Период,НомерСтроки,ЦФО,СтатьяОборотов,ВидСтатьи,Проект,Аналитика_2,СуммаРуб,СуммаНДСРуб";
		
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-28 (#3192)
		пИменаКолонокТаблиц = пИменаКолонокТаблиц + ",СчетУчета";
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-28 (#3192)
		
		пРасчетыВВалюте = ПараметрыПроведения.Реквизиты[0].ВалютаВзаиморасчетов<>ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		
		Если пРасчетыВВалюте Тогда 
			пИменаКолонокТаблиц = пИменаКолонокТаблиц + ",СуммаВзаиморасчетов,СуммаНДСВзаиморасчетов";
		КонецЕсли;
				
		Если ПараметрыПроведения.ДопРасходыТаблица.Колонки.Найти("Ссылка") = Неопределено И 
			 ПараметрыПроведения.ДопРасходыТаблица.Колонки.Найти("Регистратор") <> Неопределено
		Тогда 
			пИменаКолонокТаблиц = пИменаКолонокТаблиц + ",Регистратор";
		Иначе
			пИменаКолонокТаблиц = пИменаКолонокТаблиц + ",Ссылка";
		КонецЕсли;
		
		пТаблицаТовары = ПараметрыПроведения.ДопРасходыТаблица.Скопировать(,пИменаКолонокТаблиц);
		
		пКолонкаРегистратор = пТаблицаТовары.Колонки.Найти("Регистратор");
		Если пКолонкаРегистратор <> Неопределено Тогда 
			пКолонкаРегистратор.Имя = "Ссылка";
		КонецЕсли;
		
		стрТаблицыДляПроведенияПоБК.Вставить("ТаблицаТовары", пТаблицаТовары);
			
		Если НЕ пРасчетыВВалюте Тогда 
			Для Каждого КлючЗнчТаб Из стрТаблицыДляПроведенияПоБК Цикл 
				пТаблица = КлючЗнчТаб.Значение;
				пТаблица.Колонки.Добавить("СуммаВзаиморасчетов"	  , Новый ОписаниеТипов("Число"));
				пТаблица.Колонки.Добавить("СуммаНДСВзаиморасчетов", Новый ОписаниеТипов("Число"));
				Для Каждого СтрТаб Из пТаблица Цикл 
					СтрТаб.СуммаВзаиморасчетов	  = СтрТаб.СуммаРуб;
					СтрТаб.СуммаНДСВзаиморасчетов = СтрТаб.СуммаНДСРуб;
				КонецЦикла;				
			КонецЦикла;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ТаблицыДокумента", стрТаблицыДляПроведенияПоБК);			
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-28 (#3192)
		стрТаблицыДляПроведенияПоБК.ТаблицаТовары.Колонки.СчетУчета.Имя = "Счет";
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-28 (#3192)
		
	КонецЕсли;	
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-23 (#3192)
	//Перенесено выше
	//Если ДополнительныеСвойства.Свойство("ок_ФВБ") Тогда 
	//	Если НЕ ДополнительныеСвойства.Свойство("ДополнительныеАналитики") Тогда 
	//		ДополнительныеСвойства.Вставить("ДополнительныеАналитики", Новый Соответствие);
	//	КонецЕсли;
	//	
	//	ДополнительныеСвойства.ДополнительныеАналитики.Вставить("NЗАЯВКИ", ДополнительныеСвойства.ок_ФВБ);
	//	
	//КонецЕсли;		
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-23 (#3192)	
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-18 (#3049)
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");	
	ПараметрыДействия.СостояниеФлага = Ложь;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПоДокументуОснованию(ДанныеЗаполнения)

	ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ПоступлениеТоваровУслуг") Тогда
		ЗаполнитьПоПоступлению(ДанныеЗаполнения);
	КонецЕсли;

КонецПроцедуры

Процедура ЗаполнитьПоПоступлению(ДанныеЗаполнения) Экспорт

	ВалютаРеглУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ДокументОснование", ДанныеЗаполнения);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Док.Ссылка КАК Ссылка,
	|	Док.Организация КАК Организация,
	|	Док.ПодразделениеОрганизации КАК ПодразделениеОрганизации,
	|	Док.Склад КАК Склад,
	|	Док.Контрагент КАК Контрагент,
	|	Док.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Док.ВалютаДокумента КАК ВалютаДокумента,
	|	Док.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Док.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	Док.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Док.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	Док.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	Док.СчетУчетаРасчетовПоТаре КАК СчетУчетаРасчетовПоТаре
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК Док
	|ГДЕ
	|	Док.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерТабличнойЧасти,
	|	Док.НомерСтроки КАК НомерСтроки,
	|	Док.Номенклатура КАК Номенклатура,
	|	Док.Ссылка КАК Ссылка,
	|	Док.Сумма КАК Сумма,
	|	Док.СуммаНДС КАК СуммаНДС,
	|	Док.Количество КАК Количество,
	|	Док.СчетУчета КАК СчетУчета,
	|	Док.СчетУчетаНДС КАК СчетУчетаНДС,
	|	Док.СпособУчетаНДС КАК СпособУчетаНДС,
	|	Док.ОтражениеВУСН КАК ОтражениеВУСН
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-18 (#3049)
	|	,Док.ок_Период,
	|	Док.ок_СтатьяОборотов,
	|	Док.ок_ЦФО,
	|	Док.ок_Проект,
	|	Док.ок_Аналитика_2
	//ОКЕЙ Док В.И.(СофтЛаб) Конец 2018-07-18 (#3049)	
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК Док
	|		ЛЕВОЕ СОЕДИНЕНИЕ ПланСчетов.Хозрасчетный КАК СчетУчетаПоСтроке
	|		ПО Док.СчетУчета = СчетУчетаПоСтроке.Ссылка
	|ГДЕ
	|	Док.Ссылка = &ДокументОснование
	|	И НЕ ЕСТЬNULL(СчетУчетаПоСтроке.Забалансовый, ЛОЖЬ)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	Док.НомерСтроки,
	|	Док.Номенклатура,
	|	Док.Ссылка,
	|	Док.Сумма,
	|	Док.СуммаНДС,
	|	Док.Количество,
	|	Док.СчетУчета,
	|	Док.СчетУчетаНДС,
	|	Док.СпособУчетаНДС,
	|	ЗНАЧЕНИЕ(Перечисление.ОтражениеВУСН.НеПринимаются)
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-18 (#3049)
	|	,Док.ок_Период,
	|	Док.ок_СтатьяОборотов,
	|	Док.ок_ЦФО,
	|	Док.ок_Проект,
	|	Док.ок_Аналитика_2
	//ОКЕЙ Док В.И.(СофтЛаб) Конец 2018-07-18 (#3049)	
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Оборудование КАК Док
	|ГДЕ
	|	Док.Ссылка = &ДокументОснование
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерТабличнойЧасти,
	|	Док.НомерСтроки";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	ВыборкаШапки = РезультатЗапроса[0].Выбрать();
	
	Если ВыборкаШапки.Следующий() Тогда
		
		Если ВалютаДокумента <> ВалютаРеглУчета Тогда
			КурсИКратность          = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДокумента, Дата);
			КурсВзаиморасчетов      = КурсИКратность.Курс;
			КратностьВзаиморасчетов = КурсИКратность.Кратность;
		КонецЕсли;
		
		Если ДоговорКонтрагента = ВыборкаШапки.ДоговорКонтрагента Тогда
			СчетУчетаРасчетовСКонтрагентом 	= ВыборкаШапки.СчетУчетаРасчетовСКонтрагентом;
			СчетУчетаРасчетовПоАвансам 		= ВыборкаШапки.СчетУчетаРасчетовПоАвансам;
			Если НДСНеВыделять Тогда
				СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
			Иначе
				СтавкаНДС = Справочники.ДоговорыКонтрагентов.СтавкаНДСПоДоговору(Дата, ВыборкаШапки.ДоговорКонтрагента);
			КонецЕсли;
		КонецЕсли;
			
		Выборка = РезультатЗапроса[1].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			СтрокаТабличнойЧасти = Товары.Добавить();
			
			СтрокаТабличнойЧасти.Номенклатура   = Выборка.Номенклатура;
			СтрокаТабличнойЧасти.Количество     = Выборка.Количество;
			СтрокаТабличнойЧасти.ДокументПартии = ДанныеЗаполнения;
			СтрокаТабличнойЧасти.СуммаТовара    = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				Выборка.Сумма + ?(НЕ ВыборкаШапки.СуммаВключаетНДС, Выборка.СуммаНДС, 0),
				ВыборкаШапки.ВалютаДокумента,
				ВалютаДокумента,
				ВыборкаШапки.КурсВзаиморасчетов,
				КурсВзаиморасчетов,
				ВыборкаШапки.КратностьВзаиморасчетов,
				КратностьВзаиморасчетов);
			
			СтрокаТабличнойЧасти.СчетУчета      = Выборка.СчетУчета;
			СтрокаТабличнойЧасти.СчетУчетаНУ    = Выборка.СчетУчета;
			СтрокаТабличнойЧасти.СпособУчетаНДС = Выборка.СпособУчетаНДС;
			СтрокаТабличнойЧасти.ОтражениеВУСН  = Выборка.ОтражениеВУСН;
			
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-18 (#3049)
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти,Выборка,"ок_Период,ок_СтатьяОборотов,ок_ЦФО,ок_Проект,ок_Аналитика_2");
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-18 (#3049)
			
		КонецЦикла;
		
	КонецЕсли; 

КонецПроцедуры

Процедура УстановитьСтатусДокумента()
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("СтатусДокумента")
		И УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруПолученный(ЭтотОбъект.Ссылка) <> Неопределено Тогда
		// Записываем из списка документов. Движения по статусам сформирует счет-фактура.
		Возврат;
	КонецЕсли;
	
	Статусы = Новый Структура("СтатусДокумента, СтатусСчетаФактуры");
	ЗаполнитьЗначенияСвойств(Статусы, ДополнительныеСвойства);
	
	Если Статусы.СтатусСчетаФактуры = Неопределено Или ПометкаУдаления Тогда
		// При записи из формы не определили статус счета-фактуры. Рассчитаем его по реквизитам документа.
		Статусы.СтатусСчетаФактуры = СтатусСчетаФактурыПоУмолчанию();
	КонецЕсли;
	
	УстановленСтатусДокумента = Истина;
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ЭтотОбъект.Ссылка, Статусы.СтатусДокумента, , Статусы.СтатусСчетаФактуры);
	
КонецПроцедуры

Функция СтатусСчетаФактурыПоУмолчанию()
	
	Если НДСНеВыделять
		Или Не Справочники.ДоговорыКонтрагентов.ТребуетсяСчетФактураПолученный(ДоговорКонтрагента) Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	ИначеЕсли НЕ РаботаСДоговорамиКонтрагентовБП.ДокументСНДС(ЭтотОбъект.Ссылка) Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	КонецЕсли;
	
	Возврат Перечисления.СтатусыСчетаФактуры.Отсутствует;
	
КонецФункции

#КонецОбласти

//БИТ Тртилек 12022013 в процедуре устанавливается видимость вкладки собственных реализаций в зависимости от контрагента 
Функция УстановитьВидимостьРеализацияУСобственногоЮЛ() Экспорт
	
	ВидимостьВкладкиРеализацияУСобственногоЮЛ = ЛОЖЬ;
	
	Отбор = Новый Структура("Контрагент, ВидСвязи");
	Отбор.Контрагент = Контрагент;
	Отбор.ВидСвязи = Перечисления.бит_ВидыСобственныхКонтрагентов.Организация;
	НаборЗаписейАналитики = бит_ОбщегоНазначения.ПрочитатьНаборЗаписейРегистраСведений(Отбор,"бит_СобственныеКонтрагенты");
	
	//МассивОрганизаций = НаборЗаписейАналитики.ВыгрузитьКолонку("Объект");
	
	//Если НЕ МассивОрганизаций.Найти(Организация) = Неопределено Тогда
	Если НаборЗаписейАналитики.Количество() > 0 Тогда
		
		
		Если Товары.Количество() > 0 И ЗначениеЗаполнено(Товары[0].СчетУчета) Тогда
			Если бит_ОбщегоНазначения.ОбъектПодчинен(Товары[0].СчетУчета, ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке) ИЛИ бит_ОбщегоНазначения.ОбъектПодчинен(Товары[0].СчетУчета, ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы) Тогда
				ВидимостьВкладкиРеализацияУСобственногоЮЛ = Истина;	
			КонецЕсли;
		КонецЕсли;
		
		//Если ОбъектыСтроительства.Количество() > 0 И ЗначениеЗаполнено(ОбъектыСтроительства[0].СчетУчета) Тогда
		//	Если бит_ОбщегоНазначения.ОбъектПодчинен(ОбъектыСтроительства[0].СчетУчета, ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке) ИЛИ бит_ОбщегоНазначения.ОбъектПодчинен(ОбъектыСтроительства[0].СчетУчета, ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы) Тогда
		//		ВидимостьВкладкиРеализацияУСобственногоЮЛ = Истина;	
		//	КонецЕсли;
		//КонецЕсли;
		//
	КонецЕсли;	
	
	Возврат ВидимостьВкладкиРеализацияУСобственногоЮЛ;
	
КонецФункции

#Область Инициализация

УстановленСтатусДокумента = Ложь;

#КонецОбласти

#КонецЕсли