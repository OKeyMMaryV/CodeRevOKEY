
#Область ОписаниеПеременных

&НаКлиенте 
Перем мКэшРеквизитовФормы; // Хранит текущие значения реквизитов формы. Клиент.

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
    
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект, Объект);
	
	фКоличествоСубконтоМУ = 4;
		
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Заполняем виды операций для группы подменю.
	Для каждого ЭлементЗаполнения Из Объект.ВидОперации.Метаданные().ЗначенияПеречисления Цикл
		
		ИмяЗначенияПеречисления = ЭлементЗаполнения.Имя;
		ЗначениеПеречисления 	= Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам[ИмяЗначенияПеречисления];
		
		ИмяКнопки = ИмяЗначенияПеречисления;
		
		ИмяКоманды = "Команда_" + ИмяКнопки;
		
		// Добавляем новую команду обработки выбора вида операции.
		КомандаУстановитьОперацию = ЭтотОбъект.Команды.Добавить(ИмяКоманды);
		КомандаУстановитьОперацию.Действие = "ДействияФормыДействиеУстановитьОперацию";
		
		КнопкаГруппыПодменю = ЭтотОбъект.Элементы.Добавить(ИмяКнопки, Тип("КнопкаФормы"), ЭтотОбъект.Элементы.ПодменюВидаОперации);
		КнопкаГруппыПодменю.Вид		   = ВидКнопкиФормы.КнопкаКоманднойПанели;
		КнопкаГруппыПодменю.ИмяКоманды = ИмяКоманды;
		КнопкаГруппыПодменю.Заголовок  = ЭлементЗаполнения.Синоним;
			
	КонецЦикла;
	
	// Заполняем кэш значений.
	ЗаполнитьКэшЗначений(фКэшЗначений);
		
	// Установить видимость реквизитов и заголовков колонок.	
	УправлениеЭлементамиФормыСервер();
	
	// Заполним валюту МСФО.
	ЗаполнитьВалютуМСФО();
	
	Если Параметры.Ключ.Пустая() Тогда
		УстановитьСостояниеДокумента();
	КонецЕсли;
	
	бит_РаботаСДиалогамиСервер.УстановитьФормуВыбораДоговораКонтрагентаУппКа(Элементы);
	
	УстановитьПараметрыВыбораДляСчетов();
	
	// Установка параметров выбора для Договоров.
	бит_РаботаСДиалогамиСервер.УстановитьСвязиПараметровВыбораДоговора(Элементы.ДанныеРаспределенияДоговорКонтрагента, "Объект.ДанныеРаспределения.Контрагент");

КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ВидОперации) Тогда

			Объект.ВидОперации = Неопределено;
			СформироватьДеревоВидовОпераций();
			
			Если Не бит_УправлениеПользователямиСервер.ЗначениеПоУмолчаниюТиповой("ЗапрашиватьВидОперацииПриВводеНового") Тогда
				
				ПриОткрытииЭтоНовыйОкончание(Неопределено, Отказ);
				
			Иначе 
			
				Оповещение = Новый ОписаниеОповещения("ВыборВидаОперацииОкончание", ЭтотОбъект); 
				
				ПараметрыФормы = Новый Структура;
				ПараметрыФормы.Вставить("СсылкаДокумента"    , Объект.Ссылка);
				ПараметрыФормы.Вставить("ДеревоВидовОпераций", фДеревоВидовОпераций);
				ОткрытьФорму("ОбщаяФорма.бит_ФормаВыбораВидаОперацииИзДереваУправляемая", ПараметрыФормы, ЭтотОбъект,,,,Оповещение,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
				
			КонецЕсли;	
				
		Иначе
			
			ПриОткрытииПромежуточная(Отказ);
			ПриОткрытииОкончание();
			
		КонецЕсли;
						
	Иначе
		
		ПриОткрытииОкончание();
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	УстановитьЗаголовокФормыДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	УстановитьСостояниеДокумента();
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "Проведениебит_му_РаспределениеПлатежейПоКредитамЗаймам";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если Объект.ДанныеРаспределения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Табличная часть «Договоры» будет очищена. Продолжить?'");
	ОповещениеВопрос = Новый ОписаниеОповещения("ВопросДатаПриИзменении", ЭтотОбъект);
	
	ПоказатьВопрос(ОповещениеВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);
		
КонецПроцедуры

// Обработка оповещения вопроса пользователю. 
// 
// Параметры:
//  Ответ - Строка.
// 
&НаКлиенте
Процедура ВопросДатаПриИзменении(Ответ, ДопПараметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
	
	    Объект.ДанныеРаспределения.Очистить();

		ДобавитьВКэш("Дата");
		
		ИзменениеВалютыКлиент();
		
	Иначе
		
		ИзвлечьИзКэша("Дата");
	КонецЕсли;

КонецПроцедуры // ВопросДатаПриИзменении()

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ВалютаДокументаЗаполнена = ?(ЗначениеЗаполнено(Объект.ВалютаДокумента)
								,Истина
								,Ложь);
	
	ИзменениеОрганизацииСервер();
	
	Если Не ВалютаДокументаЗаполнена
		И ЗначениеЗаполнено(Объект.ВалютаДокумента) Тогда
		
		ДобавитьВКэш("ВалютаДокумента");
		ДобавитьВКэш("КратностьДокумента");
		ДобавитьВКэш("КурсДокумента");
		
	КонецЕсли;
	
	ОрганизацияОбъекта = Объект.Организация;
	ТаблицаОбъекта = Объект.ДанныеРаспределения;
	
	Если ТаблицаОбъекта.Количество() > 0 Тогда
					  
		ТекстВопроса = НСтр("ru = 'Табличная часть «Договоры» будет очищена. Продолжить?'");
		ОповешениеВопрос = Новый ОписаниеОповещения("ВопросОрганизацияПриИзменении", ЭтотОбъект, ТаблицаОбъекта);
		
		ПоказатьВопрос(ОповешениеВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);
				
	КонецЕсли;

КонецПроцедуры

// Обработка оповещения вопроса пользователю. 
// 
// Параметры:
//  Отчет - Строка.
// 
&НаКлиенте
Процедура ВопросОрганизацияПриИзменении(Ответ, ТаблицаОбъекта) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ТаблицаОбъекта.Очистить();			
	КонецЕсли;

КонецПроцедуры // ВопросОрганизацияПриИзменении()

&НаКлиенте
Процедура ОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
		
	Если Объект.ДанныеРаспределения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
				  
	ТекстВопроса = НСтр("ru = 'Табличная часть «Договоры» будет очищена. Продолжить?'");
	ОповещениеВопрос = Новый ОписаниеОповещения("ВопросОрганизацияОбработкаВыбора", ЭтотОбъект, ВыбранноеЗначение);
	
	ПоказатьВопрос(ОповещениеВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);

КонецПроцедуры

// Обработка оповещения вопроса пользователю. 
// 
// Параметры:
//  Отве - Строка.
// 
&НаКлиенте
Процедура ВопросОрганизацияОбработкаВыбора(Ответ, ВыбранноеЗначение) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		Объект.Организация = ВыбранноеЗначение;
		Объект.ДанныеРаспределения.Очистить();
	КонецЕсли;
	
КонецПроцедуры // ВопросОрганизацияОбработкаВыбора()

&НаКлиенте
Процедура ВалютаДокументаПриИзменении(Элемент)
	
	ИзменениеВалютыКлиент();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыДанныеРаспределения

&НаКлиенте
Процедура ДоговорКонтрагентаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачалоВыбораДоговораКлиент(Элемент, ТекущиеДанные.ДоговорКонтрагента, ТекущиеДанные.Контрагент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменениеКонтрагента(ТекущиеДанные.ПолучитьИдентификатор());
	
	УстановитьПараметрыВыбораДоговораКонтрагента(Элементы.ДанныеРаспределенияДоговорКонтрагента, ТекущиеДанные.Контрагент);
    
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменениеДоговораСервер(ТекущиеДанные.ДоговорКонтрагента, ТекущиеДанные.Контрагент);
	
	КомандаОбновитьТекущуюСтроку(Команды.КомандаОбновитьТекущуюСтроку);
	
	ЗаполнитьКурсВзаиморасчетов(ТекущиеДанные.ПолучитьИдентификатор());	
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеРаспределенияДоговорКонтрагентаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если НЕ ВозможноЗаполнятьДокумент() Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеРаспределенияСуммаКРаспределениюПриИзменении(Элемент)
	
	ВычислитьСуммаПогашенияОбязательства();
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеРаспределенияСуммаПроцентовПогашенныхПриИзменении(Элемент)

	ВычислитьСуммаПогашенияОбязательства();
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеРаспределенияКурсВзаиморасчетовПриИзменении(Элемент)
	
	ДанныеРаспределенияСуммаКРаспределениюВзаиморасчетыПриИзменении(Элементы.ДанныеРаспределенияСуммаКРаспределениюВзаиморасчеты);
	ДанныеРаспределенияСуммаПроцентовПогашенныхВзаиморасчетыПриИзменении(Элементы.ДанныеРаспределенияСуммаПроцентовПогашенныхВзаиморасчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеРаспределенияСуммаКРаспределениюВзаиморасчетыПриИзменении(Элемент)
	
	ВычислитьСуммаПогашенияОбязательстваВзаиморасчеты();
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаКРаспределению = ВычислитьСумму(ТекущиеДанные.СуммаКРаспределениюВзаиморасчеты,
													   ТекущиеДанные.КурсВзаиморасчетов, 	  Объект.КурсДокумента,
													   ТекущиеДанные.КратностьВзаиморасчетов, Объект.КратностьДокумента);
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеРаспределенияСуммаПроцентовПогашенныхВзаиморасчетыПриИзменении(Элемент)

	ВычислитьСуммаПогашенияОбязательстваВзаиморасчеты();
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаПроцентовПогашенных = ВычислитьСумму(ТекущиеДанные.СуммаПроцентовПогашенныхВзаиморасчеты,
													   	 	ТекущиеДанные.КурсВзаиморасчетов, 	   Объект.КурсДокумента,
													   		ТекущиеДанные.КратностьВзаиморасчетов, Объект.КратностьДокумента);
	
КонецПроцедуры
               
&НаКлиенте
Процедура ДанныеРаспределенияСуммаПогашенияОбязательстваВзаиморасчетыПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаПогашенияОбязательства = ВычислитьСумму(ТекущиеДанные.СуммаПогашенияОбязательстваВзаиморасчеты,
													   	 	   ТекущиеДанные.КурсВзаиморасчетов, 	  Объект.КурсДокумента,
													   		   ТекущиеДанные.КратностьВзаиморасчетов, Объект.КратностьДокумента);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область СтандартныеПодсистемыОбработчикиКоманд

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура КомандаЗаполнить(Команда)
	
	Если НЕ ВозможноЗаполнятьДокумент() Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.ДанныеРаспределения.Количество() > 0 Тогда
					  
		ТекстВопроса = НСтр("ru = 'Табличная часть «Договоры» будет очищена. Продолжить?'");
		ОповещениеВопрос = Новый ОписаниеОповещения("ВопросКомандаЗаполнить", ЭтотОбъект);
		
		ПоказатьВопрос(ОповещениеВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);
				
	Иначе
		
		ЗаполнитьДанныеДляРаспределения();

	КонецЕсли;
	
КонецПроцедуры

// Обработка оповещения вопроса пользователю. 
// 
// Параметры:
//  Ответ - Строка.
// 
&НаКлиенте
Процедура ВопросКомандаЗаполнить(Ответ, ДопПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Объект.ДанныеРаспределения.Очистить();

		ЗаполнитьДанныеДляРаспределения();

	КонецЕсли;
	
КонецПроцедуры // ВопросКомандаЗаполнить()

&НаКлиенте
Процедура КомандаОбновитьТекущуюСтроку(Команда)
	
	Если НЕ ВозможноЗаполнятьДокумент() Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СписокИдентификаторов = Новый Массив;
	СписокИдентификаторов.Добавить(ТекущиеДанные.ПолучитьИдентификатор());
	
	ЗаполнитьДанныеДляРаспределения(СписокИдентификаторов);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОбновить(Команда)
	
	Если НЕ ВозможноЗаполнятьДокумент() Тогда
		Возврат;
	КонецЕсли;
	
	СписокИдентификаторов = Новый Массив;	
	Для каждого ТекСтр Из Объект.ДанныеРаспределения Цикл
		СписокИдентификаторов.Добавить(ТекСтр.ПолучитьИдентификатор());
	КонецЦикла;
	
	ЗаполнитьДанныеДляРаспределения(СписокИдентификаторов);
	
КонецПроцедуры

&НаКлиенте 
Процедура ДействияФормыДействиеУстановитьОперацию(Команда)

	// Сохраним предыдущие реквизиты для сравнения с новыми значениями.
	ПредыдущийВидОперации = Объект.ВидОперации; 
	
	Если Команда = Неопределено Тогда
		Возврат;
	КонецЕсли; 
	
	ИмяКоманды		= Команда.Имя;
	ИмяКнопки       = СтрЗаменить(ИмяКоманды, "Команда_", "");
	Позиция         = Найти(ИмяКнопки, 	"_");
	ИмяПодменю      = Лев(ИмяКнопки, 	Позиция - 1);
	ИмяВидаОперации = Сред(ИмяКнопки, 	Позиция + 1);
	
	Объект.ВидОперации = фКэшЗначений.Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам[ИмяВидаОперации];
	
	// Если операция не поменялась, то выходим.
	Если ПредыдущийВидОперации = Объект.ВидОперации Тогда
		Возврат;
	КонецЕсли;
	
	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	УстановитьЗаголовокФормыДокумента();
																
	ОбработатьИзменениеВидаОперацииСервер();
	
	// Определим список доступных видов договоров.
	ОпределитьСписокДоступныхВидовДоговоров();
	
КонецПроцедуры // ДействияФормыДействиеУстановитьОперацию()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ОбщиеСлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = бит_ОбщегоНазначения.СостояниеДокумента(Объект);
	
КонецПроцедуры

// Процедура обрабатывает изменение вида операции.
// 
&НаСервере
Процедура ОбработатьИзменениеВидаОперацииСервер()

	// Установить видимость реквизитов и заголовков колонок.	
	УправлениеЭлементамиФормыСервер();
	
	Модифицированность = Истина;

КонецПроцедуры // ОбработатьИзменениеВидаОперацииСервер() 

// Процедура заполняет валюту и курс взаиморасчетов для строки табличной части.
// 
// Параметры:
//  Идентификатор - Число - Индентификатор строки табличной части.
// 
&НаСервере         
Процедура ЗаполнитьКурсВзаиморасчетов(Идентификатор)

	ТекущиеДанные = Объект.ДанныеРаспределения.НайтиПоИдентификатору(Идентификатор);
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.ВалютаВзаиморасчетов = ТекущиеДанные.ДоговорКонтрагента.ВалютаВзаиморасчетов;
		
	СтрКурса = бит_КурсыВалют.ПолучитьКурсВалюты(ТекущиеДанные.ВалютаВзаиморасчетов, Объект.Дата);
	
	ТекущиеДанные.КурсВзаиморасчетов 		= СтрКурса.Курс;
	ТекущиеДанные.КратностьВзаиморасчетов 	= СтрКурса.Кратность;

КонецПроцедуры // ЗаполнитьКурсВзаиморасчетов()

// Формирует список видов операций для выбора вида операции перед созданием документа.
// 
&НаКлиенте
Процедура СформироватьДеревоВидовОпераций()

	ЭлементыДерева = фДеревоВидовОпераций.ПолучитьЭлементы();
	
	СтрокаНовая = ЭлементыДерева.Добавить();
	СтрокаНовая.ВидОперации = "Доступные виды операций";
	
	ЭлементыДерева = СтрокаНовая.ПолучитьЭлементы();
	
	Для каждого ТекСтр Из фКэшЗначений.Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам Цикл
		СтрокаНовая = ЭлементыДерева.Добавить();
		СтрокаНовая.ВидОперации = ТекСтр.Значение;	
	КонецЦикла;
	
КонецПроцедуры // СформироватьДеревоВидовОпераций()

// Процедура заполняет кэш значений, необходимый при работе на клиенте.
// 
// Параметры:
//  КэшированныеЗначения - Структура.
// 
&НаСервере
Процедура ЗаполнитьКэшЗначений(КэшированныеЗначения)
	
	КэшированныеЗначения = Новый Структура;
	
	// Перечисления.
	КэшПеречисления = Новый Структура;
	// Адаптация для ERP. Начало. 18.03.2014{{
	КэшПеречисления.Вставить("ВидыДоговоровКонтрагентов", бит_ОбщегоНазначения.КэшироватьЗначенияПеречисления(Перечисления[бит_ОбщегоНазначения.ПолучитьИмяПеречисленияВидыДоговоровКонтрагентов()]));
	// Адаптация для ERP. Конец. 18.03.2014}}
	КэшПеречисления.Вставить("бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам", бит_ОбщегоНазначения.КэшироватьЗначенияПеречисления(Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам));
	
	КэшированныеЗначения.Вставить("Перечисления", КэшПеречисления);
	
	// Типы договоров
	КэшированныеЗначения.Вставить("ТипДоговораЗаймыПолученные", 	Справочники.бит_ТипыДоговоров.ЗаймыПолученные);
	КэшированныеЗначения.Вставить("ТипДоговораКредитыПолученные", 	Справочники.бит_ТипыДоговоров.КредитыПолученные);
	КэшированныеЗначения.Вставить("ТипДоговораЗаймыВыданные", 		Справочники.бит_ТипыДоговоров.ЗаймыВыданные);
	
	// Запишем параметр для формирования заголовка.
	КэшированныеЗначения.Вставить("ПредставлениеОбъекта", Объект.Ссылка.Метаданные().ПредставлениеОбъекта);
	
	// Получим и запишем валюту МСФО в кэш значений.
	ПолучитьИЗаписатьВалютуМСФОВКэшЗначений(КэшированныеЗначения);

КонецПроцедуры // ЗаполнитьКэшЗначений()

// Процедура получает и записывает валюту международного учета в кэш значений.
// 
// Параметры:
//  КэшированныеЗначения - Струкрута.
// 
&НаСервере 
Процедура ПолучитьИЗаписатьВалютуМСФОВКэшЗначений(КэшированныеЗначения)
	
	// Получим валюту международного учета.
	ВалютаМСФО = бит_му_ОбщегоНазначения.ПолучитьВалютуМеждународногоУчета(Объект.Организация);
	
	КэшированныеЗначения.Вставить("ВалютаМСФО", ВалютаМСФО);
	
КонецПроцедуры // ПолучитьИЗаписатьВалютуМСФОВКэшЗначений()

// Процедура обрабатывает изменение валюты документа.
// 
&НаСервере
Процедура ИзменениеВалютыСервер()
	                 
	ДокументОбъект = ДанныеФормыВЗначение(Объект, ПолучитьТипОбъекта(Объект.Ссылка));
	ДокументОбъект.ИзменениеВалютыМодуль();
	
	ЗначениеВДанныеФормы(ДокументОбъект, Объект);
	  
КонецПроцедуры // ИзменениеВалютыСервер()

// Функция получает тип объекта.
// 
// Параметры:
//  ОбъектСсылка - ДокументСсылка.
// 
// Возвращаемое значение:
//  ТипОбъекта - Тип.
// 
&НаСервереБезКонтекста 
Функция ПолучитьТипОбъекта(ОбъектСсылка)
	
	ИмяОбъекта = ОбъектСсылка.Метаданные().Имя;
	ТипОбъекта = Тип("ДокументОбъект." + ИмяОбъекта);
	
	Возврат ТипОбъекта;
	
КонецФункции // ПолучитьТипОбъекта()

// Процедура выполняет действия необходимые при изменении Организации. Сервер.
// 
&НаСервере
Процедура ИзменениеОрганизацииСервер()
	
	ДокументОбъект = ДанныеФормыВЗначение(Объект, ПолучитьТипОбъекта(Объект.Ссылка));
	
	// Выполняем общие действия для всех документов при изменении Организации.
	бит_ОбщегоНазначения.ПриИзмененииЗначенияОрганизации(ДокументОбъект, бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь"));
	ЗначениеВДанныеФормы(ДокументОбъект, Объект);
	
	// Получим и запишем валюту МСФО в кэш значений.
	ПолучитьИЗаписатьВалютуМСФОВКэшЗначений(фКэшЗначений);
	
	Если Не ЗначениеЗаполнено(Объект.ВалютаДокумента) 
		И ЗначениеЗаполнено(фКэшЗначений.ВалютаМСФО) Тогда
		
		Объект.ВалютаДокумента = фКэшЗначений.ВалютаМСФО;
		
		ИзменениеВалютыСервер();
		
	КонецЕсли;
	
	// Заполним валюту МСФО.
	ЗаполнитьВалютуМСФО();
	
КонецПроцедуры // ИзменениеОрганизацииСервер()

// Процедура обрабатывает изменение контрагента. 
// 
&НаСервере
Процедура ИзменениеКонтрагента(ИдСтроки)

	ТекущаяСтрока = Объект.ДанныеРаспределения.НайтиПоИдентификатору(ИдСтроки);
	
	Если ТекущаяСтрока = Неопределено Тогда	
		Возврат;	
	КонецЕсли; 
	
	СтруктураДополнительныхПараметров = Новый Структура;
	
	Если фСписокТиповДоговоров.Количество() > 0 Тогда
		СтруктураОтбора = Новый Структура;
		СтруктураОтбора.Вставить("ЗначениеОтбора", 		ФСписокТиповДоговоров);
		СтруктураОтбора.Вставить("ВидСравненияОтбора",	ВидСравнения.ВСписке);
		
		СтруктураДополнительныхПараметров.Вставить("бит_ТипДоговора", СтруктураОтбора);
	КонецЕсли;
	
	// Выполняем общие действия для всех документов при изменении Контрагента.
	бит_МеханизмыТиповых.УстановитьДоговорКонтрагента(ТекущаяСтрока.ДоговорКонтрагента
														  ,ТекущаяСтрока.Контрагент
														  ,Объект.Организация
														  ,фСписокВидовДоговоров
														  ,СтруктураДополнительныхПараметров);
														  
	// Могли поменять договор.
	ИзменениеДоговораСервер(ТекущаяСтрока.ДоговорКонтрагента, ТекущаяСтрока.Контрагент);
	
	СписокИдентификаторов = Новый Массив;
	СписокИдентификаторов.Добавить(ТекущаяСтрока.ПолучитьИдентификатор());
	
	ЗаполнитьДанныеДляРаспределения(СписокИдентификаторов);
	
	ЗаполнитьКурсВзаиморасчетов(ТекущаяСтрока.ПолучитьИдентификатор());	

КонецПроцедуры // ИзменениеКонтрагента()

// Процедура обрабатывает изменение договора. Сервер.
// 
// Параметры:
//  Договор				- СправочникСсылка.ДоговорыКонтрагентов.
//  Контрагент_Владелец - СправочникСсылка.Контрагенты.
// 
&НаСервере 
Процедура ИзменениеДоговораСервер(Договор, Контрагент_Владелец)
	
	Если Не ЗначениеЗаполнено(Договор) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Объект.Организация = Договор.Организация;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Контрагент_Владелец) Тогда
		Контрагент_Владелец = бит_ОбщегоНазначения.ОпределитьКонтрагентаПоДоговору(Договор);
	КонецЕсли;
	
КонецПроцедуры // ИзменениеДоговораСервер()

// Функция возвращает выборку для заполнения табличной части.
// 
// Параметры:
//  СписокДоговоров - Массив.
// 
// Возвращаемое значение:
// 	Выборка - Выборка из результата запроса.
// 
&НаСервере 
Функция ПолучитьВыборкуДляЗаполненияДокумента(СписокДоговоров = Неопределено)

    Если бит_ОбщегоНазначения.ЭтоСемействоERP() Тогда
		мРасчетыВУсловныхЕдиницах = "ПорядокОплаты = ЗНАЧЕНИЕ(Перечисление.ПорядокОплатыПоСоглашениям.РасчетыВВалютеОплатаВВалюте)";
	Иначе
		мРасчетыВУсловныхЕдиницах = "РасчетыВУсловныхЕдиницах";
	КонецЕсли; 
    
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Валюты.Ссылка КАК Валюта,
	               |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК Курс,
	               |	ВЫБОР
	               |		КОГДА ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 0) = 0
	               |			ТОГДА 1
	               |		ИНАЧЕ КурсыВалютСрезПоследних.Кратность
	               |	КОНЕЦ КАК Кратность
	               |ПОМЕСТИТЬ КурсыВалютСрезПоследних
	               |ИЗ
	               |	Справочник.Валюты КАК Валюты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаДок, ) КАК КурсыВалютСрезПоследних
	               |		ПО Валюты.Ссылка = КурсыВалютСрезПоследних.Валюта
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	бит_му_ФинансовыеНачисленияОбороты.Контрагент,
	               |	бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента,
	               |	СУММА(ВЫБОР
	               |			КОГДА бит_му_ПараметрыФинИнструментовСрезПоследних3.ЗначениеПараметра = ЗНАЧЕНИЕ(Перечисление.бит_му_ВидыКредитовЗаймов.Рыночный)
	               |					И бит_му_ФинансовыеНачисленияОбороты.ВидСтавки = ЗНАЧЕНИЕ(Перечисление.бит_му_ВидыСтавокФинансовыхПроцентов.НоминальнаяСтавкаПроцента)
	               |				ТОГДА бит_му_ФинансовыеНачисленияОбороты.ПогашеннаяСуммаФинансовыхПроцентовВзаиморасчетыОборот * &Коэффициент
	               |			КОГДА бит_му_ПараметрыФинИнструментовСрезПоследних3.ЗначениеПараметра = ЗНАЧЕНИЕ(Перечисление.бит_му_ВидыКредитовЗаймов.Нерыночный)
	               |					И бит_му_ФинансовыеНачисленияОбороты.ВидСтавки = ЗНАЧЕНИЕ(Перечисление.бит_му_ВидыСтавокФинансовыхПроцентов.ЭффективнаяСтавкаПроцента)
	               |				ТОГДА бит_му_ФинансовыеНачисленияОбороты.ПогашеннаяСуммаФинансовыхПроцентовВзаиморасчетыОборот * &Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаПроцентовПогашенных,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних.ЗначениеПараметра КАК СчетКраткосрочныхПроцентов,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних1.ЗначениеПараметра КАК СчетКраткосрочныхОбязательств,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних2.ЗначениеПараметра КАК СчетРаспределяемыхПлатежей,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних3.ЗначениеПараметра КАК ВидКредитаЗайма,
	               |	бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов
	               |ПОМЕСТИТЬ ФинансовыеНачисления
	               |ИЗ
	               |	РегистрНакопления.бит_му_ФинансовыеНачисления.Обороты(
	               |			&ДатаНач,
	               |			&ДатаКон,
	               |			Месяц,
	               |			Организация = &Организация
	               |				И ВидИсточникаЗадолженности = &ВидИсточникаЗадолженности
				   |" + ?(НЕ ЗначениеЗаполнено(СписокДоговоров), "", "
				   |				И ДоговорКонтрагента В (&СписокДоговоров)") + "
				   |) КАК бит_му_ФинансовыеНачисленияОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_ПараметрыФинИнструментов.СрезПоследних(&ДатаКон, Параметр = &СчетКраткосрочныхПроцентов) КАК бит_му_ПараметрыФинИнструментовСрезПоследних
	               |		ПО бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента = бит_му_ПараметрыФинИнструментовСрезПоследних.ДоговорКонтрагента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_ПараметрыФинИнструментов.СрезПоследних(&ДатаКон, Параметр = &СчетКраткосрочныхОбязательств) КАК бит_му_ПараметрыФинИнструментовСрезПоследних1
	               |		ПО бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента = бит_му_ПараметрыФинИнструментовСрезПоследних1.ДоговорКонтрагента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_ПараметрыФинИнструментов.СрезПоследних(&ДатаКон, Параметр = &СчетРаспределяемыхПлатежей) КАК бит_му_ПараметрыФинИнструментовСрезПоследних2
	               |		ПО бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента = бит_му_ПараметрыФинИнструментовСрезПоследних2.ДоговорКонтрагента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_ПараметрыФинИнструментов.СрезПоследних(&ДатаКон, Параметр = &ВидКредитаЗайма) КАК бит_му_ПараметрыФинИнструментовСрезПоследних3
	               |		ПО бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента = бит_му_ПараметрыФинИнструментовСрезПоследних3.ДоговорКонтрагента
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	бит_му_ФинансовыеНачисленияОбороты.Контрагент,
	               |	бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних.ЗначениеПараметра,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних1.ЗначениеПараметра,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних2.ЗначениеПараметра,
	               |	бит_му_ПараметрыФинИнструментовСрезПоследних3.ЗначениеПараметра,
	               |	бит_му_ФинансовыеНачисленияОбороты.ДоговорКонтрагента.ВалютаВзаиморасчетов
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФинансовыеНачисления.СчетРаспределяемыхПлатежей
	               |ПОМЕСТИТЬ СчетаУчета
	               |ИЗ
	               |	ФинансовыеНачисления КАК ФинансовыеНачисления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ФинансовыеНачисления.Контрагент,
	               |	ФинансовыеНачисления.ДоговорКонтрагента,
	               |	СУММА(ФинансовыеНачисления.СуммаПроцентовПогашенных) КАК СуммаПроцентовПогашенныхВзаиморасчеты,
	               |	ФинансовыеНачисления.СчетКраткосрочныхПроцентов,
	               |	ФинансовыеНачисления.СчетКраткосрочныхОбязательств,
	               |	ФинансовыеНачисления.СчетРаспределяемыхПлатежей,
	               |	СУММА(ВЫБОР
	               |			КОГДА ФинансовыеНачисления.СчетРаспределяемыхПлатежей = бит_Дополнительный_2Остатки.Счет
	               |				ТОГДА ЕСТЬNULL(бит_Дополнительный_2Остатки.СуммаРеглОстаток, 0) / КурсыВалютСрезПоследних.Курс * КурсыВалютСрезПоследних.Кратность
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаКРаспределениюВзаиморасчеты,
	               |	СУММА(ВЫБОР
	               |			КОГДА ФинансовыеНачисления.СчетРаспределяемыхПлатежей = бит_Дополнительный_2Остатки.Счет
	               |				ТОГДА ЕСТЬNULL(бит_Дополнительный_2Остатки.СуммаРеглОстаток, 0) / КурсыВалютСрезПоследних.Курс * КурсыВалютСрезПоследних.Кратность
	               |			ИНАЧЕ 0
	               |		КОНЕЦ - ФинансовыеНачисления.СуммаПроцентовПогашенных) КАК СуммаПогашенияОбязательстваВзаиморасчеты,
	               |	КурсыВалютСрезПоследних.Валюта КАК ВалютаВзаиморасчетов,
	               |	КурсыВалютСрезПоследних.Курс КАК КурсВзаиморасчетов,
	               |	КурсыВалютСрезПоследних.Кратность КАК КратностьВзаиморасчетов,
	               |	СУММА(ФинансовыеНачисления.СуммаПроцентовПогашенных * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность * &Коэффициент) КАК СуммаПроцентовПогашенных,
	               |	СУММА(ВЫБОР
	               |			КОГДА ФинансовыеНачисления.СчетРаспределяемыхПлатежей = бит_Дополнительный_2Остатки.Счет
	               |				ТОГДА ЕСТЬNULL(бит_Дополнительный_2Остатки.СуммаРеглОстаток, 0) * &Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаКРаспределению,
	               |	СУММА(ВЫБОР
	               |			КОГДА ФинансовыеНачисления.СчетРаспределяемыхПлатежей = бит_Дополнительный_2Остатки.Счет
	               |				ТОГДА ЕСТЬNULL(бит_Дополнительный_2Остатки.СуммаРеглОстаток, 0) * &Коэффициент
	               |			ИНАЧЕ 0
	               |		КОНЕЦ - ФинансовыеНачисления.СуммаПроцентовПогашенных * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность * &Коэффициент) КАК СуммаПогашенияОбязательства
	               |ИЗ
	               |	ФинансовыеНачисления КАК ФинансовыеНачисления
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.бит_Дополнительный_2.Остатки(
	               |				&ДатаКон,
	               |				Счет В
	               |					(ВЫБРАТЬ
	               |						СчетаУчета.СчетРаспределяемыхПлатежей
	               |					ИЗ
	               |						СчетаУчета КАК СчетаУчета),
	               |				&МассивСубконто,
	               |				Организация = &Организация) КАК бит_Дополнительный_2Остатки
	               |		ПО ФинансовыеНачисления.Контрагент = бит_Дополнительный_2Остатки.Субконто1
	               |			И ФинансовыеНачисления.ДоговорКонтрагента = бит_Дополнительный_2Остатки.Субконто2
	               |		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалютСрезПоследних КАК КурсыВалютСрезПоследних
	               |		ПО (ВЫБОР
	               |				КОГДА ФинансовыеНачисления.ДоговорКонтрагента." + мРасчетыВУсловныхЕдиницах + "
	               |					ТОГДА &ВалютаРеглУчета = КурсыВалютСрезПоследних.Валюта
	               |				ИНАЧЕ ФинансовыеНачисления.ДоговорКонтрагента.ВалютаВзаиморасчетов = КурсыВалютСрезПоследних.Валюта
	               |			КОНЕЦ)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ФинансовыеНачисления.Контрагент,
	               |	ФинансовыеНачисления.ДоговорКонтрагента,
	               |	ФинансовыеНачисления.СчетКраткосрочныхПроцентов,
	               |	ФинансовыеНачисления.СчетКраткосрочныхОбязательств,
	               |	ФинансовыеНачисления.СчетРаспределяемыхПлатежей,
	               |	КурсыВалютСрезПоследних.Валюта,
	               |	КурсыВалютСрезПоследних.Курс,
	               |	КурсыВалютСрезПоследних.Кратность,
	               |	ФинансовыеНачисления.СуммаПроцентовПогашенных * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность,
	               |	ВЫБОР
	               |		КОГДА ФинансовыеНачисления.СчетРаспределяемыхПлатежей = бит_Дополнительный_2Остатки.Счет
	               |			ТОГДА ЕСТЬNULL(бит_Дополнительный_2Остатки.СуммаРеглОстаток, 0)
	               |		ИНАЧЕ 0
	               |	КОНЕЦ,
	               |	ВЫБОР
	               |		КОГДА ФинансовыеНачисления.СчетРаспределяемыхПлатежей = бит_Дополнительный_2Остатки.Счет
	               |			ТОГДА ЕСТЬNULL(бит_Дополнительный_2Остатки.СуммаРеглОстаток, 0) * &Коэффициент
	               |		ИНАЧЕ 0
	               |	КОНЕЦ - ФинансовыеНачисления.СуммаПроцентовПогашенных * КурсыВалютСрезПоследних.Курс / КурсыВалютСрезПоследних.Кратность * &Коэффициент
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ ФинансовыеНачисления
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ СчетаУчета
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |УНИЧТОЖИТЬ КурсыВалютСрезПоследних";
				   
	Запрос = Новый Запрос(ТекстЗапроса);
	
	ВидыСубконто = ПланыВидовХарактеристик.бит_ВидыСубконтоДополнительные_2;
	
	МассивСубконто = Новый Массив;
	МассивСубконто.Добавить(ВидыСубконто.Контрагенты);
	МассивСубконто.Добавить(ВидыСубконто.ДоговорыКонтрагентов);
	
	ВидыПараметров = ПланыВидовХарактеристик.бит_му_ВидыПараметровФинИнструментов;
	
	Запрос.УстановитьПараметр("ДатаНач", 							НачалоМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("ДатаКон", 							КонецМесяца(Объект.Дата));
	Запрос.УстановитьПараметр("ДатаДок", 							Объект.Дата);
	Запрос.УстановитьПараметр("Организация", 						Объект.Организация);
	Запрос.УстановитьПараметр("ВалютаРеглУчета",					Константы.ВалютаРегламентированногоУчета.Получить());
	
	ВидыОпераций = фКэшЗначений.Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам;
	ВидыИсточниковЗадолженности = Перечисления.бит_му_ВидыИсточниковЗадолженности;
	
	Если Объект.ВидОперации = ВидыОпераций.КредитыЗаймыПолученные Тогда		
		Запрос.УстановитьПараметр("ВидИсточникаЗадолженности",		ВидыИсточниковЗадолженности.КредитыЗаймы);
		Запрос.УстановитьПараметр("СчетКраткосрочныхОбязательств", 	ВидыПараметров.СчетКраткосрочныхОбязательств);
		
	ИначеЕсли Объект.ВидОперации = ВидыОпераций.ЗаймыВыданные Тогда
		Запрос.УстановитьПараметр("ВидИсточникаЗадолженности",		ВидыИсточниковЗадолженности.ЗаймыВыданные);
		Запрос.УстановитьПараметр("СчетКраткосрочныхОбязательств", 	ВидыПараметров.СчетКраткосрочныхАктивов);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СчетКраткосрочныхПроцентов", 		ВидыПараметров.СчетКраткосрочныхПроцентов);
	Запрос.УстановитьПараметр("СчетРаспределяемыхПлатежей", 		ВидыПараметров.СчетРаспределяемыхПлатежей);
	Запрос.УстановитьПараметр("ВидКредитаЗайма", 					ВидыПараметров.ВидКредитаЗайма);
	Запрос.УстановитьПараметр("Коэффициент",						ПолучитьКоэффициентВалют(Объект));
	Запрос.УстановитьПараметр("СписокДоговоров",					СписокДоговоров);
	Запрос.УстановитьПараметр("МассивСубконто",						МассивСубконто);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Возврат Выборка;
	
КонецФункции // ПолучитьВыборкуДляЗаполненияДокумента() 
	
// Процедура заполняет табличную часть "ДанныеРаспределения".
// 
// Параметры:
//  СписокИдентификаторов - Массив.
// 
&НаСервере 
Процедура ЗаполнитьДанныеДляРаспределения(СписокИдентификаторов = Неопределено)
	
	// Сформируем список договоров.
	СписокДоговоров = Неопределено;
	Если СписокИдентификаторов <> Неопределено Тогда
		
		СписокДоговоров = Новый Массив;
		Для каждого ТекСтр Из СписокИдентификаторов Цикл
			СтрокаТЧ = Объект.ДанныеРаспределения.НайтиПоИдентификатору(ТекСтр);
			СписокДоговоров.Добавить(СтрокаТЧ.ДоговорКонтрагента);
		КонецЦикла;
		
	КонецЕсли;
	
	// Сформируем список колонок для очистки.
	ТабЗнач = Объект.ДанныеРаспределения.Выгрузить();
	Колонки = Новый Массив;
	Для каждого ТекСтр Из ТабЗнач.Колонки Цикл
		Если (ТекСтр.Имя <> "Контрагент") И (ТекСтр.Имя <> "ДоговорКонтрагента") 
			И (ТекСтр.Имя <> "НомерСтроки") И (ТекСтр.Имя <> "ИсходныйНомерСтроки") Тогда
			Колонки.Добавить(ТекСтр.Имя);
		КонецЕсли;
	КонецЦикла;
	
	Выборка = ПолучитьВыборкуДляЗаполненияДокумента(СписокДоговоров);
	
	Если СписокИдентификаторов = Неопределено Тогда
		
		// Заполним ТЧ.
		Пока Выборка.Следующий() Цикл
			НоваяСтрока = Объект.ДанныеРаспределения.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЦикла;
		
	Иначе
		
		Для каждого ТекСтр Из СписокИдентификаторов Цикл
			СтрокаТЧ = Объект.ДанныеРаспределения.НайтиПоИдентификатору(ТекСтр);
			
			// Очистим данные в строке ТЧ.
			Для каждого ТекКолонка Из Колонки Цикл
				СтрокаТЧ[ТекКолонка] = Неопределено;
			КонецЦикла;
			
			// Найдем данные в выборке и заполним строку ТЧ.
			СтуктураПоиска = Новый Структура("Контрагент, ДоговорКонтрагента");
			СтуктураПоиска.Контрагент 			= СтрокаТЧ.Контрагент;
			СтуктураПоиска.ДоговорКонтрагента	= СтрокаТЧ.ДоговорКонтрагента;
			
			Выборка.Сбросить();
			Если Выборка.НайтиСледующий(СтуктураПоиска) Тогда
				ЗаполнитьЗначенияСвойств(СтрокаТЧ, Выборка);
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьДанныеДляРаспределения()

// Функция возвращает коэффициент пересчета сумм из валюты регламентированного учета в валюту документа.
// 
// Параметры:
//  Документ.
//
&НаСервере 
Функция ПолучитьКоэффициентВалют(Документ)
	
	ВидыКурсов			= Новый Структура("Упр,Регл,МУ,Документ");
	ДокументОбъект 		= ДанныеФормывЗначение(Документ, Тип("ДокументОбъект.бит_му_РаспределениеПлатежейПоКредитамЗаймам"));
	СтруктураКурсыВалют = бит_му_ОбщегоНазначения.ПолучитьСтруктуруКурсовВалют(ДокументОбъект, ДокументОбъект.Дата, ВидыКурсов);
	
	КурсыРегл  	= СтруктураКурсыВалют.Регл;
	КурсыДок 	= СтруктураКурсыВалют.Документ;

	Коэффициент = бит_КурсыВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(1, КурсыРегл.Валюта, 		КурсыДок.Валюта,
																	 КурсыРегл.Курс, 		КурсыДок.Курс,
																	 КурсыРегл.Кратность, 	КурсыДок.Кратность);
																	 
	Возврат Коэффициент;
	
КонецФункции

// Процедура выполняет заполнение в форме документа валюту МСФО.
// 
&НаСервере 
Процедура ЗаполнитьВалютуМСФО()
	
	// Получим строковое представление валюты международного учета.
	СтрокаВалютаМСФО = Строка(фКэшЗначений.ВалютаМСФО);
	
	Элементы.ВалютаМСФО.Заголовок = ?(ПустаяСтрока(СтрокаВалютаМСФО), "НЕ УСТАНОВЛЕНА", СтрокаВалютаМСФО);
	
КонецПроцедуры // ЗаполнитьВалютуМСФО()

&НаКлиенте
Процедура ДанныеРаспределенияПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	УстановитьПараметрыВыбораДоговораКонтрагента(Элементы.ДанныеРаспределенияДоговорКонтрагента, ТекущиеДанные.Контрагент);
	
КонецПроцедуры

#КонецОбласти

#Область РаботаСКэшемРеквизитовФормы

// Процедура заполняет кэш реквизитов формы данными объекта.
// 
&НаКлиенте 
Процедура ЗаполнитьТекущиеЗначенияРеквизитовФормы()
	
	Для Каждого КлючИЗначение Из мКэшРеквизитовФормы Цикл
		мКэшРеквизитовФормы[КлючИЗначение.Ключ] = Объект[КлючИЗначение.Ключ];
	КонецЦикла; 
	
КонецПроцедуры // ЗаполнитьТекущиеЗначенияРеквизитовФормы()

// Процедура добавляет в кэш реквизитов текущее значение заданного реквизита.
// 
// Параметры:
// 	ИмяРеквизита - Строка.
// 
&НаКлиенте
Процедура ДобавитьВКэш(ИмяРеквизита)
	
	мКэшРеквизитовФормы[ИмяРеквизита] = Объект[ИмяРеквизита];	
	
КонецПроцедуры // ДобавитьВКэш()

// Процедура извлекает из кэша и присваивает объекту значение заданного реквизита.
// 
// Параметры:
// 	ИмяРеквизита - Строка.
// 
&НаКлиенте
Процедура ИзвлечьИзКэша(ИмяРеквизита)
	
	Объект[ИмяРеквизита] = мКэшРеквизитовФормы[ИмяРеквизита];
	
КонецПроцедуры // ИзвлечьИзКэша()

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

// Процедура устанвливает параметры выбора для элементов формы. 
// 
// Параметры:
//  МассивЭлементов - Массив.
// 
&НаСервере
Процедура УстановитьПараметрыВыбораДляСчетов()

	МассивЭлементов = Новый Массив;
	МассивЭлементов.Добавить(Элементы.ДанныеРаспределенияСчетКраткосрочныхПроцентов);
	МассивЭлементов.Добавить(Элементы.ДанныеРаспределенияСчетКраткосрочныхОбязательств);
	МассивЭлементов.Добавить(Элементы.ДанныеРаспределенияСчетРаспределяемыхПлатежей);
	
	бит_РаботаСДиалогамиСервер.УстановитьПараметрыВыбораДляЭлементов(МассивЭлементов);
	
КонецПроцедуры // УстановитьПараметрыВыбораДляСчетов() 

// Процедура формирует список доступных видов договоров.
// 
&НаКлиенте 
Процедура ОпределитьСписокДоступныхВидовДоговоров()
	
	ВидыОпераций				= фКэшЗначений.Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам;
	ВидыДоговоровКонтрагентов 	= фКэшЗначений.Перечисления.ВидыДоговоровКонтрагентов;
	
	фСписокВидовДоговоров = Новый СписокЗначений;
												   
	Если Объект.ВидОперации = ВидыОпераций.КредитыЗаймыПолученные Тогда
		
		Если НЕ бит_ОбщегоНазначения.ЭтоСемействоERP() Тогда
			фСписокВидовДоговоров.Добавить(ВидыДоговоровКонтрагентов.Прочее);		
		КонецЕсли; 
				
		фСписокТиповДоговоров = Новый СписокЗначений;
		фСписокТиповДоговоров.Добавить(фКэшЗначений.ТипДоговораЗаймыПолученные);
		фСписокТиповДоговоров.Добавить(фКэшЗначений.ТипДоговораКредитыПолученные);
		
	ИначеЕсли Объект.ВидОперации = ВидыОпераций.ЗаймыВыданные Тогда
		
		Если НЕ бит_ОбщегоНазначения.ЭтоСемействоERP() Тогда		
			фСписокВидовДоговоров.Добавить(ВидыДоговоровКонтрагентов.Прочее);		
		КонецЕсли; 
				
		фСписокТиповДоговоров = Новый СписокЗначений;
		фСписокТиповДоговоров.Добавить(фКэшЗначений.ТипДоговораЗаймыВыданные);
		
	КонецЕсли;

КонецПроцедуры // ОпределитьСписокДоступныхВидовДоговоровПоОперации()

// Процедура устанавливает отборы для договора контрагента.
// 
// Параметры:
//  Элемент    			- ПолеФормы. Для которого необходимо установить параметры выбора.
//  Контрагент_Владелец - СправочникСсылка.Контрагенты.
// 
&НаКлиенте
Процедура УстановитьПараметрыВыбораДоговораКонтрагента(Элемент, Контрагент_Владелец)

	СтруктураОтбора = Новый Структура;
	
	Если ЗначениеЗаполнено(Контрагент_Владелец) Тогда
		ИмяРеквизитаКонтрагента = бит_ОбщегоНазначения.ОпределитьИмяРеквизитаКонтрагента();
		СтруктураОтбора.Вставить(ИмяРеквизитаКонтрагента, Контрагент_Владелец);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		СтруктураОтбора.Вставить("Организация", Объект.Организация);
	КонецЕсли;
	
	Если НЕ бит_ОбщегоНазначения.ЭтоСемействоERP() Тогда
		Если фСписокВидовДоговоров.Количество() > 0 Тогда
			СтруктураОтбора.Вставить("ВидДоговора", фСписокВидовДоговоров);
		КонецЕсли;
	    Если фСписокТиповДоговоров.Количество() > 0 Тогда
			СтруктураОтбора.Вставить("бит_ТипДоговора", фСписокТиповДоговоров);
		КонецЕсли;
	КонецЕсли;

	// Установим параметры выбора.
	бит_ОбщегоНазначенияКлиентСервер.УстановитьПараметрыВыбораЭлемента(Элемент, СтруктураОтбора);

	
КонецПроцедуры // УстановитьПараметрыВыбораДоговораКонтрагента()


// Процедура обрабатывает изменение валюты документа. Клиент.
// 
&НаКлиенте
Процедура ИзменениеВалютыКлиент() 
	
	ИзменениеВалютыСервер();
	
	ТаблицаОбъекта = Объект.ДанныеРаспределения;
	
	// Спросим, а не пересчитать ли суммы.
	Если Не мКэшРеквизитовФормы.ВалютаДокумента = Объект.ВалютаДокумента 
		И ТаблицаОбъекта.Количество() > 0 Тогда
		
		ТекстВопроса = НСтр("ru = 'Изменилась валюта документа. Пересчитать сумму документа?'");
		ОповещениеВопрос = Новый ОписаниеОповещения("ВопросИзменениеВалютыКлиент", ЭтотОбъект, ТаблицаОбъекта);
		
		ПоказатьВопрос(ОповещениеВопрос, ТекстВопроса, РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога);					  
				
	Иначе
		
		ДобавитьВКэш("ВалютаДокумента");
		ДобавитьВКэш("КратностьДокумента");
		ДобавитьВКэш("КурсДокумента");
		
	КонецЕсли; 
		
КонецПроцедуры // ИзменениеВалютыКлиент()

// Обработка оповещения вопроса пользователю. 
// 
// Параметры:
//  Ответ - Строка.
// 
&НаКлиенте
Процедура ВопросИзменениеВалютыКлиент(Ответ, ТаблицаОбъекта) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
			
		// Выполним пересчет сумм строк табличной части.
		Для Каждого СтрокаТаблицы Из ТаблицаОбъекта Цикл
			
			СтрокаТаблицы.СуммаКРаспределению = бит_КурсыВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.СуммаКРаспределению
																				 ,мКэшРеквизитовФормы.ВалютаДокумента
																				 ,Объект.ВалютаДокумента
																				 ,мКэшРеквизитовФормы.КурсДокумента
																				 ,Объект.КурсДокумента
																				 ,мКэшРеквизитовФормы.КратностьДокумента
																				 ,Объект.КратностьДокумента);
																				 
			СтрокаТаблицы.СуммаПроцентовПогашенных = бит_КурсыВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.СуммаПроцентовПогашенных
																				 ,мКэшРеквизитовФормы.ВалютаДокумента
																				 ,Объект.ВалютаДокумента
																				 ,мКэшРеквизитовФормы.КурсДокумента
																				 ,Объект.КурсДокумента
																				 ,мКэшРеквизитовФормы.КратностьДокумента
																				 ,Объект.КратностьДокумента);
																				 
			СтрокаТаблицы.СуммаПогашенияОбязательства = бит_КурсыВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(СтрокаТаблицы.СуммаПогашенияОбязательства
																				 ,мКэшРеквизитовФормы.ВалютаДокумента
																				 ,Объект.ВалютаДокумента
																				 ,мКэшРеквизитовФормы.КурсДокумента
																				 ,Объект.КурсДокумента
																				 ,мКэшРеквизитовФормы.КратностьДокумента
																				 ,Объект.КратностьДокумента);
																				 
		КонецЦикла;
		
	КонецЕсли;

	ДобавитьВКэш("ВалютаДокумента");
	ДобавитьВКэш("КратностьДокумента");
	ДобавитьВКэш("КурсДокумента");

КонецПроцедуры // ВопросИзменениеВалютыКлиент()

// Процедура выполняет действия необходимые при начале выбора Договора. Клиент.
// 
// Параметры:
//  Элемент    			 - ПолеФормы. Для которого необходимо установить параметры выбора.
//  Договор				 - СправочникСсылка.ДоговорыКонтрагентов.
//  Контрагент_Владелец  - СправочникСсылка.Контрагенты.
//  СтандартнаяОбработка - Булево.
// 
&НаКлиенте 
Процедура НачалоВыбораДоговораКлиент(Элемент, Договор, Контрагент_Владелец, СтандартнаяОбработка)
	
	// Установим отбор для договора контрагента.
	УстановитьПараметрыВыбораДоговораКонтрагента(Элемент, Контрагент_Владелец);
	
КонецПроцедуры // НачалоВыбораДоговораКлиент()

// Функция проверяет можно ли заполнять документ.
// 
// Возвращаемое значение:
//  Булево.
// 
&НаКлиенте 
Функция ВозможноЗаполнятьДокумент()

	Результат = Истина;
	
	Если Объект.Проведен Тогда
		СтрокаСообщения = НСтр("ru = 'Невозможно корректировать проведенный документ!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(СтрокаСообщения);
		Результат = Ложь;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции // ВозможноЗаполнятьДокумент()
 
// Процедура вычисляет сумму погашения обязательства.
// 
&НаКлиенте 
Процедура ВычислитьСуммаПогашенияОбязательства()

	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаПогашенияОбязательства = ТекущиеДанные.СуммаКРаспределению - ТекущиеДанные.СуммаПроцентовПогашенных;

КонецПроцедуры // ВычислитьСуммаПогашенияОбязательства() 

// Процедура вычисляет сумму погашения обязательства.
// 
&НаКлиенте 
Процедура ВычислитьСуммаПогашенияОбязательстваВзаиморасчеты()

	ТекущиеДанные = Элементы.ДанныеРаспределения.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные.СуммаПогашенияОбязательстваВзаиморасчеты = ТекущиеДанные.СуммаКРаспределениюВзаиморасчеты - ТекущиеДанные.СуммаПроцентовПогашенныхВзаиморасчеты;
	
	ДанныеРаспределенияСуммаПогашенияОбязательстваВзаиморасчетыПриИзменении(Элементы.ДанныеРаспределенияСуммаПогашенияОбязательстваВзаиморасчеты);

КонецПроцедуры // ВычислитьСуммаПогашенияОбязательства() 

// Функция вычисляет сумму в валюте документа.
// 
// Параметры:
//  Сумма			- Число.
//  ПоКурсуНач 		- Число.
//  ПоКратностьНач 	- Число.
//  ПоКурсуКон 		- Число.
//  ПоКратностьКон 	- Число.
// 
// Возвращаемое значение:
//  Число.
// 
&НаКлиенте 
Функция ВычислитьСумму(Сумма, ПоКурсуНач, ПоКратностьНач, ПоКурсуКон, ПоКратностьКон)

	Результат = 0;
	
	Если (ПоКурсуКон = 0) ИЛИ (ПоКратностьНач = 0) Тогда
		ТекстСообщения = "При пересчете суммы обнаружен нулевой курс!";	 
		бит_ОбщегоНазначения.СообщитьОбОшибке(ТекстСообщения);
	Иначе
		Результат = (Сумма * ПоКурсуНач * ПоКратностьКон) / (ПоКурсуКон * ПоКратностьНач);
	КонецЕсли;
		
	Возврат Результат;
	
КонецФункции // ВычислитьСумму()

#КонецОбласти

#Область ПроцедурыИФункцииДляУправленияВнешнимВидомФормы

// Процедура устнавливает заголовок формы документа.
// 
&НаКлиенте 
Процедура УстановитьЗаголовокФормыДокумента()
	
	СтруктураЗаголовка = Новый Структура;
	СтруктураЗаголовка.Вставить("ПредставлениеОбъекта", фКэшЗначений.ПредставлениеОбъекта);
	СтруктураЗаголовка.Вставить("СтрокаВидаОперации"  , Строка(Объект.ВидОперации));
	СтруктураЗаголовка.Вставить("ЭтоНовый"			  , Параметры.Ключ.Пустая());
	СтруктураЗаголовка.Вставить("ДокументПроведен"	  , Объект.Проведен);
	
	бит_РаботаСДиалогамиКлиент.УстановитьЗаголовокФормыДокумента(ЭтотОбъект
																,СтруктураЗаголовка);
	
КонецПроцедуры // УстановитьЗаголовокФормыДокумента()

// Процедура осуществляет управление доступностью/видимостью элементов формы.
// 
&НаСервере 
Процедура УправлениеЭлементамиФормыСервер()
	
	ВидыОпераций = фКэшЗначений.Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам;
	
	Если Объект.ВидОперации = ВидыОпераций.КредитыЗаймыПолученные Тогда		
		ЗаголовокАктивовОбязательств = "Счет краткосрочных обязательств"
		
	ИначеЕсли Объект.ВидОперации = ВидыОпераций.ЗаймыВыданные Тогда
		ЗаголовокАктивовОбязательств = "Счет краткосрочных активов"
	КонецЕсли;
	
	Элементы.ДанныеРаспределенияСчетКраткосрочныхОбязательств.Заголовок = ЗаголовокАктивовОбязательств;
	
	Элементы.ОтразитьВЗакрытомПериоде.Видимость = бит_ПраваДоступа.ПолучитьЗначениеДопПраваПользователя(
	 					                           бит_ОбщиеПеременныеСервер.ЗначениеПеременной("ТекущийПользователь"),
	 					                           ПланыВидовХарактеристик.бит_ДополнительныеПраваПользователей.РазрешеноОтражениеДокументовВЗакрытомПериоде);

КонецПроцедуры // УправлениеЭлементамиФормыСервер()

// Процедура окончание выбора вида операции.
// 
&НаКлиенте 
Процедура ВыборВидаОперацииОкончание(СтруктураПараметров, ДополнительныеПараметры) Экспорт
	
	Отказ = Ложь;	
	Если СтруктураПараметров = Неопределено Тогда
		Отказ = Истина;	
	КонецЕсли;
	
	ПриОткрытииЭтоНовыйОкончание(СтруктураПараметров, Отказ);
		
КонецПроцедуры // ВыборВидаОперацииОкончание()

&НаКлиенте
Процедура ПриОткрытииЭтоНовыйОкончание(СтруктураПараметров, Отказ)

	Если (ТипЗнч(СтруктураПараметров) = Тип("Структура")) И СтруктураПараметров.Свойство("ВидОперации") Тогда
		Объект.ВидОперации = СтруктураПараметров.ВидОперации;																		   
	Иначе
		Объект.ВидОперации = фКэшЗначений.Перечисления.бит_му_ВидыОперацийбитРаспределениеПлатежейПоКредитамЗаймам.КредитыЗаймыПолученные;
	КонецЕсли;
	
	ОбработатьИзменениеВидаОперацииСервер();
	
	ПриОткрытииПромежуточная(Отказ);
	ПриОткрытииОкончание();

КонецПроцедуры // ПриОткрытииЭтоНовыйОкончание()

&НаКлиенте
Процедура ПриОткрытииПромежуточная(Отказ)

	Если Отказ Тогда
		Модифицированность = Ложь;
		ЭтотОбъект.Закрыть();
	КонецЕсли;

КонецПроцедуры // ПриОткрытииПромежуточная()

// Процедура выполняет окончательные операции по открытию формы. 
// 
&НаКлиенте
Процедура ПриОткрытииОкончание()

	// Вывести в заголовке формы вид операции и статус документа (новый, не проведен, проведен).
	УстановитьЗаголовокФормыДокумента();
	
	// Запомним текущие значения реквизитов формы.
	ЗаполнитьТекущиеЗначенияРеквизитовФормы();
	
	// Определим список доступных видов договоров.
	ОпределитьСписокДоступныхВидовДоговоров();

КонецПроцедуры // ПриОткрытииОкончание()


#КонецОбласти

#КонецОбласти

#Область Инициализация

// Зададим имена реквизитов, подлежащих кешированию.
мКэшРеквизитовФормы = Новый Структура;
мКэшРеквизитовФормы.Вставить("ВалютаДокумента");
мКэшРеквизитовФормы.Вставить("КурсДокумента");
мКэшРеквизитовФормы.Вставить("КратностьДокумента");
мКэшРеквизитовФормы.Вставить("Дата");

#КонецОбласти

