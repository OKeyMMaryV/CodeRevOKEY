//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-06-01 (#ТП_БП05_ФР16) 

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|   Истина как Выбран,
		|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.ДокументРасчетов КАК ДокументРасчетов,
		|	ВЫБОР
		|		КОГДА &ЭтоДолгиСотрудника
		|			ТОГДА ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток
		|		ИНАЧЕ -ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток
		|	КОНЕЦ КАК Сумма,
		|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.ДенежныйДокумент КАК ДенежныйДокумент,
		|	ВЫБОР
		|		КОГДА &ЭтоДолгиСотрудника
		|			ТОГДА ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаРеглОстаток
		|		ИНАЧЕ -ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаРеглОстаток
		|	КОНЕЦ КАК СуммаРегл,
		|	ВЫБОР
		|		КОГДА &ЭтоДолгиСотрудника
		|			ТОГДА ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток
		|		ИНАЧЕ -ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток
		|	КОНЕЦ КАК СуммаЗачета
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Остатки(
		|			&Дата,
		|			Организация = &Организация
		|				И Валюта = &Валюта
		|				И ПодотчетноеЛицо = &ПодотчетноеЛицо
		|				И НЕ ДокументРасчетов В (&МассивДокументовРасчетов)) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки
		|ГДЕ
		|	ВЫБОР
		|			КОГДА &ЭтоДолгиСотрудника
		|				ТОГДА ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток > 0
		|			ИНАЧЕ ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток < 0
		|		КОНЕЦ";
	
	Запрос.УстановитьПараметр("Валюта", Параметры.Валюта);
	Если НачалоДня(Параметры.Дата) = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя()) Тогда
		Запрос.УстановитьПараметр("Дата",   Неопределено);
	Иначе	
		Запрос.УстановитьПараметр("Дата", Новый Граница(Параметры.Ссылка.МоментВремени(), ВидГраницы.Исключая));
	КонецЕсли;
	Запрос.УстановитьПараметр("МассивДокументовРасчетов", Параметры.ДокументыРасчетов);
	Запрос.УстановитьПараметр("Организация", Параметры.Организация);
	Запрос.УстановитьПараметр("ПодотчетноеЛицо", Параметры.ПодотчетноеЛицо);
	Если Параметры.ИмяТЧ = "ДолгиСотрудника" Тогда
		Запрос.УстановитьПараметр("ЭтоДолгиСотрудника", Истина);
	Иначе
		Запрос.УстановитьПараметр("ЭтоДолгиСотрудника", Ложь);
	КонецЕсли;
	
	ЭтотОбъект.ИмяТЧ = Параметры.ИмяТЧ;
	РезультатЗапроса = Запрос.Выполнить();
	
	Остатки.Загрузить(РезультатЗапроса.Выгрузить());	

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы
&НаКлиенте
Процедура ВыбратьВсе(Команда)
	Для Каждого Строка Из Остатки Цикл 
		Строка.Выбран = Истина;
	КонецЦикла;	
КонецПроцедуры

&НаКлиенте
Процедура ОтменитьВыбор(Команда)
	Для Каждого Строка Из Остатки Цикл 
		Строка.Выбран = Ложь;
	КонецЦикла;
КонецПроцедуры


&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	МассивВыбранныхСтрок = остатки.НайтиСтроки(Новый Структура("Выбран", Истина));
	Если МассивВыбранныхСтрок.количество() Тогда
		РезультатЗакрытия = Новый Структура;
		РезультатЗакрытия.Вставить("ИмяТЧ", ЭтотОбъект.ИмяТЧ);
		РезультатЗакрытия.Вставить("МассивСтрок", МассивВыбранныхСтрок);
		Закрыть(РезультатЗакрытия);
	Иначе 		
		Закрыть(Неопределено);
	КонецЕсли;
КонецПроцедуры 

#КонецОбласти
//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-06-01 (#ТП_БП05_ФР16)
