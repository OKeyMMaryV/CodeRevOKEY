#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	ДополнительныеСвойства.Вставить("БылПроведен", Проведен);
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	РежимЗаписи = ДополнительныеСвойства.РежимЗаписи;
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;			
	КонецЕсли; 
		
	Если НЕ Отказ Тогда
		Если НЕ РежимЗаписи = РежимЗаписиДокумента.Проведение  Тогда
			УстановитьСтатус(РежимЗаписи);
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.бит_СправкаОПодтверждающихДокументах.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	РеквизитыДокумента = ПараметрыПроведения.Реквизиты[0];
	
	Документы.бит_СправкаОПодтверждающихДокументах.ПроверитьВозможностьПроведения(РеквизитыДокумента, ЭтотОбъект, Метаданные().Реквизиты, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	Документы.бит_СправкаОПодтверждающихДокументах.ДвиженияПоРегиструДанныеПоПаспортамСделок(ПараметрыПроведения.ПодтверждающиеДокументы
																							, РеквизитыДокумента
																							, Движения
																							, Отказ);
	
	Если НЕ Отказ Тогда
		
		// Заполним перечень виз
		РежимОбновленияВиз = Константы.бит_РежимОбновленияПеречняВиз.Получить();
		Если РежимОбновленияВиз = Перечисления.бит_РежимыОбновленияПеречняВиз.ОбновлятьПриПерепроведении 
			ИЛИ НЕ ДополнительныеСвойства.БылПроведен Тогда
			
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("СтруктурноеПодразделение", РеквизитыДокумента.Организация);
			бит_Визирование.ОбновитьПереченьВиз(РеквизитыДокумента.Регистратор, РеквизитыДокумента.Дата, СтруктураПараметров);
			
		КонецЕсли; 
		
		УстановитьСтатус(РежимЗаписиДокумента.Проведение);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	Документы.бит_СправкаОПодтверждающихДокументах.ПроверитьВозможностьУдаленияПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	бит_ОбщегоНазначения.УдалитьДвиженияРегистратора(ЭтотОбъект, Отказ);
	
	Если НЕ Отказ Тогда
		
		// Очистим визы
		бит_Визирование.ОчиститьВсеВизыБезусловно(ЭтотОбъект.Ссылка);
		
	КонецЕсли; 
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Ответственный = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("ТекущийПользователь");
	
	Если ДанныеЗаполнения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.бит_ПаспортаСделок") Тогда
		
		ЗаполнитьДокументПоПаспортуСделки(ДанныеЗаполнения);
		
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.бит_СправкаОПодтверждающихДокументах") Тогда	
		
		Корректировка = Истина;
		ЗаполнитьДокументПоСправкеОПодтверждающихДокументах(ДанныеЗаполнения);
		
		Для каждого Строка Из ПодтверждающиеДокументы Цикл
			Строка.ПризнакКорректировки = ДанныеЗаполнения.Дата;
		КонецЦикла; 
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Корректировка = Ложь;
	ПорядковыйНомерКорректировки = 0;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьДокументПоПаспортуСделки(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ПаспортСделки,
	|	Реквизиты.Банк,
	|	Реквизиты.Организация
	|ИЗ
	|	Справочник.бит_ПаспортаСделок КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Выборка);
	КонецЕсли; 

КонецПроцедуры

Процедура ЗаполнитьДокументПоСправкеОПодтверждающихДокументах(ДанныеЗаполнения)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументОснование,
	|	Реквизиты.ПорядковыйНомерКорректировки + 1 КАК ПорядковыйНомерКорректировки,
	|	Реквизиты.ДатаЗаполнения КАК ДатаЗаполнения,
	|	Реквизиты.Банк КАК Банк,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ПаспортСделки КАК ПаспортСделки,
	|	ИСТИНА КАК Корректировка,
	|	Реквизиты.ПодтверждающиеДокументы.(
	|		ПодтверждающийДокумент КАК ПодтверждающийДокумент,
	|		Номер КАК Номер,
	|		Дата КАК Дата,
	|		КодВидаПодтверждающегоДокумента КАК КодВидаПодтверждающегоДокумента,
	|		ВалютаДокумента КАК ВалютаДокумента,
	|		СуммаДокумента КАК СуммаДокумента,
	|		СуммаВВалютеДоговора КАК СуммаВВалютеДоговора,
	|		ПризнакПоставки КАК ПризнакПоставки,
	|		ОжидаемыйСрок КАК ОжидаемыйСрок,
	|		СтранаКонтрагента КАК СтранаКонтрагента,
	|		Примечание КАК Примечание
	|	) КАК Таблица
	|ИЗ
	|	Документ.бит_СправкаОПодтверждающихДокументах КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДанныеЗаполнения);
	
	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаСсылка = РезультатЗапроса.Выбрать();
	Пока ВыборкаСсылка.Следующий() Цикл
	
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ВыборкаСсылка);
		ВыборкаДетали = ВыборкаСсылка.Таблица.Выбрать();
		Пока ВыборкаДетали.Следующий() Цикл
			НоваяСтрока = ПодтверждающиеДокументы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаДетали);
		КонецЦикла; 
	КонецЦикла; 

КонецПроцедуры

// Функция определяет текущий статус заявки на затраты.
// 
// Параметры:
//  ТекущийОбъект    - ДокументСсылка.бит_ЗаявкаНаЗатраты
//  НачальныйСтатус  - Справочник - текущее значение статуса.
//  ВсеВизыПолучены  - Булево.
// 
// Возвращаемое значение:
//   РезСтатус   - СправочникСсылка.бит_СтатусыОбъектов.
// 
Функция ОпределитьСтатус(ТекущийОбъект
							,НачальныйСтатус
							,РежимЗаписи
							,ВсеВизыПолучены) Экспорт

	РезСтатус = НачальныйСтатус;
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение 
		ИЛИ (РежимЗаписи = РежимЗаписиДокумента.Запись И ТекущийОбъект.Проведен) Тогда
		РезСтатус = Справочники.бит_СтатусыОбъектов.СправкаОПодтверждающихДокументах_Рабочая;
	Иначе
		РезСтатус = Справочники.бит_СтатусыОбъектов.СправкаОПодтверждающихДокументах_Черновик;
	КонецЕсли; 
	
	Если РезСтатус = Справочники.бит_СтатусыОбъектов.СправкаОПодтверждающихДокументах_Утверждена Тогда
	
		Если ВсеВизыПолучены Тогда
			РезСтатус = Справочники.бит_СтатусыОбъектов.СправкаОПодтверждающихДокументах_Утверждена;
		Иначе	
			РезСтатус = Справочники.бит_СтатусыОбъектов.СправкаОПодтверждающихДокументах_Рабочая;
		КонецЕсли;	
				
	КонецЕсли; 
	
	Возврат РезСтатус;
  
КонецФункции // ОпределитьСтатусСПД()

// Процедура устанавливает статус.
// 
// Параметры:
//  вхРежимЗаписи			 - РежимЗаписиДокумента.
//  вхСтруктураЗатрат		 - Структура.
//  вхСинхронизироватьЗатраты - Булево. По умолчанию Истина.
//  ПереданныйСтатус - СправочникСсылка.бит_СтатусыОбъектов.
// 
Процедура УстановитьСтатус(вхРежимЗаписи = Неопределено) Экспорт
						  
	СтрАлгоритмы             = бит_уп_Сервер.ПолучитьАлгоритмыОбъектаСистемы(ЭтотОбъект,Перечисления.бит_уп_ВидыАлгоритмов.ИзменениеСтатусовОбъектов);
	АлгоритмИзмененияСтатуса = СтрАлгоритмы.ИзменениеСтатусовОбъектов;
						  
	Если НЕ ЗначениеЗаполнено(АлгоритмИзмененияСтатуса) Тогда
		
		Если вхРежимЗаписи = Неопределено Тогда
			 РежимЗаписи = РежимЗаписиДокумента.Запись;
		Иначе	
			 РежимЗаписи = вхРежимЗаписи;
		КонецЕсли; 
		
		// Вычислим переменные, необходимые для установки статуса.
		ВсеВизыПолучены = бит_Визирование.ВизыПолучены(Ссылка);
		
		// Определим статус заявки
		Статус = ОпределитьСтатус(ЭтотОбъект, ДополнительныеСвойства.ТекущийСтатус, РежимЗаписи, ВсеВизыПолучены);
		
		// Если статус изменился - запишем его
		Если ДополнительныеСвойства.ТекущийСтатус <> Статус Тогда
			
			ДатаИзмененияСтатуса = ТекущаяДата();
			
			ДействиеВыполнено = бит_Визирование.УстановитьСтатусОбъекта(Ссылка
																		,Перечисления.бит_ВидыСтатусовОбъектов.Статус
																		,Статус
																		,ДополнительныеСвойства.ТекущийСтатус
																		,бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь")
																		,ДатаИзмененияСтатуса);
			
			Если ДействиеВыполнено Тогда
				
				ДополнительныеСвойства.ТекущийСтатус = Статус;
				ДополнительныеСвойства.ДатаИзмененияСтатуса = ДатаИзмененияСтатуса;
				
			КонецЕсли;
			
		КонецЕсли; 
		
	Иначе	
		
		СтруктураКонтекст = Новый Структура;
		СтруктураКонтекст.Вставить("ТекущийОбъект",ЭтотОбъект);
		
		// Выполнение установки статуса согласно алгоритма изменения статусов.
		КомментироватьВыполнение = бит_УправлениеПользователямиСервер.ПолучитьЗначениеПоУмолчанию("КомментироватьХодВыполненияАлгоритмовПроцессов",
								бит_ОбщиеПеременныеСервер.ЗначениеПеременной("глТекущийПользователь"));	
			
	    РежимСообщений = ?(КомментироватьВыполнение,"Все","Ошибки");			
			
		бит_уп_Сервер.ВыполнитьАлгоритм(АлгоритмИзмененияСтатуса,СтруктураКонтекст,РежимСообщений);
		
	КонецЕсли; 
	
КонецПроцедуры // УстановитьСтатус()

#КонецОбласти 

#Область Инициализация

РезСтруктура   = бит_Визирование.ПолучитьСтатусОбъекта(Ссылка);
ДополнительныеСвойства.Вставить("ДатаИзмененияСтатуса",РезСтруктура.Дата);
ДополнительныеСвойства.Вставить("ТекущийСтатус",       ?(ЗначениеЗаполнено(РезСтруктура.Статус),РезСтруктура.Статус,Справочники.бит_СтатусыОбъектов.ПустаяСсылка()));

#КонецОбласти 

#КонецЕсли
