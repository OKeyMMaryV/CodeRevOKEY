&НаКлиенте
Перем ИдентификаторСтрокиПроводки;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	                     	
	ЗаполнитьКэшЗначений();
	// Отображение дополнительных измерений в табличных полях.
	бит_МеханизмДопИзмерений.ОтобразитьДополнительныеИзмеренияВТабличномПоле_Управляемая(ЭтаФорма
																	, "ок_ОперацияБухАксапта"
																	, фКэшЗначений.ИзмеренияДоп
																	, фКэшЗначений.НастройкиИзмерений);
																	
	

	Элементы.ок_ОперацияБухАксапта.Видимость = Объект.бтИсправление;
	Элементы.ок_ОперацияБухАксапта.Видимость = Объект.бтИсправление;
	Если Объект.бтИсправление Тогда
		
		Если ЗначениеЗаполнено(Объект.Ссылка)
			 И НЕ ЗначениеЗаполнено(ок_ОперацияБух)
		Тогда 
			ок_ОперацияБух = Документы.бит_ок_ОперацияАксапты12.ПолучитьСвязаннуюОперациюБУХ(Объект.Ссылка);
		КонецЕсли;
		
		ЗаполнитьТЧОперацияБухАксапта();
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВаучерИсходногоДокумента)
		И НЕ ЗначениеЗаполнено(СсылкаНаИсходныйДокумент)Тогда
		
		СсылкаНаИсходныйДокумент = Документы.бит_ок_ОперацияАксапты12.ПолучитьОперациюАксаптыПоВаучеру(Объект.ВаучерИсходногоДокумента);
	
	КонецЕсли; 
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоТаблицы(
			ок_ОперацияБухАксапта, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
			
	УстановитьУсловноеОформление();		
	
	ок_УправлениеФормами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.бтИсправление И
		 ЗначениеЗаполнено(Объект.Ссылка)
	Тогда 
		ок_ОперацияБух = Документы.бит_ок_ОперацияАксапты12.ПолучитьСвязаннуюОперациюБУХ(Объект.Ссылка);
	КонецЕсли;
	
	УстановитьВидимостьХозрасчетный();
	УстановитьВидимостьМеждународный();
	ОтобразитьИсправления(); 	
	УстановитьДоступностьОборотыАксапты12();
	УстановитьВидимостьКорректировкиРСБУ();
	
	// стандартные действия при создании на сервере
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
		
	Элементы.Группа_ок_СтруктураПодчиненностиБК.Видимость = Пользователи.ЭтоПолноправныйПользователь();
	
	ок_УправлениеФормами.ПриЧтенииНаСервере(ЭтаФорма, Объект);
	
	Если УправлениеДоступом.ЕстьРоль("бит_БК_ГлавныйБК") Тогда 
		
		НоваяКоманда = Команды.Добавить("ок_ТранслироватьВБК");
		НоваяКоманда.Действие  = "ТранслироватьВБККоманда";
		НоваяКоманда.Заголовок = "Транслировать в БК";
		НоваяКоманда.Подсказка = "Транслировать выделенные документы в БК";
		
		НоваяКнопка = Элементы.Добавить("ТранслироватьВБК", Тип("КнопкаФормы"), Элементы.ФормаКоманднаяПанель);
		НоваяКнопка.ИмяКоманды = "ок_ТранслироватьВБК";			

	КонецЕсли;
	
	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
КонецПроцедуры

&НаСервере
Функция ПослеЗаписи_Сервер(глРежимЗаписи)
	
	УстановитьВидимостьКорректировкиРСБУ();
	
	Если ТребуетсяПерезапись = Истина Тогда
		
		Записать();
		ТребуетсяПерезапись = Ложь;
		
	КонецЕсли;
	
	ОтобразитьИсправления();
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПослеЗаписи_Сервер(ПараметрыЗаписи.РежимЗаписи);
	
	Если ДоступноИсправление() Тогда
		Элементы.Исправление.Доступность = Истина;
	Иначе
		Элементы.Исправление.Доступность = Ложь;	
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РежимЗаписи = ПараметрыЗаписи.РежимЗаписи;
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		//Автозаполнение реквизитов Период и Организация
		Для Каждого Проводка Из ТекущийОбъект.Движения.Хозрасчетный Цикл
			
			Проводка.Период      = ТекущийОбъект.Дата;
			Проводка.Организация = ТекущийОбъект.Организация;
			
		КонецЦикла;
		
		ТекущийОбъект.Движения.Хозрасчетный.Записывать = Истина;
		
		// Установим флаг "Загружено с ошибкой" если есть не сопоставленные элементы
		ЕстьНеСопоставленные = Ложь;
		Если ТекущийОбъект.Движения.Хозрасчетный.Количество() > 0 Тогда
			
			// получим все элементы "Не сопоставлено"л
			МассивНеСопоставлено = ПолучитьНеСопоставленные().ВыгрузитьКолонку("Значение");
			
			// обойдём проводки и 
			Для каждого Проводка из ТекущийОбъект.Движения.Хозрасчетный Цикл
				
				//заполнение незаполненной аналитики справочниками "Не сопоставлено"
				Для каждого Субконто из Проводка.СубконтоДт Цикл
					НайдЭлемент = МассивНеСопоставлено.Найти(Субконто.Значение);
					Если НайдЭлемент <> Неопределено Тогда
						ЕстьНеСопоставленные = Истина;
					КонецЕсли;
				КонецЦикла;
				
				Для каждого Субконто из Проводка.СубконтоКт Цикл
					НайдЭлемент = МассивНеСопоставлено.Найти(Субконто.Значение);
					Если НайдЭлемент <> Неопределено Тогда
						ЕстьНеСопоставленные = Истина;
					КонецЕсли;				
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		ТекущийОбъект.ЗагруженоСОшибкой = ЕстьНеСопоставленные;
		
		//Автозаполнение реквизитов Период и Организация
		Для Каждого Проводка Из ТекущийОбъект.Движения.Хозрасчетный Цикл
			
			Проводка.Период      = ТекущийОбъект.Дата;
			Проводка.Организация = ТекущийОбъект.Организация;
			
		КонецЦикла;
		
		ТекущийОбъект.Движения.Хозрасчетный.Записывать = Истина;
		
		// Установим флаг "Загружено с ошибкой" если есть не сопоставленные элементы
		Если ТекущийОбъект.Движения.бит_Дополнительный_2.Количество() > 0 Тогда
			// получим все элементы "Не сопоставлено"л
			
			МассивНеСопоставлено = ПолучитьНеСопоставленные().ВыгрузитьКолонку("Значение");
			
			// обойдём проводки и 
			Для каждого Проводка из ТекущийОбъект.Движения.бит_Дополнительный_2 Цикл
				
				//заполнение незаполненной аналитики справочниками "Не сопоставлено"
				Для каждого Субконто из Проводка.СубконтоДт Цикл
					НайдЭлемент = МассивНеСопоставлено.Найти(Субконто.Значение);
					Если НайдЭлемент <> Неопределено Тогда
						ЕстьНеСопоставленные = Истина;
					КонецЕсли;
				КонецЦикла;
				
				Для каждого Субконто из Проводка.СубконтоКт Цикл
					НайдЭлемент = МассивНеСопоставлено.Найти(Субконто.Значение);
					Если НайдЭлемент <> Неопределено Тогда
						ЕстьНеСопоставленные = Истина;
					КонецЕсли;				
				КонецЦикла;
				
			КонецЦикла;
			
		КонецЕсли;
		
		ТекущийОбъект.ЗагруженоСОшибкой = ЕстьНеСопоставленные;
		
	КонецЕсли;
	
	Если ТекущийОбъект.бтИсправление Тогда 
		СинхронизироватьСДокументомОперация(ТекущийОбъект);	
	КонецЕсли;
	
	ТекущийОбъект.ДополнительныеСвойства.Вставить("ок_ОперацияБух", ок_ОперацияБух);
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИсправлениеПриИзменении(Элемент)
	
	Элементы.СсылкаНаОперацию.Видимость = Объект.бтИсправление 
										  И ЗначениеЗаполнено(ок_ОперацияБух);
	УстановкаОтбораПоРегистру();
	
	Элементы.ок_ОперацияБухАксапта.Видимость = Объект.бтИсправление;
	Если Объект.бтИсправление Тогда 
		ЗаполнитьТЧОперацияБухАксапта();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаОперациюНажатие(Элемент, СтандартнаяОбработка)
	
	Структура_Параметры = Новый Структура("Ключ",ок_ОперацияБух);
	ОткрытьФорму("Документ.ОперацияБух.ФормаОбъекта",Структура_Параметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ВаучерИсходногоДокументаСсылкаНажатие(Элемент, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВаучерИсходногоДокументаСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьЗначение(Объект.ВаучерИсходногоДокументаСсылка);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерЗаявкиНажатие(Элемент, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.НомерЗаявки) Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьЗначение(Объект.НомерЗаявки);
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораОперБухАксаптаСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка)
		
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Элементы.ок_ОперацияБухАксапта.ТекущиеДанные, ПараметрыУстановкиСвойствСубконто(ЭтотОбъект));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоСубконтоАксаптаПриИзменении(Элемент)
	
	УправлениеФормой();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_КорректировкаПроводок

&НаКлиенте
Процедура КорректировкаПроводокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура КорректировкаПроводокПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ОборотыАксапты

&НаКлиенте
Процедура ОборотыАксаптыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыАксаптыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОборотыАксаптыПриИзменении(Элемент)
	
	ОбновитьСуммуДокумента();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ДвиженияБУ

&НаКлиенте
Процедура ТабличноеПолеДвиженияБУСубконтоДтНачалоВыбораСтроки(Элемент, СтандартнаяОбработка,ТекущаяСтрока)

	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Дата",         Объект.Дата);
	СписокПараметров.Вставить("СчетУчета",    ТекущаяСтрока.СчетДт);
	СписокПараметров.Вставить("Организация",  ТекущаяСтрока.Организация);
		
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить("СубконтоДт%Индекс%", "%Индекс%", Индекс);
		Если ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ТекущаяСтрока[ИмяПоля]);
		ИначеЕсли БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьОписаниеТиповДоговора().СодержитТип(ТипЗнч(ТекущаяСтрока[ИмяПоля])) Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ТекущаяСтрока[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ТекущаяСтрока[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ТекущаяСтрока[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;

	СписокПараметров.Вставить("СторонаПроводки", "Дт");
    СписокПараметров.Вставить("ЭтоНовыйДокумент",Объект.Ссылка.Пустая());
		
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, СписокПараметров);

КонецПроцедуры

&НаКлиенте
Процедура ТабличноеПолеДвиженияБУСубконтоКтНачалоВыбораСтроки(Элемент, СтандартнаяОбработка,ТекущаяСтрока)
   
	СписокПараметров = Новый Структура;
	СписокПараметров.Вставить("Дата",         Объект.Дата);
	СписокПараметров.Вставить("СчетУчета",    ТекущаяСтрока.СчетКт);
	Для Индекс = 1 По 3 Цикл
		ИмяПоля = СтрЗаменить("СубконтоКт%Индекс%", "%Индекс%", Индекс);
		Если ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ТекущаяСтрока[ИмяПоля]);
		ИначеЕсли БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьОписаниеТиповДоговора().СодержитТип(ТипЗнч(ТекущаяСтрока[ИмяПоля])) Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ТекущаяСтрока[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ТекущаяСтрока[ИмяПоля]);
		ИначеЕсли ТипЗнч(ТекущаяСтрока[ИмяПоля]) = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ТекущаяСтрока[ИмяПоля]);
		КонецЕсли;
	КонецЦикла;
	СписокПараметров.Вставить("Организация",  ТекущаяСтрока.Организация);
	СписокПараметров.Вставить("СторонаПроводки", "Кт");
    СписокПараметров.Вставить("ЭтоНовыйДокумент",Объект.Ссылка.Пустая());
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтаФорма, Элемент, СтандартнаяОбработка, СписокПараметров);

КонецПроцедуры   

&НаКлиенте
Процедура ТабличноеПолеДвиженияБУСубконтоДтНачалоВыбораРСБУ(Элемент, СтандартнаяОбработка)

	ТабличноеПолеДвиженияБУСубконтоДтНачалоВыбораСтроки(Элемент, СтандартнаяОбработка,Элементы.РСБУ.ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ТабличноеПолеДвиженияБУСубконтоКтНачалоВыбораРСБУ(Элемент, СтандартнаяОбработка)

	ТабличноеПолеДвиженияБУСубконтоКтНачалоВыбораСтроки(Элемент, СтандартнаяОбработка,Элементы.РСБУ.ТекущиеДанные);

КонецПроцедуры  

&НаКлиенте
Процедура ТабличноеПолеДвиженияБУСубконтоДтНачалоВыбораИсправления(Элемент, СтандартнаяОбработка)
	
	ТабличноеПолеДвиженияБУСубконтоДтНачалоВыбораСтроки(Элемент, СтандартнаяОбработка,Элементы.КорректировкаПроводок.ТекущиеДанные);

КонецПроцедуры

&НаКлиенте
Процедура ТабличноеПолеДвиженияБУСубконтоКтНачалоВыбораИсправления(Элемент, СтандартнаяОбработка)
	
	ТабличноеПолеДвиженияБУСубконтоКтНачалоВыбораСтроки(Элемент, СтандартнаяОбработка,Элементы.КорректировкаПроводок.ТекущиеДанные);
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_РСБУ

&НаКлиенте
Процедура РСБУСубконтоДт1ПриИзменении(Элемент)

	РасчетСуммПоНУ("Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура РСБУСубконтоДт2ПриИзменении(Элемент)
	
	РасчетСуммПоНУ("Дт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСубконтоДт3ПриИзменении(Элемент)

	РасчетСуммПоНУ("Дт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСубконтоКт1ПриИзменении(Элемент)

	РасчетСуммПоНУ("Кт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСубконтоКт2ПриИзменении(Элемент)

	РасчетСуммПоНУ("Кт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСубконтоКт3ПриИзменении(Элемент)

	РасчетСуммПоНУ("Кт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСчетДтПриИзменении(Элемент)

	РасчетСуммПоНУ("Дт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСчетКтПриИзменении(Элемент)

	РасчетСуммПоНУ("Кт");

КонецПроцедуры

&НаКлиенте
Процедура РСБУСуммаПриИзменении(Элемент)

	РасчетСуммПоНУ("Дт"); 
	РасчетСуммПоНУ("Кт");

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ОперацияБухАксапта

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоДт1ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
	ПриИзмененииСубконтоЗаполнениеАналитик(,1);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоДт2ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
	ПриИзмененииСубконтоЗаполнениеАналитик(,2);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоДт3ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Дт");
	
	ПриИзмененииСубконтоЗаполнениеАналитик(,3);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоКт1ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
	ПриИзмененииСубконтоЗаполнениеАналитик("Кт",1);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоКт2ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
	ПриИзмененииСубконтоЗаполнениеАналитик("Кт",2);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоКт3ПриИзменении(Элемент)
	
	ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "Кт");
	
	ПриИзмененииСубконтоЗаполнениеАналитик("Кт",3);	
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоДт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоДт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоДт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Дт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоКт1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоКт2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСубконтоКт3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораСубконто("Кт", Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСчетДтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета(ЭтотОбъект, "Дт");
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаСчетКтПриИзменении(Элемент)
	
	ОбработатьИзменениеСчета(ЭтотОбъект, "Кт");
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.ок_ОперацияБухАксапта.ТекущиеДанные;
	ИдСтроки      = Элементы.ок_ОперацияБухАксапта.ТекущаяСтрока;
	
	Если ИдСтроки <> ИдентификаторСтрокиПроводки Тогда
	
		ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
			"ок_ОперацияБухАксаптаСубконтоДт1", "ок_ОперацияБухАксаптаСубконтоДт2", "ок_ОперацияБухАксаптаСубконтоДт3");
		БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетДт, ЭтотОбъект, ПоляФормы, Неопределено, Истина, Ложь);
	
		ПоляФормы = Новый Структура("Субконто1, Субконто2, Субконто3",
			"ок_ОперацияБухАксаптаСубконтоКт1", "ок_ОперацияБухАксаптаСубконтоКт2", "ок_ОперацияБухАксаптаСубконтоКт3");
		БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(ТекущиеДанные.СчетКт, ЭтотОбъект, ПоляФормы, Неопределено, Истина, Ложь);
		
		ИзменитьПараметрыВыбораПолейСубконто(ЭтотОбъект, "", Ложь);
		
		ИдентификаторСтрокиПроводки = ИдСтроки;
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОперацияБухАксаптаПриАктивизацииСтроки(Элемент)
	
	Если Элемент.ТекущиеДанные<> Неопределено Тогда 
		Если ПроверитьПринадлежностьСчетаРБП(Элемент.ТекущиеДанные.СчетДт) Тогда
			Элементы.ок_СоздатьРБП.Доступность = Истина;
		Иначе 
			Элементы.ок_СоздатьРБП.Доступность = Ложь;
		КонецЕсли;	
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТранслироватьВБККоманда(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		Возврат;
	КонецЕсли;
	
	ТранслироватьВБКНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ТранслироватьВБКНаСервере()
	
	Документы.бит_ок_ОперацияАксапты12.ТранслироватьВБК(ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(Объект.Ссылка));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()

	//Количество субконто
	КоличествоСубконтоАксапта = ОК_ОбщегоНазначения.ПолучитьЗначениеУниверсальнойНастройки("Аксапта 12", "Количество субконто Аксапта", 6);
	
	Для Инд = 1 По 12 Цикл
	
		Если КоличествоСубконтоАксапта < Инд Тогда
		
			Элементы["ОборотыАксаптыСубконто1СДт" + Инд].Видимость 		= Ложь;
			Элементы["ОборотыАксаптыСубконтоАксаптаКт" + Инд].Видимость = Ложь;
			Элементы["ОборотыАксаптыСубконто1СКт" + Инд].Видимость 		= Ложь;
			Элементы["ОборотыАксаптыСубконтоАксаптаДт" + Инд].Видимость = Ложь;
		
		КонецЕсли; 
	
	КонецЦикла; 
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();

	// Субконто
	Для Сч = 1 По 3 Цикл

		// Видимость СубконтоДт

		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаСубконтоДт" + Сч);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ок_ОперацияБухАксапта.СубконтоДт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


		// Выделение не заполненного СубконтоДт

		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаСубконтоДт" + Сч);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ок_ОперацияБухАксапта.СубконтоДт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ок_ОперацияБухАксапта.СубконтоДт" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
		

		// Видимость СубконтоКт

		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаСубконтоКт" + Сч);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ок_ОперацияБухАксапта.СубконтоКт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		

		// Выделение не заполненного СубконтоКт
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаСубконтоКт" + Сч);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ок_ОперацияБухАксапта.СубконтоКт" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"ок_ОперацияБухАксапта.СубконтоКт" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
		
	КонецЦикла;

	
	// ПодразделениеДт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаПодразделениеДт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ПодразделениеДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ПодразделениеДт", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));


	// ХозрасчетныйПодразделениеДт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаПодразделениеДт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ПодразделениеДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ПодразделениеКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаПодразделениеКт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ПодразделениеКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ПодразделениеКт", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));


	// ХозрасчетныйПодразделениеКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаПодразделениеКт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ПодразделениеКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// Количество и валюта

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаКоличествоДт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.КоличественныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ВалютныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ХозрасчетныйКоличествоКт, ХозрасчетныйНадписьКоличествоКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаКоличествоКт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.КоличественныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ВалютныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Истина);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ХозрасчетныйВалютаДт, ХозрасчетныйВалютнаяСуммаДт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаВалютаДт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаВалютнаяСуммаДт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ВалютныйДтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);


	// ХозрасчетныйВалютаКт, ХозрасчетныйВалютнаяСуммаКт

	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаВалютаКт");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ок_ОперацияБухАксаптаВалютнаяСуммаКт");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ок_ОперацияБухАксапта.ВалютныйКтДоступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьХозрасчетный()
	
	Если Объект.Движения.Хозрасчетный.Количество() > 0 Тогда
		Элементы.РСБУ.Видимость = Истина;
	Иначе
		Элементы.РСБУ.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьМеждународный()
	
	//Установим видимость только для тех пользователей, у кого есть доступ
	//к подсистеме МСФО
	Элементы.МСФО.Видимость = Ложь;
	РолиПользователяИБ = ПользователиИнформационнойБазы.ТекущийПользователь().Роли;
	Для Каждого Роль из РолиПользователяИБ Цикл
		
		Если Роль.Имя = "бит_му_ПолныеПрава" ИЛИ Роль.Имя = "ПолныеПрава" ИЛИ Роль.Имя = "ок_МСФО_просмотр" Тогда
			Если Объект.Движения.бит_Дополнительный_2.Количество() > 0 Тогда	
				Элементы.МСФО.Видимость = Истина;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКорректировкиРСБУ()
	
	РазрешеноПользователю = РольДоступна("ПолныеПрава") ИЛИ РольДоступна("бит_му_ПолныеПрава");
		
	Элементы.СБ_КорректировкиРСБУ.Видимость = РазрешеноПользователю 
		И Объект.Движения.СБ_КорректировкаРСБУ.Количество() > 0;
		
КонецПроцедуры

&НаСервере
Функция ПолучитьНеСопоставленные()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	бит_ок_НастройкиМеханизмаИмпортаДанных.ИмяНастройки,
		|	бит_ок_НастройкиМеханизмаИмпортаДанных.Значение
		|ИЗ
		|	РегистрСведений.бит_ок_НастройкиМеханизмаИмпортаДанных КАК бит_ок_НастройкиМеханизмаИмпортаДанных
		|ГДЕ
		|	бит_ок_НастройкиМеханизмаИмпортаДанных.Группа = &Группа";

	Запрос.УстановитьПараметр("Группа", "Служебные элементы Не сопоставлено");

	Результат = Запрос.Выполнить();

	Возврат Результат.Выгрузить();
	
КонецФункции

&НаСервере
Процедура УстановитьДоступностьОборотыАксапты12()
	
	Если РольДоступна("бтРедактированиеОперацииАксапты") или РольДоступна("ПолныеПрава") Тогда
		Элементы.РСБУ.ТолькоПросмотр = Ложь;	
	КонецЕсли;
	
	Если Не РольДоступна("бтРедактированиеОперацииАксапты") Тогда
		Если Не РольДоступна("ПолныеПрава") Тогда
			Элементы.ОборотыАксапты.ТолькоПросмотр = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановкаОтбораПоРегистру()

	Если НЕ ок_ОперацияБух = ПредопределенноеЗначение("Документ.ОперацияБух.ПустаяСсылка") 
		И НЕ ИнтеграцияС1СДокументооборотВызовСервера.ЗначениеРеквизитаОбъекта(ок_ОперацияБух, "ПометкаУдаления") Тогда
		
		ОтобразитьИсправления()
		
	ИначеЕсли Объект.бтИсправление Тогда
		
		НаборДок = Объект.Движения.Хозрасчетный.Выгрузить();
		КорректировкаПроводок.Загрузить(НаборДок);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СоздатьДокументОперация_Сервер(ДатаОткрытогоПериодаМСФО, Документ, ТекущийОбъект)
	
	Если ЗначениеЗаполнено(ДатаОткрытогоПериодаМСФО) И Объект.Дата < ДатаОткрытогоПериодаМСФО Тогда		
		
		ДвиженияДок = Документ.Движения.бит_Дополнительный_2;
		ДвиженияДок.Прочитать();
		ТаблицаДвижений = ДвиженияДок.Выгрузить();
		
		Для Каждого Стр Из ТаблицаДвижений Цикл
			Стр.Период = ДатаОткрытогоПериодаМСФО;	
		КонецЦикла;
		
		Документ.Записать(РежимЗаписиДокумента.Запись);
		
		ДвиженияДок.Загрузить(ТаблицаДвижений);
		ДвиженияДок.Записать();
		
	КонецЕсли;
	
	ТребуетсяПерезапись = Истина;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Создан документ " + Документ.Ссылка);
	ок_ОперацияБух = Документ.Ссылка;	

КонецФункции

&НаСервере
Процедура СоздатьДокументОперация(ТекущийОбъект)
	Документ = Документы.ОперацияБух.СоздатьДокумент();
	Документ.Организация = Объект.Организация;
	Документ.Дата = Объект.Дата;
	Документ.СпособЗаполнения = "Вручную";
	Документ.СуммаОперации = 0;
	Документ.ТиповаяОперация = Объект.Ссылка;
	Документ.Содержание = "Создано на основании документа " + Объект.Ссылка;
//ОК Ванюков К. +//  при проставлении в операции АХ галки "Исправление" и создании операции бух., в нее копировался реквизит ID исходной операции АХ // 2013-02-20
    Документ.ОК_ID_Разноска	= Объект.ОК_ID_Разноска;
//ОК Ванюков К. -	
//ОК Калинин М. 260313
	Документ.Ответственный= ПользователиКлиентСервер.ТекущийПользователь();
//ОК Калинин М. 

	Документ.Записать(РежимЗаписиДокумента.Запись);

	// BIT AMerkulov 03092014 ++
	
	РБ_МУ = СБ_МеханизмКонтроляТрансляцииПовтИсп.ОбъектМетаданных_РБ_МУ();	
	ДатыОткрытогоПериода = бит_КонтрольЗакрытогоПериода.ПолучитьДатыОткрытогоПериода(Объект.Организация, РБ_МУ);
	ДатаОткрытогоПериодаМСФО = ДатыОткрытогоПериода[РБ_МУ];
	
	СоздатьДокументОперация_Сервер(ДатаОткрытогоПериодаМСФО,Документ, ТекущийОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОтобразитьИсправления()
	
	Элементы.СсылкаНаОперацию.Видимость = Объект.бтИсправление И ЗначениеЗаполнено(ок_ОперацияБух);
	Элементы.Исправление.Доступность = ДоступноИсправление();

КонецПроцедуры

&НаСервере
Функция СинхронизироватьСДокументомОперация(ТекущийОбъект)
	
	Если ок_ОперацияБухАксапта.Количество()=0 Тогда 
		Возврат "";
	КонецЕсли;
	
	НачатьТранзакцию();
	
	Если Не ЗначениеЗаполнено(ок_ОперацияБух) Тогда
		
		пОперацияОбъект = Документы.ОперацияБух.СоздатьДокумент();
		
		пОперацияОбъект.Дата = Объект.Дата;
		пОперацияОбъект.СпособЗаполнения		 = "Сторно";
		пОперацияОбъект.СторнируемыйДокумент  = Объект.Ссылка;
		пОперацияОбъект.Организация           = Объект.Организация;
						
		пОперацияОбъект.Содержание	  = "Создано на основании документа " + Объект.Ссылка;
		пОперацияОбъект.ОК_ID_Разноска = Объект.ОК_ID_Разноска;
		пОперацияОбъект.Ответственный  = ПользователиКлиентСервер.ТекущийПользователь();
		
		Если Не ЗначениеЗаполнено(пОперацияОбъект.Ссылка) Тогда 
			
			пОперацияОбъект.ОбменДанными.Загрузка = Истина;
			
			Попытка	
				
				пОперацияОбъект.Записать(РежимЗаписиДокумента.Запись);
				ок_ОперацияБух = пОперацияОбъект.Ссылка;
				
			Исключение
				
				ОтменитьТранзакцию();
				пТекстОшибки = НСтр("ru = 'Не удалось создать операцию бух. по причине:%1'");
				пТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(пТекстОшибки, ОписаниеОшибки());
				Возврат пТекстОшибки;
				
			КонецПопытки;	
			
		КонецЕсли;
		
	Иначе 
		
		пОперацияОбъект = ок_ОперацияБух.ПолучитьОбъект();
		пОперацияОбъект.СторнируемыйДокумент  = Объект.Ссылка;
		пОперацияОбъект.Организация           = Объект.Организация;
						
		пОперацияОбъект.Содержание			  = "Создано на основании документа " + Объект.Ссылка;
		пОперацияОбъект.ОК_ID_Разноска		  = Объект.ОК_ID_Разноска;
		пОперацияОбъект.Ответственный 	      = ПользователиКлиентСервер.ТекущийПользователь();
		
	КонецЕсли;   
	
	пОперацияОбъект.ОбменДанными.Загрузка = Ложь;
	пОперацияОбъект = ок_ОперацияБух.ПолучитьОбъект();	
	
	СтруктураНеСопоставлено = Новый Структура("Сценарий, СтатьяОборотов");	
 	СтруктураНеСопоставлено.Сценарий 		= бит_БК_Общий.ПолучитьЗначениеНастройкиМеханизмаИмпортаДанных("Служебные элементы Не сопоставлено", "Сценарии", ПредопределенноеЗначение("Справочник.СценарииПланирования.ПустаяСсылка"));
 	СтруктураНеСопоставлено.СтатьяОборотов 	= бит_БК_Общий.ПолучитьЗначениеНастройкиМеханизмаИмпортаДанных("Служебные элементы Не сопоставлено", "Статьи оборотов", ПредопределенноеЗначение("Справочник.бит_СтатьиОборотов.ПустаяСсылка"));	
	
	пОперацияОбъект.Движения.Хозрасчетный.Записывать = Истина;	
	
	пСвойствоСчетаКэш = Новый Соответствие;
	
	ТекНомерСтроки = 1;
			
	Для Каждого пДвижение ИЗ Объект.Движения.Хозрасчетный Цикл 
				
		пСтрокаХР = пОперацияОбъект.Движения.Хозрасчетный.Добавить();
		ЗаполнитьЗначенияСвойств(пСтрокаХР, пДвижение);		
					
		БухгалтерскийУчет.УстановитьСубконтоПоКешуСвойствСчета(пСтрокаХР.СчетДт, пСтрокаХР.СубконтоДт, пСвойствоСчетаКэш, пДвижение.СубконтоДт1, пДвижение.СубконтоДт2, пДвижение.СубконтоДт3);		
		БухгалтерскийУчет.УстановитьСубконтоПоКешуСвойствСчета(пСтрокаХР.СчетКт, пСтрокаХР.СубконтоКт, пСвойствоСчетаКэш, пДвижение.СубконтоКт1, пДвижение.СубконтоКт2, пДвижение.СубконтоКт3);
		
		пСтрокаХР.Регистратор = ок_ОперацияБух;		
		
		Если ЗначениеЗаполнено(пСтрокаХР.Сумма) Тогда 
			пСтрокаХР.Сумма			  = -пСтрокаХР.Сумма;
		КонецЕсли;
		         
		Если ЗначениеЗаполнено(пСтрокаХР.ВалютнаяСуммаДт) Тогда 
			пСтрокаХР.ВалютнаяСуммаДт = -пСтрокаХР.ВалютнаяСуммаДт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.ВалютнаяСуммаКт) Тогда
			пСтрокаХР.ВалютнаяСуммаКт = -пСтрокаХР.ВалютнаяСуммаКт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.КоличествоДт) Тогда
			пСтрокаХР.КоличествоДт	  = -пСтрокаХР.КоличествоДт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.КоличествоКт) Тогда
			пСтрокаХР.КоличествоКт	  = -пСтрокаХР.КоличествоКт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.СуммаВРДт) Тогда
			пСтрокаХР.СуммаВРДт 	  = -пСтрокаХР.СуммаВРДт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.СуммаНУДт) Тогда
			пСтрокаХР.СуммаНУДт 	  = -пСтрокаХР.СуммаНУДт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.СуммаПРДт) Тогда
			пСтрокаХР.СуммаПРДт		  = -пСтрокаХР.СуммаПРДт;		
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.СуммаВРКт) Тогда
			пСтрокаХР.СуммаВРКт		  = -пСтрокаХР.СуммаВРКт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.СуммаНУКт) Тогда
			пСтрокаХР.СуммаНУКт		  = -пСтрокаХР.СуммаНУКт;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(пСтрокаХР.СуммаПРКт) Тогда
			пСтрокаХР.СуммаПРКт		  = -пСтрокаХР.СуммаПРКт;	
		КонецЕсли;
		
		пСтрокаХР.Активность 	  = Истина;
							
		ТекНомерСтроки = ТекНомерСтроки + 1;
		
	КонецЦикла;
	
	пНомерСтрокиОпБ = 1;
	мдРесурсыОпБ = Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам.Ресурсы;
	
	Для Каждого пДвижение ИЗ Объект.Движения.бит_ОборотыПоБюджетам Цикл 
		
		пСтрокаОпБ = пОперацияОбъект.Движения.бит_ОборотыПоБюджетам.Добавить();
		ЗаполнитьЗначенияСвойств(пСтрокаОпБ, пДвижение);		
		
		пСтрокаОпБ.Регистратор = ок_ОперацияБух;
		
		Для Каждого Ресурс Из мдРесурсыОпБ Цикл 
			Если ЗначениеЗаполнено(пСтрокаОпБ[Ресурс.Имя]) И
				 ТипЗнч(пСтрокаОпБ[Ресурс.Имя]) = Тип("Число")
			Тогда 
			    пСтрокаОпБ[Ресурс.Имя] = -пСтрокаОпБ[Ресурс.Имя];
			КонецЕсли;
		КонецЦикла;		
				
		пСтрокаОпБ.Активность 	  = Истина;
					
		пНомерСтрокиОпБ = пНомерСтрокиОпБ + 1;
		
	КонецЦикла;
			
	пИсточник = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыБухгалтерии.Хозрасчетный);
	пПриемник = бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам);
	
	пВалютаУпрУчета = Константы.бит_ВалютаУправленческогоУчета.Получить();
	п19Сч = ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям");	
	
	соотСценарийОчищаемыеПоля = Новый Соответствие;
	мдЗнчПеречисления = Метаданные.Перечисления.бит_БК_ИзмеренияКонтроляБюджета.ЗначенияПеречисления;
	КолонкиОпБ = пОперацияОбъект.Движения.бит_ОборотыПоБюджетам.Выгрузить().Колонки;
		
	Для Каждого пСтрокаОперацияАксапта Из ок_ОперацияБухАксапта Цикл 
					
		пСтрокаХР = пОперацияОбъект.Движения.Хозрасчетный.Добавить();
		ЗаполнитьЗначенияСвойств(пСтрокаХР, пСтрокаОперацияАксапта);		
		
		БухгалтерскийУчет.УстановитьСубконтоПоКешуСвойствСчета(пСтрокаХР.СчетДт, пСтрокаХР.СубконтоДт, пСвойствоСчетаКэш, пСтрокаОперацияАксапта.СубконтоДт1, пСтрокаОперацияАксапта.СубконтоДт2, пСтрокаОперацияАксапта.СубконтоДт3);		
		БухгалтерскийУчет.УстановитьСубконтоПоКешуСвойствСчета(пСтрокаХР.СчетКт, пСтрокаХР.СубконтоКт, пСвойствоСчетаКэш, пСтрокаОперацияАксапта.СубконтоКт1, пСтрокаОперацияАксапта.СубконтоКт2, пСтрокаОперацияАксапта.СубконтоКт3);
		
		пСтрокаХР.Регистратор = ок_ОперацияБух;
				
		пСтрокаХР.Активность  = Истина;
				
		Если фКэшЗначений.Свойство("ИсправленияПоБК") И
			 фКэшЗначений.ИсправленияПоБК
		Тогда 
		
			Если Не ЗначениеЗаполнено(пСтрокаОперацияАксапта.Сценарий) Тогда 
				Продолжить;
			КонецЕсли;
					
			СтрокаОпБ = пОперацияОбъект.Движения.бит_ОборотыПоБюджетам.Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаОпБ, пСтрокаОперацияАксапта);			
			
			// оформим сценарий
			СтрокаОпБ.Сценарий = ОК_ОбщегоНазначения.ОпределитьСценарийПоСтатьеОборотов(СтрокаОпБ.СтатьяОборотов, СтруктураНеСопоставлено);
			СтрокаОпБ.Регистратор = ок_ОперацияБух;
			СтрокаОпБ.Активность  = Истина;
			
			//Расчет сумм (начало)
			СтрокаОпБ.СуммаРегл = пСтрокаОперацияАксапта.Сумма;
			
			сохрСтавкаНДС = СтрокаОпБ.СтавкаНДС;
			ВалютаБылаЗаполнена = ЗначениеЗаполнено(СтрокаОпБ.Валюта);
			
			СтрокаОпБ.СтавкаНДС = Неопределено;
			Если НЕ ВалютаБылаЗаполнена Тогда 
				СтрокаОпБ.Валюта = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
			КонецЕсли;
			
			мИсклчаемые = Новый Массив;
			мИсклчаемые.Добавить("СуммаРегл");					
			
			пВалютаСценария = СтрокаОпБ.Сценарий.Валюта;
			Если Не ЗначениеЗаполнено(пВалютаСценария) Тогда 
				пВалютаСценария = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
			КонецЕсли;
			
			ОК_ОбщегоНазначения.ПересчитатьСуммыОпБПоСуммеРегл(СтрокаОпБ,мИсклчаемые,пВалютаСценария,пВалютаУпрУчета,Объект.Дата); 	
			
			Если НЕ ВалютаБылаЗаполнена Тогда 
				СтрокаОпБ.Валюта = Неопределено;
			КонецЕсли;
			
			СтрокаОпБ.СтавкаНДС = сохрСтавкаНДС;
									
			Если пСтрокаОперацияАксапта.СчетДт.ПринадлежитЭлементу(п19Сч) ИЛИ
				 пСтрокаОперацияАксапта.СчетКт.ПринадлежитЭлементу(п19Сч)
			Тогда 
			    СтрокаОпБ.Сумма			 = 0;
				СтрокаОпБ.СуммаРегл		 = 0;
				СтрокаОпБ.СуммаУпр		 = 0;
				СтрокаОпБ.СуммаСценарий	 = 0;
			Иначе
			    СтрокаОпБ.СуммаСНДС			 = СтрокаОпБ.Сумма;
				СтрокаОпБ.СуммаСНДСРегл		 = СтрокаОпБ.СуммаРегл;
				СтрокаОпБ.СуммаСНДСУпр		 = СтрокаОпБ.СуммаУпр;
				СтрокаОпБ.СуммаСНДССценарий	 = СтрокаОпБ.СуммаСценарий;				
			КонецЕсли;				 

			//Расчет сумм (конец)
			
			СтрокаОпБ.бит_БК_Организация = Объект.Организация;
			
			СтрокаСоотЗаписейТрансляции = пОперацияОбъект.Движения.бит_СоответствиеЗаписейТрансляции.Добавить();
			СтрокаСоотЗаписейТрансляции.Источник			 = пИсточник;
			СтрокаСоотЗаписейТрансляции.Приемник			 = пПриемник;
			СтрокаСоотЗаписейТрансляции.Организация			 = Объект.Организация;
			СтрокаСоотЗаписейТрансляции.НомерСтрокиИсточника = ТекНомерСтроки;
			СтрокаСоотЗаписейТрансляции.НомерСтрокиПриемника = пНомерСтрокиОпБ;
			
			СтрокаСоотЗаписейТрансляции.Регистратор = ок_ОперацияБух;
			СтрокаСоотЗаписейТрансляции.Активность  = Истина;
			
			бит_КонтрольПоБюджету.ОчиститьНеиспользуемыеИзмеренияКонтроля(СтрокаОпБ,соотСценарийОчищаемыеПоля,мдЗнчПеречисления,КолонкиОпБ);
			
			ТекНомерСтроки  = ТекНомерСтроки  + 1;
			пНомерСтрокиОпБ = пНомерСтрокиОпБ + 1;
			
		КонецЕсли;
	КонецЦикла;
	
	Если пОперацияОбъект.Движения.бит_ОборотыПоБюджетам.Количество() Тогда 
		пОперацияОбъект.Движения.бит_ОборотыПоБюджетам.Записывать = Истина;
		пОперацияОбъект.Движения.бит_СоответствиеЗаписейТрансляции.Записывать = Истина;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.НомерЗаявки) Тогда
		пОперацияОбъект.ДополнительныеСвойства.Вставить("ок_ФВБ"	  , Объект.НомерЗаявки);
		пОперацияОбъект.ДополнительныеСвойства.Вставить("Аналитика"	  , ПланыВидовХарактеристик.бит_ВидыДополнительныхАналитик.НайтиПоКоду("NЗАЯВКИ"));			
	КонецЕсли;      		 
	
	пОперацияОбъект.Записать(РежимЗаписиДокумента.Запись);
	
	ЗафиксироватьТранзакцию();
	
	Возврат Истина;

КонецФункции

&НаСервере
Процедура ЗаполнитьДвиженияПоКорректировке(Движение, Строка, МетаданныеОбъект)

	ЗаполнитьЗначенияСвойств(Движение, Строка,,"Период,Регистратор");
	Движение.Активность = Истина;
	// субконто
	Если МетаданныеОбъект.Корреспонденция Тогда
				
		Для каждого Субконто Из Строка.СубконтоДт Цикл
			Если ЗначениеЗаполнено(Субконто.Ключ)Тогда
				Движение.СубконтоДт[Субконто.Ключ] = Субконто.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого Субконто Из Строка.СубконтоКт Цикл
			Если ЗначениеЗаполнено(Субконто.Ключ)Тогда
				Движение.СубконтоКт[Субконто.Ключ] = Субконто.Значение;
			КонецЕсли;
		КонецЦикла;
		
	Иначе

		Для каждого Субконто Из Строка.Субконто Цикл
			Движение.Субконто[Субконто.Ключ] = Субконто.Значение;
		КонецЦикла;

	КонецЕсли;
	
	// ресурсы
	Для Каждого МДОбъект из МетаданныеОбъект.Ресурсы Цикл

		Если МДОбъект.ПризнакУчета = Неопределено Тогда
			Движение[МДОбъект.Имя] =  Строка[МДОбъект.Имя];
		Иначе

			Если ЗначениеЗаполнено(Строка[МДОбъект.Имя + "Дт"]) Тогда
				Движение[МДОбъект.Имя + "Дт"] =  Строка[МДОбъект.Имя + "Дт"];
			КонецЕсли;

			Если ЗначениеЗаполнено(Строка[МДОбъект.Имя + "Кт"]) Тогда
				Движение[МДОбъект.Имя + "Кт"] =  Строка[МДОбъект.Имя + "Кт"];
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ЗаполнитьДвиженияСторноПоРегиструБухгалтерии()

&НаСервере
Процедура ЗаполнитьДвиженияСторноПоРегиструБухгалтерии(Движение, Строка, МетаданныеОбъект)

	ЗаполнитьЗначенияСвойств(Движение, Строка,,"Период,Регистратор");
	Движение.Активность = Истина;
	// субконто
	Если МетаданныеОбъект.Корреспонденция Тогда

		Для каждого Субконто Из Строка.СубконтоДт Цикл
			Если ЗначениеЗаполнено(Субконто.Ключ)Тогда
				Движение.СубконтоДт[Субконто.Ключ] = Субконто.Значение;
			КонецЕсли;
		КонецЦикла;
		
		Для каждого Субконто Из Строка.СубконтоКт Цикл
			Если ЗначениеЗаполнено(Субконто.Ключ)Тогда
				Движение.СубконтоКт[Субконто.Ключ] = Субконто.Значение;
			КонецЕсли;
		КонецЦикла;

	Иначе

		Для каждого Субконто Из Строка.Субконто Цикл
			Движение.Субконто[Субконто.Ключ] = Субконто.Значение;
		КонецЦикла;

	КонецЕсли;
	
	// ресурсы
	Для Каждого МДОбъект из МетаданныеОбъект.Ресурсы Цикл

		Если МДОбъект.ПризнакУчета = Неопределено Тогда
			Движение[МДОбъект.Имя] = - Строка[МДОбъект.Имя];
		Иначе

			Если ЗначениеЗаполнено(Строка[МДОбъект.Имя + "Дт"]) Тогда
				Движение[МДОбъект.Имя + "Дт"] = - Строка[МДОбъект.Имя + "Дт"];
			КонецЕсли;

			Если ЗначениеЗаполнено(Строка[МДОбъект.Имя + "Кт"]) Тогда
				Движение[МДОбъект.Имя + "Кт"] = - Строка[МДОбъект.Имя + "Кт"];
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;

КонецПроцедуры // ЗаполнитьДвиженияСторноПоРегиструБухгалтерии()

&НаКлиенте
Процедура РасчетСуммПоНУ(ВидКорреспонденции)
	
	ТекущиеДанные = Элементы.КорректировкаПроводок.ТекущиеДанные;
	
	ТекущиеДанные["СуммаПР" + ВидКорреспонденции] = 0;
	
	ДанныеСчета = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ТекущиеДанные["Счет" + ВидКорреспонденции]);
	
	Если ДанныеСчета.НалоговыйУчет  Тогда
		ТекущиеДанные["СуммаНУ" + ВидКорреспонденции] = ТекущиеДанные.Сумма;		
	Иначе
		ТекущиеДанные["СуммаНУ" + ВидКорреспонденции] = 0;			
	КонецЕсли;
	
	ЭтоСубконтоНеУчитываемыеВЦеляхНалогообложения = Ложь;
	ЭтоСубконтоПрочиеДоходыИРасходыПринятиеКналоговомуУчету = Ложь;
	
	Для НомерСубконто = 1 По ДанныеСчета.КоличествоСубконто цикл
		ЭтоСубконтоНеУчитываемыеВЦеляхНалогообложения = ЭтоСубконтоНеУчитываемыеВЦеляхНалогообложения(ТекущиеДанные["Субконто" 	+ ВидКорреспонденции + НомерСубконто]);
		Если ЭтоСубконтоНеУчитываемыеВЦеляхНалогообложения Тогда Прервать; КонецЕсли;
	КонецЦикла;
	
	Для НомерСубконто = 1 По ДанныеСчета.КоличествоСубконто цикл
		ЭтоСубконтоПрочиеДоходыИРасходыПринятиеКналоговомуУчету = ЭтоСубконтоПрочиеДоходыИРасходыПринятиеКналоговомуУчету(ТекущиеДанные["Субконто" 	+ ВидКорреспонденции + НомерСубконто]);
		Если ЭтоСубконтоПрочиеДоходыИРасходыПринятиеКналоговомуУчету Тогда Прервать; КонецЕсли;
	КонецЦикла;

			
	Если ЭтоСубконтоНеУчитываемыеВЦеляхНалогообложения Тогда
			ТекущиеДанные["СуммаПР" + ВидКорреспонденции] = ТекущиеДанные.Сумма;
			ТекущиеДанные["СуммаНУ" + ВидКорреспонденции] = 0;	
	КонецЕсли;

	Если ЭтоСубконтоПрочиеДоходыИРасходыПринятиеКналоговомуУчету Тогда
			ТекущиеДанные["СуммаПР" + ВидКорреспонденции] = ТекущиеДанные.Сумма;
			ТекущиеДанные["СуммаНУ" + ВидКорреспонденции]= 0;				
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЭтоСубконтоПрочиеДоходыИРасходыПринятиеКналоговомуУчету(Субконто)
	
	Возврат ТипЗнч(Субконто) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы")
		И ЗначениеЗаполнено(Субконто)
		И Субконто.ПринятиеКналоговомуУчету = Ложь;
		
КонецФункции
	
&НаСервере
Функция ЭтоСубконтоНеУчитываемыеВЦеляхНалогообложения(Субконто)
	
	Возврат ТипЗнч(Субконто) = Тип("СправочникСсылка.СтатьиЗатрат") И Субконто <> Неопределено 
		И Субконто.ВидРасходовНУ 	= Перечисления.ВидыРасходовНУ.НеУчитываемыеВЦеляхНалогообложения;
		
КонецФункции

&НаСервере
Процедура ЗаполнитьТЧОперацияБухАксапта()
	
	ок_ОперацияБухАксапта.Очистить();
		
	Если Модифицированность 
		ИЛИ НЕ ЗначениеЗаполнено(ок_ОперацияБух) 
	Тогда 
		
		пЗапрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СоотЗаписейТрансляцииОбАксЖРБП.Регистратор КАК ОперацияАксапты,
		|	СоотЗаписейТрансляцииОбАксЖРБП.НомерСтрокиПриемника КАК НомерСтрокиРБП,
		|	СоотЗаписейТрансляцииОбАксОпБ.НомерСтрокиПриемника КАК НомерСтрокиОпБ
		|ПОМЕСТИТЬ ВТ0_СоответствиеЖРПБ_ОпБ
		|ИЗ
		|	РегистрСведений.бит_СоответствиеЗаписейТрансляции.СрезПоследних(
		|			&Дата,
		|			Регистратор = &ОперацияАксапты
		|				И Источник = &ОборотыАксапты
		|				И Приемник = &ЖурналПроводок) КАК СоотЗаписейТрансляцииОбАксЖРБП
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.бит_СоответствиеЗаписейТрансляции.СрезПоследних(
		|				&Дата,
		|				Регистратор = &ОперацияАксапты
		|					И Источник = &ОборотыАксапты
		|					И Приемник = &ОборотыПоБюджетам) КАК СоотЗаписейТрансляцииОбАксОпБ
		|		ПО СоотЗаписейТрансляцииОбАксЖРБП.Регистратор = СоотЗаписейТрансляцииОбАксОпБ.Регистратор
		|			И СоотЗаписейТрансляцииОбАксЖРБП.НомерСтрокиИсточника = СоотЗаписейТрансляцииОбАксОпБ.НомерСтрокиИсточника
		|
		|СГРУППИРОВАТЬ ПО
		|	СоотЗаписейТрансляцииОбАксЖРБП.Регистратор,
		|	СоотЗаписейТрансляцииОбАксОпБ.НомерСтрокиПриемника,
		|	СоотЗаписейТрансляцииОбАксЖРБП.НомерСтрокиПриемника
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	НомерСтрокиРБП,
		|	НомерСтрокиОпБ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ХозрасчетныйДвиженияССубконто.СчетДт КАК СчетДт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК СубконтоДт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт2 КАК СубконтоДт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт3 КАК СубконтоДт3,
		|	ХозрасчетныйДвиженияССубконто.СчетКт КАК СчетКт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК СубконтоКт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК СубконтоКт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК СубконтоКт3,
		|	ХозрасчетныйДвиженияССубконто.ВалютаДт КАК ВалютаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютаКт КАК ВалютаКт,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт КАК ПодразделениеДт,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт КАК ПодразделениеКт,
		|	ХозрасчетныйДвиженияССубконто.Сумма КАК Сумма,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоДт КАК КоличествоДт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоКт КАК КоличествоКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУДт КАК СуммаНУДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУКт КАК СуммаНУКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаПРДт КАК СуммаПРДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаПРКт КАК СуммаПРКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаВРДт КАК СуммаВРДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаВРКт КАК СуммаВРКт,
		|	ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание,
		|	бит_ОборотыПоБюджетам.Сценарий КАК Сценарий,
		|	бит_ОборотыПоБюджетам.ЦФО КАК ЦФО,
		|	бит_ОборотыПоБюджетам.СтатьяОборотов КАК СтатьяОборотов,
		|	бит_ОборотыПоБюджетам.Контрагент КАК Контрагент,
		|	бит_ОборотыПоБюджетам.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	бит_ОборотыПоБюджетам.Проект КАК Проект,
		|	бит_ОборотыПоБюджетам.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	бит_ОборотыПоБюджетам.БанковскийСчет КАК БанковскийСчет,
		|	бит_ОборотыПоБюджетам.Аналитика_1 КАК Аналитика_1,
		|	бит_ОборотыПоБюджетам.Аналитика_2 КАК Аналитика_2,
		|	бит_ОборотыПоБюджетам.Аналитика_3 КАК Аналитика_3,
		|	бит_ОборотыПоБюджетам.Аналитика_4 КАК Аналитика_4,
		|	бит_ОборотыПоБюджетам.Аналитика_5 КАК Аналитика_5,
		|	бит_ОборотыПоБюджетам.Аналитика_6 КАК Аналитика_6,
		|	бит_ОборотыПоБюджетам.Аналитика_7 КАК Аналитика_7,
		|	бит_ОборотыПоБюджетам.Количество КАК Количество,
		|	бит_ОборотыПоБюджетам.Сумма КАК СуммаОпБ,
		|	бит_ОборотыПоБюджетам.СуммаРегл КАК СуммаРегл,
		|	бит_ОборотыПоБюджетам.СуммаУпр КАК СуммаУпр,
		|	бит_ОборотыПоБюджетам.СуммаСценарий КАК СуммаСценарий,
		|	бит_ОборотыПоБюджетам.СуммаСНДС КАК СуммаСНДС,
		|	бит_ОборотыПоБюджетам.СуммаСНДСРегл КАК СуммаСНДСРегл,
		|	бит_ОборотыПоБюджетам.СуммаСНДСУпр КАК СуммаСНДСУпр,
		|	бит_ОборотыПоБюджетам.СуммаСНДССценарий КАК СуммаСНДССценарий,
		|	бит_ОборотыПоБюджетам.Валюта КАК Валюта,
		|	ВЫБОР
		|		КОГДА бит_ОборотыПоБюджетам.Регистратор ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ОтображатьКолонкиБК,
		|	бит_ОборотыПоБюджетам.Период КАК Период		
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор = &ОперацияАксапты, , ) КАК ХозрасчетныйДвиженияССубконто
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ0_СоответствиеЖРПБ_ОпБ КАК ВТ0_СоответствиеЖРПБ_ОпБ
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.бит_ОборотыПоБюджетам КАК бит_ОборотыПоБюджетам
		|			ПО ВТ0_СоответствиеЖРПБ_ОпБ.ОперацияАксапты = бит_ОборотыПоБюджетам.Регистратор
		|				И ВТ0_СоответствиеЖРПБ_ОпБ.НомерСтрокиОпБ = бит_ОборотыПоБюджетам.НомерСтроки
		|		ПО (ВТ0_СоответствиеЖРПБ_ОпБ.ОперацияАксапты = ХозрасчетныйДвиженияССубконто.Регистратор)
		|			И (ВТ0_СоответствиеЖРПБ_ОпБ.НомерСтрокиРБП = ХозрасчетныйДвиженияССубконто.НомерСтроки)");
		
		пЗапрос.УстановитьПараметр("Дата"				,	Объект.Дата);
		пЗапрос.УстановитьПараметр("ОперацияАксапты"	,	Объект.Ссылка);
		пЗапрос.УстановитьПараметр("ОборотыАксапты" 	, 	бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыНакопления.бит_ок_ОборотыАксапты12));
		пЗапрос.УстановитьПараметр("ЖурналПроводок"		, 	бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыБухгалтерии.Хозрасчетный));
		пЗапрос.УстановитьПараметр("ОборотыПоБюджетам"  , 	бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам));
		
	Иначе
		
		пЗапрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ХозрасчетныйДвиженияССубконто.СчетДт КАК СчетДт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт1 КАК СубконтоДт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт2 КАК СубконтоДт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт3 КАК СубконтоДт3,
		|	ХозрасчетныйДвиженияССубконто.СчетКт КАК СчетКт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1 КАК СубконтоКт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт2 КАК СубконтоКт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт3 КАК СубконтоКт3,
		|	ХозрасчетныйДвиженияССубконто.ВалютаДт КАК ВалютаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютаКт КАК ВалютаКт,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт КАК ПодразделениеДт,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт КАК ПодразделениеКт,
		|	ХозрасчетныйДвиженияССубконто.Сумма КАК Сумма,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт КАК ВалютнаяСуммаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт КАК ВалютнаяСуммаКт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоДт КАК КоличествоДт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоКт КАК КоличествоКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУДт КАК СуммаНУДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУКт КАК СуммаНУКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаПРДт КАК СуммаПРДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаПРКт КАК СуммаПРКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаВРДт КАК СуммаВРДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаВРКт КАК СуммаВРКт,
		|	ХозрасчетныйДвиженияССубконто.Содержание КАК Содержание,
		|	бит_ОборотыПоБюджетам.Сценарий КАК Сценарий,
		|	бит_ОборотыПоБюджетам.ЦФО КАК ЦФО,
		|	бит_ОборотыПоБюджетам.СтатьяОборотов КАК СтатьяОборотов,
		|	бит_ОборотыПоБюджетам.Контрагент КАК Контрагент,
		|	бит_ОборотыПоБюджетам.ДоговорКонтрагента КАК ДоговорКонтрагента,
		|	бит_ОборотыПоБюджетам.Проект КАК Проект,
		|	бит_ОборотыПоБюджетам.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	бит_ОборотыПоБюджетам.БанковскийСчет КАК БанковскийСчет,
		|	бит_ОборотыПоБюджетам.Аналитика_1 КАК Аналитика_1,
		|	бит_ОборотыПоБюджетам.Аналитика_2 КАК Аналитика_2,
		|	бит_ОборотыПоБюджетам.Аналитика_3 КАК Аналитика_3,
		|	бит_ОборотыПоБюджетам.Аналитика_4 КАК Аналитика_4,
		|	бит_ОборотыПоБюджетам.Аналитика_5 КАК Аналитика_5,
		|	бит_ОборотыПоБюджетам.Аналитика_6 КАК Аналитика_6,
		|	бит_ОборотыПоБюджетам.Аналитика_7 КАК Аналитика_7,
		|	СУММА(бит_ОборотыПоБюджетам.Количество) КАК Количество,
		|	СУММА(бит_ОборотыПоБюджетам.Сумма) КАК СуммаОпБ,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаРегл) КАК СуммаРегл,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаУпр) КАК СуммаУпр,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаСценарий) КАК СуммаСценарий,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаСНДС) КАК СуммаСНДС,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаСНДСРегл) КАК СуммаСНДСРегл,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаСНДСУпр) КАК СуммаСНДСУпр,
		|	СУММА(бит_ОборотыПоБюджетам.СуммаСНДССценарий) КАК СуммаСНДССценарий,
		|	бит_ОборотыПоБюджетам.Валюта КАК Валюта,
		|	ВЫБОР
		|		КОГДА бит_ОборотыПоБюджетам.Регистратор ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ КАК ОтображатьКолонкиБК,
		|	МИНИМУМ(бит_ОборотыПоБюджетам.Период) КАК Период
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор = &ОперацияБУХ, , ) КАК ХозрасчетныйДвиженияССубконто
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_СоответствиеЗаписейТрансляции.СрезПоследних(
		|				&Дата,
		|				Регистратор = &ОперацияБУХ
		|					И Источник = &ЖурналПроводок
		|					И Приемник = &ОборотыПоБюджетам) КАК СоотЗаписейТрансляцииОбАксЖРБП
		|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.бит_ОборотыПоБюджетам КАК бит_ОборотыПоБюджетам
		|			ПО ((ВЫРАЗИТЬ(СоотЗаписейТрансляцииОбАксЖРБП.Регистратор КАК Документ.ОперацияБух)) = бит_ОборотыПоБюджетам.Регистратор)
		|				И ((ВЫРАЗИТЬ(СоотЗаписейТрансляцииОбАксЖРБП.НомерСтрокиПриемника КАК ЧИСЛО)) = бит_ОборотыПоБюджетам.НомерСтроки)
		|		ПО (СоотЗаписейТрансляцииОбАксЖРБП.Регистратор = ХозрасчетныйДвиженияССубконто.Регистратор)
		|			И (СоотЗаписейТрансляцииОбАксЖРБП.НомерСтрокиИсточника = ХозрасчетныйДвиженияССубконто.НомерСтроки)
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Хозрасчетный.ДвиженияССубконто(, , Регистратор = &ОперацияАксапта, , ) КАК ХозрасчетныйДвиженияССубконтоАксапта
		|		ПО ХозрасчетныйДвиженияССубконто.Период = ХозрасчетныйДвиженияССубконтоАксапта.Период
		|			И ХозрасчетныйДвиженияССубконто.СчетДт = ХозрасчетныйДвиженияССубконтоАксапта.СчетДт
		|			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.СубконтоДт1, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ХозрасчетныйДвиженияССубконтоАксапта.СубконтоДт1, НЕОПРЕДЕЛЕНО))
		|			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.СубконтоДт2, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ХозрасчетныйДвиженияССубконтоАксапта.СубконтоДт2, НЕОПРЕДЕЛЕНО))
		|			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.СубконтоДт3, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ХозрасчетныйДвиженияССубконтоАксапта.СубконтоДт3, НЕОПРЕДЕЛЕНО))
		|			И ХозрасчетныйДвиженияССубконто.СчетКт = ХозрасчетныйДвиженияССубконтоАксапта.СчетКт
		|			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.СубконтоКт1, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ХозрасчетныйДвиженияССубконтоАксапта.СубконтоКт1, НЕОПРЕДЕЛЕНО))
		|			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.СубконтоКт2, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ХозрасчетныйДвиженияССубконтоАксапта.СубконтоКт2, НЕОПРЕДЕЛЕНО))
		|			И (ЕСТЬNULL(ХозрасчетныйДвиженияССубконто.СубконтоКт3, НЕОПРЕДЕЛЕНО) = ЕСТЬNULL(ХозрасчетныйДвиженияССубконтоАксапта.СубконтоКт3, НЕОПРЕДЕЛЕНО))
		|			И (-ХозрасчетныйДвиженияССубконто.Сумма = ХозрасчетныйДвиженияССубконтоАксапта.Сумма)
		|ГДЕ
		|	ХозрасчетныйДвиженияССубконтоАксапта.Регистратор ЕСТЬ NULL
		|
		|СГРУППИРОВАТЬ ПО
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеКт,
		|	ХозрасчетныйДвиженияССубконто.Сумма,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютнаяСуммаКт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоДт,
		|	ХозрасчетныйДвиженияССубконто.КоличествоКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаНУКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаПРДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаПРКт,
		|	ХозрасчетныйДвиженияССубконто.СуммаВРДт,
		|	ХозрасчетныйДвиженияССубконто.СуммаВРКт,
		|	ХозрасчетныйДвиженияССубконто.СчетДт,
		|	ХозрасчетныйДвиженияССубконто.ВалютаКт,
		|	бит_ОборотыПоБюджетам.ДоговорКонтрагента,
		|	ХозрасчетныйДвиженияССубконто.СчетКт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт3,
		|	бит_ОборотыПоБюджетам.Аналитика_1,
		|	бит_ОборотыПоБюджетам.Аналитика_6,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт1,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт2,
		|	бит_ОборотыПоБюджетам.Валюта,
		|	бит_ОборотыПоБюджетам.ЦФО,
		|	бит_ОборотыПоБюджетам.НоменклатурнаяГруппа,
		|	бит_ОборотыПоБюджетам.Аналитика_3,
		|	бит_ОборотыПоБюджетам.СтатьяОборотов,
		|	ХозрасчетныйДвиженияССубконто.Содержание,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт3,
		|	бит_ОборотыПоБюджетам.Контрагент,
		|	бит_ОборотыПоБюджетам.Аналитика_2,
		|	бит_ОборотыПоБюджетам.Аналитика_4,
		|	бит_ОборотыПоБюджетам.Проект,
		|	бит_ОборотыПоБюджетам.Аналитика_7,
		|	бит_ОборотыПоБюджетам.БанковскийСчет,
		|	ХозрасчетныйДвиженияССубконто.ПодразделениеДт,
		|	бит_ОборотыПоБюджетам.Аналитика_5,
		|	ХозрасчетныйДвиженияССубконто.ВалютаДт,
		|	ХозрасчетныйДвиженияССубконто.СубконтоДт2,
		|	ХозрасчетныйДвиженияССубконто.СубконтоКт1,
		|	бит_ОборотыПоБюджетам.Сценарий,
		|	ВЫБОР
		|		КОГДА бит_ОборотыПоБюджетам.Регистратор ЕСТЬ NULL
		|			ТОГДА ЛОЖЬ
		|		ИНАЧЕ ИСТИНА
		|	КОНЕЦ");
		           		
		пЗапрос.УстановитьПараметр("Дата"				,	Объект.Дата);
		пЗапрос.УстановитьПараметр("ОперацияБУХ"		,	ок_ОперацияБух);
		пЗапрос.УстановитьПараметр("ЖурналПроводок"		, 	бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыБухгалтерии.Хозрасчетный));
		пЗапрос.УстановитьПараметр("ОборотыПоБюджетам"  , 	бит_ПраваДоступа.ПолучитьОбъектДоступаПоМетаданным(Метаданные.РегистрыНакопления.бит_ОборотыПоБюджетам));	
		пЗапрос.УстановитьПараметр("ОперацияАксапта"  	, 	Объект.Ссылка);	
		
	КонецЕсли;
	
	Выборка = пЗапрос.Выполнить().Выбрать();
	
	пОтображатьКолонкиБКИсправлений = Ложь;
	
	Пока Выборка.Следующий() Цикл
		
		пСтрокаИсправления = ок_ОперацияБухАксапта.Добавить();
		ЗаполнитьЗначенияСвойств(пСтрокаИсправления, Выборка);
		пСтрокаИсправления.Организация = Объект.Организация;
		
		Элементы.ок_ОперацияБухАксапта.ТекущаяСтрока = пСтрокаИсправления.ПолучитьИдентификатор();
		ОбработатьИзменениеСчета(ЭтотОбъект, "Дт");
		ОбработатьИзменениеСчета(ЭтотОбъект, "Кт");
		
		Если Выборка.ОтображатьКолонкиБК Тогда 
			пОтображатьКолонкиБКИсправлений = Истина;
		КонецЕсли;
		
	КонецЦикла;
			 
	фКэшЗначений.Вставить("ИсправленияПоБК"		  , пОтображатьКолонкиБКИсправлений);
			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКэшЗначений()

	фКэшЗначений = Новый Структура;
			
	// Данные по дополнительным аналитикам
	фКэшЗначений.Вставить("НастройкиИзмерений"	  , бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_НастройкиДополнительныхИзмерений"));
	фКэшЗначений.Вставить("ИзмеренияДоп"      	  , бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Дополнительные", "Синоним"));
	
	// Произвольные измерения
	фКэшЗначений.Вставить("Измерения"         	  , бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Произвольные", "Синоним"));
	фКэшЗначений.Вставить("ИсправленияПоБК"		  , Ложь);
	фКэшЗначений.Вставить("СчетаИсключенияДляОперАХ", бит_БК_Общий.ПолучитьЗначениеНастройкиБК("Проведение по БК", "Список счетов БУ исключений для операции Аксапты", Новый СписокЗначений));
			
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствСубконто(Форма,ДтКт = "Дт")

	Результат = БухгалтерскийУчетКлиентСервер.ПараметрыУстановкиСвойствСубконтоПоШаблону(
		"ок_ОперацияБухАксаптаСубконто"+ДтКт, "ок_ОперацияБухАксаптаПодразделение"+ДтКт, "Субконто"+ДтКт, "Подразделение"+ДтКт, "Счет"+ДтКт);
		
	Результат.ПоляОбъекта.Вставить("ПодразделениеДоступность", "Подразделение"+ДтКт+"Доступность");
	Результат.ПоляОбъекта.Вставить("УчетПоПодразделениям"	 , "Подразделение"+ДтКт+"Доступность");	
	
	Результат.ЗначенияПоУмолчанию.Вставить(
		ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.СтатьиЗатрат"),
		ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтатьиЗатрат.ПрочиеЗатраты"));
	Результат.ЗначенияПоУмолчанию.Вставить(
		ПредопределенноеЗначение("ПланВидовХарактеристик.ВидыСубконтоХозрасчетные.ПрочиеДоходыИРасходы"),
		ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ПрочиеДоходыИРасходы.ПрочиеВнереализационныеДоходыРасходы"));
	
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	Результат.СкрыватьСубконто = Ложь;
	
	Возврат Результат;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработатьИзменениеСчета(Форма, ДтКт)

	ИдСтроки = Форма.Элементы.ок_ОперацияБухАксапта.ТекущаяСтрока;
	Если ИдСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = Форма.ок_ОперацияБухАксапта.НайтиПоИдентификатору(ИдСтроки);
	
	ПоляФормы = Новый Структура("Субконто1,Субконто2,Субконто3");
	ПоляФормы.Субконто1 = "ок_ОперацияБухАксаптаСубконто" + ДтКт + "1";
	ПоляФормы.Субконто2 = "ок_ОперацияБухАксаптаСубконто" + ДтКт + "2";
	ПоляФормы.Субконто3 = "ок_ОперацияБухАксаптаСубконто" + ДтКт + "3";
	БухгалтерскийУчетКлиентСервер.ПриВыбореСчета(СтрокаТаблицы["Счет" + ДтКт], Форма, ПоляФормы, Неопределено, Истина, Ложь);
	
	ПоляОбъекта = Новый Структура("Субконто1,Субконто2,Субконто3,Подразделение,Валютный,Количественный,Организация");
	ПоляОбъекта.Субконто1      = "Субконто" + ДтКт + "1";
	ПоляОбъекта.Субконто2      = "Субконто" + ДтКт + "2";
	ПоляОбъекта.Субконто3      = "Субконто" + ДтКт + "3";
	ПоляОбъекта.Подразделение  = "Подразделение" + ДтКт;
	ПоляОбъекта.Валютный       = "Валютный" + ДтКт;
	ПоляОбъекта.Количественный = "Количественный" + ДтКт;
	ПоляОбъекта.Организация    = Форма.Объект.Организация;
	БухгалтерскийУчетКлиентСервер.ПриИзмененииСчета(СтрокаТаблицы["Счет" + ДтКт], СтрокаТаблицы, ПоляОбъекта, Истина, , Ложь);
	
	ИзменитьПараметрыВыбораПолейСубконто(Форма, ДтКт);
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ИзменитьПараметрыВыбораПолейСубконто(Форма, ДтКт = "", ОчищатьСвязанныеСубконто = Истина)
	
	ИдСтроки = Форма.Элементы.ок_ОперацияБухАксапта.ТекущаяСтрока;
	Если ИдСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СтрокаТаблицы = Форма.ок_ОперацияБухАксапта.НайтиПоИдентификатору(ИдСтроки);
	
	Если ДтКт <> "Кт" Тогда
		
		ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоДт%Индекс%", "СчетДт");
		
		Если ОчищатьСвязанныеСубконто Тогда
			ЗначенияСубконтоСтрокиТаблицы = СтрокаТаблицы;
		Иначе
			ЗначенияСубконтоСтрокиТаблицы = Новый Структура("СубконтоДт1,СубконтоДт2,СубконтоДт3");
			ЗаполнитьЗначенияСвойств(ЗначенияСубконтоСтрокиТаблицы, СтрокаТаблицы);
		КонецЕсли;
		
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
			Форма, ЗначенияСубконтоСтрокиТаблицы, "СубконтоДт%Индекс%", "ок_ОперацияБухАксаптаСубконтоДт%Индекс%", ПараметрыДокумента);
			
	КонецЕсли;
	Если ДтКт <> "Дт" Тогда
		
		ПараметрыДокумента = ПолучитьСписокПараметров(Форма, СтрокаТаблицы, "СубконтоКт%Индекс%", "СчетКт");
		
		Если ОчищатьСвязанныеСубконто Тогда
			ЗначенияСубконтоСтрокиТаблицы = СтрокаТаблицы;
		Иначе
			ЗначенияСубконтоСтрокиТаблицы = Новый Структура("СубконтоКт1,СубконтоКт2,СубконтоКт3");
			ЗаполнитьЗначенияСвойств(ЗначенияСубконтоСтрокиТаблицы, СтрокаТаблицы);
		КонецЕсли;
		
		БухгалтерскийУчетКлиентСервер.ИзменитьПараметрыВыбораПолейСубконто(
			Форма, ЗначенияСубконтоСтрокиТаблицы, "СубконтоКт%Индекс%", "ок_ОперацияБухАксаптаСубконтоКт%Индекс%", ПараметрыДокумента);
			
	КонецЕсли;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокПараметров(Форма, ТекущиеДанные, ШаблонИмяПоляОбъекта, ИмяПоляСчетУчета)

	СписокПараметров = Новый Структура("Организация,СчетУчета,ОстаткиОбороты",
										Форма.Объект.Организация, ТекущиеДанные[ИмяПоляСчетУчета], "Кт");
	ТипыДоговора = БухгалтерскийУчетКлиентСерверПереопределяемый.ПолучитьОписаниеТиповДоговора().Типы();
	
	Для Индекс = 1 По 3 Цикл
		
		ИмяПоля    = СтрЗаменить(ШаблонИмяПоляОбъекта, "%Индекс%", Индекс);
		ДанныеПоля = ТекущиеДанные[ИмяПоля];
		ТипПоля    = ТипЗнч(ДанныеПоля);
		Если ТипПоля = Тип("СправочникСсылка.Контрагенты") Тогда
			СписокПараметров.Вставить("Контрагент", ДанныеПоля);
		ИначеЕсли ТипыДоговора.Найти(ТипПоля) <> Неопределено Тогда
			СписокПараметров.Вставить("ДоговорКонтрагента", ДанныеПоля);
		ИначеЕсли ТипПоля = Тип("СправочникСсылка.Номенклатура") Тогда
			СписокПараметров.Вставить("Номенклатура", ДанныеПоля);
		ИначеЕсли ТипПоля = Тип("СправочникСсылка.Склады") Тогда
			СписокПараметров.Вставить("Склад", ДанныеПоля);
		КонецЕсли;
		
	КонецЦикла;

	Возврат СписокПараметров;

КонецФункции

&НаКлиенте
Процедура ОбработатьНачалоВыбораСубконто(ДтКт, Элемент, СтандартнаяОбработка)

	ТекущиеДанные      = Элементы.ок_ОперацияБухАксапта.ТекущиеДанные;
	ПараметрыДокумента = ПолучитьСписокПараметров(ЭтотОбъект, ТекущиеДанные, "Субконто" + ДтКт + "%Индекс%", "Счет" + ДтКт);
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ПараметрыДокумента);

КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСубконтоЗаполнениеАналитик(ДтКт = "Дт", НомерСубконто)

	Если НЕ фКэшЗначений.ИсправленияПоБК Тогда 
		Возврат;
	КонецЕсли;
	
	ТекущаяСтрока = Элементы.ок_ОперацияБухАксапта.ТекущиеДанные;
	
	Если фКэшЗначений.СчетаИсключенияДляОперАХ.НайтиПоЗначению(ТекущаяСтрока["Счет" + ДтКт])<>Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Значение = ТекущаяСтрока["Субконто"+ДтКт+НомерСубконто];
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.ОбъектыСтроительства") Тогда
		ТекущаяСтрока.Аналитика_2 = ПолучитьОбъектСтроительстваДляАналитики2(Значение);	
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
		ТекущаяСтрока.Аналитика_2 = бит_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение,"Объект");	
		ТекущаяСтрока.ЦФО = бит_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение,"ОК_Функция");
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Подразделения") Тогда
		ТекущаяСтрока.ЦФО = Значение;	
	КонецЕсли;	
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
		ТекущаяСтрока.Аналитика_2 = бит_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение,"Объект");	
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Контрагенты") Тогда 
		ТекущаяСтрока.Контрагент = Значение;
		ТекущаяСтрока.ДоговорКонтрагента = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.ДоговорыКонтрагентов") Тогда 
		ТекущаяСтрока.Контрагент		 = бит_ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Значение,"Владелец");
		ТекущаяСтрока.ДоговорКонтрагента = Значение;
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
		
		стрРезультат = ПолучитьСтатьюОборотовНаСервере(Значение);
		Если стрРезультат.Свойство("Ошибки") Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(стрРезультат.Ошибки,,"Объект.ок_ОперацияБухАксапта["+Объект.ок_ОперацияБухАксапта.Индекс(ТекущаяСтрока)+"].Субконто" +ДтКт+ НомерСубконто);
			ТекущаяСтрока.СтатьяОборотов = ПредопределенноеЗначение("Справочник.бит_СтатьиОборотов.ПустаяСсылка");
		ИначеЕсли стрРезультат.Свойство("СтатьяОборотов") Тогда 
			ТекущаяСтрока.СтатьяОборотов = стрРезультат.СтатьяОборотов;
		КонецЕсли;		
		
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда	
		
		пСтатьяОборотов = ПолучитьСтатьюПДР(Значение);
		Если ЗначениеЗаполнено(пСтатьяОборотов) Тогда 
			ТекущаяСтрока.СтатьяОборотов = пСтатьяОборотов;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьОбъектСтроительстваДляАналитики2(ОбъектСтроительства)
	
	пЗапрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектыСтроительства.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|ГДЕ
	|	ОбъектыСтроительства.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ");
	пЗапрос.УстановитьПараметр("Ссылка",	ОбъектСтроительства);
	
	пРезультат = пЗапрос.Выполнить();
	
	Если пРезультат.Пустой() Тогда 
		Возврат ОбъектСтроительства;
	КонецЕсли;
	
	Выборка = пРезультат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтатьюОборотовНаСервере(СтатьяЗатрат);
	
	Возврат ОК_ОбщегоНазначения.ПолучитьСтатьюОборотовНаСервере(СтатьяЗатрат);
			
КонецФункции

&НаСервере
Процедура ОчиститьНеиспользуемыеИзмеренияКонтроля(Движение,соотСценарийОчищаемыеПоля,мдЗнчПеречисления,КолонкиТаблицыДвижений)
	
	мОчищаемыеПоля = соотСценарийОчищаемыеПоля.Получить(Движение.Сценарий);
	
	Если мОчищаемыеПоля = Неопределено Тогда 
		
		мОчищаемыеПоля = Новый Массив;
		
		бит_СписокИзмеренийКонтроляБюджета = бит_БК_Общий.ПолучитьЗначениеНастройкиБК("Измерения контроля бюджета", Движение.Сценарий, Новый СписокЗначений, "СписокЗначений"); 					
		
		Для Каждого ЗнчПеречисления ИЗ мдЗнчПеречисления Цикл 
			Если бит_СписокИзмеренийКонтроляБюджета.НайтиПоЗначению(Перечисления.бит_БК_ИзмеренияКонтроляБюджета[ЗнчПеречисления.Имя]) = Неопределено И
				НЕ ЗнчПеречисления.Имя = "Сценарий" И
				НЕ ЗнчПеречисления.Имя = "СтатьяОборотов"			
				Тогда  
				Если КолонкиТаблицыДвижений.Найти(ЗнчПеречисления.Имя)<>Неопределено Тогда 
					мОчищаемыеПоля.Добавить(ЗнчПеречисления.Имя);				
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;  
		
		соотСценарийОчищаемыеПоля.Вставить(Движение.Сценарий, мОчищаемыеПоля);		
		
	КонецЕсли;
	
	Если мОчищаемыеПоля.Количество()>0 Тогда 
		Для Каждого пИмяПоля Из мОчищаемыеПоля Цикл 
			Движение[пИмяПоля] = Неопределено;
		КонецЦикла;			
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтатьюПДР(Статья)
	
	Запрос = Новый Запрос;	
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов,
	|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл
	|ИЗ
	|	РегистрСведений.бит_СтатьиОборотов_СтатьиРегл КАК бит_СтатьиОборотов_СтатьиРегл
	|ГДЕ
	|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл ССЫЛКА Справочник.ПрочиеДоходыИРасходы 
	| И бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл = &Статья";					   
	Запрос.УстановитьПараметр("Статья",Статья);
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Выборка = Результат.Выбрать();
		Выборка.Следующий();
		Возврат Выборка.СтатьяОборотов;	
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьПослеСозданияРБП(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		НайденнаяСтрока = ок_ОперацияБухАксапта.Получить(ДополнительныеПараметры.ИндексСтроки);
		НайденнаяСтрока.СубконтоДт1 = РезультатЗакрытия;
	КонецЕсли;	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьПринадлежностьСчетаРБП(ТекущийСчет)
	
	Если ТекущийСчет.ПринадлежитЭлементу(ПланыСчетов.Хозрасчетный.РасходыБудущихПериодов) Тогда
		Возврат Истина;
	Иначе 
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция ДоступноИсправление()
	
	Результат = НЕ ЗначениеЗаполнено(Объект.НомерЗаявки);
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Функция ПолучитьСтавкуНДСДляРБП()
	
	ЗапросПоНДС = Новый Запрос;
	ЗапросПоНДС.Текст = 
		"ВЫБРАТЬ
		|	СчетаТЧ.СчетДт КАК СчетДт
		|ПОМЕСТИТЬ ВТ_Счета
		|ИЗ
		|	&СчетаТЧ КАК СчетаТЧ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВТ_Счета.СчетДт КАК СчетДт
		|ИЗ
		|	ВТ_Счета КАК ВТ_Счета
		|ГДЕ
		|	ВТ_Счета.СчетДт В ИЕРАРХИИ(&СчетНДС)"
	;
	ЗапросПоНДС.УстановитьПараметр("СчетаТЧ", ок_ОперацияБухАксапта.Выгрузить(,"СчетДт"));
	ЗапросПоНДС.УстановитьПараметр("СчетНДС", ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымЦенностям);
	Результат = ЗапросПоНДС.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20");
	Иначе
		Возврат ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСуммуДокумента()
	
	ИтогСумма = Объект.ОборотыАксапты.Итог("Сумма");
	Если Объект.СуммаДокумента <> ИтогСумма Тогда	
		
		Объект.СуммаДокумента = ИтогСумма;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти
