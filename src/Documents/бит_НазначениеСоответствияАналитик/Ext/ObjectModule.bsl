
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
		
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;
	
	Дата 		  = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	бит_ОбщегоНазначенияПереопределяемый.ОбработкаПроведенияНачало(ЭтотОбъект, РежимПроведения, Отказ, Ложь);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
		
	ПараметрыПроведения = Документы.бит_НазначениеСоответствияАналитик.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Документы.бит_НазначениеСоответствияАналитик.ПроверитьУникальностьЗаписей(
												ПараметрыПроведения.ТаблицаСоответствияАналитик, 
												ПараметрыПроведения.ТаблицаРеквизиты, Отказ);
												
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	Документы.бит_НазначениеСоответствияАналитик.СформироватьДвиженияСоответствиеАналитик(
												ПараметрыПроведения.ТаблицаСоответствияАналитик,
												Движения, Отказ);
												
	бит_ОбщегоНазначенияПереопределяемый.ОбработкаПроведенияЗавершение(ЭтотОбъект); 
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)
	
	бит_ОбщегоНазначенияПереопределяемый.ОбработкаУдаленияПроведения(ЭтотОбъект, Отказ); 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;	
	КонецЕсли; 
	
	ДополнительныеСвойства.Вставить("Проведен", Проведен);
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата 		  = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры
	
#КонецОбласти 

#КонецЕсли