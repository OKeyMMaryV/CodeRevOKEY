
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
    
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтотОбъект, Объект);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли; 
	
	СформироватьИнформациюСтатус(Статус, ДатаИзмененияСтатуса, ИнформацияСтатус);
	
	// Заполняем кэш значений.
	ЗаполнитьКэшЗначений();
	
	// Настроим отображение полей доп. аналитик в форме.
	бит_МеханизмДопИзмерений.ОтобразитьДополнительныеИзмеренияНаФорме_Управляемая(ЭтотОбъект
																				 ,мКэшЗначений.ИзмеренияДоп
																				 ,мКэшЗначений.НастройкиИзмерений);
	// бит_Финанс изменения кода. Начало: 23.07.2018. BF-2298																			 
	бит_МеханизмДопИзмеренийКлиентСервер.УстановитьНачальныеСвойстваАналитикШапки(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствАналитик(ЭтотОбъект));
	// бит_Финанс изменения кода. Конец 23.07.2018. 
	
	// Сформируем гиперссылку Цены и валюта.		
	СформироватьНадписьЦеныИВалюта();
	
	// Перенести куда-нибудь.
	ИспользоватьТипыЦенНоменклатуры  = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры");
	
	// Сформировать список выбора номенклатуры
	фСписокТиповНоменклатура = Новый СписокЗначений;
	МассивЗначений = Новый Массив;     
	МассивЗначений.Добавить(Тип("СправочникСсылка.бит_мто_Потребности"));
	МассивЗначений.Добавить(Тип("СправочникСсылка.Номенклатура"));
	фСписокТиповНоменклатура.ЗагрузитьЗначения(МассивЗначений);
	
	// В зависимости от СтатьиОборотов
	НастроитьВидимостьАналитикБюджетирования();
	
	// Обновляем группу итоги
	ОбновитьИтоги(ЭтотОбъект);
	
	ТекущийВидОперации = Объект.ВидОперации;
	УправлениеЭлементамиФормы();
	УстановитьЗаголовокСтраницыОсновные(ЭтотОбъект);
	УстановитьФункциональныеОпцииФормы();
	УстановитьУсловноеОформление();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ЭтоНовый = ?(Параметры.Ключ.Пустая(), Истина, Ложь);
	
	Если ЭтоНовый Тогда
		ИзменениеСтатьиОборотов(Объект);
	КонецЕсли;
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбработанаТабличнаяЧастьТовары" И ТипЗнч(Параметр) = Тип("Структура") 
		И Параметр.Свойство("ИдентификаторВызывающейФормы")
		И Параметр.ИдентификаторВызывающейФормы = УникальныйИдентификатор Тогда
		
		ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметр);
	ИначеЕсли ИмяСобытия = "ИзмененСтатус" И Источник = УникальныйИдентификатор Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметр);
		СформироватьИнформациюСтатус(Статус, ДатаИзмененияСтатуса, ИнформацияСтатус);
		УправлениеЭлементамиФормы();
	КонецЕсли;	
		
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "Проведениебит_мто_ЗаявкаНаПотребность";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
	#Если НЕ ВебКлиент Тогда
		
		// Надо отследить момент окончания проведения или отказа от него
		// в веб-клиенте функциональность отключена, т.к. приводит к возникновению исключения.
		ПодключитьОбработчикОжидания("ОжиданиеПроведенияДокумента", 0.1, Истина);
		
	#КонецЕсли
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если НЕ ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Объект", Объект.Ссылка);
		КоличествоВиз = бит_Визирование.ОпределитьКоличествоВиз(СтруктураПараметров);
		Если КоличествоВиз = 0 Тогда
			ЗаполнитьВизы();
		КонецЕсли; 
	КонецЕсли; 
	
	ОбновитьПараметрыСтатуса();
	СформироватьИнформациюСтатус(Статус, ДатаИзмененияСтатуса, ИнформацияСтатус);
	
	УправлениеЭлементамиФормы();
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ОбновитьПараметрыСтатуса();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнформацияСтатусНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Фильтр 		   = Новый Структура("Объект", Объект.Ссылка);
	ПараметрыФормы = Новый Структура("Отбор", Фильтр);
	Открытьформу("РегистрСведений.бит_ИсторияИзмененияСтатусовОбъектов.ФормаСписка", 
		ПараметрыФормы, ЭтотОбъект, ЭтотОбъект,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ОрганизацияПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	УстановитьДоговорКонтрагентаСервер();
	
КонецПроцедуры

// Процедура - обработчик нажатия гиперссылки "ЦеныИВалюта".
// 
&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзмененияПоКнопкеЦеныИВалюта();
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

// Процедура - обработчик перед началом добавления "Товары".
// 
&НаКлиенте
Процедура ТоварыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если НЕ ПроверитьВозможностьРедактированияСпискаНоменклатуры() Тогда 
		Отказ = Истина;
	КонецЕсли; 
	
	ЗадатьПолюНоменклатураНачальныйТип(Элемент, Копирование);
	
КонецПроцедуры

// Процедура - обработчик перед удалением "Товары".
// 
&НаКлиенте
Процедура ТоварыПередУдалением(Элемент, Отказ)
	
	Если НЕ ПроверитьВозможностьРедактированияСпискаНоменклатуры() Тогда 
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И НЕ Копирование Тогда
		Элемент.ТекущиеДанные.СтавкаНДС = УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(Объект.Дата, ПлательщикНДС);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик при окончании редактирования "Товары".
// 
&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	//ОбновитьИтоги(ЭтаФорма); !!!
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "СтатьяОборотов".
// 
&НаКлиенте
Процедура СтатьяОборотовПриИзменении(Элемент)
	
	ИзменениеСтатьиОборотов(Объект);
	НастроитьВидимостьАналитикБюджетирования();
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "ДоговорКонтрагента".
// 
&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)
	
	ДоговорКонтрагентаПриИзмененииСервер();
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "ВидОперации".
// 
&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.ВидОперации) Тогда
		Объект.ВидОперации = ТекущийВидОперации;
	КонецЕсли; 
	
	Если Объект.ВидОперации = ТекущийВидОперации Тогда
		Возврат;	
	КонецЕсли; 

	Оповещение = Новый ОписаниеОповещения("ВопросОчисткиВидОперации", ЭтотОбъект); 
	
	Если Объект.Товары.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Оповещение, КодВозвратаДиалога.Да);
	Иначе	
		ПоказатьВопрос(Оповещение, НСтр("ru = 'Изменен вид операции документа. Очистить табличную часть?'"), РежимДиалогаВопрос.ДаНет);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте 
Процедура ВопросОчисткиВидОперации(Ответ, ДополнительныеДанные) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Объект.Товары.Очистить();
		УправлениеЭлементамиФормы();
		УстановитьЗаголовокСтраницыОсновные(ЭтотОбъект);
	Иначе
		Объект.ВидОперации = ТекущийВидОперации;
	КонецЕсли; 
	ТекущийВидОперации = Объект.ВидОперации;
	
КонецПроцедуры

// Процедура - обработчик события "НачалоВыбора" полей ввода "Аналитика".
// 
&НаКлиенте
Процедура Аналитика_НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	бит_МеханизмДопИзмеренийКлиент.ВыбратьТипСоставнойАналитики(ЭтотОбъект
	                                                      	   ,Элемент
															   ,Объект
															   ,Элемент.Имя
															   ,СтандартнаяОбработка
															   ,мКэшЗначений.НастройкиИзмерений);
	
КонецПроцедуры

// Процедура - обработчик события "АвтоПодбор" полей ввода "Аналитика".
// 
&НаКлиенте
Процедура Аналитика_АвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
    
    бит_МеханизмДопИзмеренийКлиент.ВыбратьТипСоставнойАналитики(ЭтотОбъект
	                                                      ,Элемент
	                                                      ,Объект
														  ,Элемент.Имя
														  ,СтандартнаяОбработка
														  ,мКэшЗначений.НастройкиИзмерений);
                                                          
КонецПроцедуры

// Процедура - обработчик события "Очистка" полей ввода "Аналитика_<N>".
// 
&НаКлиенте
Процедура Аналитика_Очистка(Элемент, СтандартнаяОбработка)
	
	бит_МеханизмДопИзмеренийКлиент.ОбработкаОчисткиДополнительногоИзмерения(Элемент
																			,Объект
	                                                                        ,Элемент.Имя
																			,СтандартнаяОбработка
																			,мКэшЗначений.НастройкиИзмерений);
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

// Процедура - обработчик начало выбора поля "Номенклатура".
// 
&НаКлиенте
Процедура ТоварыНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ИДстроки = ТекДанные.ПолучитьИдентификатор();
	
	Если Элемент.ТекстРедактирования = "" Тогда  
	
		НачалоВыбораСоставного(ЭтотОбъект
                               , Элемент
                               , ТекДанные
                               , "Номенклатура"
							   , фСписокТиповНоменклатура
							   , СтандартнаяОбработка);
							   
	ИначеЕсли ТипЗнч(ТекДанные.Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда 
		
		СтандартнаяОбработка = Ложь;
		Элемент.ВыбиратьТип  = Истина;   
		Оповещение = Новый ОписаниеОповещения("ЗакрытиеФормыВыбораНоменклатуры", ЭтотОбъект, ИДстроки); 
		
		// Изменение кода. Начало. 04.08.2016{{
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекущаяСтрока", Элементы.Товары.ТекущиеДанные.Номенклатура);
		
		ОткрытьФорму("Справочник.бит_мто_Потребности.Форма.ФормаВыбора",ПараметрыФормы,,,,,Оповещение, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);					   
		// Изменение кода. Конец. 04.08.2016}}
		
	ИначеЕсли ТипЗнч(ТекДанные.Номенклатура) = Тип("СправочникСсылка.Номенклатура") Тогда 
		
		СтандартнаяОбработка = Ложь;
		Элемент.ВыбиратьТип  = Истина;   
		
		// Изменение кода. Начало. 04.08.2016{{
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ТекущаяСтрока", Элементы.Товары.ТекущиеДанные.Номенклатура);
		
		ФормаНом = ПолучитьФорму("Справочник.Номенклатура.Форма.ФормаВыбора",ПараметрыФормы,Элемент);
		// Изменение кода. Конец. 04.08.2016}}
		
		эо = ФормаНом.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		эо.Использование  = Истина;
		эо.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Услуга");
		эо.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		
		Если ЭтоУслуга() Тогда
			эо.ПравоеЗначение = Истина;
		Иначе	
			эо.ПравоеЗначение = Ложь;
		КонецЕсли; 
		
		ФормаНом.Открыть();
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура - обработчик обработка выбора поля "Номенклатура".
// 
&НаКлиенте
Процедура ТоварыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ТекДанные = Элементы.Товары.ТекущиеДанные;
	ИДстроки = ТекДанные.ПолучитьИдентификатор();
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("СправочникСсылка.Номенклатура") Тогда
		СтандартнаяОбработка = Ложь;
		Элемент.ВыбиратьТип  = Истина;   
		ОбработкаВыбораИзСпискаПотребностейНоменклатуры(ИДстроки, ВыбранноеЗначение);
	КонецЕсли; 
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.Номенклатура") Тогда
		ТоварыНоменклатураПриИзмененииСервер(ИДстроки);
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)

	ИД = Элементы.Товары.ТекущиеДанные.ПолучитьИдентификатор();
	ТоварыНоменклатураПриИзмененииСервер(ИД);
	
КонецПроцедуры

// Процедура - обработчик АвтоПодбор поля "Номенклатура".
// 
&НаКлиенте
Процедура ТоварыНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	Если ТипЗнч(Элементы.Товары.ТекущиеДанные.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		ИЛИ ТипЗнч(Элементы.Товары.ТекущиеДанные.Номенклатура) = Тип("СправочникСсылка.бит_мто_Потребности") Тогда 
		
		МассивПараметровВыбора = Новый Массив;
		Если ЭтоУслуга() Тогда
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Услуга", Истина));
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Заголовок", "Услуги"));
		Иначе	
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Услуга", Ложь));
		КонецЕсли; 
		
		Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
		
	КонецЕсли; 
	
	Если ТипЗнч(Элементы.Товары.ТекущиеДанные.Номенклатура) = Тип("СправочникСсылка.Номенклатура")
		ИЛИ ТипЗнч(Элементы.Товары.ТекущиеДанные.Номенклатура) = Тип("СправочникСсылка.бит_мто_Потребности") Тогда
		Возврат;
	КонецЕсли; 
	
	Если СтрДлина(Текст) < 7
		И СтрДлина(Текст) > 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = Новый СписокЗначений;
		Услуга = ЭтоУслуга();
		ТоварыНоменклатураАвтоПодборНаСервере(Текст, ДанныеВыбора, Услуга);
	КонецЕсли; 
	
КонецПроцедуры

// Процедура подбор по тексту из справочника бит_мто_Потребности. 
// 
// Параметры:
// 	Текст - Строка
// 	Список - СписокЗначений.
//  Услуга - Булево.
// 
&НаСервереБезКонтекста
Процедура ТоварыНоменклатураАвтоПодборНаСервере(Текст, Список, Услуга)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_мто_Потребности.Ссылка,
	               |	бит_мто_Потребности.Наименование
	               |ИЗ
	               |	Справочник.бит_мто_Потребности КАК бит_мто_Потребности
	               |ГДЕ
	               |	бит_мто_Потребности.Наименование ПОДОБНО &Текст
	               |	И бит_мто_Потребности.Услуга = &Услуга
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Номенклатура.Ссылка,
	               |	Номенклатура.Наименование
	               |ИЗ
	               |	Справочник.Номенклатура КАК Номенклатура
	               |ГДЕ
	               |	Номенклатура.Наименование ПОДОБНО &Текст
	               |	И Номенклатура.ЭтоГруппа = ЛОЖЬ
	               |	И Номенклатура.Услуга = &Услуга";
	
	Запрос.УстановитьПараметр("Текст", Текст + "%");
	Запрос.УстановитьПараметр("Услуга", Услуга);
	
	Результат = Запрос.ВыполнитьПакет();
    ВыборкаПотребность = Результат[0].Выбрать();
	
	Пока ВыборкаПотребность.Следующий() Цикл
	
		Список.Добавить(ВыборкаПотребность.Наименование, ВыборкаПотребность.Наименование + " (""Потребности"")");
		
	КонецЦикла; 
	
    ВыборкаНоменклатура = Результат[1].Выбрать();
	
	Пока ВыборкаНоменклатура.Следующий() Цикл
	
		Список.Добавить(ВыборкаНоменклатура.Наименование, ВыборкаНоменклатура.Наименование + " (""Номенклатура"")");
		
	КонецЦикла; 
	
КонецПроцедуры

// Процедура - обработчик очистка "Номенклатура".
// 
&НаКлиенте
Процедура ТоварыНоменклатураОчистка(Элемент, СтандартнаяОбработка)
	
	ЗадатьПолюНоменклатураНачальныйТип(Элемент, Ложь);
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "Цена".
// 
&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ПриИзмененииКоличествоЦена(мКэшЗначений.ЗначениеПустогоКоличества);
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "Количество".
// 
&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ПриИзмененииКоличествоЦена(мКэшЗначений.ЗначениеПустогоКоличества);
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "Сумма".
// 
&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	ПриИзмененииСумма(мКэшЗначений.ЗначениеПустогоКоличества);
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "СтавкаНДС".
// 
&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
	
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "СуммаНДС".
// 
&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	
КонецПроцедуры

// Процедура - обработчик ПриИзменении поля "ЕдиницаИзмерения".
// 
&НаКлиенте
Процедура ТоварыЕдиницаИзмеренияПриИзменении(Элемент)
	
	Элементы.Товары.ТекущиеДанные.Коэффициент = 1;
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиКомандФормы

#Область СтандартныеПодсистемыОбработчикиКоманд

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаКлиенте
Процедура КомандаПодборНоменклатуры(Команда)
	
	ПодборНоменклатуры();
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаИзменитьНоменклатуру(Команда)
	
	АдресХранилищаНоменклатура = ПоместитьНоменлатуруВоВременноеХранилищеНаСервере();
	ИзменитьНоменклатуру(АдресХранилищаНоменклатура);
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции

#Область ОбщиеСлужебныеПроцедурыИФункции

// Процедура заполняет кэш значений, необходимый при работе на клиенте.
// 
&НаСервере
Процедура ЗаполнитьКэшЗначений()
	
	мКэшЗначений = Новый Структура;
	
	ДокументЗаявкаНаПотребность = ДанныеФормыВЗначение(Объект, Тип("ДокументОбъект.бит_мто_ЗаявкаНаПотребность"));
	
	мКэшЗначений.Вставить("ИзмеренияДоп"      		  , ДокументЗаявкаНаПотребность.мИзмеренияДоп);
	мКэшЗначений.Вставить("НастройкиИзмерений"		  , ДокументЗаявкаНаПотребность.мНастройкиИзмерений);
	мКэшЗначений.Вставить("ВалютаРеглУчета"			  , Константы.ВалютаРегламентированногоУчета.Получить());
	
КонецПроцедуры

// Процедура устанавливает доступность элементам формы.
// 
// Параметры:
//  Доступность - Булево.
// 
&НаСервере
Процедура УстановитьДоступностьЭлементов()

	ЭлементыТолькоПросмотр = ?( Статус = Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Черновик
			ИЛИ Статус = Справочники.бит_СтатусыОбъектов.ПустаяСсылка(), Ложь, Истина);

	Исключения = Новый Массив();
	Исключения.Добавить(ВРег("ТоварыВсего"));
			
	Для каждого Элемент Из Элементы Цикл
		Если ТипЗнч(Элемент) = Тип("ПолеФормы") 
			И Исключения.Найти(ВРег(Элемент.Имя)) = Неопределено Тогда  
			
			Элемент.ТолькоПросмотр = ЭлементыТолькоПросмотр;
		КонецЕсли; 
	КонецЦикла;

КонецПроцедуры

// Процедура управляем видимостью-доступностью элементов формы. 
// 
&НаСервере
Процедура УправлениеЭлементамиФормы()

	ЗначениеПустогоКоличества = ?(Объект.ВидОперации = Перечисления.бит_мто_ВидыОперацийЗаявокНаПотребность.Услуги, 1, 0);
	мКэшЗначений.Вставить("ЗначениеПустогоКоличества", ЗначениеПустогоКоличества);
	
	// Управление доступностью некоторых элементов в зависимости от статуса Черновик.
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Черновик);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ПустаяСсылка());
	Если ТипЗнч(МассивСтатусов.Найти(Статус)) = Тип("Неопределено") Тогда
		Элементы.ЦеныИВалюта.Доступность = Ложь;
		Элементы.Товары.КоманднаяПанель.Доступность = Ложь;
		Элементы.Товары.КонтекстноеМеню.Доступность = Ложь;
	Иначе
		Элементы.ЦеныИВалюта.Доступность = Истина;
		Элементы.Товары.КоманднаяПанель.Доступность = Истина;
		Элементы.Товары.КонтекстноеМеню.Доступность = Истина;
	КонецЕсли;
	
	// Управление доступностью команды создать на основании.
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Утверждена);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Выдача);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Закупка);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_ЧастичноИсполнена);
	Если ТипЗнч(МассивСтатусов.Найти(Статус)) = Тип("Неопределено") Тогда
		
		Элементы.ФормаКнопкаСоздатьЗаявкаНаЗакупку.Доступность = Ложь;
		
	Иначе
		Элементы.ФормаКнопкаСоздатьЗаявкаНаЗакупку.Доступность = Истина;
	КонецЕсли;
	
	// С видом операции услуга недоступны команды.
	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Утверждена);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Выдача);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Закупка);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_ЧастичноИсполнена);
	Если Объект.ВидОперации = Перечисления.бит_мто_ВидыОперацийЗаявокНаПотребность.Услуги Тогда
		Элементы.ФормаКнопкаСоздатьПередачаВОбеспечение.Доступность = Ложь;
		Элементы.ФормаКнопкаВыдачаТМЦ.Доступность = Ложь;
	ИначеЕсли НЕ ТипЗнч(МассивСтатусов.Найти(Статус)) = Тип("Неопределено") Тогда 	
		Элементы.ФормаКнопкаСоздатьПередачаВОбеспечение.Доступность = Истина;
		Элементы.ФормаКнопкаВыдачаТМЦ.Доступность = Истина;
	Иначе	
		Элементы.ФормаКнопкаСоздатьПередачаВОбеспечение.Доступность = Ложь;
		Элементы.ФормаКнопкаВыдачаТМЦ.Доступность = Ложь;
	КонецЕсли; 
	
	УстановитьДоступностьЭлементов();
	
	Элементы.ТоварыКоличество.АвтоОтметкаНезаполненного = ?(Объект.ВидОперации = Перечисления.бит_мто_ВидыОперацийЗаявокНаПотребность.Услуги, Ложь, Истина);
	
	// Вызов механизма доступности элементов управления.
	УстановитьНастройкиДоступностиЭлементов();		
	
	Если Статус = Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Закрыта Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;

КонецПроцедуры

// Процедура осуществляет выбор составного типа из списка в управляемой форме.
// 
&НаКлиенте
Процедура НачалоВыбораСоставного(ТекущаяФорма, ЭлементВладелец, Контейнер, ИмяРеквизита, 
	СписокВыбора, СтандартнаяОбработка)
	
   Если Контейнер = Неопределено Тогда
	   Возврат;
   КонецЕсли; 
   ТекущееЗначение= Контейнер[ИмяРеквизита];
   ЭлементВладелец.ВыбиратьТип     = Ложь;      
   Если ТекущееЗначение = Неопределено ИЛИ ТекущееЗначение = "" Тогда
	   
		ЭлементВладелец.ОграничениеТипа = Новый ОписаниеТипов;
	   
		СтандартнаяОбработка            = Ложь;
		ЭлементВладелец.ВыбиратьТип     = Истина;   

		// Выбираем тип из списка
		ДопПарам = Новый Структура("Контейнер, ИмяРеквизита, ЭлементВладелец", Контейнер, ИмяРеквизита, ЭлементВладелец);
		Оповещение = Новый ОписаниеОповещения("НачалоВыбораСоставногоОкончание", ЭтотОбъект, ДопПарам); 
		ТекущаяФорма.ПоказатьВыборИзСписка(Оповещение, СписокВыбора);
	   
   КонецЕсли; // Текущее значение неопределено
   
КонецПроцедуры

// Процедура задает начальный тип номенклатуре.
// 
&НаКлиенте
Процедура ЗадатьПолюНоменклатураНачальныйТип(Элемент, Копирование)

	Если НЕ Копирование Тогда
		КС = Новый КвалификаторыСтроки(100);
		Массив = Новый Массив;
		Массив.Добавить(Тип("Строка"));
		ОписаниеТиповС = Новый ОписаниеТипов(Массив, , КС);
		Элементы.Товары.ПодчиненныеЭлементы.ТоварыНоменклатура.ОграничениеТипа = ОписаниеТиповС; 
		Элементы.Товары.ПодчиненныеЭлементы.ТоварыНоменклатура.ВыбиратьТип = Истина;
	КонецЕсли; 
	

КонецПроцедуры

// Процедура окончание процедуры "НачалоВыбораСоставного".
// 
&НаКлиенте 
Процедура НачалоВыбораСоставногоОкончание(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент <> Неопределено Тогда
       
	    // Устанавливаем выбранный тип
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(ВыбранныйЭлемент.Значение);
		Описание = Новый ОписаниеТипов(МассивТипов);
		ДополнительныеПараметры.Контейнер[ДополнительныеПараметры.ИмяРеквизита] = Описание.ПривестиЗначение();
		ДополнительныеПараметры.ЭлементВладелец.ВыбиратьТип = Ложь;

		ИДстроки = ДополнительныеПараметры.Контейнер.ПолучитьИдентификатор();
		Если ВыбранныйЭлемент.Значение = Тип("СправочникСсылка.Номенклатура") Тогда
			
			ФормаНом = ПолучитьФорму("Справочник.Номенклатура.Форма.ФормаВыбора",,Элементы.Товары.ТекущийЭлемент);
			
			эо = ФормаНом.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			эо.Использование  = Истина;
			эо.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Услуга");
			эо.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			
			Если ЭтоУслуга() Тогда
				эо.ПравоеЗначение = Истина;
			Иначе	
				эо.ПравоеЗначение = Ложь;
			КонецЕсли; 
			
			
			ФормаНом.Открыть();
			
		ИначеЕсли ВыбранныйЭлемент.Значение = Тип("СправочникСсылка.бит_мто_Потребности") Тогда	
			
			ФормаНом = ПолучитьФорму("Справочник.бит_мто_Потребности.Форма.ФормаВыбора",,Элементы.Товары.ТекущийЭлемент);
			
			эо = ФормаНом.Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
			эо.Использование  = Истина;
			эо.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Услуга");
			эо.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
			
			Если ЭтоУслуга() Тогда
				эо.ПравоеЗначение = Истина;
			Иначе	
				эо.ПравоеЗначение = Ложь;
			КонецЕсли; 
			
			ФормаНом.Открыть();
			
		КонецЕсли; 
		
   КонецЕсли; 
	
КонецПроцедуры

// Процедура является обработчиком ожидания,
// отслеживает окончание транзакции записи/проведения документа.
// 
&НаКлиенте
Процедура ОжиданиеПроведенияДокумента()
	
	ОтключитьОбработчикОжидания("ОжиданиеПроведенияДокумента");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьВизы()
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("СтруктурноеПодразделение", Объект.Организация);
	СтруктураПараметров.Вставить("ЦФО"					   , Объект.ЦФО);		
	
	бит_Визирование.ОбновитьПереченьВиз(Объект.Ссылка, Объект.Дата, СтруктураПараметров);
	
КонецПроцедуры

// Процедура применяет настройки доступности и видимости для элементов формы.
// 
&НаСервере
Процедура УстановитьНастройкиДоступностиЭлементов()
	
	бит_ОбщегоНазначения.УстановитьНастройкиДоступностиЭлементовДляФормы(ЭтотОбъект, Объект, Статус);
	
КонецПроцедуры

// Процедура осуществляет подбор номеклатуры.
// 
&НаКлиенте
Процедура ПодборНоменклатуры()

	ПараметрыПодбора = ПолучитьПараметрыПодбора();
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора,
			ЭтотОбъект, ЭтотОбъект.УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

// Процедура подбирает значения для передачи в типовую обработку "ПодборНоменклатуры".
// 
&НаКлиенте
Функция ПолучитьПараметрыПодбора()
	
	ПараметрыФормы = Новый Структура;
	
	ДатаРасчетов     = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата);
	ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры в %1 (%2)'");
	
	Если ЭтоУслуга() Тогда
		
		ИмяТаблицы = "Услуги";
		ПредставлениеТаблицы = НСтр("ru = 'Услуги'");
	
	Иначе	
		ПредставлениеТаблицы = НСтр("ru = 'Товары'");
		ИмяТаблицы = "Товары";
	КонецЕсли; 
	
	ПараметрыФормы.Вставить("ПоказыватьОстатки"  , Истина);
	ПараметрыФормы.Вставить("ПоказыватьСчетУчета", Истина);
	
	ЗаголовокПодбора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПодбора, Объект.Ссылка, ПредставлениеТаблицы);
	
	ПараметрыФормы.Вставить("ЕстьЦена"          , Истина);
	ПараметрыФормы.Вставить("ЕстьКоличество"    , Истина);
	ПараметрыФормы.Вставить("ТипЦен"            , Объект.ТипЦен);
	ПараметрыФормы.Вставить("Валюта"            , Объект.ВалютаДокумента);
	ПараметрыФормы.Вставить("ДатаРасчетов"      , ДатаРасчетов);
	ПараметрыФормы.Вставить("ДоговорКонтрагента", Объект.ДоговорКонтрагента);
	ПараметрыФормы.Вставить("Контрагент"        , Объект.Контрагент);
	ПараметрыФормы.Вставить("Организация"       , Объект.Организация);
	ПараметрыФормы.Вставить("Заголовок"         , ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора"        , "");
	ПараметрыФормы.Вставить("ИмяТаблицы"        , ИмяТаблицы);
	ПараметрыФормы.Вставить("Услуги"            , ИмяТаблицы = "Услуги");
	ПараметрыФормы.Вставить("Склад"             , Объект.Склад);
	
	Возврат ПараметрыФормы;
	
КонецФункции

// Процедура осуществляет изменение номенклатуры при нажатии кнопки "Изменить".
// 
&НаКлиенте
Процедура ИзменитьНоменклатуру(АдресХранилищаТовары)

	ПараметрыФормы = ПолучитьПараметрыИзменения(АдресХранилищаТовары);
	
	Если ПараметрыФормы <> Неопределено Тогда
		ОткрытьФорму("Обработка.ИзменениеТаблицыТоваров.Форма.Форма", ПараметрыФормы,
						ЭтотОбъект, ЭтотОбъект.УникальныйИдентификатор);
	КонецЕсли;

КонецПроцедуры

// Функция Процедура подбирает значения для передачи в типовую обработку "ИзменениеТаблицыТоваров".
// 
&НаКлиенте
Функция ПолучитьПараметрыИзменения(АдресХранилищаТовары)

	ПараметрыОбработки = Новый Структура;
	
	ПараметрыОбработки.Вставить("АдресХранилищаТовары", 		АдресХранилищаТовары);
	ПараметрыОбработки.Вставить("ЗаполнятьЦеныПоПокупке", 		Истина);
	
	ПараметрыОбработки.Вставить("ДокументСсылка", 				Объект.Ссылка);
	ПараметрыОбработки.Вставить("ДокументДата", 				Объект.Дата);
	ПараметрыОбработки.Вставить("ДокументОрганизация", 			Объект.Организация);
	ПараметрыОбработки.Вставить("ДокументВалюта", 				Объект.ВалютаДокумента);
	ПараметрыОбработки.Вставить("ДокументКурс", 				Объект.КурсДокумента);
	ПараметрыОбработки.Вставить("ДокументКратность", 			Объект.КратностьДокумента);
	ПараметрыОбработки.Вставить("ДокументСуммаВключаетНДС", 	Объект.СуммаВключаетНДС);
	ПараметрыОбработки.Вставить("ДокументТипЦен", 				Объект.ТипЦен);

	Возврат ПараметрыОбработки;
	
КонецФункции

// Процедура рассчитывает сумму в строке табличной части документа.
// 
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа.
// 
&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, ЗначениеПустогоКоличества = 0)

	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Цена * ?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество);

КонецПроцедуры

// Процедура осуществляет расчет суммы НДС, исходя из постоянной суммы.
// 
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа.
// 
&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС = Ложь)

	Если ТипЗнч(СтрокаТабличнойЧасти)=Тип("Структура") Тогда
		Если СтрокаТабличнойЧасти.Свойство("Сумма") И СтрокаТабличнойЧасти.Свойство("СтавкаНДС") Тогда
			СтрокаТабличнойЧасти.СуммаНДС = бит_БухгалтерияКлиентСервер.РассчитатьСуммуНДС(
												СтрокаТабличнойЧасти.Сумма,
												Истина,
												СуммаВключаетНДС,
												бит_БухгалтерияСервер.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
		КонецЕсли;
	Иначе // Строка табличной части
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СуммаНДС = бит_БухгалтерияКлиентСервер.РассчитатьСуммуНДС(
												СтрокаТабличнойЧасти.Сумма,
												Истина,
												СуммаВключаетНДС,
												бит_БухгалтерияСервер.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

// Процедура обновляет итоги по всем суммам.
// 
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа.
// 
&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Объект = Форма.Объект;
	Форма.ИтогиВсего    = Объект.Товары.Итог("Всего");
	Форма.ИтогиВсегоНДС = Объект.Товары.Итог("СуммаНДС");
	
КонецПроцедуры

// Процедура рассчитывает сумму Всего в строке табличной части документа.
// 
// Параметры:
//  СтрокаТабличнойЧасти - строка табличной части документа.
// 
&НаСервере
Процедура РассчитатьСуммуВсего(СтрокаТаблицы)
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

// Процедура пересчитывает суммы табличной части при изменении цены или количества.
// 
&НаКлиенте
Процедура ПриИзмененииКоличествоЦена(ЗначениеПустогоКоличества = 0)

	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;
	
	СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Цена * ?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество);

	РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);

	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);

КонецПроцедуры

// Процедура пересчитывает суммы табличной части при изменении суммы.
// 
&НаКлиенте
Процедура ПриИзмененииСумма(ЗначениеПустогоКоличества = 0)

	СтрокаТабличнойЧасти = Элементы.Товары.ТекущиеДанные;

	Если (СтрокаТабличнойЧасти.Количество = 0) И (ЗначениеПустогоКоличества = 0) Тогда
		СтрокаТабличнойЧасти.Цена = 0;
	Иначе
		СтрокаТабличнойЧасти.Цена = СтрокаТабличнойЧасти.Сумма / ?(СтрокаТабличнойЧасти.Количество = 0, ЗначениеПустогоКоличества, СтрокаТабличнойЧасти.Количество);
	КонецЕсли;

	РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
	СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);

КонецПроцедуры

// Процедура обрабатывает выбор номенклатуры.
// 
&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ПараметрыОбъекта = Неопределено;
	
	Если ТипЗнч(ПараметрыОбъекта) <> Тип("Структура") Тогда
		ПараметрыОбъекта = Новый Структура;
	КонецЕсли;
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	
	ДанныеОбъекта = Новый Структура("Дата, ВидОперации, Организация, ПодразделениеОрганизации, Склад, ЭтоКомиссия, ДоговорКонтрагента");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта);
		
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		ЦенаВключаетНДС	= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипЦен, "ЦенаВключаетНДС");
	Иначе
		ЦенаВключаетНДС	= Объект.СуммаВключаетНДС;
	КонецЕсли;
		
	Для Каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СтруктураОтбора = Новый Структура("Номенклатура, Цена", СтрокаТовара.Номенклатура, СтрокаТовара.Цена);
		
		МассивНайденныхСтрок = Объект.Товары.НайтиСтроки(СтруктураОтбора);
		Если МассивНайденныхСтрок.Количество() > 0 Тогда
			СтрокаТабличнойЧасти = МассивНайденныхСтрок[0];
		Иначе	
			СтрокаТабличнойЧасти = Неопределено;
		КонецЕсли;
		
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			// Нашли - увеличиваем количество.
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + СтрокаТовара.Количество;
			
			// Рассчитываем реквизиты табличной части
			РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, мКэшЗначений.ЗначениеПустогоКоличества);
			РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
			РассчитатьСуммуВсего(СтрокаТабличнойЧасти);
			
		Иначе
			
			СтрокаТабличнойЧасти = Объект.Товары.Добавить();
			
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара);
			
			СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТовара.Номенклатура);
			Если СведенияОНоменклатуре = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			// Документы.ПоступлениеТоваровУслуг.ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(
			// ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТаблицы, СведенияОНоменклатуре);
			
			// Заполняем реквизиты табличной части
			СтрокаТабличнойЧасти.ЕдиницаИзмерения		= СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти.СтавкаНДС				= СведенияОНоменклатуре.СтавкаНДС;
			
			СтрокаТабличнойЧасти.Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				СтрокаТабличнойЧасти.Цена, ЦенаВключаетНДС, Объект.СуммаВключаетНДС,
				бит_БухгалтерияСервер.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
			
			// Рассчитываем реквизиты табличной части
			РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, мКэшЗначений.ЗначениеПустогоКоличества);
			РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
			РассчитатьСуммуВсего(СтрокаТабличнойЧасти);
			
		КонецЕсли;
		
	КонецЦикла;	
		
КонецПроцедуры

// Процедура выполняет действия при нажатии на гиперссылку "ЦеныИВалюта".
// 
&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюта()

	// Формирование структуры параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("ВалютаДокумента"     , Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("КурсДокумента"       , Объект.КурсДокумента);
	СтруктураПараметров.Вставить("КратностьДокумента"  , Объект.КратностьДокумента);
	СтруктураПараметров.Вставить("ТипЦен"              , Объект.ТипЦен);
	СтруктураПараметров.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("ДатаДокумента"              , Объект.Дата);

	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", ЭтотОбъект);
	
	ОткрытьФорму("Документ.бит_мто_ЗаявкаНаПотребность.Форма.ФормаЦеныИВалюта"
		,СтруктураПараметров,,
		,ВариантОткрытияОкна.ОтдельноеОкно,
		,ОповещениеОЗакрытии,РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);

КонецПроцедуры

// Процедура окончание процедуры "ОбработатьИзмененияПоКнопкеЦеныИВалюта".
// 
&НаКлиенте 
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	  
	Если ТипЗнч(РезультатЗакрытия) = Тип("Структура") Тогда
		
		ЭтотОбъект.Модифицированность = Истина;
		
		ПриИзмененииЦеныИВалюты(РезультатЗакрытия);
		
		ОбновитьИтоги(ЭтотОбъект);
		СформироватьНадписьЦеныИВалюта();
		
	КонецЕсли; 
	
КонецПроцедуры

// Процедура пересчитывает цены и суммы в табличной части "Товары".
// 
// Параметры:
//  СтруктураЦеныИВалюта - Структура.
// 
&НаСервере
Процедура ПриИзмененииЦеныИВалюты(СтруктураЦеныИВалюта)

	ВалютаДоИзменения    = Объект.ВалютаДокумента;
	КурсДоИзменения      = Объект.КурсДокумента;
	КратностьДоИзменения = Объект.КратностьДокумента;
	
	Объект.ТипЦен               = СтруктураЦеныИВалюта.ТипЦен;
	Объект.ВалютаДокумента      = СтруктураЦеныИВалюта.ВалютаДокумента;
	Объект.КурсДокумента      	= СтруктураЦеныИВалюта.КурсДокумента;
	Объект.КратностьДокумента 	= СтруктураЦеныИВалюта.КратностьДокумента;
	
	ПересчитатьНДС = СтруктураЦеныИВалюта.СуммаВключаетНДС <> Объект.СуммаВключаетНДС;
	
	Объект.СуммаВключаетНДС        = СтруктураЦеныИВалюта.СуммаВключаетНДС;
	
	СписокНоменклатуры = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(СписокНоменклатуры,
		ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "Номенклатура"));
	
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда	
		ТаблицаЦенНоменклатуры = Ценообразование.ПолучитьТаблицуЦенНоменклатуры(
			СписокНоменклатуры,
			Объект.ТипЦен,
			Объект.Дата);
	Иначе
		ТаблицаЦенНоменклатуры = Ценообразование.ПолучитьТаблицуЦенНоменклатурыДокументов(
			СписокНоменклатуры,
			Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам,
			Объект.Дата);
	КонецЕсли;	
	
	Если КурсДоИзменения <> 0 И КратностьДоИзменения <> 0 Тогда
		СтруктураКурса = Новый Структура("Курс, Кратность", КурсДоИзменения, КратностьДоИзменения);
	Иначе
		СтруктураКурса = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", ВалютаДоИзменения));
		
		СтруктураКурса.Вставить("Валюта",    ВалютаДоИзменения);
		СтруктураКурса.Вставить("ДатаКурса", Объект.Дата);
	КонецЕсли;
	
	Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
		
		НайденнаяСтрока	= ТаблицаЦенНоменклатуры.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
		
		Если НайденнаяСтрока <> Неопределено Тогда
			
			Цена = бит_КурсыВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
				НайденнаяСтрока.Цена, НайденнаяСтрока.Валюта, Объект.ВалютаДокумента, НайденнаяСтрока.Курс,
				Объект.КурсДокумента, НайденнаяСтрока.Кратность, Объект.КратностьДокумента);
			ЦенаВключаетНДС = НайденнаяСтрока.ЦенаВключаетНДС;
			
		Иначе
			
			Цена = бит_КурсыВалютКлиентСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТаблицы.Цена, ВалютаДоИзменения, Объект.ВалютаДокумента, СтруктураКурса.Курс,
				Объект.КурсДокумента, СтруктураКурса.Кратность, Объект.КратностьДокумента);
			// Признак того, что цена включает НДС, хранится в реквизите СуммаВключаетНДС документа.
			ЦенаВключаетНДС = ?(ПересчитатьНДС, НЕ Объект.СуммаВключаетНДС, Объект.СуммаВключаетНДС);
			
		КонецЕсли;

		Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(Цена, ЦенаВключаетНДС,
			Объект.СуммаВключаетНДС, бит_БухгалтерияСервер.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС));

		СтрокаТаблицы.Цена = Цена;
			
		СтрокаТаблицы.Сумма = Цена * ?(СтрокаТаблицы.Количество = 0, мКэшЗначений.ЗначениеПустогоКоличества, СтрокаТаблицы.Количество);

		СтрокаТаблицы.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(СтрокаТаблицы.Сумма,
			Объект.СуммаВключаетНДС, бит_БухгалтерияСервер.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС));
			
		РассчитатьСуммуВсего(СтрокаТаблицы);	
			
	КонецЦикла;

КонецПроцедуры

// Процедура обрабатывает строку табличной части "Товары" при изменении номенклатуры.
// 
// Параметры:
//  ИДстроки - Число.
//  ВыбранноеЗначение - СправочникСсылка.Номенклатура.
// 
&НаСервере
Процедура ТоварыНоменклатураПриИзмененииСервер(ИДстроки)

	СтрокаТабличнойЧасти = Объект.Товары.НайтиПоИдентификатору(ИДстроки);
	
	Если ТипЗнч(СтрокаТабличнойЧасти.Номенклатура) <> Тип("СправочникСсылка.Номенклатура") Тогда
		Возврат;
	КонецЕсли;
	
	бит_мто.ЗаполнитьДанныеПоНоменклатуре(СтрокаТабличнойЧасти, ЭтотОбъект);
	
	РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти, мКэшЗначений.ЗначениеПустогоКоличества);
	РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);

	РассчитатьСуммуВсего(СтрокаТабличнойЧасти);
	
КонецПроцедуры

// Процедура выполняет подстановку в поле номенклатура.
// 
&НаКлиенте 
Процедура ЗакрытиеФормыВыбораНоменклатуры(РезультатВыбора, ДополнительныеПараметры) Экспорт
	
	Если РезультатВыбора <> Неопределено Тогда
		ИДстроки = ДополнительныеПараметры;
		СтрокаТабличнойЧасти = Объект.Товары.НайтиПоИдентификатору(ИДстроки);
		МассивТипов = Новый Массив;
		Если ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.Номенклатура") Тогда
			МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
		ИначеЕсли ТипЗнч(РезультатВыбора) = Тип("СправочникСсылка.бит_мто_Потребности") Тогда	
			МассивТипов.Добавить(Тип("СправочникСсылка.бит_мто_Потребности"));
		КонецЕсли; 
		Описание = Новый ОписаниеТипов(МассивТипов);
		СтрокаТабличнойЧасти.Номенклатура = Описание.ПривестиЗначение();
		СтрокаТабличнойЧасти.Номенклатура = РезультатВыбора;
		ТоварыНоменклатураПриИзмененииСервер(ИДстроки);
	КонецЕсли; 
	
КонецПроцедуры

// Процедура обрабатывает выбор из списка потребностей.
// 
// Параметры:
//  ИДстроки - Число.
//  СтроковоеЗначение - Строка.
// 
&НаСервере
Процедура ОбработкаВыбораИзСпискаПотребностейНоменклатуры(ИДстроки, СтроковоеЗначение)

	СтрокаТабличнойЧасти = Объект.Товары.НайтиПоИдентификатору(ИДстроки);
	Элементы.Товары.ПодчиненныеЭлементы.ТоварыНоменклатура.ОграничениеТипа = Новый ОписаниеТипов;
	МассивТипов = Новый Массив;
	
	Если Справочники.Номенклатура.НайтиПоНаименованию(СтроковоеЗначение, Истина) = Справочники.Номенклатура.ПустаяСсылка() Тогда
		ТипЗначения = Тип("СправочникСсылка.бит_мто_Потребности");
	Иначе	
		ТипЗначения = Тип("СправочникСсылка.Номенклатура");
	КонецЕсли; 
	
	МассивТипов.Добавить(ТипЗначения);
	Описание = Новый ОписаниеТипов(МассивТипов);
	СтрокаТабличнойЧасти.Номенклатура = Описание.ПривестиЗначение();
	
	Если ТипЗначения = Тип("СправочникСсылка.Номенклатура") Тогда
		СтрокаТабличнойЧасти.Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию(СтроковоеЗначение);
	ИначеЕсли ТипЗначения = Тип("СправочникСсылка.бит_мто_Потребности") Тогда 	
		СтрокаТабличнойЧасти.Номенклатура = Справочники.бит_мто_Потребности.НайтиПоНаименованию(СтроковоеЗначение);
	КонецЕсли; 
	
КонецПроцедуры

// Функция помещает во временное хранилище таб часть "Товары".
// 
&НаСервере
Функция ПоместитьНоменлатуруВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Объект.Товары.Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

// Процедура обрабатывает табличную часть "Товары".
// 
&НаСервере
Процедура ОбработкаОповещенияОбработкиТабличнойЧастиТоварыНаСервере(Параметры)

	ТаблицаОбработки = ПолучитьИзВременногоХранилища(Параметры.АдресОбработаннойТабличнойЧастиТоварыВХранилище);
	
	Объект.Товары.Загрузить(ТаблицаОбработки);
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);

	Для Каждого СтрокаТаблицы Из Объект.Товары Цикл
		СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

// Процедура настраивает видимость элементов формы от конткретной статьи оборотов.
// 
&НаСервере
Процедура НастроитьВидимостьАналитикБюджетирования()
	
	НастройкиИзмерений = Неопределено;

	МассивСтатей = Новый Массив;
	МассивСтатей.Добавить(Объект.СтатьяОборотов);
	Префикс = "";
	
	МассивИсключаемыхИзмерений = Новый Массив;
	МассивИсключаемыхИзмерений.Добавить("Организация");
	МассивИсключаемыхИзмерений.Добавить("Сценарий");
	МассивИсключаемыхИзмерений.Добавить("ЦФО");
	МассивИсключаемыхИзмерений.Добавить("СтатьяОборотов");
	МассивИсключаемыхИзмерений.Добавить("Проект");
	МассивИсключаемыхИзмерений.Добавить("Контрагент");
	МассивИсключаемыхИзмерений.Добавить("ДоговорКонтрагента");
	МассивИсключаемыхИзмерений.Добавить("ФизическоеЛицо");
	
	НастройкаВидимости = бит_Бюджетирование.ПолучитьНастройкуВидимостиКолонок(МассивСтатей, НастройкиИзмерений);
	
	Для Каждого ТекИзмерение Из МассивИсключаемыхИзмерений Цикл
		Если НЕ НастройкаВидимости.Свойство(ТекИзмерение) Тогда
			Продолжить;
		КонецЕсли;
		НастройкаВидимости.Удалить(ТекИзмерение);
	КонецЦикла;
	
	бит_РаботаСДиалогамиСервер.УправлениеВидимостьюКолонокТабличногоПоля(Элементы, НастройкаВидимости, Префикс);
	ВидимостьНазначенныхАналитик();
	
КонецПроцедуры

// Процедура управляет видимостью полей назначенных аналитик.
// 
&НаСервере
Процедура ВидимостьНазначенныхАналитик()

	Сч=0;
	Для Сч=Сч+1 По 7 Цикл
		Если ТипЗнч(Объект["Аналитика_"+Сч]) = Тип("СправочникСсылка.Номенклатура") Тогда
			Элементы["Аналитика_"+Сч].Видимость = Ложь;
		КонецЕсли; 
	КонецЦикла; 

	СовпадающиеАналитики = Документы.бит_мто_ЗаявкаНаПотребность.НайтиАналитикиСовпадающиеСРеквизитами(Объект.Ссылка.метаданные());
	
	Для каждого Соотв Из СовпадающиеАналитики Цикл
		ИмяАналитики = Соотв.Ключ; 
		Элементы[ИмяАналитики].Видимость = Ложь;
	КонецЦикла; 
	
КонецПроцедуры

// Процедура изменение договора на сервере.
// 
&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииСервер()

	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		
		СтруктураДоговора = Новый Структура;
		
		Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента.ТипЦен) Тогда
			ТипЦен = Объект.ДоговорКонтрагента.ТипЦен; 
			СтруктураДоговора.Вставить("ТипЦен", ТипЦен);
			СтруктураДоговора.Вставить("СуммаВключаетНДС", ТипЦен.ЦенаВключаетНДС);
		Иначе	
			СтруктураДоговора.Вставить("ТипЦен", Объект.ТипЦен);
			СтруктураДоговора.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
		КонецЕсли; 
		
		ВалютаДокумента = Объект.ДоговорКонтрагента.ВалютаВзаиморасчетов;
		СтрКурса = бит_КурсыВалют.ПолучитьКурсВалюты(ВалютаДокумента,Объект.Дата);
		КурсДокумента      = СтрКурса.Курс;
		КратностьДокумента = СтрКурса.Кратность;
		
		СтруктураДоговора.Вставить("ВалютаДокумента",      ВалютаДокумента);
		СтруктураДоговора.Вставить("КратностьДокумента",   КратностьДокумента);
		СтруктураДоговора.Вставить("КурсДокумента",        КурсДокумента);
		
		ПриИзмененииЦеныИВалюты(СтруктураДоговора);
		
		СформироватьНадписьЦеныИВалюта();
		
	КонецЕсли; 

КонецПроцедуры

// Процедура устанавливает договор контрагента в зависимости от текущих значений.
// 
&НаСервере
Процедура УстановитьДоговорКонтрагентаСервер()

	// Сформируем список видов договоров
	мСписокВидовДоговоров = Новый СписокЗначений;
	мСписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	мСписокВидовДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	
	бит_МеханизмыТиповых.УстановитьДоговорКонтрагента(Объект.ДоговорКонтрагента
														  ,Объект.Контрагент
														  ,Объект.Организация
														  ,мСписокВидовДоговоров);

КонецПроцедуры

// Функция проверяет вид операции документа.
// 
&НаСервере
Функция ЭтоУслуга()

	Возврат ?(Объект.ВидОперации = Перечисления.бит_мто_ВидыОперацийЗаявокНаПотребность.Услуги, Истина, Ложь);
	
КонецФункции

// Функция возможность редактирования списка номенклатуры.
// 
&НаСервере
Функция ПроверитьВозможностьРедактированияСпискаНоменклатуры()

	МассивСтатусов = Новый Массив;
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ЗаявкаНаПотребность_Черновик);
	МассивСтатусов.Добавить(Справочники.бит_СтатусыОбъектов.ПустаяСсылка());
	Если ТипЗнч(МассивСтатусов.Найти(Статус)) = Тип("Неопределено") Тогда
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;

КонецФункции

// Процедура выполняет действия на сервере,
//  необходимые при изменении СтатьиОборотов.
// 
// Параметры:
//  Контейнер - ДанныеФормыСтруктура, СтрокаТабличнойЧасти.
// 
&НаКлиенте 
Процедура ИзменениеСтатьиОборотов(Контейнер)

	Если Контейнер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
    ИзменениеСтатьиОборотовСервер();
	
КонецПроцедуры

// Процедура обрабатывает изменение статьи на сервере 
//
&НаСервере
Процедура ИзменениеСтатьиОборотовСервер()

	Контейнер = Объект;

	Если ЗначениеЗаполнено(Контейнер.СтатьяОборотов) Тогда
		
		НастройкиСтатьи = Справочники.бит_СтатьиОборотов.ПолучитьНастройки(Контейнер.СтатьяОборотов);
		бит_Бюджетирование.УстановитьИспользованиеАналитики(Контейнер, НастройкиСтатьи, мКэшЗначений.ИзмеренияДоп);
		
	КонецЕсли; 
	
	бит_МеханизмДопИзмеренийКлиентСервер.УстановитьСвойстваАналитикШапкиПриИзмененииСтатьиОборотов(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствАналитик(ЭтотОбъект));
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПараметрыУстановкиСвойствАналитик(Форма)

	Результат = бит_МеханизмДопИзмеренийКлиентСервер.ПараметрыУстановкиСвойствАналитикПоШаблону(
		"Аналитика_", "Аналитика_", "СтатьяОборотов");
		
	Результат.ДопРеквизиты.Вставить("Организация", Форма.Объект.Организация);
	
	Возврат Результат;

КонецФункции

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	бит_МеханизмДопИзмеренийКлиентСервер.УстановитьСвойстваАналитикШапкиПриИзмененииОрганизации(
		ЭтотОбъект, Объект, ПараметрыУстановкиСвойствАналитик(ЭтотОбъект));	
	
	УстановитьФункциональныеОпцииФормы();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()

	УсловноеОформление.Элементы.Очистить();
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтавкаНДС");
	
	ГруппаИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ЭлементУО.Отбор.Элементы,"ГруппаИ",
				ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
				
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИ,
		"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.НеЗаполнено, "");
				
	ГруппаИЛИ = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(ГруппаИ,"ГруппаИЛИ",
				ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИЛИ,
		"Объект.Товары.Сумма", ВидСравненияКомпоновкиДанных.Заполнено, "");
				
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаИЛИ,
		"Объект.Товары.Всего", ВидСравненияКомпоновкиДанных.Заполнено, "");
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);

КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ПлательщикНДС  = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата);
	
КонецПроцедуры

#КонецОбласти 

#Область УправлениеВнешнимВидомФормы

// Процедура формирует гиперссылку с данными по ценам и валюте.
// 
&НаСервере
Процедура СформироватьНадписьЦеныИВалюта()

	СтруктураНадписи = Новый Структура();
	СтруктураНадписи.Вставить("ТипЦен", 		  	Объект.ТипЦен);
	СтруктураНадписи.Вставить("ВалютаДокумента",  	Объект.ВалютаДокумента);
	СтруктураНадписи.Вставить("Курс",             	Объект.КурсДокумента);
	СтруктураНадписи.Вставить("Кратность",        	Объект.КратностьДокумента);
	СтруктураНадписи.Вставить("СуммаВключаетНДС", 	Объект.СуммаВключаетНДС);
	СтруктураНадписи.Вставить("ВалютаРегламентированногоУчета", мКэшЗначений.ВалютаРеглУчета);
	
	ЭтотОбъект.ЦеныИВалюта = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура УстановитьЗаголовокСтраницыОсновные(Форма)

	Элементы = Форма.Элементы;	
	Объект   = Форма.Объект;
	Элементы.ОсновныеАналитики.Заголовок = Строка(Объект.ВидОперации); 
	
КонецПроцедуры
 
#КонецОбласти 

&НаСервере
Процедура ОбновитьПараметрыСтатуса()
	
	Результат = бит_Визирование.ПолучитьСтатусОбъекта(Объект.Ссылка);
	
	Статус 				 = Результат.Статус;
	ДатаИзмененияСтатуса = Результат.Дата;	

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура СформироватьИнформациюСтатус(Статус, ДатаИзмененияСтатуса, ИнформацияСтатус)

	СтруктураПараметров = Новый Структура("Статус, ДатаИзмененияСтатуса", Статус, ДатаИзмененияСтатуса);
	ИнформацияСтатус    = бит_ОбщегоНазначенияКлиентСервер.СформироватьИнформациюСтатус(СтруктураПараметров);
	
КонецПроцедуры

#КонецОбласти