
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ДатаНачала")
		И ЗначениеЗаполнено(Параметры.ДатаНачала) Тогда
		Период.ДатаНачала = Параметры.ДатаНачала;
	Иначе
		Период.ДатаНачала = НачалоМесяца(ТекущаяДата());
	КонецЕсли; 
	
	Если Параметры.Свойство("ДатаОкончания")
		И ЗначениеЗаполнено(Параметры.ДатаОкончания) Тогда
		Период.ДатаОкончания = Параметры.ДатаОкончания;
	Иначе
		Период.ДатаОкончания = НачалоМесяца(ТекущаяДата());
	КонецЕсли;
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтборПоПериодуПриИзменении(Элемент)
	
	//Устанавливаем параметр для очистки ДС
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"НеВыполнятьОчисткуДинамическогоСписка", 
		Ложь);
		
	Элементы.Список.Обновить();
	
	//После обновление формы устанавливаем отбор по измененным параметрам, для того, что бы ДС развернулся
	ПодключитьОбработчикОжидания("ОтборПоПериодуПриИзмененииОбработчикОжидания", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПоПериодуПриИзмененииОбработчикОжидания()
	
	УстановитьОтбор();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РазвернутьВсе(Команда)
	
	// Развернуть
	Элементы.Список.Развернуть(ПредопределенноеЗначение("Документ.рс_ЗаявкаНаДоговор.ПустаяСсылка"), Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьВсе(Команда)
	
	// Свернуть 
	Элементы.Список.Свернуть(ПредопределенноеЗначение("Документ.рс_ЗаявкаНаДоговор.ПустаяСсылка")); 

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьОтбор()
	
	ИсточникДоступныхНастроек = Список.КомпоновщикНастроек.ПолучитьИсточникДоступныхНастроек();
	Список.КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
	
	//Отбор по периоду От
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"ИспользоватьОтборНачалоПериода", 
		ЗначениеЗаполнено(НачалоДня(Период.ДатаНачала)));
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"НачалоПериода", 
		НачалоДня(Период.ДатаНачала));
		
	//Отбор по периоду До
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"ИспользоватьОтборКонецПериода", 
		ЗначениеЗаполнено(НачалоДня(Период.ДатаОкончания)));	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"КонецПериода", 
		КонецДня(Период.ДатаОкончания));

	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, 
		"НеВыполнятьОчисткуДинамическогоСписка", 
		Истина);
		
	Элементы.Список.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокумент(Команда)
	
	ОткрытьДокументПоЗначению(Элементы.Список.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьДокументПоЗначению(ЗначениеДляОткрытия)
	
	Если ЗначениеДляОткрытия = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(ЗначениеДляОткрытия) Тогда
		Возврат;
	КонецЕсли; 
	
	ВыбраннаяЗаявкаНаДоговор = Неопределено;
	Если ТипЗнч(ЗначениеДляОткрытия) = Тип("КлючСтрокиДинамическогоСписка") Тогда
		
		Для каждого КлючЗначение Из ЗначениеДляОткрытия Цикл
			
			Если КлючЗначение.Ключ = "Ссылка"
				И ТипЗнч(КлючЗначение.Значение) = Тип("ДокументСсылка.рс_ЗаявкаНаДоговор") Тогда
			
				ВыбраннаяЗаявкаНаДоговор = КлючЗначение.Значение;
			
			КонецЕсли;
		
		КонецЦикла; 
		
	ИначеЕсли ТипЗнч(ЗначениеДляОткрытия) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		
		Если ЗначениеДляОткрытия.ИмяГруппировки = "Ссылка"
			И ТипЗнч(ЗначениеДляОткрытия.Ключ) = Тип("ДокументСсылка.рс_ЗаявкаНаДоговор") Тогда
		
			ВыбраннаяЗаявкаНаДоговор = ЗначениеДляОткрытия.Ключ;
			
		ИначеЕсли ЗначениеДляОткрытия.РодительскаяГруппировка <> Неопределено Тогда 
			
			ОткрытьДокументПоЗначению(ЗначениеДляОткрытия.РодительскаяГруппировка);
			Возврат;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(ЗначениеДляОткрытия) = Тип("ДокументСсылка.рс_ЗаявкаНаДоговор") Тогда
		
		ВыбраннаяЗаявкаНаДоговор = ЗначениеДляОткрытия;
		
	КонецЕсли;  
	
	Если ВыбраннаяЗаявкаНаДоговор <> Неопределено Тогда
	
		ОткрытьЗначение(ВыбраннаяЗаявкаНаДоговор);
	
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти