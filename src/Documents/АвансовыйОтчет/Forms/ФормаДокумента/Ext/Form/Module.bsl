#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АвансовыйОтчетФормы.ПриСозданииНаСервере(ЭтаФорма, Отказ, СтандартнаяОбработка);
		
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФормуНаСервере();
		
		// Если документ скопирован и есть расчетв у.е., пересчитаем суммы взаиморасчетов на дату документа
		Если ЕстьРасчетыВУсловныхЕдиницах И ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
			ПересчитатьСуммыВзаиморасчетов();
		КонецЕсли;
	КонецЕсли;
	
	// Активизировать первую непустую табличную часть
	СписокТабличныхЧастей = Новый СписокЗначений;
	СписокТабличныхЧастей.Добавить("ВыданныеАвансы",    "ВыданныеАвансы");
	СписокТабличныхЧастей.Добавить("Товары",            "Товары");
	СписокТабличныхЧастей.Добавить("ВозвратнаяТара",    "ВозвратнаяТара");
	СписокТабличныхЧастей.Добавить("ОплатаПоставщикам", "ОплатаПоставщикам");
	СписокТабличныхЧастей.Добавить("Прочее",            "Прочее");
	
	АктивизироватьТабличнуюЧасть = ОбщегоНазначенияБПВызовСервера.ПолучитьПервуюНепустуюВидимуюТабличнуюЧасть(
		ЭтотОбъект, СписокТабличныхЧастей);
	ОбщегоНазначенияБПВызовСервера.АктивизироватьЭлементФормы(ЭтотОбъект, АктивизироватьТабличнуюЧасть);
	
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление(); // Вызываем после установки текущей страницы.
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтаФорма,
		"БП.Документ.АвансовыйОтчет",
		"ФормаДокумента",
		НСтр("ru='Новости: Авансовый отчет'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
		
	// бит_Финанс изменения кода. Начало.
	бит_ПриСозданииНаСервереЗавершение();
	// бит_Финанс добавление кода. Конец. 
	
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-05-12 (#ТП_БП05_ФР15)
	ок_МодификацияКонфигурацииПереопределяемый.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-05-12 (#ТП_БП05_ФР15)
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ПодготовитьФормуНаСервере(); 
	ЭтаФорма.ок_ЕстьСуточные = Истина;
	ок_ОбновитьПредыдущийОстатокПерерасход();
	ок_ОбновитьИтогиПоРегиструВзаиморасчетыСПодотчетнымиЛицами();
	ок_УстановитьДоступностьРасчетаКомандировок();
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
		
	АвансовыйОтчетФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)	
	//Ванюков К. + // Программный вывод добавленных реквизитов на форму // 2012-02-13
	Элементы.ОК_РазнесениеОборотов.ТолькоПросмотр	= Не (ПроверитьРоль("ПолныеПрава") или ПроверитьРоль("бит_БК_ГлавныйБК"));
	//Ванюков К. -
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "ПроведениеАвансовыйОтчет";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
	ПараметрыЗаписи.Вставить("РегистрироватьСчетаФактурыСпецРежимы", ПолучитьФункциональнуюОпциюИнтерфейса("РегистрироватьСчетаФактурыСпецРежимы"));
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

//1С-ИжТиСи, Кондратьев, 03.2020, обновление, некомментированный исправленный код (
&НаСервереБезКонтекста
Функция ПроверитьРоль(ИмяРоли)
	Возврат РольДоступна(СокрЛП(ИмяРоли));
КонецФункции // ПроверитьРоль()
//1С-ИжТиСи, Кондратьев, 03.2020, обновление, некомментированный исправленный код )

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	АвансовыйОтчетФормы.УстановитьЗаголовокФормы(ЭтотОбъект);
	УстановитьСостояниеДокумента();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами	
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	ИтогАвансов = Объект.ВыданныеАвансы.Итог("СуммаАванса");
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		Ценообразование.ОбновитьЦеныНоменклатуры(
			Объект.Ссылка,
			Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам,
			Объект.ВалютаДокумента,
			Объект.СуммаВключаетНДС);
	КонецЕсли;
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-24 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ОбновитьИтогиПоРегиструВзаиморасчетыСПодотчетнымиЛицами();
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-24 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.Свойство("РегистрироватьСчетаФактурыСпецРежимы") И
		ПараметрыЗаписи.РегистрироватьСчетаФактурыСпецРежимы <> ПолучитьФункциональнуюОпциюИнтерфейса("РегистрироватьСчетаФактурыСпецРежимы") Тогда
		ОбновитьИнтерфейс();
	КонецЕсли;
	
	Оповестить("Запись_АвансовыйОтчет", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
	КонецЕсли;
	
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-05-12 (#ТП_БП05_ФР15)
	Если ИсточникВыбора.ИмяФормы = 	"Справочник.ФизическиеЛица.Форма.ФормаСписка" Тогда
		Объект.ФизЛицо = ВыбранноеЗначение;
		ФизЛицоПриИзменении(Элементы.ФизЛицо);
	КонецЕсли;
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-05-12 (#ТП_БП05_ФР15)
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	Если ИсточникВыбора.ИмяФормы = 	"Документ.АвансовыйОтчет.Форма.ПериодКомандировки" Тогда
		Если ВыбранноеЗначение = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Если Объект.ДатаНачалаКомандировки = ВыбранноеЗначение.ДатаНачала 
			И Объект.ДатаОкончанияКомандировки = ВыбранноеЗначение.ДатаОкончания Тогда
			Возврат;
		КонецЕсли;	
		
		Объект.ДатаНачалаКомандировки = ВыбранноеЗначение.ДатаНачала;
		Объект.ДатаОкончанияКомандировки = ВыбранноеЗначение.ДатаОкончания;
		ок_ПериодКомандировкиПриИзмененииНаСервере();
		Модифицированность = Истина;
		ОбновитьИтоги(ЭтаФорма);
	ИначеЕсли ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Документ.АвансовыйОтчет.Форма.Суточные") Тогда
		
		Если ВыбранноеЗначение = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		// Возвращается адрес таблицы расчета суточных во временном хранилище
		ок_ЗагрузитьДанныеСуточных(ВыбранноеЗначение);
		
		ЭтаФорма.ок_ЕстьСуточные = (Объект.Суточные.Количество() <> 0);
		
		Элементы.ок_СуточныеСуммаСверхНормы.Видимость = (Объект.Суточные.Итог("СуммаСверхНормы") > 0);
		
		Модифицированность = Истина;
		ОбновитьИтоги(ЭтаФорма);
	КонецЕсли;
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;
	
	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата,
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
	
	// Если есть договоры в валюте, то необходимо получение курсов валют.
	Если НЕ ТребуетсяВызовСервера Тогда
		ТребуетсяВызовСервера = ЕстьДоговорыВВалюте;
	КонецЕсли;
	
	// Если определили, что изменение даты может повлиять на какие-либо параметры,
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодразделениеОрганизацииПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ПодразделениеОрганизации) Тогда
		ПодразделениеОрганизацииПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СкладПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Склад) Тогда
		СкладПриИзмененииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФизЛицоПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
		ПерезаполнитьАвансы();
	КонецЕсли;
	
	// Перезаполним таблицу билетов
	Если Объект.Билеты.Количество() > 0 Тогда
		ЗаполнитьБилетыНаСервере();
	КонецЕсли; 
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-06-01 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ОбновитьПредыдущийОстатокПерерасход(); 
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-06-01 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
	//ОКЕЙ Завьялова Е.А.(ПервыйБИТ) Начало 2021-09-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ПерезаполнитьОбъектИФункцию();
	//ОКЕЙ Завьялова Е.А.(ПервыйБИТ) Конец  2021-09-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОбработатьИзмененияПоКнопкеЦеныИВалюты();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаСтраницыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДокументовПриИзменении(Элемент)
	
	Если ПустаяСтрока(Объект.КоличествоЛистов) Тогда
		Объект.КоличествоЛистов = Объект.КоличествоДокументов;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВыданныеАвансы

&НаКлиенте
Процедура ВыданныеАвансыПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыданныеАвансыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВыданныеАвансыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_УпорядочитьДокументыАванса();
	//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыданныеАвансыДокументАвансаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ВыданныеАвансы.ТекущиеДанные;
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументАванса) Тогда
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		// Закомментировано
		// Суммы по документам аванса теперь храним в документе
		//СтрокаТаблицы.СуммаАванса = 0;
		СтрокаТаблицы.ок_СуммаАванса = 0;
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		СтрокаТаблицы.Валюта      = Неопределено;
	Иначе
		ЗначенияРеквизитов = ЗначенияРеквизитовИзДокументаАванса(СтрокаТаблицы.ДокументАванса, ЗначениеЗаполнено(Объект.ФизЛицо));
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		// Закомментировано
		// Суммы по документам аванса теперь храним в документе
		//СтрокаТаблицы.СуммаАванса = 0;
		СтрокаТаблицы.ок_СуммаАванса = ЗначенияРеквизитов.СуммаДокумента;
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		СтрокаТаблицы.Валюта      = ЗначенияРеквизитов.ВалютаДокумента;
		
		Если ЗначенияРеквизитов.Свойство("ФизЛицо") Тогда
			Объект.ФизЛицо = ЗначенияРеквизитов.ФизЛицо;
		ИначеЕсли ЗначенияРеквизитов.Свойство("Контрагент") Тогда
			Объект.ФизЛицо = ЗначенияРеквизитов.Контрагент;
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПараметрыОтбораДокументАванса();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыданныеАвансыДокументАвансаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ТекДанныеДокументАванса = Элементы.ВыданныеАвансы.ТекущиеДанные.ДокументАванса;
	Если ТипЗнч(ТекДанныеДокументАванса) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		СтандартнаяОбработка = Ложь;
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Организация", Объект.Организация);
		СтруктураПараметров.Вставить("ФизЛицо",     Объект.ФизЛицо);
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		СтруктураПараметров.Вставить("Ссылка",     Объект.Ссылка);
		СтруктураПараметров.Вставить("Дата",     Объект.Дата);
		СтруктураПараметров.Вставить("ВалютаДокумента",    Объект.ВалютаДокумента);
		СтруктураПараметров.Вставить("МассивДокументовТЧ",    ок_ПолучитьМассивДокументов());
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

		ОповещениеПриЗакрытии = Новый ОписаниеОповещения("ок_ОбработатьВыборДокументовСписаниеСРасчетногоСчетаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.СписаниеСРасчетногоСчета.Форма.ок_ФормаВыбораДляАвансовогоОтчета",СтруктураПараметров, ЭтаФорма,,,,ОповещениеПриЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	Иначе		
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		
		УстановитьПараметрыОтбораДокументАванса();
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	КонецЕсли;
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
КонецПроцедуры

&НаКлиенте
Процедура ВыданныеАвансыДокументАвансаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	Если ВыбранноеЗначение = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
		СтандартнаяОбработка = Ложь;  		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Организация", Объект.Организация);
		СтруктураПараметров.Вставить("ФизЛицо",     Объект.ФизЛицо);
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
		СтруктураПараметров.Вставить("Ссылка",     Объект.Ссылка);
		СтруктураПараметров.Вставить("Дата",     Объект.Дата);
		СтруктураПараметров.Вставить("ВалютаДокумента",    Объект.ВалютаДокумента);
		СтруктураПараметров.Вставить("МассивДокументовТЧ",    ок_ПолучитьМассивДокументов());
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

		ОповещениеПриЗакрытии = Новый ОписаниеОповещения("ок_ОбработатьВыборДокументовСписаниеСРасчетногоСчетаЗавершение", ЭтотОбъект);
		ОткрытьФорму("Документ.СписаниеСРасчетногоСчета.Форма.ок_ФормаВыбораДляАвансовогоОтчета",СтруктураПараметров, ЭтаФорма,,,,ОповещениеПриЗакрытии,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли; 	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		СтандартнаяОбработка = Ложь;
		ПервыйДокумент       = Истина;
		СтрокаТаблицы = Элементы.ВыданныеАвансы.ТекущиеДанные;
		Для каждого ДокументАванса Из ВыбранноеЗначение Цикл
			ЗначенияРеквизитов = ЗначенияРеквизитовИзДокументаАванса(ДокументАванса, Ложь);
			Если ПервыйДокумент Тогда
				Объект.ФизЛицо = ЗначенияРеквизитов.Контрагент;
			Иначе
				Если Объект.ФизЛицо <> ЗначенияРеквизитов.Контрагент Тогда
					Продолжить;
				КонецЕсли;
				
				СтрокаТаблицы = Объект.ВыданныеАвансы.Добавить();
			КонецЕсли;
			
			СтрокаТаблицы.ДокументАванса = ДокументАванса;
			//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
			// Закомментировано
			// Суммы по документам аванса теперь храним в документе
			//СтрокаТаблицы.СуммаАванса    = ЗначенияРеквизитов.СуммаДокумента;
			СтрокаТаблицы.ок_СуммаАванса = ЗначенияРеквизитов.СуммаДокумента;
			//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
			СтрокаТаблицы.Валюта         = ЗначенияРеквизитов.ВалютаДокумента;
			
			ПервыйДокумент = Ложь;
		КонецЦикла;
		
		Элементы.ВыданныеАвансы.ТекущаяСтрока = СтрокаТаблицы.ПолучитьИдентификатор();
		УстановитьПараметрыОтбораДокументАванса();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура(
		"Номенклатура, Количество,
		|Цена, Сумма, СтавкаНДС, СуммаНДС, ПредъявленСФ,
		|НомерГТД, СтранаПроисхождения, ОтражениеВУСН, Всего");
	
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура(
		"Дата, Организация, Склад, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	Если Объект.НДСНеВыделять Тогда
		ДанныеОбъекта.Вставить("СтавкаНДС", ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"));
		ДанныеОбъекта.Вставить("СуммаВключаетНДС", Истина);
	КонецЕсли;
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"Товары.Номенклатура",
		Объект,
		ТекущиеДанные,
		ДанныеОбъекта,
		ДанныеСтрокиТаблицы);
	
	ТоварыНоменклатураПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, Материалы");
	
	РаботаСНоменклатуройКлиент.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Товары, Материалы");
	
	РаботаСНоменклатуройКлиент.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если Не НоваяСтрока Или Копирование Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	СтрокаТаблицы.ОтражениеВУСН = ПредопределенноеЗначение("Перечисление.ОтражениеВУСН.Принимаются");
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	
	// Рассчитать реквизиты табличной части.
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы);
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	
	// Рассчитать реквизиты табличной части.
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы);
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	
	РасчетноеКоличество = ?(СтрокаТаблицы.Количество = 0, 0, СтрокаТаблицы.Количество);
	Если РасчетноеКоличество = 0 Тогда
		СтрокаТаблицы.Цена = 0;
	Иначе
		СтрокаТаблицы.Цена = СтрокаТаблицы.Сумма / РасчетноеКоличество;
	КонецЕсли;
	
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСуммаНДСПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПредъявленСФПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если НЕ ТекущиеДанные.ПредъявленСФ Тогда
		ТекущиеДанные.ДатаСФ      = '00010101';
		ТекущиеДанные.НомерСФ     = "";
		ТекущиеДанные.СчетФактура = Неопределено;
	КонецЕсли;
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("Товары.ПредъявленСФ", Объект, ТекущиеДанные);
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСчетФактураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СчетФактураПолученный") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтрокаТаблицы = Элементы.Товары.ТекущиеДанные;
		ПроверитьВыбраннуюСчетФактуру(СтрокаТаблицы, ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ТаблицаФормыВыбор(ЭтотОбъект, Элемент, Поле);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоставщикПриИзменении(Элемент)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.Товары);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоставщикАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоставщикОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПоставщикОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыДатаСФПриИзменении(Элемент)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСпособУчетаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	Если ТекущиеДанные.СпособучетаНДС = ПредопределенноеЗначение("Перечисление.СпособыУчетаНДС.Списывается")
		И ТекущиеДанные.ПредъявленСФ Тогда
		ТекущиеДанные.ПредъявленСФ = Ложь;
		ТекущиеДанные.ДатаСФ      = '00010101';
		ТекущиеДанные.НомерСФ     = "";
		ТекущиеДанные.СчетФактура = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыВозвратнаяТара

&НаКлиенте
Процедура ВозвратнаяТараПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ВозвратнаяТара.ТекущиеДанные;
	
	ДанныеСтрокиТаблицы = Новый Структура(
		"Номенклатура, Количество, Цена, Сумма, СчетУчета");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ДанныеОбъекта = Новый Структура(
		"Дата, Организация, Склад, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсДокумента, КратностьДокумента, Склад");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	ДанныеОбъекта.СуммаВключаетНДС = Истина;
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"ВозвратнаяТара.Номенклатура",
		Объект,
		ТекущиеДанные,
		ДанныеОбъекта,
		ДанныеСтрокиТаблицы);
	
	ВозвратнаяТараНоменклатураПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтрокиТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "ВозвратнаяТара");
	
	РаботаСНоменклатуройКлиент.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "ВозвратнаяТара");
	
	РаботаСНоменклатуройКлиент.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКоличествоПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ВозвратнаяТара.ТекущиеДанные;
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараЦенаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ВозвратнаяТара.ТекущиеДанные;
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТаблицы);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараСуммаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ВозвратнаяТара.ТекущиеДанные;
	
	Если СтрокаТаблицы.Количество = 0 Тогда
		СтрокаТаблицы.Цена = 0;
	Иначе
		СтрокаТаблицы.Цена = СтрокаТаблицы.Сумма / СтрокаТаблицы.Количество;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКонтрагентПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ВозвратнаяТара.ТекущиеДанные;
	
	// Передадим данные на сервер
	ДанныеОбъекта = Новый Структура;
	ДанныеСтроки  = Новый Структура;
	
	// - для заполнения договора
	ДанныеОбъекта.Вставить("Организация");
	ДанныеСтроки.Вставить("Контрагент");
	ДанныеСтроки.Вставить("ДоговорКонтрагента");
	
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	ЗаполнитьЗначенияСвойств(ДанныеСтроки,  СтрокаТаблицы);
	
	// - для заполнения счетов
	ПричиныИзменения = Новый Массив;
	ПричиныИзменения.Добавить("ВозвратнаяТара.Контрагент");
	ПричиныИзменения.Добавить("ВозвратнаяТара.ДоговорКонтрагента"); // Может измениться в ходе серверного вызова
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		ПричиныИзменения,
		Объект,
		СтрокаТаблицы,
		ДанныеОбъекта,
		ДанныеСтроки);
	
	// Получим данные с сервера
	Изменения = ВозвратнаяТараКонтрагентПриИзмененииНаСервере(ДанныеОбъекта, ДанныеСтроки, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Изменения);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.ВозвратнаяТара);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараДоговорКонтрагентаПриИзменении(Элемент)
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"ВозвратнаяТара.Контрагент",
		Объект,
		Элементы.ВозвратнаяТара.ТекущиеДанные);
	
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ВозвратнаяТараВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ТаблицаФормыВыбор(ЭтотОбъект, Элемент, Поле);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОплата

&НаКлиенте
Процедура ОплатаПоставщикамПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	ОпределитьНаличиеРасчетовВУсловныхЕдиницах(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока И ЕстьРасчетыВУсловныхЕдиницах Тогда
		ЗаполнитьНадписиВРасшифровке(Элементы.ОплатаПоставщикам.ТекущиеДанные);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамКонтрагентПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ОплатаПоставщикам.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта = Новый Структура("Организация, Дата, ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ДанныеОбъекта.Вставить("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
	
	ДанныеСтрокиТабличнойЧасти = Новый Структура("Контрагент, ДоговорКонтрагента, КурсВзаиморасчетов, КратностьВзаиморасчетов, Сделка,
		|СпособПогашенияЗадолженности, Сумма, СуммаВзаиморасчетов,
		|ДоговорКонтрагентаВалютаВзаиморасчетов, ДоговорКонтрагентаРасчетыВУсловныхЕдиницах");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТабличнойЧасти, СтрокаТаблицы);
	
	ПричиныИзменения = Новый Массив;
	ПричиныИзменения.Добавить("ОплатаПоставщикам.Контрагент");
	ПричиныИзменения.Добавить("ОплатаПоставщикам.ДоговорКонтрагента"); // Может быть изменен в ходе серверного вызова
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		ПричиныИзменения,
		Объект,
		СтрокаТаблицы,
		ДанныеОбъекта,
		ДанныеСтрокиТабличнойЧасти);
	
	ОплатаПоставщикамКонтрагентПриИзмененииНаСервере(ДанныеСтрокиТабличнойЧасти, ДанныеОбъекта, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ДанныеСтрокиТабличнойЧасти);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.ОплатаПоставщикам);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамКонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамКонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамДоговорКонтрагентаПриИзменении(Элемент)
	
	СтрокаПлатеж = Элементы.ОплатаПоставщикам.ТекущиеДанные;
	
	Если НЕ ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеОбъекта = Новый Структура("Организация, Дата, ВалютаДокумента, КурсДокумента, КратностьДокумента");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ДанныеСтрокиТабличнойЧасти = Новый Структура("Контрагент, ДоговорКонтрагента, КурсВзаиморасчетов, КратностьВзаиморасчетов,
		|СпособПогашенияЗадолженности, Сделка,
		|Сумма, СуммаВзаиморасчетов,
		|ДоговорКонтрагентаВалютаВзаиморасчетов, ДоговорКонтрагентаРасчетыВУсловныхЕдиницах");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТабличнойЧасти, СтрокаПлатеж);
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"ОплатаПоставщикам.ДоговорКонтрагента",
		Объект,
		СтрокаПлатеж,
		ДанныеОбъекта,
		ДанныеСтрокиТабличнойЧасти);
	
	ОплатаПоставщикамДоговорКонтрагентаПриИзмененииНаСервере(
		ДанныеСтрокиТабличнойЧасти,
		ДанныеОбъекта,
		ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	
	ЗаполнитьЗначенияСвойств(СтрокаПлатеж, ДанныеСтрокиТабличнойЧасти);
	
	ЕстьРасчетыВУсловныхЕдиницах = ЕстьРасчетыВУсловныхЕдиницах ИЛИ СтрокаПлатеж.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах;
	ЕстьДоговорыВВалюте          = ЕстьДоговорыВВалюте
		ИЛИ СтрокаПлатеж.ДоговорКонтрагентаВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета;
	
	ЗаполнитьНадписиВРасшифровке(СтрокаПлатеж);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамСпособПогашенияЗадолженностиПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.ОплатаПоставщикам.ТекущиеДанные;
	
	Если СтрокаТаблицы.СпособПогашенияЗадолженности <> ПредопределенноеЗначение("Перечисление.СпособыПогашенияЗадолженности.ПоДокументу") Тогда
		СтрокаТаблицы.Сделка = Неопределено;
	КонецЕсли;
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"ОплатаПоставщикам.СпособПогашенияЗадолженности",
		Объект,
		СтрокаТаблицы);
	
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамСделкаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элементы.ОплатаПоставщикам.ТекущиеДанные;
	
	ПараметрыОбъекта = Новый Структура;
	ПараметрыОбъекта.Вставить("Дата",                  Объект.Дата);
	ПараметрыОбъекта.Вставить("ДоговорКонтрагента",    ТекущиеДанные.ДоговорКонтрагента);
	ПараметрыОбъекта.Вставить("Контрагент",            ТекущиеДанные.Контрагент);
	ПараметрыОбъекта.Вставить("СчетУчета",             ТекущиеДанные.СчетУчетаРасчетовСКонтрагентом);
	ПараметрыОбъекта.Вставить("Организация",           Объект.Организация);
	ПараметрыОбъекта.Вставить("ОстаткиОбороты",        "Кт");
	ПараметрыОбъекта.Вставить("РежимОтбораДокументов", ПредопределенноеЗначение("Перечисление.РежимОтбораДокументов.ПоОстаткам"));
	ПараметрыОбъекта.Вставить("ТипыДокументов",
		"Метаданные.Документы.АвансовыйОтчет.ТабличныеЧасти.ОплатаПоставщикам.Реквизиты.Сделка.Тип");
	
	ПараметрыФормы = Новый Структура("ПараметрыОбъекта", ПараметрыОбъекта);
	ОткрытьФорму("Документ.ДокументРасчетовСКонтрагентом.ФормаВыбора", ПараметрыФормы, Элемент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамСуммаПриИзменении(Элемент)
	
	РассчитатьСуммуВзаиморасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамКурсВзаиморасчетовПриИзменении(Элемент)
	
	РассчитатьСуммуВзаиморасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамКурсВзаиморасчетовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	// Отменить стандартную обработку.
	СтандартнаяОбработка = Ложь;
	
	СтрокаПлатежа = Элементы.ОплатаПоставщикам.ТекущиеДанные;
	// Если не заполнен договор, то курс взаиморасчетов выбрать нельзя
	Если НЕ ЗначениеЗаполнено(СтрокаПлатежа.ДоговорКонтрагента) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Для выбора курса взаиморасчетов нужно выбрать договор контрагента.'"));
		Возврат;
	КонецЕсли;
	
	ПараметрыДокумента = Новый Структура;
	ПараметрыДокумента.Вставить("ДоговорКонтрагента",      СтрокаПлатежа.ДоговорКонтрагента);
	ПараметрыДокумента.Вставить("КратностьВзаиморасчетов", СтрокаПлатежа.КратностьВзаиморасчетов);
	ПараметрыДокумента.Вставить("КурсВзаиморасчетов",      СтрокаПлатежа.КурсВзаиморасчетов);
	ПараметрыДокумента.Вставить("Дата",                    Объект.Дата);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СтрокаПлатежа", СтрокаПлатежа);
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения(
		"ОплатаПоставщикамКурсВзаиморасчетовНачалоВыбораЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ОткрытьФорму("Общаяформа.ФормаВводаКурсаИКратности", ПараметрыДокумента,,,,, ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаПоставщикамВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ТаблицаФормыВыбор(ЭтотОбъект, Элемент, Поле);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыБилеты

&НаКлиенте
Процедура БилетыПередНачаломИзменения(Элемент, Отказ)
	
	ТекущиеДанные = Элементы.Билеты.ТекущиеДанные;
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоСтроки(
		ЭтотОбъект,
		ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Билеты", , Ложь));
		
КонецПроцедуры

&НаКлиенте
Процедура БилетыБилетПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Билеты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.Билет) Тогда
		
		ЗаполнитьБилетыНаСервере(Элементы.Билеты.ТекущаяСтрока);
		
		БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
			ЭтотОбъект,
			ТекущиеДанные,
			АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Билеты", , Ложь));
		
	Иначе
		ТекущиеДанные.Сумма = 0;
		ТекущиеДанные.СуммаНДС = 0;
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСчетЗатратПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Билеты.ТекущиеДанные;
	
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(
		АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("Билеты.СчетЗатрат", Объект, ТекущиеДанные));
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
		ЭтотОбъект,
		ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Билеты", , Ложь));
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСубконто1ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоБилеты(1);
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСубконто1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоБилеты(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСубконто2ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоБилеты(2);
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСубконто2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоБилеты(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСубконто3ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоБилеты(3);
	
КонецПроцедуры

&НаКлиенте
Процедура БилетыСубконто3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоБилеты(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПрочее

&НаКлиенте
Процедура ПрочееПриИзменении(Элемент)
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПередНачаломИзменения(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоСтроки(
		ЭтотОбъект,
		ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
		
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоСтроки(
		ЭтотОбъект,
		ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если Не НоваяСтрока Или Копирование Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
	СтрокаТаблицы.ОтражениеВУСН = ПредопределенноеЗначение("Перечисление.ОтражениеВУСН.Принимаются");
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Объект.НДСНеВыделять Тогда
		ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено И НЕ ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) Тогда
			ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС");
		КонецЕсли;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	Если НоваяСтрока И ОтменаРедактирования Тогда
		ОбновитьИтоги(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСчетФактураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СчетФактураПолученный") И ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
		ПроверитьВыбраннуюСчетФактуру(СтрокаТаблицы, ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееНоменклатураПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
	
	// Подготовим данные для передачи на сервер
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад, СуммаВключаетНДС, ПодразделениеОрганизации");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	Если Объект.НДСНеВыделять Тогда
		ДанныеОбъекта.Вставить("СтавкаНДС", ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"));
		ДанныеОбъекта.Вставить("СуммаВключаетНДС", Истина);
	КонецЕсли;
	
	ДанныеСтрокиТаблицы = Новый Структура("Номенклатура, Содержание, СтавкаНДС, ПредъявленСФ, ОтражениеВУСН");
	ЗаполнитьЗначенияСвойств(ДанныеСтрокиТаблицы, ТекущиеДанные);
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"Прочее.Номенклатура",
		Объект,
		ТекущиеДанные,
		ДанныеОбъекта,
		ДанныеСтрокиТаблицы);
	
	// Заполним
	Изменения = ПрочееНоменклатураПриИзмененииНаСервере(ДанныеСтрокиТаблицы, ДанныеОбъекта, ПараметрыЗаполненияСчетовУчета.КЗаполнению);
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, Изменения);
	
	Если Изменения.Свойство("СчетЗатрат") Тогда
		БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
			ЭтотОбъект,
			ТекущиеДанные,
			АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
	КонецЕсли;
	
	Если Изменения.Свойство("СчетЗатратНУ") Тогда
		БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
			ЭтотОбъект,
			ТекущиеДанные,
			АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПредъявленСФПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
	Если НЕ ТекущиеДанные.ПредъявленСФ Тогда
		ТекущиеДанные.БланкСтрогойОтчетности = Ложь;
		ТекущиеДанные.ДатаСФ      = '00010101';
		ТекущиеДанные.НомерСФ     = "";
		ТекущиеДанные.СчетФактура = Неопределено;
	КонецЕсли;
	
	ПараметрыЗаполненияСчетовУчета = АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(
		"Прочее.ПредъявленСФ", 
		Объект, 
		ТекущиеДанные);
		
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(ПараметрыЗаполненияСчетовУчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееБланкСтрогойОтчетностиПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
	Если СтрокаТаблицы.БланкСтрогойОтчетности Тогда
		СтрокаТаблицы.ДатаСФ  = СтрокаТаблицы.ДатаВходящегоДокумента;
		СтрокаТаблицы.НомерСФ = СтрокаТаблицы.НомерВходящегоДокумента;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСуммаПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
	ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСуммаНДСПриИзменении(Элемент)
	
	СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСчетЗатратПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
	
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(
		АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("Прочее.СчетЗатрат", Объект, ТекущиеДанные));
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
		ЭтотОбъект,
		ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
		ЭтотОбъект,
		ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
		
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-06-01 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ЗаполнитьСубконтоПоАналитикеКлиент("ок_ЦФО");
	ок_ЗаполнитьСубконтоПоАналитикеКлиент("ок_Аналитика_2");
	ок_ЗаполнитьАналитику(Элементы.Прочее.ТекущиеДанные.НомерСтроки-1); 
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-06-01 (#ТП_БП05_ФР12, #ТП_БП05_ФР13) 		
	
	//ОКЕЙ Завьялова Е.А.(ПервыйБИТ) Начало 2021-09-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	МассивИдентификаторовСтрок = Новый Массив;
	МассивИдентификаторовСтрок.Добавить(Элементы.Прочее.ТекущиеДанные.ПолучитьИдентификатор());
	ок_ЗаполнитьОбъектИФункцию(МассивИдентификаторовСтрок);
	//ОКЕЙ Завьялова Е.А.(ПервыйБИТ) Конец  2021-09-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

КонецПроцедуры

&НаКлиенте
Процедура ПрочееСчетЗатратНУПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
	
	СчетаУчетаВДокументахКлиент.ЗаполнитьПриИзменении(
		АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("Прочее.СчетЗатратНУ", Объект, ТекущиеДанные));
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСчета(
		ЭтотОбъект, 
		ТекущиеДанные, 
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконто1ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(1);
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-31 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ПриИзмененииСубконто(Элементы.Прочее.ТекущиеДанные.НомерСтроки-1, Элементы.Прочее.ТекущиеДанные.Субконто1,1);
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-31 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконто1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконто2ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(2);
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-31 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ПриИзмененииСубконто(Элементы.Прочее.ТекущиеДанные.НомерСтроки-1, Элементы.Прочее.ТекущиеДанные.Субконто2, 2);
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-31 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконто2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконто3ПриИзменении(Элемент)
	
	ПриИзмененииСубконто(3);
	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-31 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_ПриИзмененииСубконто(Элементы.Прочее.ТекущиеДанные.НомерСтроки-1, Элементы.Прочее.ТекущиеДанные.Субконто3, 3);
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-31 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконто3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконтоНУ1ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоНУ(1);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконтоНУ1НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоНУ(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконтоНУ2ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоНУ(2);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконтоНУ2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоНУ(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконтоНУ3ПриИзменении(Элемент)
	
	ПриИзмененииСубконтоНУ(3);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСубконтоНУ3НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НачалоВыбораСубконтоНУ(Элемент, ДанныеВыбора, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ТаблицаФормыВыбор(ЭтотОбъект, Элемент, Поле);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПоставщикПриИзменении(Элемент)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.Прочее);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПоставщикАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПоставщикОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура ПрочееПоставщикОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееДатаСФПриИзменении(Элемент)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПрочееСпособУчетаНДСПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Прочее.ТекущиеДанные;
	
	Если ТекущиеДанные.СпособУчетаНДС = ПредопределенноеЗначение("Перечисление.СпособыУчетаНДС.Списывается")
		И ТекущиеДанные.ПредъявленСФ Тогда
		ТекущиеДанные.ПредъявленСФ = Ложь;
		ТекущиеДанные.БланкСтрогойОтчетности = Ложь;
		ТекущиеДанные.ДатаСФ      = '00010101';
		ТекущиеДанные.НомерСФ     = "";
		ТекущиеДанные.СчетФактура = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодборТовары(Команда)
	
	ПараметрыПодбора = ПолучитьПараметрыПодбора("Товары");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора, ЭтотОбъект, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборВозвратнаяТара(Команда)
	
	ПараметрыПодбора = ПолучитьПараметрыПодбора("ВозвратнаяТара");
	Если ПараметрыПодбора <> Неопределено Тогда
		ОткрытьФорму("Обработка.ПодборНоменклатуры.Форма.Форма", ПараметрыПодбора, ЭтотОбъект, УникальныйИдентификатор);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтаФорма,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьБилеты(Команда)
	
	ОбработкаЗавершения = Новый ОписаниеОповещения("ЗаполнитьБилетыЗавершение", ЭтотОбъект); 
	
	Если Объект.Билеты.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru='Перед заполнением таблица билетов будет очищена. Продолжить?'");
		ПоказатьВопрос(ОбработкаЗавершения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ВыполнитьОбработкуОповещения(ОбработкаЗавершения, КодВозвратаДиалога.Да);
	КонецЕсли;	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ТекущаяДатаДокумента           = Объект.Дата;
	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();
	
	Элементы.ГруппаПрочееСчетЗатратПодразделениеЗатрат.Заголовок = ?(ПолучитьФункциональнуюОпцию("ВестиУчетЗатратПоПодразделениям"),
		НСтр("ru = 'Счет затрат / Подразделение'"), НСтр("ru = 'Счет затрат'"));
		
	Элементы.ВозвратнаяТараКонтрагент.Заголовок = ?(ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам"),
		НСтр("ru = 'Контрагент / договор'"), НСтр("ru = 'Контрагент'"));
		
	Элементы.ОплатаПоставщикамКонтрагент.Заголовок = ?(ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам"),
		НСтр("ru = 'Контрагент / договор'"), НСтр("ru = 'Контрагент'"));

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	УстановитьСостояниеДокумента();
	
	ВидыДоговоровОплата = Метаданные.Документы.АвансовыйОтчет.ТабличныеЧасти.ОплатаПоставщикам.Реквизиты.ДоговорКонтрагента.ПараметрыВыбора[0].Значение;
	ОплатаВВалюте       = Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета;
	УстановитьПараметрыВыбораДоговораСКонтрагентом(ЭтотОбъект, ВидыДоговоровОплата, ОплатаВВалюте);
	
	УправлениеФормой(ЭтотОбъект);
	АвансовыйОтчетФормы.УстановитьЗаголовокФормы(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы()
	
	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ПлательщикНалогаНаПрибыль       = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация,  Объект.Дата);
	ПлательщикНДС                   = УчетнаяПолитика.ПлательщикНДС(Объект.Организация,              Объект.Дата);
	ПрименяетсяУСН                  = УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация,             Объект.Дата);
	ПлательщикНДФЛ                  = УчетнаяПолитика.ПлательщикНДФЛ(Объект.Организация,             Объект.Дата);
	РаздельныйУчетНДСНаСчете19      = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	ИспользоватьТипыЦенНоменклатуры = ПолучитьФункциональнуюОпцию("ИспользоватьТипыЦенНоменклатуры");
	ЕстьВалютныйУчет                = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	ИспользоватьОднуНоменклатурнуюГруппу = БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ПересчитатьСуммыВзаиморасчетов();
	
	Объект.НДСВключенВСтоимость = НЕ ПлательщикНДС;
	
	НДСНеВыделять = Не УчетНДСБП.ВыделятьНДСприПоступленииТоваровУслуг(Объект.Организация, Объект.Дата);
	Если Объект.НДСНеВыделять <> НДСНеВыделять Тогда
		Объект.НДСНеВыделять = НДСНеВыделять;
		ЗаполнитьРассчитатьСуммы(Объект.ВалютаДокумента, Объект.КурсДокумента, Объект.КратностьДокумента, , , Истина);
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("Дата", Объект));
	
	УправлениеФормой(ЭтотОбъект);
	
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()
	
	УстановитьФункциональныеОпцииФормы();
	
	ПричиныИзменения = Новый Массив();
	ПричиныИзменения.Добавить("Организация");
	
	ПодразделениеПоУмолчанию = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновноеПодразделениеОрганизации");
	Если БухгалтерскийУчетПереопределяемый.ПодразделениеПринадлежитОрганизации(ПодразделениеПоУмолчанию, Объект.Организация) Тогда
		Объект.ПодразделениеОрганизации = ПодразделениеПоУмолчанию;
		ПричиныИзменения.Добавить("ПодразделениеОрганизации");
	КонецЕсли;
	
	ИзменилсяДоговорКонтрагента = Ложь;
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоТаблицыПриИзмененииОрганизации(
		Объект.Прочее,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоТаблицыПриИзмененииОрганизации(
		Объект.Прочее,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	
	Для каждого СтрокаТаблицы Из Объект.ВозвратнаяТара Цикл
		Изменения = ВозвратнаяТараУстановитьДоговорКонтрагента(
			СтрокаТаблицы.Контрагент,
			СтрокаТаблицы.ДоговорКонтрагента,
			Объект.Организация);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Изменения);
		Если Изменения.Свойство("ДоговорКонтрагента") Тогда
			ИзменилсяДоговорКонтрагента = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ИзменилсяДоговорКонтрагента Тогда
		ПричиныИзменения.Добавить("ВозвратнаяТара.ДоговорКонтрагента");
	КонецЕсли;
	
	ИзменилсяДоговорКонтрагента = Ложь;
	
	ОплатаВВалюте = Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета;
	Для каждого СтрокаТаблицы Из Объект.ОплатаПоставщикам Цикл
		Изменения = ОплатаПоставщикамУстановитьДоговорКонтрагента(
			СтрокаТаблицы.Контрагент,
			СтрокаТаблицы.ДоговорКонтрагента,
			Объект.Организация,
			Объект.ВалютаДокумента,
			ОплатаВВалюте);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Изменения);
		Если Изменения.Свойство("ДоговорКонтрагента") Тогда
			ИзменилсяДоговорКонтрагента = Истина;
		КонецЕсли;
	КонецЦикла;
	
	Если ИзменилсяДоговорКонтрагента Тогда
		ПричиныИзменения.Добавить("ОплатаПоставщикам.ДоговорКонтрагента");
	КонецЕсли;
	
	Объект.НДСВключенВСтоимость = НЕ ПлательщикНДС;
	
	НДСНеВыделять = Не УчетНДСБП.ВыделятьНДСприПоступленииТоваровУслуг(Объект.Организация, Объект.Дата);
	Если Объект.НДСНеВыделять <> НДСНеВыделять Тогда
		Объект.НДСНеВыделять = НДСНеВыделять;
		ЗаполнитьРассчитатьСуммы(Объект.ВалютаДокумента, Объект.КурсДокумента, Объект.КратностьДокумента, , , Истина);
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(ПричиныИзменения, Объект));
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	// Суточные
	ЭтаФорма.ок_НормыСуточных = Неопределено;
	Если ЭтаФорма.ок_ЕстьСуточные Тогда
		Документы.АвансовыйОтчет.УстановитьРасчетСуточныхПоУмолчанию(Объект, ЭтаФорма.ок_НормыСуточных);
		Элементы.ок_СуточныеСуммаСверхНормы.Видимость = (Объект.Суточные.Итог("СуммаСверхНормы") > 0);
	КонецЕсли; 	
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	
	ПерезаполнитьАвансы();
	
	УправлениеФормой(ЭтотОбъект);
	
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ПодразделениеОрганизацииПриИзмененииНаСервере()
	
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(
		АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("ПодразделениеОрганизации", Объект));
	
КонецПроцедуры

&НаСервере
Процедура СкладПриИзмененииНаСервере()
	
	Объект.ПодразделениеОрганизации = ОбщегоНазначенияБПВызовСервера.ПолучитьПодразделение(Объект.Организация, Объект.Склад);
	СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(
		АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета("Склад, ПодразделениеОрганизации", Объект));
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСуммыВзаиморасчетов()
	
	КэшКурсов = Новый Соответствие;
	Если Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета Тогда
		СтруктураКурсаДокумента   = РаботаСКурсамиВалют.ПолучитьКурсВалюты(Объект.ВалютаДокумента, Объект.Дата);
		Объект.КурсДокумента      = СтруктураКурсаДокумента.Курс;
		Объект.КратностьДокумента = СтруктураКурсаДокумента.Кратность;
		КэшКурсов.Вставить(Объект.ВалютаДокумента, СтруктураКурсаДокумента);
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Объект.ОплатаПоставщикам Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
			Продолжить;
		КонецЕсли;
		ВалютаДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.ДоговорКонтрагента, "ВалютаВзаиморасчетов");
		Если ВалютаДоговора = ВалютаРегламентированногоУчета Тогда
			Продолжить;
		КонецЕсли;
		Если КэшКурсов[ВалютаДоговора] = Неопределено Тогда
			СтруктураКурсаДоговора = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДоговора, Объект.Дата);
			КэшКурсов.Вставить(ВалютаДоговора, СтруктураКурсаДоговора);
		КонецЕсли;
		СтрокаТаблицы.КурсВзаиморасчетов      = КэшКурсов[ВалютаДоговора].Курс;
		СтрокаТаблицы.КратностьВзаиморасчетов = КэшКурсов[ВалютаДоговора].Кратность;
		СтрокаТаблицы.СуммаВзаиморасчетов     = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТаблицы.Сумма,
			Объект.ВалютаДокумента, ВалютаДоговора,
			Объект.КурсДокумента, СтрокаТаблицы.КурсВзаиморасчетов,
			Объект.КратностьДокумента, СтрокаТаблицы.КратностьВзаиморасчетов);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц()
	
	Для каждого СтрокаТаблицы Из Объект.Товары Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из Объект.ВыданныеАвансы Цикл
		Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДокументАванса) Тогда
			Продолжить;
		КонецЕсли;
		
		РеквизитыДокумента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			СтрокаТаблицы.ДокументАванса,
			"СуммаДокумента, ВалютаДокумента");
		Если РеквизитыДокумента <> Неопределено Тогда
			//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
			// Закомментировано
			// Суммы по документам аванса теперь храним в документе
			//СтрокаТаблицы.СуммаАванса = РеквизитыДокумента.СуммаДокумента;
			//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
			СтрокаТаблицы.Валюта      = РеквизитыДокумента.ВалютаДокумента;
		КонецЕсли;
	КонецЦикла;
	
	ЕстьДоговорыВВалюте = Ложь;
	ЕстьРасчетыВУсловныхЕдиницах = Ложь;
	МассивДоговоров   = Объект.ОплатаПоставщикам.Выгрузить(, "ДоговорКонтрагента").ВыгрузитьКолонку("ДоговорКонтрагента");
	СвойстваДоговоров = ОбщегоНазначения.ЗначенияРеквизитовОбъектов(
		МассивДоговоров, "ВалютаВзаиморасчетов, РасчетыВУсловныхЕдиницах");
	
	Для каждого СтрокаПлатеж Из Объект.ОплатаПоставщикам Цикл
		Если ЗначениеЗаполнено(СтрокаПлатеж.ДоговорКонтрагента) Тогда
			СтрокаПлатеж.ДоговорКонтрагентаВалютаВзаиморасчетов     = СвойстваДоговоров[
				СтрокаПлатеж.ДоговорКонтрагента].ВалютаВзаиморасчетов;
			СтрокаПлатеж.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах = СвойстваДоговоров[
				СтрокаПлатеж.ДоговорКонтрагента].РасчетыВУсловныхЕдиницах;
		Иначе
			СтрокаПлатеж.ДоговорКонтрагентаВалютаВзаиморасчетов     = Объект.ВалютаДокумента;
			СтрокаПлатеж.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах = Ложь;
		КонецЕсли;
		
		ЕстьДоговорыВВалюте = ЕстьДоговорыВВалюте
			ИЛИ СтрокаПлатеж.ДоговорКонтрагентаВалютаВзаиморасчетов <> ВалютаРегламентированногоУчета;
		ЕстьРасчетыВУсловныхЕдиницах = ЕстьРасчетыВУсловныхЕдиницах
			ИЛИ СтрокаПлатеж.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах;
	КонецЦикла;
	
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоТаблицы(
		Объект.Прочее,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
	БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоТаблицы(
		Объект.Прочее,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	
	Для каждого СтрокаТаблицы Из Объект.Прочее Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПрочее(СтрокаТаблицы, Объект.СуммаВключаетНДС);
	КонецЦикла;
	
	Если ЕстьРасчетыВУсловныхЕдиницах Тогда
		Для каждого СтрокаПлатеж Из Объект.ОплатаПоставщикам Цикл
			ЗаполнитьНадписиВРасшифровке(СтрокаПлатеж);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьАвансы()
	
	Если Объект.ВыданныеАвансы.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаАвансы = Объект.ВыданныеАвансы[0];
	
	Если НЕ ЗначениеЗаполнено(СтрокаАвансы.ДокументАванса) Тогда
		Возврат;
	КонецЕсли;
	
	// Проверим реквизиты документа из первой строки - если они совпадают
	// с текущим Подотчетным лицом, то ничего не делаем
	
	ЗначенияРеквизитов = ЗначенияРеквизитовИзДокументаАванса(СтрокаАвансы.ДокументАванса, Ложь);
	Если ЗначенияРеквизитов.Свойство("ФизЛицо") Тогда
		Подотчетник = ЗначенияРеквизитов.ФизЛицо;
	ИначеЕсли ЗначенияРеквизитов.Свойство("Контрагент") Тогда
		Подотчетник = ЗначенияРеквизитов.Контрагент;
	КонецЕсли;
	
	Если Подотчетник <> Объект.ФизЛицо Тогда
		Объект.ВыданныеАвансы.Очистить();
		
		СтрокаАвансы = Объект.ВыданныеАвансы.Добавить();
		СтрокаАвансы.СуммаАванса = ИтогАвансов;
		СтрокаАвансы.Валюта      = ЗначенияРеквизитов.ВалютаДокумента;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИмяТаблицы)
	
	ТаблицаТоваров = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобраннойНоменклатурыВХранилище);
	
	СтрокиДляЗаполненияСчетовУчета = Новый Массив;
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, Склад, ПодразделениеОрганизации");
	
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	Если Объект.НДСНеВыделять Тогда
		ДанныеОбъекта.Вставить("СтавкаНДС", Перечисления.СтавкиНДС.БезНДС);
	КонецЕсли;
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь);
	
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		ЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипЦен, "ЦенаВключаетНДС");
	Иначе
		Если ИмяТаблицы = "ВозвратнаяТара" Тогда
			ЦенаВключаетНДС = Истина;
		Иначе
			ЦенаВключаетНДС = Объект.СуммаВключаетНДС;
		КонецЕсли;
	КонецЕсли;
	
	Для каждого СтрокаТовара Из ТаблицаТоваров Цикл
		
		СтруктураОтбора = Новый Структура("Номенклатура, Цена", СтрокаТовара.Номенклатура, СтрокаТовара.Цена);
		СтрокаТабличнойЧасти = НайтиСтрокуТабличнойЧасти(ИмяТаблицы, СтруктураОтбора);
		
		Если СтрокаТабличнойЧасти <> Неопределено Тогда
			// Нашли - увеличиваем количество.
			СтрокаТабличнойЧасти.Количество = СтрокаТабличнойЧасти.Количество + СтрокаТовара.Количество;
			
			Если ИмяТаблицы = "Товары" Тогда
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
				
				ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
				
			ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
				// Рассчитываем реквизиты табличной части.
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
			КонецЕсли;
			
		Иначе
			
			СтрокаТабличнойЧасти = Объект[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТовара);
			
			СтрокиДляЗаполненияСчетовУчета.Добавить(СтрокаТабличнойЧасти);
			
			СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТовара.Номенклатура);
			Если СведенияОНоменклатуре = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			Если ИмяТаблицы = "Товары" Тогда
				
				СтрокаТабличнойЧасти.НомерГТД            = СведенияОНоменклатуре.НомерГТД;
				СтрокаТабличнойЧасти.СтранаПроисхождения = СведенияОНоменклатуре.СтранаПроисхождения;
				СтрокаТабличнойЧасти.СтавкаНДС           = СведенияОНоменклатуре.СтавкаНДС;
				
				Если ЦенаВключаетНДС <> Объект.СуммаВключаетНДС Тогда
					
					СтрокаТабличнойЧасти.Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
						СтрокаТабличнойЧасти.Цена, ЦенаВключаетНДС, Объект.СуммаВключаетНДС,
						УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС));
					
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
				КонецЕсли;
				
				ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
				ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти, Объект.СуммаВключаетНДС);
				
			ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
				
				Если НЕ ЦенаВключаетНДС Тогда
					СтрокаТабличнойЧасти.Цена = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
						СтрокаТабличнойЧасти.Цена, ЦенаВключаетНДС, Истина,
						УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СведенияОНоменклатуре.СтавкаНДС));
					
					ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
				КонецЕсли; 
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СчетаУчетаВДокументах.ЗаполнитьСтроки(СтрокиДляЗаполненияСчетовУчета, ИмяТаблицы, ДанныеОбъекта, Документы.АвансовыйОтчет);
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Функция НайтиСтрокуТабличнойЧасти(ИмяТабличнойЧасти, СтруктураОтбора)
	
	СтрокаТабличнойЧасти = Неопределено;
	
	МассивНайденныхСтрок = Объект[ИмяТабличнойЧасти].НайтиСтроки(СтруктураОтбора);
	Если МассивНайденныхСтрок.Количество() > 0 Тогда
		// Нашли. Вернем первую найденную строку.
		СтрокаТабличнойЧасти = МассивНайденныхСтрок[0];
	КонецЕсли;
	
	Возврат СтрокаТабличнойЧасти;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ТоварыНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)
	
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.АвансовыйОтчет,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"Товары",
		СтрокаТабличнойЧасти);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЗаполненныеСчетаУчета);
	
	Если Не ЗначениеЗаполнено(ДанныеОбъекта.ТипЦен) Тогда
		ДанныеОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам);
	КонецЕсли;
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура,
		ДанныеОбъекта,
		Ложь);
		
	Если СведенияОНоменклатуре <> Неопределено Тогда
		СтрокаТабличнойЧасти.Цена                = СведенияОНоменклатуре.Цена;
		СтрокаТабличнойЧасти.СтавкаНДС           = СведенияОНоменклатуре.СтавкаНДС;
		СтрокаТабличнойЧасти.НомерГТД            = СведенияОНоменклатуре.НомерГТД;
		СтрокаТабличнойЧасти.СтранаПроисхождения = СведенияОНоменклатуре.СтранаПроисхождения;
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, ДанныеОбъекта.СуммаВключаетНДС);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТабличнойЧасти, ДанныеОбъекта.СуммаВключаетНДС);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВозвратнаяТараНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)
	
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.АвансовыйОтчет,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"ВозвратнаяТара",
		СтрокаТабличнойЧасти);
	
	ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, ЗаполненныеСчетаУчета);
	
	Если НЕ ЗначениеЗаполнено(ДанныеОбъекта.ТипЦен) Тогда
		ДанныеОбъекта.Вставить("СпособЗаполненияЦены", Перечисления.СпособыЗаполненияЦен.ПоЗакупочнымЦенам);
	КонецЕсли;
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура,
		ДанныеОбъекта,
		Ложь);
	
	Если СведенияОНоменклатуре <> Неопределено Тогда
		СтрокаТабличнойЧасти.Цена = СведенияОНоменклатуре.Цена;
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыТовары(СтрокаТаблицы, СуммаВключаетНДС)
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыПрочее(СтрокаТаблицы, СуммаВключаетНДС)
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОплатаПоставщикамКонтрагентПриИзмененииНаСервере(СтрокаТаблицы, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)
	
	ОплатаВВалюте = ДанныеОбъекта.ВалютаДокумента <> ДанныеОбъекта.ВалютаРегламентированногоУчета;
	Изменения = ОплатаПоставщикамУстановитьДоговорКонтрагента(
		СтрокаТаблицы.Контрагент,
		СтрокаТаблицы.ДоговорКонтрагента,
		ДанныеОбъекта.Организация,
		ДанныеОбъекта.ВалютаДокумента,
		ОплатаВВалюте);
	Если Изменения.Свойство("ДоговорКонтрагента") Тогда
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Изменения);
		ОплатаПоставщикамДоговорКонтрагентаПриИзмененииНаСервере(СтрокаТаблицы, ДанныеОбъекта, СчетаУчетаКЗаполнению);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОплатаПоставщикамДоговорКонтрагентаПриИзмененииНаСервере(СтрокаТаблицы, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)
	
	РеквизитыДоговораКонтрагента = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СтрокаТаблицы.ДоговорКонтрагента,
		"Владелец, ВалютаВзаиморасчетов, РасчетыВУсловныхЕдиницах");
		
	Если ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) И НЕ ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
		СтрокаТаблицы.Контрагент = РеквизитыДоговораКонтрагента.Владелец;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
		СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
			РеквизитыДоговораКонтрагента.ВалютаВзаиморасчетов,
			ДанныеОбъекта.Дата);
		СтрокаТаблицы.КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
		СтрокаТаблицы.КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;
	Иначе
		СтрокаТаблицы.КурсВзаиморасчетов      = 1;
		СтрокаТаблицы.КратностьВзаиморасчетов = 1;
	КонецЕсли;
	
	ВалютаВзаиморасчетов = РеквизитыДоговораКонтрагента.ВалютаВзаиморасчетов;
	
	СтрокаТаблицы.ДоговорКонтрагентаВалютаВзаиморасчетов     = ВалютаВзаиморасчетов;
	СтрокаТаблицы.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах = РеквизитыДоговораКонтрагента.РасчетыВУсловныхЕдиницах;
	
	СтрокаТаблицы.СуммаВзаиморасчетов = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
		СтрокаТаблицы.Сумма,
		ДанныеОбъекта.ВалютаДокумента,    ВалютаВзаиморасчетов,
		ДанныеОбъекта.КурсДокумента,      СтрокаТаблицы.КурсВзаиморасчетов,
		ДанныеОбъекта.КратностьДокумента, СтрокаТаблицы.КратностьВзаиморасчетов);
	
	СтрокаТаблицы.СпособПогашенияЗадолженности = Перечисления.СпособыПогашенияЗадолженности.Автоматически;
	СтрокаТаблицы.Сделка = Неопределено;
	
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.АвансовыйОтчет,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"ОплатаПоставщикам",
		СтрокаТаблицы);
	
	ЗаполнитьЗначенияСвойств(СтрокаТаблицы, ЗаполненныеСчетаУчета);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗначенияРеквизитовИзДокументаАванса(Знач ДокументАванса, Знач ФизЛицоЗаполнено)
	
	РеквизитыСтрока = "СуммаДокумента, ВалютаДокумента";
	
	// Если ФизЛицо еще не было выбрано, заполним его по документу аванса
	Если НЕ ФизЛицоЗаполнено Тогда
		РеквизитыСтрока = РеквизитыСтрока + ", Контрагент";
	КонецЕсли;
	
	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументАванса, РеквизитыСтрока);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПараметрыСчетФактуры(СчетФактура, ДокументОснование)
	
	ПараметрыСчетФактуры = Новый Структура("НомерВходящегоДокумента, ДатаВходящегоДокумента, ЕстьОснование");
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("СчетФактура",       СчетФактура);
	Запрос.УстановитьПараметр("ДокументОснование", ДокументОснование);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СчетФактураПолученный.НомерВходящегоДокумента,
	|	СчетФактураПолученный.ДатаВходящегоДокумента
	|ИЗ
	|	Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|ГДЕ
	|	СчетФактураПолученный.Ссылка = &СчетФактура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	ИСТИНА КАК ЕстьОснование
	|ИЗ
	|	Документ.СчетФактураПолученный.ДокументыОснования КАК СчетФактураПолученныйДокументыОснования
	|ГДЕ
	|	СчетФактураПолученныйДокументыОснования.Ссылка = &СчетФактура
	|	И СчетФактураПолученныйДокументыОснования.ДокументОснование = &ДокументОснование";
	
	Результат = Запрос.ВыполнитьПакет();
	
	Выборка = Результат[0].Выбрать();
	Если Выборка.Следующий() Тогда
		ЗаполнитьЗначенияСвойств(ПараметрыСчетФактуры, Выборка);
	КонецЕсли;
	
	ПараметрыСчетФактуры.ЕстьОснование = НЕ Результат[1].Пустой();
	
	Возврат ПараметрыСчетФактуры;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОбновитьСчетФактуру(СчетФактура, ДокументОснование)
	
	Результат = Ложь;
	
	СчетФактураОбъект = СчетФактура.ПолучитьОбъект();
	Попытка
		СчетФактураОбъект.Заблокировать();
		НоваяСтрока = СчетФактураОбъект.ДокументыОснования.Добавить();
		НоваяСтрока.ДокументОснование = ДокументОснование;
		СчетФактураОбъект.Записать();
		СчетФактураОбъект.Разблокировать();
		Результат = Истина;
	Исключение
		ШаблонСообщения    = НСтр("ru = 'Не удалось записать документ.
                            |%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения   , ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		
		ЗаписьЖурналаРегистрации(
			НСТр("ru = 'Обновление реквизитов счетов-фактур'", ОбщегоНазначения.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,
			Метаданные.Документы.СчетФактураПолученный,
			СчетФактураОбъект.Ссылка, 
			ТекстСообщения);
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПрочееНоменклатураПриИзмененииНаСервере(Знач СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)
	
	Результат = Новый Структура;
	
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.АвансовыйОтчет,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"Прочее",
		СтрокаТабличнойЧасти);
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ЗаполненныеСчетаУчета, Истина);
	
	СведенияОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОНоменклатуре(
		СтрокаТабличнойЧасти.Номенклатура, ДанныеОбъекта, Ложь, Ложь);
	Если СведенияОНоменклатуре <> Неопределено Тогда
		Результат.Вставить("Содержание", СведенияОНоменклатуре.НаименованиеПолное);
		Результат.Вставить("СтавкаНДС",  СведенияОНоменклатуре.СтавкаНДС);
	КонецЕсли;
	
	Результат.Вставить("ОтражениеВУСН", Перечисления.ОтражениеВУСН.Принимаются);
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ОплатаПоставщикамУстановитьДоговорКонтрагента(Знач Контрагент, Знач ДоговорКонтрагента, Знач Организация, Знач Валюта, Знач ОплатаВВалюте)
	ОтборПоВалюте = Новый Структура;
	Изменения = Новый Структура;
	
	ОтборПоВалюте.Вставить("ОплатаВВалюте", Новый Структура("ЗначениеОтбора", ОплатаВВалюте));
	Если ОплатаВВалюте Тогда
		ОтборПоВалюте.Вставить("ВалютаВзаиморасчетов", Новый Структура("ЗначениеОтбора", Валюта));
	КонецЕсли;
	
	ВидыДоговоров = Метаданные.Документы.АвансовыйОтчет.ТабличныеЧасти.ОплатаПоставщикам.Реквизиты.ДоговорКонтрагента.ПараметрыВыбора[0].Значение;
	Если РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Контрагент, Организация, ВидыДоговоров, ОтборПоВалюте) Тогда
		Изменения.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
	Иначе
		Изменения.Вставить("ДоговорКонтрагента", Справочники.ДоговорыКонтрагентов.ПустаяСсылка());
	КонецЕсли;
	
	Возврат Изменения;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСуммуВзаиморасчетов(Знач СтрокаТаблицы)
	
	ВалютаВзаиморасчетов = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СтрокаТаблицы.ДоговорКонтрагента, "ВалютаВзаиморасчетов");
	
	СуммаВзаиморасчетов = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
		СтрокаТаблицы.Сумма,
		СтрокаТаблицы.ВалютаДокумента, ВалютаВзаиморасчетов,
		СтрокаТаблицы.КурсДокумента, СтрокаТаблицы.КурсВзаиморасчетов,
		СтрокаТаблицы.КратностьДокумента, СтрокаТаблицы.КратностьВзаиморасчетов);
	
	Возврат СуммаВзаиморасчетов;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВозвратнаяТараКонтрагентПриИзмененииНаСервере(Знач ДанныеОбъекта, Знач ДанныеСтроки, Знач СчетаУчетаКЗаполнению)
	
	Изменения = ВозвратнаяТараУстановитьДоговорКонтрагента(
		ДанныеСтроки.Контрагент,
		ДанныеСтроки.ДоговорКонтрагента,
		ДанныеОбъекта.Организация);
		
	Если Не Изменения.Свойство("ДоговорКонтрагента") Тогда
		СчетаУчетаКЗаполнению.Удалить("ДоговорКонтрагента");
	КонецЕсли;
		
	ЗаполненныеСчетаУчета = СчетаУчетаВДокументах.ЗаполнитьРеквизитыПриИзменении(
		Документы.АвансовыйОтчет,
		СчетаУчетаКЗаполнению,
		ДанныеОбъекта,
		"ВозвратнаяТара",
		ДанныеСтроки);
		
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Изменения, ЗаполненныеСчетаУчета, Истина);
	Возврат Изменения;
		
КонецФункции

&НаСервереБезКонтекста
Функция ВозвратнаяТараУстановитьДоговорКонтрагента(Знач Контрагент, Знач ДоговорКонтрагента, Знач Организация)
	
	Изменения = Новый Структура; // Ключ - Имя реквизита, Значение - новое значение
	
	ВидыДоговоров = Новый Массив;
	ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СПоставщиком);
	ВидыДоговоров.Добавить(Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку);
	Если РаботаСДоговорамиКонтрагентовБП.УстановитьДоговорКонтрагента(ДоговорКонтрагента, Контрагент, Организация, ВидыДоговоров) Тогда
		Изменения.Вставить("ДоговорКонтрагента", ДоговорКонтрагента);
	КонецЕсли;
	
	Возврат Изменения;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьБилетыЗавершение(Ответ, Параметры) Экспорт 
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьБилетыНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьБилетыНаСервере(ИдентификаторСтроки = Неопределено)

	Если ИдентификаторСтроки = Неопределено Тогда
		
		Объект.Билеты.Загрузить(Справочники.Билеты.СуммыБилетов(
			Объект.Организация, 
			'00010101', // без отбора "снизу" 
			Объект.Дата, 
			, 
			Объект.ФизЛицо, 
			Объект.Ссылка));
			
		Строки = Объект.Билеты;
		
	Иначе
		
		СтрокаБилета = Объект.Билеты.НайтиПоИдентификатору(ИдентификаторСтроки);
		ТаблицаБилетов = Справочники.Билеты.СуммыБилетов(
			Объект.Организация, 
			'00010101', // без отбора "снизу" 
			Объект.Дата, 
			СтрокаБилета.Билет,
			Объект.ФизЛицо, 
			Объект.Ссылка);
		
		Если ТаблицаБилетов.Количество() > 0 Тогда
			ЗаполнитьЗначенияСвойств(СтрокаБилета, ТаблицаБилетов[0], "Сумма, СуммаНДС");
		Иначе
			СтрокаБилета.Сумма = 0;
			СтрокаБилета.СуммаНДС = 0;
		КонецЕсли;
		
		Строки = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(СтрокаБилета);
		
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьСтроки(Строки, "Билеты", Объект, Документы.АвансовыйОтчет); 
	
КонецПроцедуры
 
&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Элементы.ПодразделениеОрганизации.Доступность = ЗначениеЗаполнено(Объект.Организация);
	
	ОбновитьИтоги(Форма);
	СформироватьНадписьЦеныИВалюта(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура РассчитатьСуммуВзаиморасчетов(Форма)
	
	Объект = Форма.Объект;
	СтрокаТаблицы = Объект.ОплатаПоставщикам.НайтиПоИдентификатору(Форма.Элементы.ОплатаПоставщикам.ТекущаяСтрока);
	
	Если Объект.ВалютаДокумента <> СтрокаТаблицы.ДоговорКонтрагентаВалютаВзаиморасчетов
		ИЛИ Объект.КурсДокумента <> СтрокаТаблицы.КурсВзаиморасчетов
		ИЛИ Объект.КратностьДокумента <> СтрокаТаблицы.КратностьВзаиморасчетов Тогда
		ДанныеСтрокиТабличнойЧасти = Новый Структура("ДоговорКонтрагента, КурсВзаиморасчетов, КратностьВзаиморасчетов,
			|Сумма, СуммаВзаиморасчетов, ВалютаДокумента, КурсДокумента, КратностьДокумента");
		ЗаполнитьЗначенияСвойств(ДанныеСтрокиТабличнойЧасти, Объект);
		ЗаполнитьЗначенияСвойств(ДанныеСтрокиТабличнойЧасти, СтрокаТаблицы);
		
		СтрокаТаблицы.СуммаВзаиморасчетов = ПолучитьСуммуВзаиморасчетов(ДанныеСтрокиТабличнойЧасти);
	Иначе
		СтрокаТаблицы.СуммаВзаиморасчетов = СтрокаТаблицы.Сумма;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОпределитьНаличиеРасчетовВУсловныхЕдиницах(Форма)
	
	Объект = Форма.Объект;
	
	ЕстьРасчетыВУЕ = Ложь;
	Для каждого СтрокаПлатеж Из Объект.ОплатаПоставщикам Цикл
		Если СтрокаПлатеж.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах Тогда
			ЕстьРасчетыВУЕ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Форма.ЕстьРасчетыВУсловныхЕдиницах = ЕстьРасчетыВУЕ;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Форма.ВсегоТовары    = Объект.Товары.Итог("Сумма");
	Форма.ВсегоТоварыНДС = Объект.Товары.Итог("СуммаНДС");
	
	ВсегоПрочее    = Объект.Прочее.Итог("Сумма");
	ВсегоПрочееНДС = Объект.Прочее.Итог("СуммаНДС");
	
	Если НЕ Объект.СуммаВключаетНДС Тогда
		Форма.ВсегоТовары = Форма.ВсегоТовары + Форма.ВсегоТоварыНДС;
		ВсегоПрочее = ВсегоПрочее + ВсегоПрочееНДС;
	КонецЕсли;
	
	Форма.ИтогАвансов  = Объект.ВыданныеАвансы.Итог("СуммаАванса");
	Форма.ИтогПоОтчету = Форма.ВсегоТовары + Объект.ОплатаПоставщикам.Итог("Сумма") + ВсегоПрочее;
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-06-01 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	Форма.ИтогПоОтчету = Форма.ИтогПоОтчету + Объект.Суточные.Итог("Сумма");
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-06-01 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ИтогоОстаток       = Форма.ИтогАвансов - Форма.ИтогПоОтчету;
	
	Если ИтогоОстаток >= 0 Тогда
		Форма.ИтогоОстаток     = ИтогоОстаток;
		Форма.ТипИтогаПоОтчету = НСтр("ru = 'Остаток:'");
	Иначе
		Форма.ИтогоОстаток     = -ИтогоОстаток;
		Форма.ТипИтогаПоОтчету = НСтр("ru = 'Перерасход:'");
	КонецЕсли;
	
	Форма.ОтметкаНезаполненногоСклад = Объект.Товары.Количество() > 0 ИЛИ Объект.ВозвратнаяТара.Количество() > 0;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СформироватьНадписьЦеныИВалюта(Форма)
	
	Объект = Форма.Объект;
	СтруктураНадписи = Новый Структура("ВалютаРегламентированногоУчета", Форма.ВалютаРегламентированногоУчета);
	
	Если (Форма.ЕстьВалютныйУчет) Тогда
		СтруктураНадписи.Вставить("ВалютаДокумента",  Объект.ВалютаДокумента);
		СтруктураНадписи.Вставить("Курс",             Объект.КурсДокумента);
		СтруктураНадписи.Вставить("Кратность",        Объект.КратностьДокумента);
	КонецЕсли;
	
	Если (Не Форма.ПлательщикНДС) ИЛИ (Объект.НДСНеВыделять) Тогда
		СтруктураНадписи.Вставить("НДСНеВыделять"	   , Объект.НДСНеВыделять);		
	КонецЕсли;	
	
	Если НЕ Объект.НДСНеВыделять Тогда
		СтруктураНадписи.Вставить("СуммаВключаетНДС", Объект.СуммаВключаетНДС);
		СтруктураНадписи.Вставить("НДСВключенВСтоимость", Объект.НДСВключенВСтоимость);
	КонецЕсли;
	
	Если Форма.ИспользоватьТипыЦенНоменклатуры Тогда
		СтруктураНадписи.Вставить("ТипЦен", Объект.ТипЦен);
	КонецЕсли;
	
	Форма.ЦеныИВалюта = ОбщегоНазначенияБПКлиентСервер.СформироватьНадписьЦеныИВалюта(СтруктураНадписи);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьНадписиВРасшифровке(СтрокаПлатеж)
	
	СтрокаПлатеж.НадписьКурс      = НСтр("ru = 'Курс'");
	СтрокаПлатеж.НадписьКратность = НСтр("ru = 'Кратн.'");
	СтрокаПлатеж.НадписьСумма     = НСтр("ru = 'Сумма'");
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыбраннуюСчетФактуру(СтрокаТаблицы, ВыбранноеЗначение)
	
	ПараметрыСчетФактуры = ПолучитьПараметрыСчетФактуры(ВыбранноеЗначение, Объект.Ссылка);
	
	Если НЕ СтрокаТаблицы.ПредъявленСФ Тогда
		СтрокаТаблицы.ПредъявленСФ = Истина;
	КонецЕсли;
	
	ПараметрыВыбраннойСчетФактуры = Новый Структура;
	ПараметрыВыбраннойСчетФактуры.Вставить("ОбновитьРеквизитыСтроки", Ложь);
	ПараметрыВыбраннойСчетФактуры.Вставить("ОбновитьСчетФактуру",     Ложь);
	ПараметрыВыбраннойСчетФактуры.Вставить("ПараметрыСчетФактуры",    ПараметрыСчетФактуры);
	ПараметрыВыбраннойСчетФактуры.Вставить("СтрокаТаблицы",           СтрокаТаблицы);
	ПараметрыВыбраннойСчетФактуры.Вставить("ВыбранноеЗначение",       ВыбранноеЗначение);
	
	Если СтрокаТаблицы.ДатаСФ <> ПараметрыСчетФактуры.ДатаВходящегоДокумента
		ИЛИ СтрокаТаблицы.НомерСФ <> ПараметрыСчетФактуры.НомерВходящегоДокумента Тогда
		Если НЕ ПараметрыСчетФактуры.ЕстьОснование Тогда
			ТекстВопроса = НСтр("ru = 'Дата и номер счета-фактуры, указанные в текущей строке, не соответствуют вх. дате и номеру выбранного счета-фактуры.
				|В выбранном счете-фактуре отсутствует ссылка на текущий авансовый отчет.
				|Обновить дату и номер счета-фактуры в текущей строке и добавить документ в качестве документа-основания в счет-фактуру?'");
			
			Оповещение = Новый ОписаниеОповещения("ВопросОбновитьДатуНомерДобавитьДокументЗавершение", ЭтотОбъект, ПараметрыВыбраннойСчетФактуры);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			ТекстВопроса = НСтр("ru = 'Дата и номер счета-фактуры, указанные в текущей строке, не соответствуют вх. дате и номеру выбранного счета-фактуры.
				|Обновить дату и номер счета-фактуры в текущей строке?'");
			
			Оповещение = Новый ОписаниеОповещения("ВопросОбновитьДатуНомерЗавершение", ЭтотОбъект, ПараметрыВыбраннойСчетФактуры);
			ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		
	ИначеЕсли НЕ ПараметрыСчетФактуры.ЕстьОснование Тогда
		ТекстВопроса = НСтр("ru = 'В выбранном счете-фактуре отсутствует ссылка на текущий авансовый отчет.
			|Добавить документ в качестве документа-основания в счет-фактуру?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросДобавитьДокументВОснованиеЗавершение", ЭтотОбъект, ПараметрыВыбраннойСчетФактуры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРеквизитыСтрокиОбновитьСчетФактуру(ПараметрыВыбраннойСчетФактуры)
	
	СтрокаТаблицы        = ПараметрыВыбраннойСчетФактуры.СтрокаТаблицы;
	ПараметрыСчетФактуры = ПараметрыВыбраннойСчетФактуры.ПараметрыСчетФактуры;
	ВыбранноеЗначение    = ПараметрыВыбраннойСчетФактуры.ВыбранноеЗначение;
	
	Если ПараметрыВыбраннойСчетФактуры.ОбновитьРеквизитыСтроки Тогда
		СтрокаТаблицы.ДатаСФ  = ПараметрыСчетФактуры.ДатаВходящегоДокумента;
		СтрокаТаблицы.НомерСФ = ПараметрыСчетФактуры.НомерВходящегоДокумента;
	КонецЕсли;
	
	Если ПараметрыВыбраннойСчетФактуры.ОбновитьСчетФактуру Тогда
		Результат = ОбновитьСчетФактуру(ВыбранноеЗначение, Объект.Ссылка);
		
		Если Результат Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Изменение:'"),
				ПолучитьНавигационнуюСсылку(ВыбранноеЗначение), Строка(ВыбранноеЗначение));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура выполняет пересчет в табличной части документа после изменений
// в форме "Цены и валюта".Выполняется пересчет колонок: цена, скидка, сумма,
// сумма НДС, всего.
//
&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалюты()
	
	// 1. Формируем структуру параметров для заполнения формы "Цены и Валюта".
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Организация",          Объект.Организация);
	СтруктураПараметров.Вставить("ДатаДокумента",        Объект.Дата);
	СтруктураПараметров.Вставить("ВалютаДокумента",      Объект.ВалютаДокумента);
	СтруктураПараметров.Вставить("ТипЦен",               Объект.ТипЦен);
	СтруктураПараметров.Вставить("СуммаВключаетНДС",     Объект.СуммаВключаетНДС);
	СтруктураПараметров.Вставить("НДСВключенВСтоимость", Объект.НДСВключенВСтоимость);
	СтруктураПараметров.Вставить("ТолькоПросмотр",       ТолькоПросмотр);
	
	Если Не ПлательщикНДС Тогда
		СтруктураПараметров.Вставить("НДСНеВыделять"	   , Объект.НДСНеВыделять);
	КонецЕсли;
	
	// 2. Открвыаем форму "Цены и Валюта".
	ДополнительныеПараметры = Новый Структура;
	
	Если (НЕ РаздельныйУчетНДСНаСчете19 И НЕ Объект.НДСНеВыделять)
		ИЛИ ИспользоватьТипыЦенНоменклатуры
		ИЛИ ЕстьВалютныйУчет Тогда
		ОткрыватьИзМеню = Ложь;
		СтруктураПараметров.Вставить("КурсДокумента",        Объект.КурсДокумента);
		СтруктураПараметров.Вставить("КратностьДокумента",   Объект.КратностьДокумента);
	Иначе
		ОткрыватьИзМеню = Истина;
		СтруктураПараметров.Вставить("Курс",        Объект.КурсДокумента);
		СтруктураПараметров.Вставить("Кратность",   Объект.КратностьДокумента);
		Если ПлательщикНДС Тогда
			СтруктураПараметров.Вставить("НДСНеВыделять", Ложь);
		КонецЕсли;
		ДополнительныеПараметры.Вставить("СтруктураПараметровКоманды", СтруктураПараметров);
	КонецЕсли;
	
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	Если ОткрыватьИзМеню Тогда
		СписокКоманд = Новый СписокЗначений;
		Если Не ПлательщикНДС Тогда
			СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСНеВыделять"));
		КонецЕсли;
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДССверху"));
		СписокКоманд.Добавить(ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме"));
		Если НЕ ТолькоПросмотр Тогда
			ПоказатьВыборИзМеню(ОповещениеОЗакрытии, СписокКоманд, Элементы.ЦеныИВалюта);
		КонецЕсли;
	Иначе
		ОткрытьФорму("ОбщаяФорма.ФормаЦеныИВалюта", СтруктураПараметров,,,,, ОповещениеОЗакрытии);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРассчитатьСуммы(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь)
	
	ТаблицаЦенНоменклатуры = Неопределено;
	
	Если ПерезаполнитьЦены Тогда
		
		МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "Номенклатура", Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНоменклатуры,
			ОбщегоНазначения.ВыгрузитьКолонку(Объект.ВозвратнаяТара, "Номенклатура"), Истина);
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивНоменклатуры,
			ОбщегоНазначения.ВыгрузитьКолонку(Объект.Прочее, "Номенклатура"), Истина);
		
		Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
			ТаблицаЦенНоменклатуры = Ценообразование.ПолучитьТаблицуЦенНоменклатуры(
				МассивНоменклатуры,
				Объект.ТипЦен,
				Объект.Дата);
		КонецЕсли;
		
	КонецЕсли;
	
	Если КурсДоИзменения <> 0 И КратностьДоИзменения <> 0 Тогда
		СтруктураКурса = Новый Структура("Курс, Кратность", КурсДоИзменения, КратностьДоИзменения);
	Иначе
		СтруктураКурса = РаботаСКурсамиВалют.ПолучитьКурсВалюты(ВалютаДоИзменения, Объект.Дата);
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из Объект.Товары Цикл
		ЗаполнитьРассчитатьЦеныИСуммыВСтроке(
			СтрокаТаблицы,
			ТаблицаЦенНоменклатуры,
			ВалютаДоИзменения,
			СтруктураКурса,
			ПерезаполнитьЦены,
			ПересчитатьЦены,
			ПересчитатьНДС,
			Истина, // ЕстьНДС
			Объект.НДСНеВыделять,
			Истина, // ЕстьЦена
			0);     // ЗначениеПустогоКоличества
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из Объект.ВозвратнаяТара Цикл
		ЗаполнитьРассчитатьЦеныИСуммыВСтроке(
			СтрокаТаблицы,
			ТаблицаЦенНоменклатуры,
			ВалютаДоИзменения,
			СтруктураКурса,
			ПерезаполнитьЦены,
			ПересчитатьЦены,
			ПересчитатьНДС,
			Ложь,   // ЕстьНДС
			Объект.НДСНеВыделять,
			Истина, // ЕстьЦена
			0);     // ЗначениеПустогоКоличества
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из Объект.Прочее Цикл
		ЗаполнитьРассчитатьЦеныИСуммыВСтроке(
			СтрокаТаблицы,
			ТаблицаЦенНоменклатуры,
			ВалютаДоИзменения,
			СтруктураКурса,
			Ложь,   // ПерезаполнитьЦены
			ПересчитатьЦены,
			ПересчитатьНДС,
			Истина, // ЕстьНДС
			Объект.НДСНеВыделять,
			Ложь,   // ЕстьЦена
			0);     // ЗначениеПустогоКоличества
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из Объект.ОплатаПоставщикам Цикл
		ЗаполнитьРассчитатьЦеныИСуммыВСтроке(
			СтрокаТаблицы,
			ТаблицаЦенНоменклатуры,
			ВалютаДоИзменения,
			СтруктураКурса,
			Ложь,   // ПерезаполнитьЦены
			ПересчитатьЦены,
			Ложь,   // ПересчитатьНДС,
			Ложь,   // ЕстьНДС
			Объект.НДСНеВыделять,
			Ложь,   // ЕстьЦена
			0);     // ЗначениеПустогоКоличества
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРассчитатьЦеныИСуммыВСтроке(СтрокаТаблицы, ТаблицаЦенНоменклатуры, ВалютаПередИзменением,
		СтруктураКурса, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС, ЕстьНДС, НДСНеВыделять, ЕстьЦена, ЗначениеПустогоКоличества)
	
	Если ПерезаполнитьЦены И ТаблицаЦенНоменклатуры <> Неопределено Тогда
		
		НайденнаяСтрока = ТаблицаЦенНоменклатуры.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
		Если НайденнаяСтрока <> Неопределено Тогда
			Цена = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				НайденнаяСтрока.Цена, НайденнаяСтрока.Валюта, Объект.ВалютаДокумента, НайденнаяСтрока.Курс,
				Объект.КурсДокумента, НайденнаяСтрока.Кратность, Объект.КратностьДокумента);
				ЦенаВключаетНДС = НайденнаяСтрока.ЦенаВключаетНДС;
		Иначе
			Цена = 0;
			// Признак того, что цена включает НДС, хранится в реквизите ЦенаВключаетНДС типа цен
			Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
				ЦенаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ТипЦен, "ЦенаВключаетНДС");
			Иначе
				ЦенаВключаетНДС = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		Если ПересчитатьЦены Тогда
			
			Цена = РаботаСКурсамиВалютБПКлиентСервер.ПересчитатьИзВалютыВВалюту(
				?(ЕстьЦена, СтрокаТаблицы["Цена"], СтрокаТаблицы["Сумма"]),
				ВалютаПередИзменением, Объект.ВалютаДокумента, СтруктураКурса.Курс,
				Объект.КурсДокумента, СтруктураКурса.Кратность, Объект.КратностьДокумента);
			
		Иначе
			Цена = ?(ЕстьЦена, СтрокаТаблицы["Цена"], СтрокаТаблицы["Сумма"]);
		КонецЕсли;
		
		// Признак того, что цена включает НДС, хранится в реквизите СуммаВключаетНДС документа
		ЦенаВключаетНДС = ?(ПересчитатьНДС, НЕ Объект.СуммаВключаетНДС, Объект.СуммаВключаетНДС);
	КонецЕсли;
	
	Если ЕстьНДС Тогда
		Если ЕстьЦена Тогда
			СтрокаТаблицы.Цена  = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				Цена,
				ЦенаВключаетНДС,
				Объект.СуммаВключаетНДС,
				УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС));
			
			СтрокаТаблицы.Сумма = СтрокаТаблицы.Цена *
				?(СтрокаТаблицы.Количество = 0, ЗначениеПустогоКоличества, СтрокаТаблицы.Количество);
		Иначе
			СтрокаТаблицы.Сумма = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(
				Цена,
				ЦенаВключаетНДС,
				Объект.СуммаВключаетНДС,
				УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС));
		КонецЕсли;

		Если НДСНеВыделять Тогда
			СтрокаТаблицы.СтавкаНДС = Перечисления.СтавкиНДС.БезНДС;
		КонецЕсли;
		
		СтрокаТаблицы.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(
			СтрокаТаблицы.Сумма,
			Объект.СуммаВключаетНДС,
			УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТаблицы.СтавкаНДС));
		
		Если ЕстьЦена Тогда
			СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
		КонецЕсли;
	Иначе
		Если ЕстьЦена Тогда
			СтрокаТаблицы.Цена  = Цена;
			СтрокаТаблицы.Сумма = СтрокаТаблицы.Цена * ?(СтрокаТаблицы.Количество = 0, ЗначениеПустогоКоличества, СтрокаТаблицы.Количество);
		Иначе
			СтрокаТаблицы.Сумма = Цена;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьПараметрыОтбораДокументАванса()
	
	СтрокаТаблицы = Элементы.ВыданныеАвансы.ТекущиеДанные;
	Если СтрокаТаблицы.ДокументАванса <> Неопределено Тогда
		МассивПараметров = Новый Массив();
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.Организация", Объект.Организация);
		МассивПараметров.Добавить(НовыйПараметр);
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ВалютаДокумента", Объект.ВалютаДокумента);
		МассивПараметров.Добавить(НовыйПараметр);
		
		Если ТипЗнч(СтрокаТаблицы.ДокументАванса) = Тип("ДокументСсылка.РасходныйКассовыйОрдер") Тогда
			Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
				НовыйПараметр = Новый ПараметрВыбора("Отбор.Контрагент", Объект.ФизЛицо);
				МассивПараметров.Добавить(НовыйПараметр);
				НовыйПараметр = Новый ПараметрВыбора("Отбор.ВидОперации",
					ПредопределенноеЗначение("Перечисление.ВидыОперацийРКО.ВыдачаПодотчетномуЛицу"));
				МассивПараметров.Добавить(НовыйПараметр);
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТаблицы.ДокументАванса) = Тип("ДокументСсылка.СписаниеСРасчетногоСчета") Тогда
			Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
				НовыйПараметр = Новый ПараметрВыбора("Отбор.Контрагент", Объект.ФизЛицо);
				МассивПараметров.Добавить(НовыйПараметр);
				НовыйПараметр = Новый ПараметрВыбора("Отбор.ВидОперации",
					ПредопределенноеЗначение("Перечисление.ВидыОперацийСписаниеДенежныхСредств.ПеречислениеПодотчетномуЛицу"));
				МассивПараметров.Добавить(НовыйПараметр);
				НовыйПараметр = Новый ПараметрВыбора("МножественныйВыбор", Истина);
				МассивПараметров.Добавить(НовыйПараметр);
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТаблицы.ДокументАванса) = Тип("ДокументСсылка.ВыдачаДенежныхДокументов") Тогда
			Если ЗначениеЗаполнено(Объект.ФизЛицо) Тогда
				НовыйПараметр = Новый ПараметрВыбора("Отбор.Контрагент", Объект.ФизЛицо);
				МассивПараметров.Добавить(НовыйПараметр);
			КонецЕсли;
			НовыйПараметр = Новый ПараметрВыбора("Отбор.ВидОперации",
				ПредопределенноеЗначение("Перечисление.ВидыОперацийВыдачаДенежныхДокументов.ВыдачаПодотчетномуЛицу"));
			МассивПараметров.Добавить(НовыйПараметр);
		КонецЕсли;
		
		Элементы.ВыданныеАвансыДокументАванса.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьПараметрыВыбораДоговораСКонтрагентом(Форма, Знач ВидыДоговоровОплата, Знач ОплатаВВалюте)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	НовыеПараметры = Новый Массив();
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.ОплатаВВалюте", ОплатаВВалюте));
	Если ОплатаВВалюте Тогда
		НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.ВалютаВзаиморасчетов", Объект.ВалютаДокумента));
		НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.Валютный", ОплатаВВалюте));
	КонецЕсли;
	
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.ВидДоговора", ВидыДоговоровОплата));
	Элементы.ОплатаПоставщикамДоговорКонтрагента.ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметры);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПараметрыПодбора(ИмяТаблицы)
	
	ПараметрыФормы = Новый Структура;
	
	ДатаРасчетов     = ?(НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДата()), Неопределено, Объект.Дата);
	ЗаголовокПодбора = НСтр("ru = 'Подбор номенклатуры в %1 (%2)'");
	
	Валюта = Объект.ВалютаДокумента;
	
	Если ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		Параметрыформы.Вставить("ПоказыватьЦены", Истина);
	КонецЕсли;
	
	Если ИмяТаблицы = "Товары" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Товары'");
		
		ПараметрыФормы.Вставить("ПоказыватьОстатки",   Истина);
	ИначеЕсли ИмяТаблицы = "ВозвратнаяТара" Тогда
		ПредставлениеТаблицы = НСтр("ru = 'Возвратная тара'");
	КонецЕсли;
	
	ЗаголовокПодбора = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокПодбора, Объект.Ссылка, ПредставлениеТаблицы);
	
	ПараметрыФормы.Вставить("ЕстьЦена",       Истина);
	ПараметрыФормы.Вставить("ЕстьКоличество", Истина);
	ПараметрыФормы.Вставить("ДатаРасчетов",   ДатаРасчетов);
	ПараметрыФормы.Вставить("ТипЦен",         Объект.ТипЦен);
	ПараметрыФормы.Вставить("Валюта",         Валюта);
	ПараметрыФормы.Вставить("Организация",    Объект.Организация);
	ПараметрыФормы.Вставить("Подразделение",  Объект.ПодразделениеОрганизации);
	ПараметрыФормы.Вставить("Склад",          Объект.Склад);
	ПараметрыФормы.Вставить("Заголовок",      ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВидПодбора",     ПолучитьВидПодбора(ИмяТаблицы));
	ПараметрыФормы.Вставить("ИмяТаблицы",     ИмяТаблицы);
	ПараметрыФормы.Вставить("Услуги",         ИмяТаблицы = "Услуги");
	
	Если НЕ ЗначениеЗаполнено(Объект.ТипЦен) Тогда
		ПараметрыФормы.Вставить("ЗаполнятьЦеныПоПокупке", Истина);
		Если ИмяТаблицы = "ВозвратнаяТара" Тогда
			ПараметрыФормы.Вставить("СуммаВключаетНДС"		, Истина);
		Иначе
			ПараметрыФормы.Вставить("СуммаВключаетНДС"		, Объект.СуммаВключаетНДС);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаКлиенте
Функция ПолучитьВидПодбора(ИмяТаблицы)
	
	ВидПодбора = "";
	
	Возврат ВидПодбора;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ЗАВЕРШЕНИЕ НЕМОДАЛЬНЫХ ВЫЗОВОВ

&НаКлиенте
Процедура ОплатаПоставщикамКурсВзаиморасчетовНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	СтрокаПлатежа = ДополнительныеПараметры.СтрокаПлатежа;
	
	СтруктураКурсаИКратности = РезультатЗакрытия;
	
	Если СтруктураКурсаИКратности <> Неопределено Тогда
		СтрокаПлатежа.КурсВзаиморасчетов      = СтруктураКурсаИКратности.КурсВалюты;
		СтрокаПлатежа.КратностьВзаиморасчетов = СтруктураКурсаИКратности.КратностьВалюты;
	КонецЕсли;
	
	РассчитатьСуммуВзаиморасчетов(ЭтотОбъект);
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзмененияПоКнопкеЦеныИВалютыЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.Свойство("СтруктураПараметровКоманды") Тогда
		
		СтруктураЦеныИВалюта = ДополнительныеПараметры.СтруктураПараметровКоманды;
		
		СуммаВключаетНДСДоИзменения = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Если РезультатЗакрытия = Неопределено Тогда
			Возврат;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСНеВыделять") Тогда
			СтруктураЦеныИВалюта.НДСНеВыделять 		= Истина;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Истина;
		ИначеЕсли РезультатЗакрытия.Значение = ПредопределенноеЗначение("Перечисление.ВариантыРасчетаНДС.НДСВСумме") Тогда
			СтруктураЦеныИВалюта.НДСНеВыделять 		= Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Истина;
		Иначе
			СтруктураЦеныИВалюта.НДСНеВыделять 		= Ложь;
			СтруктураЦеныИВалюта.СуммаВключаетНДС 	= Ложь;
		КонецЕсли;
		
		СтруктураЦеныИВалюта.Вставить("ПерезаполнитьЦены",    Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьЦены",      Ложь);
		СтруктураЦеныИВалюта.Вставить("ПересчитатьНДС",       СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.НДСНеВыделять <> СтруктураЦеныИВалюта.НДСНеВыделять);
		СтруктураЦеныИВалюта.Вставить("БылиВнесеныИзменения", СуммаВключаетНДСДоИзменения <> СтруктураЦеныИВалюта.СуммаВключаетНДС
			ИЛИ Объект.НДСНеВыделять <> СтруктураЦеныИВалюта.НДСНеВыделять);
		
	Иначе
		СтруктураЦеныИВалюта = РезультатЗакрытия;
	КонецЕсли;
	
	Если ТипЗнч(СтруктураЦеныИВалюта) = Тип("Структура") И СтруктураЦеныИВалюта.БылиВнесеныИзменения Тогда
		
		ВалютаДоИзменения    = Объект.ВалютаДокумента;
		КурсДоИзменения      = Объект.КурсДокумента;
		КратностьДоИзменения = Объект.КратностьДокумента;
		
		Объект.ТипЦен               = СтруктураЦеныИВалюта.ТипЦен;
		Объект.СуммаВключаетНДС     = СтруктураЦеныИВалюта.СуммаВключаетНДС;
		Объект.НДСНеВыделять		= СтруктураЦеныИВалюта.НДСНеВыделять;
		Объект.НДСВключенВСтоимость = СтруктураЦеныИВалюта.НДСВключенВСтоимость;
		Объект.ВалютаДокумента      = СтруктураЦеныИВалюта.ВалютаДокумента;
		Объект.КурсДокумента        = СтруктураЦеныИВалюта.Курс;
		Объект.КратностьДокумента   = СтруктураЦеныИВалюта.Кратность;
		
		Модифицированность = Истина;
		
		Если ВалютаДоИзменения <> Объект.ВалютаДокумента Тогда
			Если Объект.ВыданныеАвансы.Количество() > 0 Тогда
				ТекстВопроса =
					НСтр("ru = 'Изменилась валюта документа. Погашаемые данным документом авансы должны быть выписаны в той же валюте.
						|Очистить табличную часть ""Авансы""?'");
				
				Оповешение = Новый ОписаниеОповещения("ВопросОчиститьАвансыЗавершение", ЭтотОбъект);
				ПоказатьВопрос(Оповешение, ТекстВопроса, РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
			КонецЕсли;
			
			ОплатаВВалютеДоИзменения = ВалютаДоИзменения <> ВалютаРегламентированногоУчета;
			ОплатаВВалюте = Объект.ВалютаДокумента <> ВалютаРегламентированногоУчета;
			УстановитьПараметрыВыбораДоговораСКонтрагентом(ЭтотОбъект, ВидыДоговоровОплата, ОплатаВВалюте);
			
			ОчиститьДоговораОплатаПоставщикам(ОплатаВВалюте, ОплатаВВалюте <> ОплатаВВалютеДоИзменения);
		КонецЕсли;
		
		// Пересчитываем табличные части.
		Если СтруктураЦеныИВалюта.ПерезаполнитьЦены ИЛИ СтруктураЦеныИВалюта.ПересчитатьЦены ИЛИ СтруктураЦеныИВалюта.ПересчитатьНДС Тогда
			ЗаполнитьРассчитатьСуммы(
				ВалютаДоИзменения,
				КурсДоИзменения,
				КратностьДоИзменения,
				СтруктураЦеныИВалюта.ПерезаполнитьЦены,
				СтруктураЦеныИВалюта.ПересчитатьЦены,
				СтруктураЦеныИВалюта.ПересчитатьНДС);
		КонецЕсли;
		
		СформироватьНадписьЦеныИВалюта(ЭтотОбъект);
	КонецЕсли;
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-24 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	ок_УстановитьДоступностьРасчетаКомандировок();
	//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-24 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
КонецПроцедуры

&НаСервере
Процедура ОчиститьДоговораОплатаПоставщикам(ОплатаВВалюте, Знач ПерезаполнитьСчета)
	
	ИзменилсяДоговорКонтрагента = Ложь;
	Для каждого СтрокаТаблицы Из Объект.ОплатаПоставщикам Цикл
		Изменения = ОплатаПоставщикамУстановитьДоговорКонтрагента(
			СтрокаТаблицы.Контрагент,
			СтрокаТаблицы.ДоговорКонтрагента,
			Объект.Организация,
			Объект.ВалютаДокумента,
			ОплатаВВалюте);
		ЗаполнитьЗначенияСвойств(СтрокаТаблицы, Изменения);
	КонецЦикла;
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	Если ПерезаполнитьСчета Тогда
		ПричиныИзменения = Новый Массив;
		ПричиныИзменения.Добавить("ОплатаПоставщикам.ДоговорКонтрагента");
		СчетаУчетаВДокументах.ЗаполнитьОбъектПриИзменении(
			АвансовыйОтчетФормыКлиентСервер.НачатьЗаполнениеСчетовУчета(ПричиныИзменения, Объект));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОчиститьАвансыЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Объект.ВыданныеАвансы.Очистить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОбновитьДатуНомерДобавитьДокументЗавершение(Результат, ПараметрыВыбраннойСчетФактуры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыВыбраннойСчетФактуры.ОбновитьРеквизитыСтроки = Истина;
		ПараметрыВыбраннойСчетФактуры.ОбновитьСчетФактуру     = Истина;
		ОбновитьРеквизитыСтрокиОбновитьСчетФактуру(ПараметрыВыбраннойСчетФактуры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросОбновитьДатуНомерЗавершение(Результат, ПараметрыВыбраннойСчетФактуры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыВыбраннойСчетФактуры.ОбновитьРеквизитыСтроки = Истина;
		ОбновитьРеквизитыСтрокиОбновитьСчетФактуру(ПараметрыВыбраннойСчетФактуры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросДобавитьДокументВОснованиеЗавершение(Результат, ПараметрыВыбраннойСчетФактуры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыВыбраннойСчетФактуры.ОбновитьСчетФактуру = Истина;
		ОбновитьРеквизитыСтрокиОбновитьСчетФактуру(ПараметрыВыбраннойСчетФактуры);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ БСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#Область УсловноеОформление

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	НастройкиУсловногоОформления = Новый Структура();
	
	УсловноеОформление.Элементы.Очистить();
	
	// Условное оформление, связанное с видимостью, устанавливаем сразу для всех колонок.
	УстановитьУсловноеОформлениеШапкаИВидимость();
	
	// Условное оформление для полей, расположенных на страницах
	ОбновитьУсловноеОформление(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеШапкаИВидимость()
	
	// Склад
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Склад");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Склад", ВидСравненияКомпоновкиДанных.НеЗаполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ОтметкаНезаполненногоСклад", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Истина);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "Склад");
	
	ГруппаОтбораИли = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли,
		"Объект.Склад", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбораИли,
		"ОтметкаНезаполненногоСклад", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ПрочееСчетЗатратНУ
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСчетЗатратНУ");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"ПлательщикНДФЛ", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"ПрименяетсяУСН", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// ОплатаПоставщикамСделка
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамСделка");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.ОплатаПоставщикам.СпособПогашенияЗадолженности",
		ВидСравненияКомпоновкиДанных.НеРавно,
		Перечисления.СпособыПогашенияЗадолженности.ПоДокументу);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Субконто БУ и НУ
	// Первое субконто не скрываем, чтобы не мигала колонка в разных строках.
	
	Для Сч = 1 По 3 Цикл
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСубконто" + Сч);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.Прочее.Субконто" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
		// ПрочееСубконтоНУ
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСубконтоНУ" + Сч);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.Прочее.СубконтоНУ" + Сч + "Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	КонецЦикла;
	
	// Субконто НУ
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСубконтоНУ1");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Прочее.СубконтоНУ1Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Прочее.СубконтоНУ2Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Прочее.СубконтоНУ3Доступность", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ТекстЗапрещеннойЯчейкиЦвет);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<не требуется>'"));
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// БилетыСубконто БУ
	
	Для Сч = 1 По 3 Цикл
		
		// Пустое субконто в виде <...>
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "БилетыСубконто" + Сч);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			"Объект.Билеты.Субконто" + Сч + "Доступность",
			ВидСравненияКомпоновкиДанных.Равно,
			Истина);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.Билеты.Субконто" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
		
	КонецЦикла;
	
	// БилетыСубконто БУ скроем если не используется
	
	Для Сч = 2 По 3 Цикл
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "БилетыСубконто" + Сч);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			"Объект.Билеты.Субконто" + Сч + "Доступность",
			ВидСравненияКомпоновкиДанных.Равно,
			Ложь);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.Билеты.Субконто" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
		
	КонецЦикла;
	
	// БилетыПодразделениеЗатрат
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "БилетыПодразделениеЗатрат");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Билеты.ПодразделениеЗатратДоступность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Билеты.ПодразделениеЗатратНУДоступность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ОплатаПоставщикамНадписьКратность, ОплатаПоставщикамКратностьВзаиморасчетов
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамНадписьКратность");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамКратностьВзаиморасчетов");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.ОплатаПоставщикам.КратностьВзаиморасчетов", ВидСравненияКомпоновкиДанных.Равно, 0);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.ОплатаПоставщикам.КратностьВзаиморасчетов", ВидСравненияКомпоновкиДанных.Равно, 1);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Реквизиты при расчетах в у.е.
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамНадписьКурс");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамНадписьКратность");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамНадписьСумма");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамКурсВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамКратностьВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамСуммаВзаиморасчетов");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"ЕстьРасчетыВУсловныхЕдиницах", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
	// Скрыть НДС при применении УСН доходы
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыВсего");            
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПоставщик");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПредъявленСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДатаСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНомерСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетФактура");
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСтавкаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееВсего");
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееПоставщик");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееПредъявленСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееДатаСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееНомерСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСчетФактура");
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "БилетыСуммаНДС");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "БилетыПеревозчик");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.НДСНеВыделять", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеТовары() Экспорт
	
	НастройкиУсловногоОформления.Вставить("ТоварыПроинициализировано", Истина);
	
	// ТоварыСуммаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСуммаНДС");
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ТоварыПоставщик, ТоварыДатаСФ, ТоварыНомерСФ
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыПоставщик");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыДатаСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыНомерСФ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"Объект.Товары.ПредъявленСФ", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ТоварыСпособУчетаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСпособУчетаНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СпособУчетаНДС", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СуммаНДС", ВидСравненияКомпоновкиДанных.Равно, 0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ТоварыСчетУчетаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчетаНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СчетУчетаНДС", ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
		"Объект.Товары.СуммаНДС", ВидСравненияКомпоновкиДанных.Равно, 0);
	
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
		"ПлательщикНДС", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
		"Объект.НДСВключенВСтоимость", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
		"РаздельныйУчетНДСНаСчете19", ВидСравненияКомпоновкиДанных.Равно, Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
		"Объект.НДСВключенВСтоимость", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
		"Объект.Товары.ПредъявленСФ", ВидСравненияКомпоновкиДанных.Равно, Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	
	// ТоварыСчетУчетаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ТоварыСчетУчетаНДС");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
				"Объект.НДСВключенВСтоимость", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеОплатаПоставщикам() Экспорт
	
	НастройкиУсловногоОформления.Вставить("ОплатаПоставщикамПроинициализировано", Истина);
	
	// ОплатаПоставщикамСчетУчетаРасчетовСКонтрагентом
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамСчетУчетаРасчетовСКонтрагентом");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.ОплатаПоставщикам.СпособПогашенияЗадолженности",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СпособыПогашенияЗадолженности.НеПогашать);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ОплатаПоставщикамКурсВзаиморасчетов, ОплатаПоставщикамКратностьВзаиморасчетов, ОплатаПоставщикамСуммаВзаиморасчетов
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамКурсВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамКратностьВзаиморасчетов");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ОплатаПоставщикамСуммаВзаиморасчетов");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.ОплатаПоставщикам.ДоговорКонтрагентаРасчетыВУсловныхЕдиницах",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПрочее() Экспорт
	
	Если НЕ НастройкиУсловногоОформления.Свойство("ПрочееОбщееПроинициализировано") Тогда
		УстановитьУсловноеОформлениеПрочееОбщее();
	КонецЕсли;
	
	Если НЕ НастройкиУсловногоОформления.Свойство("ПрочееНУПроинициализировано") Тогда
		УстановитьУсловноеОформлениеПрочееНУ();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПрочееОбщее()
	
	НастройкиУсловногоОформления.Вставить("ПрочееОбщееПроинициализировано", Истина);
	
	// Субконто БУ
	
	Для Сч = 1 По 3 Цикл
		
		// Пустое субконто в виде <...>
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСубконто" + Сч);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			"Объект.Прочее.Субконто" + Сч + "Доступность",
			ВидСравненияКомпоновкиДанных.Равно,
			Истина);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
			"Объект.Прочее.Субконто" + Сч, ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
		
	КонецЦикла;
	
	// ПрочееПодразделениеЗатрат
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееПодразделениеЗатрат");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Прочее.ПодразделениеЗатратДоступность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Прочее.ПодразделениеЗатратНУДоступность",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
		
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ПрочееСуммаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСуммаНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Прочее.СтавкаНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СтавкиНДС.НДС0);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Прочее.СтавкаНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Перечисления.СтавкиНДС.БезНДС);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ПрочееБланкСтрогойОтчетности
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееБланкСтрогойОтчетности");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Прочее.ПредъявленСФ",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
	// ПрочееПоставщик, ПрочееДатаСФ, ПрочееНомерСФ
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееПоставщик");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееДатаСФ");
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееНомерСФ");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Прочее.ПредъявленСФ",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ПрочееСпособУчетаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСпособУчетаНДС");
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ЭлементУО.Отбор,
		"Объект.Прочее.СуммаНДС",
		ВидСравненияКомпоновкиДанных.Равно, 0);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	// ПрочееСчетУчетаНДС
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСчетУчетаНДС");
	
	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Прочее.СчетУчетаНДС",
		ВидСравненияКомпоновкиДанных.Заполнено);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора1,
		"Объект.Прочее.СуммаНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		0);
	
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"ПлательщикНДС",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"Объект.НДСВключенВСтоимость",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ГруппаОтбора1.Элементы,
		ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"РаздельныйУчетНДСНаСчете19",
		ВидСравненияКомпоновкиДанных.Равно,
		Ложь);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"Объект.НДСВключенВСтоимость",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
		ГруппаОтбора2,
		"Объект.Прочее.ПредъявленСФ",
		ВидСравненияКомпоновкиДанных.Равно,
		Истина);
	
	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	ЭлементУО = УсловноеОформление.Элементы.Добавить();
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСчетУчетаНДС");
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
				"Объект.НДСВключенВСтоимость", ВидСравненияКомпоновкиДанных.Равно, Истина);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Видимость", Ложь);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПрочееНУ()
	
	Если НЕ ПлательщикНалогаНаПрибыль Тогда
		// Реквизиты НУ для УСН и ИП на НДФЛ скрываются с помощью функциональной опции, 
		// дополнительного условного оформления не требуется.
		Возврат;
	КонецЕсли;
	
	НастройкиУсловногоОформления.Вставить("ПрочееНУПроинициализировано", Истина);
	
	// Субконто НУ
	
	Для Сч = 1 По 3 Цикл
		
		// Пустое субконто в виде <...>
		
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПрочееСубконтоНУ" + Сч);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			"Объект.Прочее.СубконтоНУ" + Сч + "Доступность",
			ВидСравненияКомпоновкиДанных.Равно,
			Истина);
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ЭлементУО.Отбор,
			"Объект.Прочее.СубконтоНУ" + Сч,
			ВидСравненияКомпоновкиДанных.НеЗаполнено);
		
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НеЗаполненноеСубконто);
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '<...>'"));
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)
	
	Элементы = Форма.Элементы;
	
	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ТоварыПроинициализировано")
		И Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаТовары Тогда
		
		Форма.УстановитьУсловноеОформлениеТовары();
		
	ИначеЕсли НЕ Форма.НастройкиУсловногоОформления.Свойство("ОплатаПоставщикамПроинициализировано")
		И Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОплатаПоставщикам Тогда
		
		Форма.УстановитьУсловноеОформлениеОплатаПоставщикам();
		
	ИначеЕсли Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаПрочее Тогда
		
		НадоИнициализировать = Ложь;
		Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ПрочееОбщееПроинициализировано") Тогда
			НадоИнициализировать = Истина;
		КонецЕсли;
		
		Если Форма.ПлательщикНалогаНаПрибыль
			И НЕ Форма.НастройкиУсловногоОформления.Свойство("ПрочееНУПроинициализировано") Тогда
			НадоИнициализировать = Истина;
		КонецЕсли;
		
		Если НадоИнициализировать Тогда
			Форма.УстановитьУсловноеОформлениеПрочее();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета()
	
	ЭлементыСчетов = Новый Массив();
	ЭлементыСчетов.Добавить("ТоварыСчетУчета");
	ЭлементыСчетов.Добавить("ТоварыСчетУчетаНДС");
	ЭлементыСчетов.Добавить("ТоварыСпособУчетаНДС");
	ЭлементыСчетов.Добавить("ВозвратнаяТараСчетУчета");
	ЭлементыСчетов.Добавить("ВозвратнаяТараСчетУчетаТары");
	ЭлементыСчетов.Добавить("ОплатаПоставщикамСчетУчетаРасчетовСКонтрагентом");
	ЭлементыСчетов.Добавить("ОплатаПоставщикамСчетУчетаРасчетовПоАвансам");
	ЭлементыСчетов.Добавить("ПрочееСчетЗатрат");
	ЭлементыСчетов.Добавить("ПрочееПодразделениеЗатрат");
	ЭлементыСчетов.Добавить("ПрочееСубконто1");
	ЭлементыСчетов.Добавить("ПрочееСубконто2");
	ЭлементыСчетов.Добавить("ПрочееСубконто3");
	ЭлементыСчетов.Добавить("ПрочееСубконтоНУ1");
	ЭлементыСчетов.Добавить("ПрочееСубконтоНУ2");
	ЭлементыСчетов.Добавить("ПрочееСубконтоНУ3");
	ЭлементыСчетов.Добавить("ПрочееСчетЗатратНУ");
	ЭлементыСчетов.Добавить("ПрочееСчетУчетаНДС");
	ЭлементыСчетов.Добавить("ПрочееСпособУчетаНДС");
	ЭлементыСчетов.Добавить("БилетыСчетЗатрат");
	ЭлементыСчетов.Добавить("БилетыПодразделениеЗатрат");
	ЭлементыСчетов.Добавить("БилетыСубконто1");
	ЭлементыСчетов.Добавить("БилетыСубконто2");
	ЭлементыСчетов.Добавить("БилетыСубконто3");
	ЭлементыСчетов.Добавить("БилетыСпособУчетаНДС");
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура ПриИзмененииСубконто(НомерСубконто)
	
	СтрокаТаблицы = Элементы.Прочее.ТекущиеДанные;
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСубконто(
		ЭтотОбъект, 
		СтрокаТаблицы,
		НомерСубконто, 
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
	
	Если ЗначениеЗаполнено(СтрокаТаблицы.СчетЗатрат)
		И ЗначениеЗаполнено(СтрокаТаблицы.СчетЗатратНУ) Тогда
		ДанныеСчетаБУ = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатрат);
		ДанныеСчетаНУ = БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(СтрокаТаблицы.СчетЗатратНУ);
		
		Для ИндексБУ = 1 По 3 Цикл
			Для ИндексНУ = 1 По 3 Цикл
				Если ДанныеСчетаБУ["ВидСубконто" + ИндексБУ + "ТипЗначения"] = ДанныеСчетаНУ["ВидСубконто" + ИндексНУ + "ТипЗначения"] Тогда
					СтрокаТаблицы["СубконтоНУ" + ИндексНУ] = СтрокаТаблицы["Субконто" + ИндексБУ];
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		БухгалтерскийУчетКлиентСервер.УстановитьНачальныеСвойстваСубконтоСтроки(
			ЭтотОбъект, 
			СтрокаТаблицы,
			АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконто(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Элементы.Прочее.ТекущиеДанные, 
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", , Истина));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСубконтоБилеты(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСубконто(
		ЭтотОбъект, 
		Элементы.Билеты.ТекущиеДанные,
		НомерСубконто,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Билеты", , Ложь));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконтоБилеты(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Элементы.Билеты.ТекущиеДанные,
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Билеты", , Ложь));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииСубконтоНУ(НомерСубконто)
	
	БухгалтерскийУчетКлиентСервер.УстановитьСвойстваСубконтоСтрокиПриИзмененииСубконто(
		ЭтотОбъект, 
		Элементы.Прочее.ТекущиеДанные,
		НомерСубконто, 
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	
КонецПроцедуры

&НаКлиенте
Процедура НачалоВыбораСубконтоНУ(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ДанныеОбъекта = БухгалтерскийУчетКлиентСервер.ДанныеУстановкиПараметровСубконто(
		Элементы.Прочее.ТекущиеДанные, 
		АвансовыйОтчетФормыКлиентСервер.ПараметрыУстановкиСвойствСубконтоТаблицы(ЭтотОбъект, "Прочее", "НУ", Истина));
	
	ОбщегоНазначенияБПКлиент.НачалоВыбораЗначенияСубконто(ЭтотОбъект, Элемент, СтандартнаяОбработка, ДанныеОбъекта);

КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

#Область бит_МодификацияФормы

// бит_Финанс изменения кода. Начало:
// Расширение функционала обраотчика "ПриСозданииНаСервере".
//
&НаСервере
Процедура бит_ПриСозданииНаСервереЗавершение()

	Если НЕ ПравоДоступа("Просмотр", Метаданные.Документы.бит_РасходнаяПозиция) Тогда
		Возврат;	
	КонецЕсли; 
	
	ПолеРасходнаяПозиция			= Элементы.Добавить("бит_РасходнаяПозиция", Тип("ПолеФормы"), Элементы.ГруппаНазначениеАвансаИтоги); 
	ПолеРасходнаяПозиция.Вид		= ВидПоляФормы.ПолеВвода;
	ПолеРасходнаяПозиция.ПутьКДанным= "Объект.бит_РасходнаяПозиция";
	ПолеРасходнаяПозиция.Подсказка	= НСтр("ru = 'Ссылка на связанный документ ""Расходная позиция"", для формирования структуры подчиненности.'");

КонецПроцедуры // бит_Финанс изменения кода. Конец.

////+Сундукова 18.04.2016 №2391	
////Ванюков К. + // При вводе ID разноски программа должна найти документ разнесения оборотов // 2012-02-13
//&НаКлиенте
//Процедура ОК_ID_РазноскаПриИзменении(Элемент) Экспорт

//	РазнесениеОборотов = НайтиПоIDРазнесениеОборотов(Объект.ОК_ID_Разноска);
//	Если РазнесениеОборотов = Неопределено Тогда
//		Объект.ОК_РазнесениеОборотов=ПредопределенноеЗначение("Документ.УдалитьИжтиси_бит_БК_РазнесениеОборотов.ПустаяСсылка");
//	    Оповещение = Новый ОписаниеОповещения("ОбработкаПредупреждения", ЭтаФорма);
//		ПоказатьВопрос(Оповещение, "Номер ID разноски неверный!", РежимДиалогаВопрос.ОК,,КодВозвратаДиалога.ОК);
//	Иначе
//		Объект.ОК_РазнесениеОборотов=РазнесениеОборотов;
//	КонецЕсли;

//КонецПроцедуры

//&НаСервереБезКонтекста
//Функция НайтиПоIDРазнесениеОборотов(ОК_ID)
//	Возврат ОК_ОбщегоНазначения.НайтиРазнесениеОборотовПоID(ОК_ID);
//КонецФункции // НайтиПоIDРазнесениеОборотов()
//-Сундукова 18.04.2016 №2391	

 //Обработка оповещенич предупреждения
&НаКлиенте
Процедура ОбработкаПредупреждения(Результат, Параметры)Экспорт
	
	//Если Результат = КодВозвратаДиалога.ОК Тогда
	//    Возврат;
	//КонецЕсли; 
	Возврат;

КонецПроцедуры // ОбработкаПредупреждения()

//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-05-12 (#ТП_БП05_ФР15)
&НаКлиенте
Процедура Подключаемый_ФизЛицоНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Парам = Новый Структура;
	Парам.Вставить("РежимВыбора", Истина);
	Парам.Вставить("ок_Организация", Объект.Организация);
	
	ОткрытьФорму("Справочник.ФизическиеЛица.Форма.ФормаСписка",Парам, ЭтотОбъект);
	
КонецПроцедуры
//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-05-12 (#ТП_БП05_ФР15)

//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Начало 2021-05-19 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
&НаКлиенте
Процедура Подключаемый_ВыбратьПериод(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ДатаНачалаКомандировки", Объект.ДатаНачалаКомандировки);
	ПараметрыФормы.Вставить("ДатаОкончанияКомандировки", Объект.ДатаОкончанияКомандировки);
	
	ОткрытьФорму("Документ.АвансовыйОтчет.Форма.ПериодКомандировки", ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПрогнозПлатежа(Команда)
	
	Если ЭтаФорма.Модифицированность Или Объект.Ссылка.Пустая() Тогда
		ТекстВопроса = НСтр("ru = 'Данные еще не записаны.
		|Выполнение команды ""Создать документы ""Прогноз платежа"""" возможно только после записи данных.
		|Данные будут записаны.'");
		
		Оповещение = Новый ОписаниеОповещения("ок_ВопросСоздатьДокументыПрогнозПлатежа", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ТекстВопроса);
		
	Иначе          		
		ок_ПроверитьДокументыПередСозданием();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ок_СоздатьДокументыПрогнозПлатежа()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ок_ДокументОснование", Объект.Ссылка);
	
	СуммаДокумента = ок_ПолучитьСуммуОстатка();
	Если СуммаДокумента <= 0 Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'По данному авансовому отчету отсутствует перерасход. Документ ""Прогноз платежа"" не будет сформирован'"));
		Возврат;
	КонецЕсли;

	СтатьяОборотов = ок_ПолучитьСтатьюОборотов();
	
	ПараметрыФормы.Вставить("Дата",               ТекущаяДата());
	ПараметрыФормы.Вставить("Организация",        Объект.Организация); 
	ПараметрыФормы.Вставить("ФормаОплаты",        ПредопределенноеЗначение("Перечисление.бит_ВидыДенежныхСредств.Безналичные"));
	ПараметрыФормы.Вставить("ВалютаДокумента",    Объект.ВалютаДокумента);
	ПараметрыФормы.Вставить("КурсДокумента",      Объект.КурсДокумента);
	ПараметрыФормы.Вставить("КратностьДокумента", Объект.КратностьДокумента);
	ПараметрыФормы.Вставить("СтатьяОборотов",     СтатьяОборотов);
	ПараметрыФормы.Вставить("СуммаРегл",          СуммаДокумента);
	ПараметрыФормы.Вставить("СуммаУпр",           СуммаДокумента);
	ПараметрыФормы.Вставить("Сумма",              СуммаДокумента);
	ПараметрыФормы.Вставить("Сценарий",           ок_ПолучитьСценарий());
	ПараметрыФормы.Вставить("Аналитика_5",        Объект.ФизЛицо);
	
	ОткрытьФорму("Документ.бит_ЗаявкаНаРасходованиеСредствОбщая.ФормаОбъекта", ПараметрыФормы);
	
КонецПроцедуры

&НаСервере
Функция ок_ПолучитьСценарий()
	
	 Возврат РегистрыСведений.СБ_НастройкиКазначейства.Получить(Новый Структура("Идентификатор", "ОперативныйСценарийПланирования")).Значение;
	 
КонецФункции

&НаСервере
Функция ок_ПолучитьСуммуОстатка()
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ЕСТЬNULL(-ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток
		|ПОМЕСТИТЬ ВТ_Взаиморасчеты
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Остатки(
		|			,
		|			ДокументРасчетов = &ДокументРасчетов
		|				И ПодотчетноеЛицо = &ФизЛицо) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЕСТЬNULL(бит_ПланируемыйРасходДенежныхСредствОстатки.СуммаОстаток, 0) КАК СуммаПланируемыРасход
		|ПОМЕСТИТЬ ВТ_ПланируемыйРасход
		|ИЗ
		|	РегистрНакопления.бит_ПланируемыйРасходДенежныхСредств.Остатки(
		|			,
		|			ДокументПланирования ССЫЛКА Документ.бит_ЗаявкаНаРасходованиеСредствОбщая
		|				И ВЫРАЗИТЬ(ДокументПланирования КАК Документ.бит_ЗаявкаНаРасходованиеСредствОбщая).ок_ДокументОснование = &ДокументРасчетов) КАК бит_ПланируемыйРасходДенежныхСредствОстатки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		//|	ВТ_ПланируемыйРасход.СуммаПланируемыРасход - ВТ_Взаиморасчеты.СуммаОстаток КАК СуммаОстаток
		|	ВТ_Взаиморасчеты.СуммаОстаток - ВТ_ПланируемыйРасход.СуммаПланируемыРасход КАК СуммаОстаток
		|ИЗ
		|	ВТ_Взаиморасчеты КАК ВТ_Взаиморасчеты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ПланируемыйРасход КАК ВТ_ПланируемыйРасход
		|		ПО (ИСТИНА)";
	
	Запрос.УстановитьПараметр("ДокументРасчетов", Объект.Ссылка);
	Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);

	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		Возврат ВыборкаДетальныеЗаписи.СуммаОстаток;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	Возврат 0;

КонецФункции

&НаСервере
Функция ок_ПолучитьСтатьюОборотов()
	
	Для Каждого СтрокаПрочее Из Объект.Прочее Цикл
		Если Не ЗначениеЗаполнено(СтрокаПрочее.ок_СтатьяОборотов) 
			Или (СтрокаПрочее.ок_СтатьяОборотов.ТипСтатьи<> Перечисления.бит_ТипыСтатейОборотов.БДР
			И СтрокаПрочее.ок_СтатьяОборотов.ТипСтатьи<> Перечисления.бит_ТипыСтатейОборотов.БДДС) Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаПрочее.ок_СтатьяОборотов.ТипСтатьи =  Перечисления.бит_ТипыСтатейОборотов.БДДС Тогда
			Возврат СтрокаПрочее.ок_СтатьяОборотов;
		Иначе
			Возврат РегистрыСведений.бит_СоответствияАналитик.ПолучитьПравуюАналитику(Справочники.бит_ВидыСоответствийАналитик.СтатьиОборотовБДР_СтатьиОборотовБДДС, СтрокаПрочее.ок_СтатьяОборотов);
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого СтрокаТовары Из Объект.Товары Цикл
		Если Не ЗначениеЗаполнено(СтрокаТовары.ок_СтатьяОборотов) 
			Или (СтрокаТовары.ок_СтатьяОборотов.ТипСтатьи<> Перечисления.бит_ТипыСтатейОборотов.БДР
			И СтрокаТовары.ок_СтатьяОборотов.ТипСтатьи<> Перечисления.бит_ТипыСтатейОборотов.БДДС) Тогда
			Продолжить;
		КонецЕсли;
		Если СтрокаТовары.ок_СтатьяОборотов.ТипСтатьи =  Перечисления.бит_ТипыСтатейОборотов.БДДС Тогда
			Возврат СтрокаТовары.ок_СтатьяОборотов;
		Иначе
			Возврат РегистрыСведений.бит_СоответствияАналитик.ПолучитьПравуюАналитику(Справочники.бит_ВидыСоответствийАналитик.СтатьиОборотовБДР_СтатьиОборотовБДДС, СтрокаТовары.ок_СтатьяОборотов);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

&НаКлиенте
Процедура ок_ПроверитьДокументыПередСозданием()
	
	ЕстьДокументы = ок_ЕстьДокументыПрогнозПлатежа();
	
	Если ЕстьДокументы Тогда
		ТекстВопроса = НСтр("ru = 'На основании документа ранее был создан ""Прогноз платежа"". Вы уверены, что хотите создать новый документ? В противном случае будет открыт имеющийся.'");
		
		Оповещение = Новый ОписаниеОповещения("ок_ВопросЕстьДокументы", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ТекстВопроса);
		
	Иначе 
		ок_СоздатьДокументыПрогнозПлатежа();
	КонецЕсли;   
	   
КонецПроцедуры

&НаКлиенте
Процедура ок_ВопросЕстьДокументы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда		
	    ок_СоздатьДокументыПрогнозПлатежа();
	Иначе
		ПервыйДокумент = ок_ПолучитьПервыйДокументПрогнозПлатежа();
		Если Не ПервыйДокумент = Неопределено Тогда
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("Ключ", ПервыйДокумент);
			
			ОткрытьФорму("Документ.бит_ЗаявкаНаРасходованиеСредствОбщая.ФормаОбъекта", ПараметрыФормы);			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ок_ПолучитьПервыйДокументПрогнозПлатежа()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ ПЕРВЫЕ 1
		|	бит_ЗаявкаНаРасходованиеСредствОбщая.Ссылка КАК Ссылка,
		|	бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних.Статус КАК Статус
		|ИЗ
		|	Документ.бит_ЗаявкаНаРасходованиеСредствОбщая КАК бит_ЗаявкаНаРасходованиеСредствОбщая
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ИсторияИзмененияСтатусовОбъектов.СрезПоследних(, Объект ССЫЛКА Документ.бит_ЗаявкаНаРасходованиеСредствОбщая) КАК бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних
		|		ПО бит_ЗаявкаНаРасходованиеСредствОбщая.Ссылка = бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних.Объект
		|ГДЕ
		|	бит_ЗаявкаНаРасходованиеСредствОбщая.ок_ДокументОснование = &Ссылка
		|	И бит_ЗаявкаНаРасходованиеСредствОбщая.Проведен
		|	И бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних.Статус <> ЗНАЧЕНИЕ(Справочник.бит_СтатусыОбъектов.ЗаявкаОбщая_Закрыта)
		|
		|УПОРЯДОЧИТЬ ПО
		|	бит_ЗаявкаНаРасходованиеСредствОбщая.Дата";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		 Возврат Выборка.Ссылка;
	 Иначе
		 Возврат Неопределено;
	КонецЕсли; 	
	
КонецФункции

&НаСервере
Функция ок_ЕстьДокументыПрогнозПлатежа()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	бит_ЗаявкаНаРасходованиеСредствОбщая.Ссылка КАК Ссылка,
		|	бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних.Статус КАК Статус
		|ИЗ
		|	Документ.бит_ЗаявкаНаРасходованиеСредствОбщая КАК бит_ЗаявкаНаРасходованиеСредствОбщая
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ИсторияИзмененияСтатусовОбъектов.СрезПоследних(, Объект ССЫЛКА Документ.бит_ЗаявкаНаРасходованиеСредствОбщая) КАК бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних
		|		ПО бит_ЗаявкаНаРасходованиеСредствОбщая.Ссылка = бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних.Объект
		|ГДЕ
		|	бит_ЗаявкаНаРасходованиеСредствОбщая.ок_ДокументОснование = &Ссылка
		|	И бит_ЗаявкаНаРасходованиеСредствОбщая.Проведен
		|	И бит_ИсторияИзмененияСтатусовОбъектовСрезПоследних.Статус <> ЗНАЧЕНИЕ(Справочник.бит_СтатусыОбъектов.ЗаявкаОбщая_Закрыта)";
	
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда
		Возврат Истина;		
	Иначе 
		Возврат Ложь;
	КонецЕсли; 
	
КонецФункции

&НаКлиенте
Процедура ок_ВопросСоздатьДокументыПрогнозПлатежа(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда		
		Записать();
		ок_ПроверитьДокументыПередСозданием();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвансыЗаполнить(Команда)
	
	Если Объект.ВыданныеАвансы.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Табличная часть будет очищена. Продолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ок_ВопросЗаполнитьАвансы", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ТекстВопроса);
		
	Иначе          		
	    ок_АвансыЗаполнитьНаСервере();		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ВопросЗаполнитьАвансы(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	    ок_АвансыЗаполнитьНаСервере();		
		//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-07-21 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ок_АвансыЗаполнитьНаСервере()
	
	Объект.ВыданныеАвансы.Очистить();
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	&Валюта КАК Валюта,
	|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.ДокументРасчетов КАК ДокументАванса,
	|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток КАК ок_СуммаАванса,
	|	ВЫБОР
	|		КОГДА ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.ДокументРасчетов ССЫЛКА Документ.ВыдачаДенежныхДокументов
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет
	|ИЗ
	|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Остатки(
	|			&ДатаДокумента,
	|			Организация = &Организация
	|				И Валюта = &Валюта
	|				И ПодотчетноеЛицо = &ФизЛицо
	|				И (ДокументРасчетов ССЫЛКА Документ.РасходныйКассовыйОрдер
	|					ИЛИ ДокументРасчетов ССЫЛКА Документ.СписаниеСРасчетногоСчета
	|					ИЛИ ДокументРасчетов ССЫЛКА Документ.ВыдачаДенежныхДокументов)) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки
	|ГДЕ
	|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет,
	|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.ДокументРасчетов.Дата";
	
	Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДокумента);
	Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать(); 	

	Пока Выборка.Следующий() Цикл
		НоваяСтрока = Объект.ВыданныеАвансы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
	КонецЦикла;
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

&НаКлиенте
Процедура ок_ОбработатьВыборДокументовСписаниеСРасчетногоСчетаЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	НомерСтроки = 1;
	Если ТипЗнч(РезультатЗакрытия) = Тип("Массив") Тогда
		Для Каждого СтрокаМассива Из РезультатЗакрытия Цикл
			Если НомерСтроки = 1 Тогда
				ТекСтрока = Объект.ВыданныеАвансы[Элементы.ВыданныеАвансы.ТекущиеДанные.НомерСтроки-1];
				НомерСтроки = НомерСтроки + 1;
				НоваяСтрока = Ложь;
			Иначе
				ТекСтрока = Объект.ВыданныеАвансы.Добавить();
			    НоваяСтрока = Истина;
			КонецЕсли;
			ТекСтрока.ДокументАванса = СтрокаМассива.СписаниеСРасчетногоСчета;
			ТекСтрока.ок_СуммаАванса = СтрокаМассива.Сумма;
			ТекСтрока.Валюта = СтрокаМассива.Валюта;
			ВыданныеАвансыПриОкончанииРедактирования(Элементы.ВыданныеАвансы, НоваяСтрока, Ложь); 			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ок_ПериодКомандировкиПриИзмененииНаСервере()
	
	ок_УстановитьТекстНадписьДней(ЭтаФорма.ок_НадписьДней, Объект.ДатаНачалаКомандировки, Объект.ДатаОкончанияКомандировки);
	
	// Суточные
	Если ЭтаФорма.ок_ЕстьСуточные Тогда
		
		Документы.АвансовыйОтчет.УстановитьРасчетСуточныхПоУмолчанию(Объект, ЭтаФорма.ок_НормыСуточных);
		Элементы.ок_СуточныеСуммаСверхНормы.Видимость = (Объект.Суточные.Итог("СуммаСверхНормы") > 0);
				
	КонецЕсли;
	
	ОбновитьИтоги(ЭтаФорма);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ок_УстановитьТекстНадписьДней(НадписьДней, ДатаНачалаКомандировки, ДатаОкончанияКомандировки)
	
	Если Не ЗначениеЗаполнено(ДатаНачалаКомандировки) Или Не ЗначениеЗаполнено(ДатаОкончанияКомандировки) Тогда
		НадписьДней = "";
		Возврат;
	КонецЕсли; 
	
	// Должна получиться строка вида:
	// 10 дней"
	ТекстШаблон = Нстр("ru='%1 %2'");
	
	ПродолжительностьОбщая = УчетКомандировокКлиентСервер.ПродолжительностьПериодаВДнях(ДатаНачалаКомандировки, ДатаОкончанияКомандировки, Истина);  // в днях, считая день начала и окончания
	
	НадписьДней = СтрШаблон(
		ТекстШаблон,
		ПродолжительностьОбщая,
		СтрокаСЧислом(Нстр("ru = '; день; ; дня; дней; дня'"),
			ПродолжительностьОбщая,
			ВидЧисловогоЗначения.Количественное,
			"L=ru"));
	
КонецПроцедуры

&НаСервере
Процедура ок_ЗагрузитьДанныеСуточных(ВыбранноеЗначение)
	
	ДанныеСуточных = ПолучитьИзВременногоХранилища(ВыбранноеЗначение);
	Объект.Суточные.Загрузить(ДанныеСуточных.Суточные);
	
	Объект.СчетЗатрат          = ДанныеСуточных.СчетЗатрат;
	Объект.ПодразделениеЗатрат = ДанныеСуточных.ПодразделениеЗатрат;
	Объект.Субконто1           = ДанныеСуточных.Субконто1;
	Объект.Субконто2           = ДанныеСуточных.Субконто2;
	Объект.Субконто3           = ДанныеСуточных.Субконто3;
		
КонецПроцедуры

&НаСервере
Процедура ок_ПодготовитьФормуНаСервере()
	
	ок_УстановитьТекстНадписьДней(ЭтаФорма.ок_НадписьДней, Объект.ДатаНачалаКомандировки, Объект.ДатаОкончанияКомандировки);
	Элементы.ок_СуточныеСуммаСверхНормы.Видимость = (Объект.Суточные.Итог("СуммаСверхНормы") > 0);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_СуточныеСуммаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПроверяемыеРеквизиты = Новый Массив;
	
	ПроверяемыеРеквизиты.Добавить("Объект.Организация");
	ПроверяемыеРеквизиты.Добавить("Объект.Дата");
	ПроверяемыеРеквизиты.Добавить("Объект.ДатаНачалаКомандировки");
	ПроверяемыеРеквизиты.Добавить("Объект.ДатаОкончанияКомандировки");
	
	Если НЕ ок_ПроверитьРеквизитыШапки(ПроверяемыеРеквизиты) Тогда
		Возврат;
	КонецЕсли;	
		
	ОткрытьФорму("Документ.АвансовыйОтчет.Форма.Суточные",
		ок_ПараметрыФормыСуточные(),
		ЭтотОбъект, 
		УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервере
Функция ок_ПараметрыФормыСуточные()
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресТаблицыСуточные",      ок_ПоместитьТаблицуОбъектаВоВременноеХранилище("Суточные"));
	ПараметрыФормы.Вставить("Организация",               Объект.Организация);
	ПараметрыФормы.Вставить("ФизЛицо",                   Объект.ФизЛицо);
	ПараметрыФормы.Вставить("ДатаНачалаКомандировки",    Объект.ДатаНачалаКомандировки);
	ПараметрыФормы.Вставить("ДатаОкончанияКомандировки", Объект.ДатаОкончанияКомандировки);
	ПараметрыФормы.Вставить("ВалютаДокумента",           Объект.ВалютаДокумента);
	ПараметрыФормы.Вставить("НормыСуточных",             ок_НормыСуточных(ЭтотОбъект));
	ПараметрыФормы.Вставить("ДатаДокумента",             Объект.Дата);
	
	ПараметрыФормы.Вставить("СчетЗатрат",          		 Объект.СчетЗатрат);
	ПараметрыФормы.Вставить("ПодразделениеЗатрат", 		 Объект.ПодразделениеЗатрат);
	ПараметрыФормы.Вставить("Субконто1",           		 Объект.Субконто1);
	ПараметрыФормы.Вставить("Субконто2",           		 Объект.Субконто2);
	ПараметрыФормы.Вставить("Субконто3",           		 Объект.Субконто3);
	
	Возврат ПараметрыФормы;
	
КонецФункции

&НаСервере
Функция ок_ПоместитьТаблицуОбъектаВоВременноеХранилище(ИмяТаблицы)
	
	Возврат ПоместитьВоВременноеХранилище(Объект[ИмяТаблицы].Выгрузить(), УникальныйИдентификатор);
	
КонецФункции

&НаСервереБезКонтекста
Функция ок_НормыСуточных(Форма)
	
	Если Форма.ок_НормыСуточных = Неопределено Тогда
		Форма.ок_НормыСуточных = УчетКомандировок.НормыСуточных(Форма.Объект.Организация, Форма.Объект.Дата);
	КонецЕсли; 
	
	Возврат Форма.ок_НормыСуточных;
	
КонецФункции

&НаКлиенте
Функция ок_ПроверитьРеквизитыШапки(ПроверяемыеРеквизиты)
	
	ТекстыОшибок = Новый Соответствие;
	
	ТекстыОшибок.Вставить("Организация", Нстр("ru='Укажите организацию'"));
	ТекстыОшибок.Вставить("Дата", Нстр("ru='Укажите дату авансового отчета'"));
	ТекстыОшибок.Вставить("ФизЛицо", Нстр("ru='Укажите сотрудника'"));
	ТекстыОшибок.Вставить("ДатаНачалаКомандировки", Нстр("ru='Укажите дату начала командировки'"));
	ТекстыОшибок.Вставить("ДатаОкончанияКомандировки", Нстр("ru='Укажите дату окончания командировки'"));
	
	Ошибки = Неопределено;
	
	Для Каждого Реквизит Из ПроверяемыеРеквизиты Цикл
		
		ИмяРеквизита = Реквизит;
		Если Найти(Реквизит, "Объект.") Тогда
			
			ИмяРеквизита = СтрЗаменить(Реквизит, "Объект.", "");
			
			Если НЕ ЗначениеЗаполнено(Объект[ИмяРеквизита]) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
					Ошибки, 
					Реквизит,
					ТекстыОшибок[ИмяРеквизита],
					"");
			КонецЕсли;	
			
		Иначе
			
			Если НЕ ЗначениеЗаполнено(ЭтотОбъект[ИмяРеквизита]) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
					Ошибки, 
					ИмяРеквизита,
					ТекстыОшибок[ИмяРеквизита],
					"");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
	
	Возврат (Ошибки = Неопределено);
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ЗаполнитьПоУправленческимАналитикамТовары(Команда)
	
	бит_БК_ОбщийКлиент.ЗаполнитьТЧПоУправленческимАналитикам(ЭтотОбъект, "Товары");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДозаполнитьПоУправленческимАналитикамТовары(Команда)
	
	бит_БК_ОбщийКлиент.ЗаполнитьТЧПоУправленческимАналитикам(ЭтотОбъект, "Товары", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьПоУправленческимАналитикамПрочее(Команда)
	
	бит_БК_ОбщийКлиент.ЗаполнитьТЧПоУправленческимАналитикам(ЭтотОбъект, "Прочее");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДозаполнитьПоУправленческимАналитикамПрочее(Команда)
	
	бит_БК_ОбщийКлиент.ЗаполнитьТЧПоУправленческимАналитикам(ЭтотОбъект, "Прочее", Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьТЧПоУправленческимАналитикам(Результат, ДопПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Результат) Тогда 
		Возврат;
	КонецЕсли;
	
	Элементы.ТоварыНоменклатура.ФиксацияВТаблице = ФиксацияВТаблице.Нет;
	
	ЗаполнитьТЧПоУправленческимАналитикамНаСервере(Результат, ДопПараметры.ИмяТЧ, ДопПараметры.Заявка);
	
	Элементы.ТоварыНоменклатура.ФиксацияВТаблице = ФиксацияВТаблице.Лево;
	
	ОбновитьИтоги(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТЧПоУправленческимАналитикамНаСервере(АдресДанныхЗаполнения, ИмяТЧ, Заявка)
	
	пДанныеЗаполнения = ПолучитьИзВременногоХранилища(АдресДанныхЗаполнения);
		
	Если ИмяТЧ = "Прочее" Тогда 
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	бит_СтатьиОборотов_СтатьиРегл.ок_ОсновноеСоответствие КАК ОсновноеСоответствие,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов КАК СтатьяОборотов,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл КАК Статья,
		|	ЛОЖЬ КАК СтатьяПДР
		|ИЗ
		|	РегистрСведений.бит_СтатьиОборотов_СтатьиРегл КАК бит_СтатьиОборотов_СтатьиРегл
		|ГДЕ
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл ССЫЛКА Справочник.СтатьиЗатрат
		|	И бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов В(&СтатьиОборотов)
		|
		|СГРУППИРОВАТЬ ПО
		|	бит_СтатьиОборотов_СтатьиРегл.ок_ОсновноеСоответствие,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	бит_СтатьиОборотов_СтатьиРегл.ок_ОсновноеСоответствие КАК ОсновноеСоответствие,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл,
		|	ИСТИНА
		|ИЗ
		|	РегистрСведений.бит_СтатьиОборотов_СтатьиРегл КАК бит_СтатьиОборотов_СтатьиРегл
		|ГДЕ
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл ССЫЛКА Справочник.ПрочиеДоходыИРасходы
		|	И бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов В(&СтатьиОборотов)
		|
		|СГРУППИРОВАТЬ ПО
		|	бит_СтатьиОборотов_СтатьиРегл.ок_ОсновноеСоответствие,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов,
		|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл";
		
		Запрос.УстановитьПараметр("СтатьиОборотов", пДанныеЗаполнения.ВыгрузитьКолонку("ок_СтатьяОборотов"));
		
		РезультатЗапроса = Запрос.Выполнить();
		
		пСоответствиеСтатей = РезультатЗапроса.Выгрузить();	
		
	КонецЕсли;
	
	пИндекс = 0;

	Для Каждого пСтрокаЗаполнения Из пДанныеЗаполнения Цикл 			
		
		Если пИндекс<Объект[ИмяТЧ].Количество() Тогда 
			пСтрокаТЧ = Объект[ИмяТЧ][пИндекс];
		Иначе
			пСтрокаТЧ = Объект[ИмяТЧ].Добавить();
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(пСтрокаТЧ, пСтрокаЗаполнения,,"Сумма");
		Если НЕ Объект.СуммаВключаетНДС Тогда 
			пСтрокаТЧ.Сумма = пСтрокаЗаполнения.СуммаБезНДС;
			пСтрокаТЧ.Всего = пСтрокаТЧ.Сумма + пСтрокаТЧ.СуммаНДС;
		Иначе
			пСтрокаТЧ.Сумма = пСтрокаЗаполнения.Сумма;
			пСтрокаТЧ.Всего = пСтрокаТЧ.Сумма;
		КонецЕсли;		
		
		Если пСтрокаТЧ.Свойство("Цена") Тогда 
			
			Если пСтрокаТЧ.Количество = 0 Тогда 
				пСтрокаТЧ.Количество = 1;
			КонецЕсли;
			
			пСтрокаТЧ.Цена = пСтрокаТЧ.Сумма/пСтрокаТЧ.Количество;
			
		КонецЕсли;

				
		пСтрокаТЧ.ок_Период = НачалоМесяца(пСтрокаТЧ.ок_Период);		
		
		Если ИмяТЧ = "Прочее" И
			(ЗначениеЗаполнено(пСтрокаТЧ.СчетЗатрат) Или
			ЗначениеЗаполнено(пСтрокаТЧ.СчетЗатратНУ))
			И (ЗначениеЗаполнено(пСтрокаТЧ.ок_ЦФО) Или
			ЗначениеЗаполнено(пСтрокаТЧ.ок_СтатьяОборотов) Или
			ЗначениеЗаполнено(пСтрокаТЧ.ок_Аналитика_2)) 			 
		Тогда 
		
			Если ЗначениеЗаполнено(пСтрокаТЧ.СчетЗатрат) Тогда 
				
				ЗаполнитьЗначенияСубконтоВСтрокеТЧ(пСтрокаТЧ,,пСоответствиеСтатей);		
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(пСтрокаТЧ.СчетЗатратНУ) Тогда 
				
				ЗаполнитьЗначенияСубконтоВСтрокеТЧ(пСтрокаТЧ,"НУ",пСоответствиеСтатей);		
				
			КонецЕсли;
			
		КонецЕсли;
		пИндекс = пИндекс+1;
	КонецЦикла;	

			
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЗначенияСубконтоВСтрокеТЧ(пСтрокаТЧ, БУНУ = "", пСоответствиеСтатей)
	
	пИндексСубконто = 1;
	
	Для Каждого ВидСубконтоСтрока Из пСтрокаТЧ["СчетЗатрат"+БУНУ].ВидыСубконто Цикл 
		
		ОписаниеТиповВидаСубконто = ВидСубконтоСтрока.ВидСубконто.ТипЗначения;
		
		Если ЗначениеЗаполнено(пСтрокаТЧ.ок_ЦФО) И
			ОписаниеТиповВидаСубконто.СодержитТип(Тип("СправочникСсылка.Подразделения")) 
		Тогда 
			пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто] = пСтрокаТЧ.ок_ЦФО;
		ИначеЕсли ОписаниеТиповВидаСубконто.СодержитТип(Тип("СправочникСсылка.ОбъектыСтроительства")) 
		Тогда 
		Если Не ЗначениеЗаполнено(пСтрокаТЧ.ок_Аналитика_2) Тогда 
			    //ОКЕЙ Михайлова М.В.(ПервыйБИТ) Начало 2021-09-17 (#ИСПРАВЛЕНИЕ_тестБП05)
				// При подобной "очистке" субконто очищался и тип, 
				// при повторном вызове процедуры объект строительства более не заполнялся
				//пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто] = Неопределено;																	
				пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто] = ПредопределенноеЗначение("Справочник.ОбъектыСтроительства.ПустаяСсылка");																	
				//ОКЕЙ Михайлова М.В.(ПервыйБИТ) Конец  2021-09-17 (#ИСПРАВЛЕНИЕ_тестБП05)
			Иначе
				пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто] = ОК_ОбщегоНазначения.ОпределитьОбъектСтроительстваОтносительноАналитикиБК(пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто], пСтрокаТЧ.ок_Аналитика_2);
			КонецЕсли;		
		ИначеЕсли ЗначениеЗаполнено(пСтрокаТЧ.ок_СтатьяОборотов) И 
			(ОписаниеТиповВидаСубконто.СодержитТип(Тип("СправочникСсылка.СтатьиЗатрат")) Или						
			ОписаниеТиповВидаСубконто.СодержитТип(Тип("СправочникСсылка.ПрочиеДоходыИРасходы")))
		Тогда 
		
			СтатьяПДиР = ОписаниеТиповВидаСубконто.СодержитТип(Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
			
			СтркутураОтбора = Новый Структура();
			СтркутураОтбора.Вставить("СтатьяОборотов",		пСтрокаТЧ.ок_СтатьяОборотов);
			СтркутураОтбора.Вставить("СтатьяПДР",			СтатьяПДиР);
			СтркутураОтбора.Вставить("ОсновноеСоответствие",Истина);
			пНайденныеСтроки = пСоответствиеСтатей.НайтиСтроки(СтркутураОтбора);
			Если пНайденныеСтроки.Количество() > 0 Тогда
				пНайденнаяСтрока = пНайденныеСтроки[0];
			Иначе
				СтркутураОтбора = Новый Структура();
				СтркутураОтбора.Вставить("СтатьяОборотов",	пСтрокаТЧ.ок_СтатьяОборотов);
				СтркутураОтбора.Вставить("СтатьяПДР",		СтатьяПДиР);
				пНайденныеСтроки = пСоответствиеСтатей.НайтиСтроки(СтркутураОтбора);
				Если пНайденныеСтроки.Количество() = 1 Тогда
					пНайденнаяСтрока = пНайденныеСтроки[0];
				ИначеЕсли пНайденныеСтроки.Количество() > 1 Тогда
					ТекстСообшенияОбОшибках = "Для статьи оборотов «%1» найдено несколько сопоставлений статье %2.";
					ТекстСообшенияОбОшибках = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообшенияОбОшибках, пСтрокаТЧ.ок_СтатьяОборотов, ?(СтатьяПДиР,"прочих доходов-расходов","затрат"));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообшенияОбОшибках);
					пНайденнаяСтрока = Неопределено;
				Иначе
					ТекстСообшенияОбОшибках = "Для статьи оборотов «%1» не найдено сопоставление статье %2. Необходимо обратиться в службу поддержки";
					ТекстСообшенияОбОшибках = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообшенияОбОшибках, пСтрокаТЧ.ок_СтатьяОборотов, ?(СтатьяПДиР,"прочих доходов-расходов","затрат"));
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообшенияОбОшибках);
					пНайденнаяСтрока = Неопределено;
				КонецЕсли;
			КонецЕсли; 
			
			Если пНайденнаяСтрока <> Неопределено Тогда 
				пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто] = пНайденнаяСтрока.Статья;
			Иначе
				пСтрокаТЧ["Субконто"+БУНУ+пИндексСубконто] = Неопределено;
			КонецЕсли;
			
		КонецЕсли;			
		
		пИндексСубконто = пИндексСубконто + 1;
		
	КонецЦикла;	
	
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ЗаполнитьПоДенежнымДокументам(Команда)
	
	Если Объект.Прочее.Количество() = 0 Тогда
		ок_ЗаполнитьПоДенежнымДокументамНаСервере()
	Иначе
		ТекстВопроса = НСтр("ru = 'Табличная часть будет очищена. Продолжить?'");
		
		Оповещение = Новый ОписаниеОповещения("ок_ВопросЗаполнитьПоДенежнымДокументам", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да, ТекстВопроса);

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ВопросЗаполнитьПоДенежнымДокументам(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда		
		Объект.Прочее.Очистить();
		ок_ЗаполнитьПоДенежнымДокументамНаСервере();		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ок_ЗаполнитьПоДенежнымДокументамНаСервере()
	
	МассивИдентификаторовСтрок = Новый Массив;

	Для Каждого Строка Из Объект.ВыданныеАвансы Цикл
		Если ТипЗнч(Строка.ДокументАванса) = Тип("ДокументСсылка.ВыдачаДенежныхДокументов") Тогда
			Для Каждого СтрокаВыдачаДенежныхДокументов Из Строка.ДокументАванса.ДенежныеДокументы Цикл
				НоваяСтрока = Объект.Прочее.Добавить();
				НоваяСтрока.ВидДокВходящий          = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.ок_ВидРасхода;
				НоваяСтрока.НомерВходящегоДокумента = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.ок_НомерБилета;
				НоваяСтрока.ДатаВходящегоДокумента  = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.ок_ДатаБилета;
				НоваяСтрока.Содержание              = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.Наименование;
				НоваяСтрока.Сумма                   = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.Стоимость;
				НоваяСтрока.СтавкаНДС               = Перечисления.СтавкиНДС.СтавкаНДС(Перечисления.ВидыСтавокНДС.Общая, Объект.Дата);
				НоваяСтрока.СуммаНДС                = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.ок_НДС;
				НоваяСтрока.Всего                   = НоваяСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, НоваяСтрока.СуммаНДС);
				НоваяСтрока.ПредъявленСФ            = Истина;
				НоваяСтрока.БланкСтрогойОтчетности  = Истина;
				НоваяСтрока.ДатаСФ                  = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.ок_ДатаБилета;
				НоваяСтрока.НомерСФ                 = СтрокаВыдачаДенежныхДокументов.ДенежныйДокумент.ок_НомерБилета;
				МассивИдентификаторовСтрок.Добавить(НоваяСтрока.ПолучитьИдентификатор());
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;	
	Если МассивИдентификаторовСтрок.Количество() > 0 Тогда 
		ок_ЗаполнитьОбъектИФункцию(МассивИдентификаторовСтрок);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ок_УстановитьДоступностьРасчетаКомандировок()
	
	ДоступенФункционалКомандировок = Объект.ВалютаДокумента = ВалютаРегламентированногоУчета;
	Элементы.ок_ГруппаПериод.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_ДатаНачалаКомандировки.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_ДатаОкончанияКомандировки.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_НадписьДней.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_ГруппаСуточные.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_СуточныеСумма.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_ВалютаДокументаСуточные.Видимость = ДоступенФункционалКомандировок;
	Элементы.ок_СуточныеСуммаСверхНормы.Видимость = ДоступенФункционалКомандировок;	
	
КонецПроцедуры

&НаСервере
Процедура ок_ОбновитьПредыдущийОстатокПерерасход()
	
	УстановитьПривилегированныйРежим(Истина);
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ 
		|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Остатки(
		|			&Дата,
		|			Организация = &Организация
		|				И ПодотчетноеЛицо = &ФизЛицо
		|				И Валюта = &Валюта
		|				И НЕ ДокументРасчетов ССЫЛКА Документ.ВыдачаДенежныхДокументов) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки";
	
	Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДокумента);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЭтаФорма.ок_ПредыдущийОстатокПерерасход = ВыборкаДетальныеЗаписи.СуммаОстаток;
	Иначе
		ЭтаФорма.ок_ПредыдущийОстатокПерерасход = 0;
	КонецЕсли;
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаСервере
Процедура ок_ОбновитьИтогиПоРегиструВзаиморасчетыСПодотчетнымиЛицами()
	
	УстановитьПривилегированныйРежим(Истина);
	// ок_ПолученоАвансов
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ 
		|	ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки.СуммаОстаток КАК СуммаОстаток
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Остатки(
		|			&Дата,
		|			Организация = &Организация
		|				И ПодотчетноеЛицо = &ФизЛицо
		|				И Валюта = &Валюта
		|				И ДокументРасчетов В (&ДокументыАванса)) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОстатки";
	
	Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДокумента);
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
	Запрос.УстановитьПараметр("ДокументыАванса", Объект.ВыданныеАвансы.Выгрузить().ВыгрузитьКолонку("ДокументАванса"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЭтаФорма.ок_ПолученоАвансов = ВыборкаДетальныеЗаписи.СуммаОстаток;
	Иначе
		ЭтаФорма.ок_ПолученоАвансов = 0;
	КонецЕсли;
	
	// ок_Израсходовано 
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ок_ВзаиморасчетыСПодотчетнымиЛицами.Сумма) КАК Сумма
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами КАК ок_ВзаиморасчетыСПодотчетнымиЛицами
		|ГДЕ
		|	ок_ВзаиморасчетыСПодотчетнымиЛицами.Регистратор = &Ссылка
		|	И ок_ВзаиморасчетыСПодотчетнымиЛицами.Организация = &Организация
		|	И ок_ВзаиморасчетыСПодотчетнымиЛицами.ПодотчетноеЛицо = &ФизЛицо
		|	И ок_ВзаиморасчетыСПодотчетнымиЛицами.Валюта = &Валюта";
	
	Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДокумента);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		ЭтаФорма.ок_Израсходовано = ВыборкаДетальныеЗаписи.Сумма;
	Иначе
		ЭтаФорма.ок_Израсходовано = 0;
	КонецЕсли;
	
	// ок_Остаток
	Если ЭтаФорма.ок_ПолученоАвансов = ЭтаФорма.ок_Израсходовано Тогда
		ЭтаФорма.ок_Остаток = 0;
		Элементы.ок_Остаток.Заголовок = "Остаток";
	ИначеЕсли ЭтаФорма.ок_ПолученоАвансов > ЭтаФорма.ок_Израсходовано Тогда
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ 
		|	СУММА(ок_ВзаиморасчетыСПодотчетнымиЛицамиОбороты.СуммаРасход) КАК СуммаРасход
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Обороты(
		|			&ДатаДокумента,
		|			,
		|			Регистратор,
		|			ДокументРасчетов В (&СписокДокументовАванс)
		|				И Организация = &Организация
		|				И ПодотчетноеЛицо = &ФизЛицо
		|				И Валюта = &Валюта) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОбороты
		|ГДЕ
		|	(ок_ВзаиморасчетыСПодотчетнымиЛицамиОбороты.Регистратор ССЫЛКА Документ.ПриходныйКассовыйОрдер
		|			ИЛИ ок_ВзаиморасчетыСПодотчетнымиЛицамиОбороты.Регистратор ССЫЛКА Документ.ок_ЗакрытиеЗадолженностиПодотчетногоЛица)";
		
		Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
		Запрос.УстановитьПараметр("СписокДокументовАванс", Объект.ВыданныеАвансы.Выгрузить().ВыгрузитьКолонку("ДокументАванса"));
		Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДокумента);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ЭтаФорма.ок_Остаток = ВыборкаДетальныеЗаписи.СуммаРасход;
		Иначе	
			ЭтаФорма.ок_Остаток = 0;
		КонецЕсли;   		
		Элементы.ок_Остаток.Заголовок = "Внесен остаток";
	Иначе
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ 
		|	СУММА(ок_ВзаиморасчетыСПодотчетнымиЛицамиОбороты.СуммаПриход) КАК СуммаПриход
		|ИЗ
		|	РегистрНакопления.ок_ВзаиморасчетыСПодотчетнымиЛицами.Обороты(
		|			&ДатаДокумента,
		|			,
		|			,
		|			ДокументРасчетов = &АвансовыйОтчет
		|				И Валюта = &Валюта
		|				И Организация = &Организация
		|				И ПодотчетноеЛицо = &ФизЛицо) КАК ок_ВзаиморасчетыСПодотчетнымиЛицамиОбороты";
		
		Запрос.УстановитьПараметр("ДатаДокумента", Объект.Дата);
		Запрос.УстановитьПараметр("АвансовыйОтчет", Объект.Ссылка);
		Запрос.УстановитьПараметр("Организация", Объект.Организация);
		Запрос.УстановитьПараметр("ФизЛицо", Объект.ФизЛицо);
		Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДокумента);

		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		Если ВыборкаДетальныеЗаписи.Следующий() Тогда
			ЭтаФорма.ок_Остаток = ВыборкаДетальныеЗаписи.СуммаПриход;
		Иначе	
			ЭтаФорма.ок_Остаток = 0;
		КонецЕсли;   		
		Элементы.ок_Остаток.Заголовок = "Выдан перерасход";		
	КонецЕсли;	
	
	// ок_ИтогоПоАО
	Если ЭтаФорма.ок_ПолученоАвансов > ЭтаФорма.ок_Израсходовано Тогда
		ЭтаФорма.ок_ИтогоПоАО = ЭтаФорма.ок_ПолученоАвансов - ЭтаФорма.ок_Израсходовано - ЭтаФорма.ок_Остаток;
	Иначе
		ЭтаФорма.ок_ИтогоПоАО = ЭтаФорма.ок_ПолученоАвансов - ЭтаФорма.ок_Израсходовано + ЭтаФорма.ок_Остаток; 
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДатаНачалаКомандировкиПриИзменении(Элемент)
	
	ок_ПериодКомандировкиПриИзмененииНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДатаОкончанияКомандировкиПриИзменении(Элемент)
	
	ок_ПериодКомандировкиПриИзмененииНаСервере();
	
КонецПроцедуры  

&НаКлиенте
Процедура Подключаемый_ТоварыДатаВходящегоДокументаПриИзменении(Элемент)
	
	пОповещениеОтветПоЗаполнениюПериодаВТЧ = Новый ОписаниеОповещения("ок_ОтветПоЗаполнениюПериодаВТЧ",ЭтотОбъект, "Товары");
	ПоказатьВопрос(пОповещениеОтветПоЗаполнениюПериодаВТЧ, НСтр("ru = 'Дата вх. документа будет установлена в поле ""Период"". Продолжить?'"),РежимДиалогаВопрос.ДаНет,30,КодВозвратаДиалога.Нет,,КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПрочееДатаВходящегоДокументаПриИзменении(Элемент)
	
	пОповещениеОтветПоЗаполнениюПериодаВТЧ = Новый ОписаниеОповещения("ок_ОтветПоЗаполнениюПериодаВТЧ",ЭтотОбъект,"Прочее");
	ПоказатьВопрос(пОповещениеОтветПоЗаполнениюПериодаВТЧ, НСтр("ru = 'Дата вх. документа будет установлена в поле ""Период"". Продолжить?'"),РежимДиалогаВопрос.ДаНет,30,КодВозвратаДиалога.Нет,,КодВозвратаДиалога.Нет);
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ОтветПоЗаполнениюПериодаВТЧ(Результат, ДопПараметры) Экспорт

	Если Результат<>КодВозвратаДиалога.Да Тогда 		
		Возврат;  		
	КонецЕсли;
	
	Если ДопПараметры = "Товары" Тогда
		Элементы.Товары.ТекущиеДанные.ок_Период = НачалоМесяца(Элементы.Товары.ТекущиеДанные.ДатаВходящегоДокумента);
	ИначеЕсли ДопПараметры = "Прочее" Тогда
		Элементы.Прочее.ТекущиеДанные.ок_Период = НачалоМесяца(Элементы.Прочее.ТекущиеДанные.ДатаВходящегоДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ок_ПриИзмененииСубконто(НомерСтроки, Значение, НомерСубконто)
	
	ТекущаяСтрока = Объект.Прочее[НомерСтроки];
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.ОбъектыСтроительства") Тогда
		ТекущаяСтрока.ок_Аналитика_2 = ок_ПолучитьОбъектСтроительстваДляАналитики2(Значение);	
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.Подразделения") Тогда
		ТекущаяСтрока.ок_ЦФО = Значение;	
	КонецЕсли;	
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.ОсновныеСредства") Тогда
		ТекущаяСтрока.ок_Аналитика_2 = Значение.Объект;	
	КонецЕсли;
	
	Если ТипЗнч(Значение) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
		
		стрРезультат = ок_ПолучитьСтатьюОборотовНаСервере(Значение);
		Если стрРезультат.Свойство("Ошибки") Тогда 
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(стрРезультат.Ошибки,,"Объект.Прочее["+Объект.Прочее.Индекс(ТекущаяСтрока)+"].Субконто" + НомерСубконто);
			ТекущаяСтрока.ок_СтатьяОборотов = ПредопределенноеЗначение("Справочник.бит_СтатьиОборотов.ПустаяСсылка");
			
		ИначеЕсли стрРезультат.Свойство("СтатьяОборотов") Тогда 
			
			ТекущаяСтрока.ок_СтатьяОборотов = стрРезультат.СтатьяОборотов;
			
		КонецЕсли;		
		
	ИначеЕсли ТипЗнч(Значение) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда	
		
		стрРезультат = ок_ПолучитьСтатьюОборотовНаСервере(Значение);
		Если стрРезультат.Свойство("Ошибки") Тогда 
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(стрРезультат.Ошибки,,"Объект.Прочее["+Объект.Прочее.Индекс(ТекущаяСтрока)+"].Субконто" + НомерСубконто);
			ТекущаяСтрока.ок_СтатьяОборотов = ПредопределенноеЗначение("Справочник.бит_СтатьиОборотов.ПустаяСсылка");
			
		ИначеЕсли стрРезультат.Свойство("СтатьяОборотов") Тогда  			
			ТекущаяСтрока.ок_СтатьяОборотов = стрРезультат.СтатьяОборотов;  			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ок_ПолучитьСтатьюОборотовНаСервере(СтатьяЗатрат)
	
	Возврат ОК_ОбщегоНазначения.ПолучитьСтатьюОборотовНаСервере(СтатьяЗатрат);
			
КонецФункции

&НаСервереБезКонтекста
Функция ок_ПолучитьОбъектСтроительстваДляАналитики2(ОбъектСтроительства)
	
	пЗапрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОбъектыСтроительства.Ссылка КАК Ссылка
	|ИЗ
	|	Справочник.ОбъектыСтроительства КАК ОбъектыСтроительства
	|ГДЕ
	|	ОбъектыСтроительства.Ссылка = &Ссылка
	|ИТОГИ ПО
	|	Ссылка ИЕРАРХИЯ");
	пЗапрос.УстановитьПараметр("Ссылка",	ОбъектСтроительства);
	
	пРезультат = пЗапрос.Выполнить();
	
	Если пРезультат.Пустой() Тогда 
		Возврат ОбъектСтроительства;
	КонецЕсли;
	
	Выборка = пРезультат.Выбрать();
	Выборка.Следующий();
	Возврат Выборка.Ссылка;
		
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПрочееАналитика_2ПриИзменении(Элемент)
	
	ок_ЗаполнитьСубконтоПоАналитикеКлиент("ок_Аналитика_2");
		
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПрочееСтатьяОборотовПриИзменении(Элемент)
	
	ок_ЗаполнитьСубконтоПоАналитикеКлиент("ок_СтатьяОборотов");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПрочееЦФОПриИзменении(Элемент)
	
	ок_ЗаполнитьСубконтоПоАналитикеКлиент("ок_ЦФО");
	
КонецПроцедуры

&НаКлиенте
Процедура ок_ЗаполнитьСубконтоПоАналитикеКлиент(ИмяРеквизитаТЧ)
	
	Перем ЗначениеРеквизитаТЧ;
	
	ТекДанные = Элементы.Прочее.ТекущиеДанные;
	Если ТекДанные = Неопределено 
		 Или (Не ЗначениеЗаполнено(ТекДанные.СчетЗатрат) И
		 	  Не ЗначениеЗаполнено(ТекДанные.СчетЗатратНУ))
		 Или (ТекДанные.Свойство(ИмяРеквизитаТЧ,ЗначениеРеквизитаТЧ) И
		 	  Не ЗначениеЗаполнено(ЗначениеРеквизитаТЧ))
	Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекДанные.СчетЗатрат) Тогда 
		ок_ЗаполнитьСубконтоПоАналитикеСервер(ИмяРеквизитаТЧ,ЗначениеРеквизитаТЧ,ТекДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекДанные.СчетЗатратНУ) Тогда 
    ок_ЗаполнитьСубконтоПоАналитикеСервер(ИмяРеквизитаТЧ,ЗначениеРеквизитаТЧ,ТекДанные.ПолучитьИдентификатор(), "НУ");
  КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ок_ЗаполнитьСубконтоПоАналитикеСервер(ИмяРеквизитаТЧ,ЗначениеРеквизитаТЧ,ИДСтроки,БУНУ = "")
	
	ТекДанные = Объект.Прочее.НайтиПоИдентификатору(ИДСтроки);
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	пИндексСубконто = 1;
	
	Пока пИндексСубконто<=3 Цикл 
		Если ТипЗнч(ТекДанные["Субконто"+БУНУ+пИндексСубконто]) = ТипЗнч(ЗначениеРеквизитаТЧ) Тогда 
			Если ИмяРеквизитаТЧ = "ок_Аналитика_2" Тогда 					
				Если Не ЗначениеЗаполнено(ЗначениеРеквизитаТЧ) Тогда 
					//ОКЕЙ Михайлова М.В.(ПервыйБИТ) Начало 2021-09-17 (#ИСПРАВЛЕНИЕ_тестБП05)
					// При подобной "очистке" субконто очищался и тип, 
					// при повторном вызове процедуры объект строительства более не заполнялся
					//ТекДанные["Субконто"+БУНУ+пИндексСубконто] = Неопределено;
					ТекДанные["Субконто"+БУНУ+пИндексСубконто] = ПредопределенноеЗначение("Справочник.ОбъектыСтроительства.ПустаяСсылка");																	
					//ОКЕЙ Михайлова М.В.(ПервыйБИТ) Конец  2021-09-17 (#ИСПРАВЛЕНИЕ_тестБП05)
				Иначе
					ТекДанные["Субконто"+БУНУ+пИндексСубконто] = ОК_ОбщегоНазначения.ОпределитьОбъектСтроительстваОтносительноАналитикиБК(ТекДанные["Субконто"+БУНУ+пИндексСубконто], ЗначениеРеквизитаТЧ);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.бит_СтатьиОборотов") И
				(ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.СтатьиЗатрат") Или
				ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы"))		
				Тогда
				ТекДанные["Субконто"+БУНУ+пИндексСубконто] = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(ЗначениеРеквизитаТЧ, Новый Структура, ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
				Прервать;			
			Иначе		
				ТекДанные["Субконто"+БУНУ+пИндексСубконто] = ЗначениеРеквизитаТЧ;
				Прервать;
			КонецЕсли;					
		ИначеЕсли ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.бит_СтатьиОборотов")
			И (ТипЗнч(ТекДанные["Субконто"+БУНУ+пИндексСубконто]) = Тип("СправочникСсылка.СтатьиЗатрат")
				Или ТипЗнч(ТекДанные["Субконто"+БУНУ+пИндексСубконто]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы"))
			Тогда
				ТекДанные["Субконто"+БУНУ+пИндексСубконто] = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(ЗначениеРеквизитаТЧ, Новый Структура, ТипЗнч(ТекДанные["Субконто"+БУНУ+пИндексСубконто]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
				Прервать;
		КонецЕсли;
		пИндексСубконто = пИндексСубконто + 1;   		
		
	КонецЦикла;
		
КонецПроцедуры

&НаСервере
Процедура ок_ЗаполнитьАналитику(НомерСтроки)
	
	СтрокаТабличнойЧасти = Объект.Прочее[НомерСтроки];
	СтрокаТабличнойЧасти.ПодразделениеЗатрат = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();

	Если Не (ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_ЦФО) И
			 ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_СтатьяОборотов) И
			 ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_Аналитика_2)) 
	Тогда 
		Возврат;
	КонецЕсли;
	
	пВидыСубконтоНУ = СтрокаТабличнойЧасти.СчетЗатратНУ.ВидыСубконто;
	
	Для инд=1 По 3 Цикл
		//БУ
		Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = ТипЗнч(СтрокаТабличнойЧасти.ок_Аналитика_2) Тогда
			 Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_Аналитика_2) Тогда 
				СтрокаТабличнойЧасти["Субконто"+инд] = СтрокаТабличнойЧасти.ок_Аналитика_2;	
			КонецЕсли;			
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = ТипЗнч(СтрокаТабличнойЧасти.ок_ЦФО) Тогда
			 Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_ЦФО) Тогда 
				СтрокаТабличнойЧасти["Субконто"+инд] = СтрокаТабличнойЧасти.ок_ЦФО;	
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
			пСтатьяРегл = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(СтрокаТабличнойЧасти.ок_СтатьяОборотов, Новый Структура);
			Если ЗначениеЗаполнено(пСтатьяРегл) Тогда 
				СтрокаТабличнойЧасти["Субконто"+инд] = пСтатьяРегл;
			КонецЕсли;
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда 
			пСтатьяРегл = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(СтрокаТабличнойЧасти.ок_СтатьяОборотов, Новый Структура, Истина);
			Если ЗначениеЗаполнено(пСтатьяРегл) Тогда 
				СтрокаТабличнойЧасти["Субконто"+инд] = пСтатьяРегл;
			КонецЕсли;
		КонецЕсли;	
						
		//НУ
		Если СтрокаТабличнойЧасти.СчетЗатрат = СтрокаТабличнойЧасти.СчетЗатратНУ Тогда 
			Если ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд]) Тогда 
				СтрокаТабличнойЧасти["СубконтоНУ"+инд] = СтрокаТабличнойЧасти["Субконто"+инд];
			КонецЕсли;
		Иначе 
			Для Каждого пВидСубконтоНУ Из пВидыСубконтоНУ Цикл 
				Если ЗначениеЗаполнено(пВидСубконтоНУ.ВидСубконто) Тогда 
					Если пВидСубконтоНУ.ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(СтрокаТабличнойЧасти.ок_Аналитика_2)) Тогда 			
						СтрокаТабличнойЧасти["СубконтоНУ"+инд] = СтрокаТабличнойЧасти.ок_Аналитика_2;
					ИначеЕсли пВидСубконтоНУ.ВидСубконто.ТипЗначения.СодержитТип(ТипЗнч(СтрокаТабличнойЧасти.ок_ЦФО)) Тогда
						СтрокаТабличнойЧасти["СубконтоНУ"+инд] = СтрокаТабличнойЧасти.ок_ЦФО;
					ИначеЕсли пВидСубконтоНУ.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.СтатьиЗатрат")) Тогда 
						пСтатьяРегл = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(СтрокаТабличнойЧасти.ок_СтатьяОборотов, Новый Структура);
						Если ЗначениеЗаполнено(пСтатьяРегл) Тогда 
							СтрокаТабличнойЧасти["СубконтоНУ"+инд] = пСтатьяРегл;
						КонецЕсли;						
					ИначеЕсли пВидСубконтоНУ.ВидСубконто.ТипЗначения.СодержитТип(Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) Тогда 
						пСтатьяРегл = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(СтрокаТабличнойЧасти.ок_СтатьяОборотов, Новый Структура, Истина);
						Если ЗначениеЗаполнено(пСтатьяРегл) Тогда 
							СтрокаТабличнойЧасти["СубконтоНУ"+инд] = пСтатьяРегл;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;			
	КонецЦикла;
КонецПроцедуры


&НаСервере
Функция ок_ЗаполнитьОбъектИФункцию(МассивИдентификаторовСтрок, ОбновитьВсюТЧ = Ложь)
	
	Если ОбновитьВсюТЧ Тогда
		МассивИдентификаторовСтрок = Новый Массив;
		Для Каждого СтрокаПрочее Из Объект.Прочее Цикл
		МассивИдентификаторовСтрок.Добавить(СтрокаПрочее.ПолучитьИдентификатор());	
		КонецЦикла;
		
	КонецЕсли;
	
	ФизическиеЛица = Новый Массив;
	ФизическиеЛица.Добавить(Объект.ФизЛицо);

	ОбъектИФункция = РегистрыСведений.ок_ДанныеФизическихЛицБОСС.ок_ПолучитьОбъектИФункцию(ФизическиеЛица, Объект.Организация, Объект.Дата);
	Для Каждого ИДСтроки Из МассивИдентификаторовСтрок Цикл
		СтрокаТЧ = Объект.Прочее.НайтиПоИдентификатору(ИДСтроки);
		СтрокаПоФизЛицу = ОбъектИФункция.Найти(Объект.ФизЛицо, "ФизическоеЛицо");
		Если СтрокаПоФизЛицу<>Неопределено Тогда
			СтрокаТЧ.ок_ЦФО = СтрокаПоФизЛицу.Функция;
			СтрокаТЧ.ок_Аналитика_2 = СтрокаПоФизЛицу.Объект;
			Если ЗначениеЗаполнено(СтрокаТЧ.СчетЗатрат) Тогда 
				ок_ЗаполнитьСубконтоПоАналитикеСервер("ок_ЦФО",СтрокаТЧ.ок_ЦФО,ИДСтроки);
				ок_ЗаполнитьСубконтоПоАналитикеСервер("ок_Аналитика_2",СтрокаТЧ.ок_Аналитика_2,ИДСтроки);
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.СчетЗатратНУ) Тогда 
				ок_ЗаполнитьСубконтоПоАналитикеСервер("ок_ЦФО",СтрокаТЧ.ок_ЦФО,ИДСтроки, "НУ");
				ок_ЗаполнитьСубконтоПоАналитикеСервер("ок_Аналитика_2",СтрокаТЧ.ок_Аналитика_2,ИДСтроки, "НУ");
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецФункции
//ОКЕЙ Дмитриева В.В. (ПервыйБИТ) Конец 2021-05-19 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Начало 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
&НаСервере
Процедура ок_УпорядочитьДокументыАванса()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТЧ_Авансы.ДокументАванса КАК ДокументАванса,
	               |	ТЧ_Авансы.ок_СуммаАванса КАК ок_СуммаАванса
	               |ПОМЕСТИТЬ ВТ_Авансы
	               |ИЗ
	               |	&ВыданныеАвансы КАК ТЧ_Авансы
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Авансы.ДокументАванса КАК ДокументАванса,
	               |	ВТ_Авансы.ок_СуммаАванса КАК ок_СуммаАванса,
	               |	ВЫБОР
	               |		КОГДА ВТ_Авансы.ДокументАванса ССЫЛКА Документ.ВыдачаДенежныхДокументов
	               |			ТОГДА 1
	               |		ИНАЧЕ 2
	               |	КОНЕЦ КАК Приоритет
	               |ИЗ
	               |	ВТ_Авансы КАК ВТ_Авансы
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Приоритет,
	               |	ДокументАванса.Дата";
	Запрос.УстановитьПараметр("ВыданныеАвансы", Объект.ВыданныеАвансы.Выгрузить());
	
	Объект.ВыданныеАвансы.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ок_ПолучитьМассивДокументов()
	
	Возврат Объект.ВыданныеАвансы.Выгрузить().ВыгрузитьКолонку("ДокументАванса");
	
КонецФункции

//ОКЕЙ Завьялова Е.А. (ПервыйБИТ) Конец 2021-08-16 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)

//ОКЕЙ Завьялова Е.А.(ПервыйБИТ) Начало 2021-09-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
&НаКлиенте
Процедура ок_ПерезаполнитьОбъектИФункцию()
	
	Если Не ок_ЕстьФВБЗаявка() Тогда
		ок_ЗаполнитьОбъектИФункцию(Неопределено, Истина);
	Иначе
		ТекстСообщения = НСтр("ru='У данного документа установлена связь с ФВБ-заявкой, управленческие аналитики не будут перезаполнены!'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ок_ЕстьФВБЗаявка()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_ДополнительныеАналитики.ЗначениеАналитики КАК ЗначениеАналитики
	               |ИЗ
	               |	РегистрСведений.бит_ДополнительныеАналитики КАК бит_ДополнительныеАналитики
	               |ГДЕ
	               |	бит_ДополнительныеАналитики.Объект = &Объект
	               |	И бит_ДополнительныеАналитики.Аналитика = &Аналитика";
	Запрос.УстановитьПараметр("Объект", Объект.Ссылка);
	Запрос.УстановитьПараметр("Аналитика", ПланыВидовХарактеристик.бит_ВидыДополнительныхАналитик.НайтиПоКоду("NЗаявки"));
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() Тогда
		Возврат Истина;
	КонецЕсли;
		
	Возврат Ложь;
	
КонецФункции
//ОКЕЙ Завьялова Е.А.(ПервыйБИТ) Конец  2021-09-20 (#ТП_БП05_ФР12, #ТП_БП05_ФР13)
#КонецОбласти

#КонецОбласти
