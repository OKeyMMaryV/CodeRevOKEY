#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Параметры регламентного задания "бит_УстановкаКонтрольныхЗначений".
// 
// Возвращаемое значение:
//  Параметры - структура, ключи: Сценарий, ВидОперации, НачалоПериода.
//
Функция НовыеПараметрыУстановкиВФоне() Экспорт

	Параметры = Новый Структура; 
	Параметры.Вставить("Сценарий",Справочники.СценарииПланирования.ПустаяСсылка());
	Параметры.Вставить("ВидОперации", Перечисления.бит_ВидыОперацийУстановкаКонтрольныхЗначений.ПустаяСсылка());
	Параметры.Вставить("НачалоПериода", '0001-01-01');

	Возврат Параметры;
	
КонецФункции

// Проведение документов по переданным параметрам.
//
// Параметры:
//  ПараметрыВыполнения	 - структура - см. НовыеПараметрыУстановкиВФоне().
//
Процедура ФоновоеПроведение(ПараметрыЗадания) Экспорт

	ПараметрыВыполнения = НовыеПараметрыУстановкиВФоне();
	Для каждого КлючЗначение Из ПараметрыВыполнения Цикл
		Значение = Неопределено;
		Если ПараметрыЗадания.Свойство(КлючЗначение.Ключ, Значение) Тогда
			ПараметрыВыполнения.Вставить(КлючЗначение.Ключ, Значение);
		КонецЕсли; 
	КонецЦикла; 
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВидОперации",   ПараметрыВыполнения.ВидОперации);
	Запрос.УстановитьПараметр("НачалоПериода", ПараметрыВыполнения.НачалоПериода);
	Запрос.УстановитьПараметр("Сценарий", 	   ПараметрыВыполнения.Сценарий);

	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Таблица.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.бит_УстановкаКонтрольныхЗначений КАК Таблица
	|ГДЕ
	|	Таблица.Проведен
	|	И Таблица.Сценарий = &Сценарий
	|	И Таблица.Дата >= &НачалоПериода
	|	И Таблица.ВидОперации = &ВидОперации";
	
	Если НЕ ЗначениеЗаполнено(ПараметрыВыполнения.Сценарий) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.Сценарий = &Сценарий", "");
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(ПараметрыВыполнения.ВидОперации) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.ВидОперации = &ВидОперации", "");
	КонецЕсли; 
	Если НЕ ЗначениеЗаполнено(ПараметрыВыполнения.НачалоПериода) Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И Таблица.Дата >= &НачалоПериода", "");
	КонецЕсли; 
	
	Запрос.Текст 	 = ТекстЗапроса;
	РезультатЗапроса = Запрос.Выполнить();
	Выборка 		 = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ТекущийОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Попытка
			ТекущийОбъект.Записать(РежимЗаписиДокумента.Проведение); 
		Исключение
			ИмяСобытия = Нстр("ru = 'БИТ.Регламентные задания.Установка контрольных значений'");
			ЗаписьЖурналаРегистрации(ИмяСобытия, УровеньЖурналаРегистрации.Ошибка, 
				Метаданные.Документы.бит_УстановкаКонтрольныхЗначений, Выборка.Ссылка, 
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки; 
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли
