
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

Функция НоваяДатаФиксации(Сценарий, ТекущаяДатаФиксации) Экспорт
	
	НоваяДатаФиксации = ТекущаяДатаФиксации;

	Если ЗначениеЗаполнено(Сценарий) Тогда
		Периодичность = бит_Бюджетирование.ПолучитьПериодичностьСценария(Сценарий);
		НоваяДатаФиксации = бит_Бюджетирование.ПолучитьКонецПериода(ТекущаяДатаСеанса(), Периодичность);
	КонецЕсли;	

	Возврат НоваяДатаФиксации;

КонецФункции

Процедура СформироватьГраницуФиксации(ТаблицаФиксации, Движения, Отказ) Экспорт
	
	НаборЗаписей = Движения.бит_ГраницыФиксацииБюджета;
	НаборЗаписей.Записывать = Истина;
	
	Для Каждого СтрокаТаблицы ИЗ ТаблицаФиксации Цикл
		Запись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(Запись, СтрокаТаблицы);
	КонецЦикла;	
	
КонецПроцедуры	

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Сценарий КАК Сценарий,
	|	Реквизиты.ДатаФиксации КАК ДатаФиксации,
	|	Реквизиты.РежимЦФО КАК РежимЦФО
	|ИЗ
	|	Документ.бит_ФиксацияБюджета КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	Если НЕ бит_ОбщегоНазначения.ВалютаУпрУчетаУстановлена(Истина, ДокументСсылка) Тогда
		Отказ = Истина;
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Сценарий",	  Выборка.Сценарий);
	Запрос.УстановитьПараметр("ДатаФиксации", Выборка.ДатаФиксации);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаГраницыФиксацииБюджетов(НомераТаблиц, Выборка.РежимЦФО);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;

	Возврат ПараметрыПроведения;

КонецФункции 
  
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТекстЗапросаГраницыФиксацииБюджетов(НомераТаблиц, РежимЦФО)

	НомераТаблиц.Вставить("ГраницыФиксацииБюджетов", НомераТаблиц.Количество());
	
	Если РежимЦФО Тогда
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	СписокЦФО.Ссылка.Сценарий КАК Сценарий,
		|	СписокЦФО.ЦФО КАК ЦФО,
		|	СписокЦФО.Ссылка.ДатаФиксации КАК ДатаФиксации,
		|	СписокЦФО.Ссылка КАК Регистратор
		|ИЗ
		|	Документ.бит_ФиксацияБюджета.АналитикаФиксации КАК СписокЦФО
		|ГДЕ
		|	СписокЦФО.Ссылка = &Ссылка"
	Иначе	
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	&Сценарий КАК Сценарий,
		|	НЕОПРЕДЕЛЕНО КАК ЦФО,
		|	&ДатаФиксации КАК ДатаФиксации,
		|	&Ссылка КАК Регистратор";
	КонецЕсли;
	
	Возврат ТекстЗапроса + бит_ОбщегоНазначения.ТекстРазделителяЗапросовПакета();

КонецФункции

#КонецОбласти 

#КонецЕсли
