
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Стандартные действия при создании на сервере.
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		УстановитьСостояниеДокумента();	
	КонецЕсли; 
	
	ЗаполнитьСписокЦФО();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
		
	Если ПараметрыЗаписи.РежимЗаписи = ПредопределенноеЗначение("РежимЗаписиДокумента.Проведение") Тогда
		КлючеваяОперация = "Проведениебит_ФиксацияБюджета";
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.АналитикаФиксации.Очистить();
	Если ТекущийОбъект.РежимЦФО Тогда
		СписокЦФО = РеквизитФормыВЗначение("ДеревоЦФО");
		НайденныеСтроки = СписокЦФО.Строки.НайтиСтроки(Новый Структура("Использовать", Истина), Истина);
		Для каждого СтрокаТаблицы Из НайденныеСтроки Цикл
			ЗаполнитьЗначенияСвойств(ТекущийОбъект.АналитикаФиксации.Добавить(), СтрокаТаблицы);
		КонецЦикла; 
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

#КонецОбласти 

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СценарийПриИзменении(Элемент)
	
	Объект.ДатаФиксации = НоваяДатаФиксации(Объект.Сценарий, Объект.ДатаФиксации);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимЦФОПриИзменении(Элемент)
	
	РежимЦФОПриИзмененииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура РежимЦФОПриИзмененииНаСервере()
	
	ЗаполнитьСписокЦФО();
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоЦФОИспользоватьПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДеревоЦФО.ТекущиеДанные;
	ОбойтиКоллекцию(ТекущаяСтрока);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста 
функция НоваяДатаФиксации(Сценарий, ДатаФиксации)
	
	Возврат Документы.бит_ФиксацияБюджета.НоваяДатаФиксации(Сценарий, ДатаФиксации);
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокЦФО()
	
	Если Объект.РежимЦФО = Ложь Тогда
		Возврат;	
	КонецЕсли; 
	
	ДеревоЦФО.ПолучитьЭлементы().Очистить();

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Подразделения.Ссылка КАК ЦФО,
	|	ВЫБОР
	|		КОГДА НЕ СписокЦФО.ЦФО ЕСТЬ NULL
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Использовать
	|ИЗ
	|	Справочник.Подразделения КАК Подразделения
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.бит_ФиксацияБюджета.АналитикаФиксации КАК СписокЦФО
	|		ПО (СписокЦФО.Ссылка = &Ссылка)
	|			И Подразделения.Ссылка = СписокЦФО.ЦФО
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЦФО ИЕРАРХИЯ
	|АВТОУПОРЯДОЧИВАНИЕ";
				   
	Результат = Запрос.Выполнить();
	ЗначениеВРеквизитФормы(Результат.Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией), "ДеревоЦФО");
	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста 
Процедура УправлениеФормой(Форма)

	Форма.Элементы.ДеревоЦФО.Видимость = Форма.Объект.РежимЦФО;

КонецПроцедуры

&НаКлиенте
Процедура ОбойтиКоллекцию(СтрокаВерх)
	
	Если НЕ СтрокаВерх = Неопределено Тогда
		Коллекция = СтрокаВерх.ПолучитьЭлементы();
		Для Каждого ТекущаяСтрока ИЗ Коллекция Цикл
			ТекущаяСтрока.Использовать = СтрокаВерх.Использовать;
			ОбойтиКоллекцию(ТекущаяСтрока);
		КонецЦикла;	
	КонецЕсли;	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = бит_ОбщегоНазначения.СостояниеДокумента(Объект);
	
КонецПроцедуры

#КонецОбласти
