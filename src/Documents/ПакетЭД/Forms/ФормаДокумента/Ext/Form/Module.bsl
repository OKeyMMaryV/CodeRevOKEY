
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтатусПакета = Объект.СтатусПакета;
	ЗаполнитьСписокВыбораСтатусовПакета();
	
	ОбновитьВидимостьДоступность();
	
	ИспользуетсяНесколькоОрганизацийЭД = ЭлектронноеВзаимодействиеСлужебный.ИспользуетсяНесколькоОрганизаций();
	
	//ОК(СофтЛаб) Вдовиченко Г.В. 20181101 №3103
	ок_УправлениеФормами.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	//ОК(СофтЛаб) Вдовиченко Г.В. 20181101 №3103
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	СтатусПакета = Объект.СтатусПакета;
	Элементы.КомандаРаспаковатьПакетЭД.Видимость = Объект.СтатусПакета = Перечисления.СтатусыПакетовЭД.КРаспаковке
		Или Объект.СтатусПакета = Перечисления.СтатусыПакетовЭД.Неизвестный;
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Не ТекущийОбъект.СтатусПакета = СтатусПакета Тогда
		ТекущийОбъект.СтатусПакета = СтатусПакета;
	КонецЕсли;
	
	ЗаполнитьСписокВыбораСтатусовПакета();

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеЭД" Тогда
		Прочитать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ЭлектронныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ЭлектронныеДокументыОбъектВладелец" Тогда
		ПоказатьЗначение(,Элемент.ТекущиеДанные.ОбъектВладелец);
		
	ИначеЕсли ТипЗнч(Элемент.ТекущиеДанные.ЭлектронныйДокумент) = Тип("СправочникСсылка.ЭДПрисоединенныеФайлы") Тогда
		СтандартнаяОбработка = Ложь;
		ОбменСКонтрагентамиСлужебныйКлиент.ОткрытьЭДДляПросмотра(Элемент.ТекущиеДанные.ЭлектронныйДокумент);
	
	Иначе
		ПоказатьЗначение(,Элемент.ТекущиеДанные.ЭлектронныйДокумент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура РаспаковатьПакетЭД(Команда)
	
	МассивПЭД = Новый Массив;
	МассивПЭД.Добавить(Объект.Ссылка);
	ОбменСКонтрагентамиСлужебныйКлиент.РаспаковатьПакетыЭДНаКлиенте(МассивПЭД);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораСтатусовПакета()
	
	Если Объект.Направление = Перечисления.НаправленияЭД.Входящий Тогда
		Элементы.СтатусПакета.СписокВыбора.ЗагрузитьЗначения(СписокСтатусовВходящий());
	Иначе
		Элементы.СтатусПакета.СписокВыбора.ЗагрузитьЗначения(СписокСтатусовИсходящий());
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СписокСтатусовВходящий()
	
	МассивСтатусов = Новый Массив;
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-04-16 (#3964)
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.КРаспаковке);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Распакован);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.РаспакованДокументыНеОбработаны);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Неизвестный);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Отменен);	
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.КРаспаковке"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Распакован"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.РаспакованДокументыНеОбработаны"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Неизвестный"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Отменен"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.ок_АрхивУдален"));
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-04-16 (#3964) 
	
	Возврат МассивСтатусов;
	
КонецФункции

&НаСервереБезКонтекста
Функция СписокСтатусовИсходящий()
	
	МассивСтатусов = Новый Массив;
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-04-16 (#3964) Оптимизация
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Доставлен);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Отправлен);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.ПодготовленКОтправке);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Отменен);
	//
	////ОКЕЙ Вдовиченко Г.В(СофтЛаб) Начало 2019-06-17 (#3377)
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.КРаспаковке);
	//МассивСтатусов.Добавить(Перечисления.СтатусыПакетовЭД.Распакован);
	////ОКЕЙ Вдовиченко Г.В(СофтЛаб) Конец 2019-06-17 (#3377)
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Доставлен"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Отправлен"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.ПодготовленКОтправке"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Отменен"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.КРаспаковке"));
	МассивСтатусов.Добавить(ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Распакован"));
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-04-16 (#3964) Оптимизация
	Возврат МассивСтатусов;
	
КонецФункции

&НаСервере
Процедура ОбновитьВидимостьДоступность()
	
	ЭтоИсходящийПакет = (Объект.Направление = Перечисления.НаправленияЭД.Исходящий);
	ЭтоВходящийПакет = (Объект.Направление = Перечисления.НаправленияЭД.Входящий);
	
	Элементы.ГруппаИсторияОбработкиВходящий.Видимость = Не ЭтоИсходящийПакет;
	Элементы.ГруппаИсторияОбработкиИсходящий.Видимость = Не ЭтоВходящийПакет;
	
	ИспользуютсяДоговорыКонтрагентов = ОбменСКонтрагентамиПовтИсп.ИспользуютсяДоговорыКонтрагентов();
	Элементы.ДоговорКонтрагента.Видимость    = ИспользуютсяДоговорыКонтрагентов;
	Элементы.ДекорацияПустаяОтступ.Видимость = ИспользуютсяДоговорыКонтрагентов;
	
КонецПроцедуры

#КонецОбласти


