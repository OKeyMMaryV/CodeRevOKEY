#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки	 - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// Заполняет список команд печати.
//
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Функция РеквизитыДокумента(ПлатежныеПозиции) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Проведен КАК Проведен,
	|	Реквизиты.РучнаяКорректировка КАК РучнаяКорректировка,
	|	Реквизиты.ДокументОснование КАК ДокументОснование,
	|	Реквизиты.Организация КАК Организация,
	|	Заявка.ЦФО КАК ЦФО,
	|	Реквизиты.Контрагент КАК Контрагент,
	|	Реквизиты.ОрганизацияПриемник КАК ОрганизацияПриемник,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	Реквизиты.КурсДокумента КАК КурсДокумента,
	|	Реквизиты.КратностьДокумента КАК КратностьДокумента,
	|	Реквизиты.Сумма КАК Сумма,
	|	Реквизиты.СтавкаНДС КАК СтавкаНДС,
	|	Реквизиты.НДС КАК НДС,
	|	Реквизиты.СуммаРегл КАК СуммаРегл,
	|	Реквизиты.НДСРегл КАК НДСРегл,
	|	Реквизиты.СуммаУпр КАК СуммаУпр,
	|	Реквизиты.НДСУпр КАК НДСУпр,
	|	Реквизиты.НазначениеПлатежа КАК НазначениеПлатежа,
	|	Реквизиты.ПлатежныйДокумент КАК ПлатежныйДокумент,
	|	Реквизиты.ДатаРасхода КАК ДатаРасхода,
	|	Реквизиты.Ответственный КАК Ответственный,
	|	Реквизиты.Комментарий КАК Комментарий,
	|	Реквизиты.БанковскийСчетКонтрагент КАК БанковскийСчетКонтрагент,
	|	Реквизиты.БанковскийСчетОрганизация КАК БанковскийСчетОрганизация,
	|	Реквизиты.БанковскийСчетОрганизацияПриемник КАК БанковскийСчетОрганизацияПриемник,
	|	Реквизиты.ТипОснованияПлатежа КАК ТипОснованияПлатежа,
	|	Реквизиты.ТипОплаты КАК ТипОплаты,
	|	Реквизиты.Важность КАК Важность,
	|	Реквизиты.УсловиеПоКурсу КАК УсловиеПоКурсу,
	|	Реквизиты.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	Реквизиты.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	Реквизиты.Процент КАК Процент,
	|	Реквизиты.СчетУчетаБУ КАК СчетУчетаБУ,
	|	Реквизиты.СчетУчетаНУ КАК СчетУчетаНУ,
	|	Реквизиты.Субконто1 КАК Субконто1,
	|	Реквизиты.Субконто2 КАК Субконто2,
	|	Реквизиты.Субконто3 КАК Субконто3,
	|	Реквизиты.СубконтоНУ1 КАК СубконтоНУ1,
	|	Реквизиты.СубконтоНУ2 КАК СубконтоНУ2,
	|	Реквизиты.СубконтоНУ3 КАК СубконтоНУ3,
	|	Реквизиты.ОтражатьВНалоговомУчете КАК ОтражатьВНалоговомУчете,
	|	Реквизиты.СтатусСоставителя КАК СтатусСоставителя,
	|	Реквизиты.КодБК КАК КодБК,
	|	Реквизиты.КодОКАТО КАК КодОКАТО,
	|	Реквизиты.ПоказательДаты КАК ПоказательДаты,
	|	Реквизиты.ПоказательНомера КАК ПоказательНомера,
	|	Реквизиты.ПоказательОснования КАК ПоказательОснования,
	|	Реквизиты.ПоказательПериода КАК ПоказательПериода,
	|	Реквизиты.ПоказательТипа КАК ПоказательТипа,
	|	Реквизиты.Сделка КАК Сделка,
	|	Реквизиты.ОчередностьПлатежа КАК ОчередностьПлатежа,
	|	Реквизиты.ВидПлатежа КАК ВидПлатежа,
	|	Реквизиты.СуммаДокумента КАК СуммаДокумента,
	|	Реквизиты.Касса КАК Касса,
	|	Реквизиты.ПеречислениеВБюджет КАК ПеречислениеВБюджет,
	|	Реквизиты.ВидПеречисленияВБюджет КАК ВидПеречисленияВБюджет,
	|	Реквизиты.ДатаПлатежаКрайняя КАК ДатаПлатежаКрайняя,
	|	Реквизиты.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности,
	|	Реквизиты.ИдентификаторПлатежа КАК ИдентификаторПлатежа,
	|	Реквизиты.УИН КАК УИН,
	|	Реквизиты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Реквизиты.БанковскийСчетФизЛица КАК БанковскийСчетФизЛица,
	|	Реквизиты.Налог КАК Налог,
	|	Реквизиты.КПППлательщика КАК КПППлательщика,
	|	Реквизиты.ВидНалоговогоОбязательства КАК ВидНалоговогоОбязательства,
	|	Реквизиты.ПереключательСчетаФизЛица КАК ПереключательСчетаФизЛица,
	|	Реквизиты.СуммаДокумента КАК СуммаПлатежныхПозиций,
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-07-17 (#3795)
	|	Заявка.ок_КодНазначенияПлатежа КАК ок_КодНазначенияПлатежа,
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-07-17 (#3795)
	|	Заявка.ФормаОплаты КАК ФормаОплаты,
	|	Заявка.ВидОперации КАК ВидОперации,
	|	Заявка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ПРЕДСТАВЛЕНИЕССЫЛКИ(Реквизиты.Ссылка) КАК Представление,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	Заявка.Проект КАК Проект,
	|	Заявка.СтатьяОборотов КАК СтатьяОборотов,
	|	Заявка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ДоговорыКонтрагентов.РасчетыВУсловныхЕдиницах, ЛОЖЬ)
	|			ТОГДА ВалютаРегламентированногоУчета.Значение
	|		ИНАЧЕ Реквизиты.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаПлатежныхПозиций,
	|	Заявка.ВидПлатежаПоКредитамЗаймам КАК ВидПлатежаПоКредитамЗаймам,
	|	Заявка.Аналитика_1 КАК Аналитика_1,
	|	Заявка.Аналитика_2 КАК Аналитика_2,
	|	Заявка.Аналитика_3 КАК Аналитика_3,
	|	Заявка.Аналитика_4 КАК Аналитика_4,
	|	Заявка.Аналитика_5 КАК Аналитика_5,
	|	Заявка.Аналитика_6 КАК Аналитика_6,
	|	Заявка.Аналитика_7 КАК Аналитика_7,
	|	Заявка.ПлатежнаяВедомость КАК ПлатежнаяВедомость,
	|	Заявка.КонтрагентЮрФизЛицо КАК КонтрагентЮрФизЛицо,
	|	Заявка.ДокументОснование КАК ЗаявкаДокументОснование,
	|	Реквизиты.Распределение.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ИД КАК ИД,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		НДС КАК НДС
	|	) КАК Распределение,
	|	Заявка.Распределение.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		ЦФО КАК ЦФО,
	|		СтатьяОборотов КАК СтатьяОборотов,
	|		ВидПлатежаПоКредитамЗаймам КАК ВидПлатежаПоКредитамЗаймам,
	|		Проект КАК Проект,
	|		ДоговорКонтрагента КАК ДоговорКонтрагента,
	|		Сумма КАК Сумма,
	|		СтавкаНДС КАК СтавкаНДС,
	|		НДС КАК НДС,
	|		НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|		ИД КАК ИД,
	|		Аналитика_1 КАК Аналитика_1,
	|		Аналитика_2 КАК Аналитика_2,
	|		Аналитика_3 КАК Аналитика_3,
	|		Аналитика_4 КАК Аналитика_4,
	|		Аналитика_5 КАК Аналитика_5,
	|		Аналитика_6 КАК Аналитика_6,
	|		Аналитика_7 КАК Аналитика_7,
	|		ИД_Прогноз КАК ИД_Прогноз
	|	) КАК ДокументОснованиеРаспределение,
	|	Заявка.ПеречислениеЗаработнойПлаты.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Ведомость КАК Ведомость,
	|		СуммаПлатежа КАК СуммаПлатежа
	|	) КАК ПеречислениеЗаработнойПлаты,
	|	Заявка.ПеречислениеДепонентов.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Ведомость КАК Ведомость,
	|		СуммаПлатежа КАК СуммаПлатежа
	|	) КАК ПеречислениеДепонентов
	|ИЗ
	|	Документ.бит_ПлатежнаяПозиция КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО Реквизиты.ДокументОснование.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.бит_ЗаявкаНаРасходованиеСредств КАК Заявка
	|		ПО Реквизиты.ДокументОснование = Заявка.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка В(&Документы)";
	
	Запрос.УстановитьПараметр("Документы", ПлатежныеПозиции);
	
	Результат = Запрос.Выполнить();
	Реквизиты = Результат.Выгрузить();
	
	Возврат Реквизиты;
	
КонецФункции

Функция ПараметрыРасшифровкиПлатежа(ПлатежныеПозиции) Экспорт

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Документы", ПлатежныеПозиции);
	Запрос.УстановитьПараметр("ВалютаРеглУчета", Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Заявка.Ссылка.Организация КАК Организация,
	|	Заявка.Ссылка.Контрагент КАК Контрагент,
	|	Позиция.Ссылка.СпособПогашенияЗадолженности КАК СпособПогашенияЗадолженности,
	|	Позиция.Ссылка.Сделка КАК Сделка,
	|	Заявка.ЦФО КАК ЦФО,
	|	Заявка.СтатьяОборотов КАК СтатьяОборотов,
	|	Заявка.ВидПлатежаПоКредитамЗаймам КАК ВидПлатежаПоКредитамЗаймам,
	|	Заявка.Проект КАК Проект,
	|	Заявка.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Заявка.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК СтатьяДвиженияДенежныхСредств,
	|	Позиция.Ссылка КАК ДокументПланирования,
	|	Позиция.Ссылка.Представление КАК ДокументПланированияПредставление,
	|	Заявка.Ссылка.ДокументОснование КАК ЗаявкаДокументОснование,
	|	Позиция.Ссылка.УсловиеПоКурсу КАК УсловиеПоКурсу,
	|	Позиция.Ссылка.Процент КАК Процент,
	|	Позиция.Сумма КАК СуммаПлатежа,
	|	Позиция.НДС КАК СуммаНДС,
	|	Позиция.СтавкаНДС КАК СтавкаНДС,
	|	Позиция.Сумма КАК СуммаВзаиморасчетов,
	|	Позиция.Ссылка.ВалютаДокумента КАК ВалютаДокумента,
	|	Позиция.Ссылка.КурсДокумента КАК КурсДокумента,
	|	Позиция.Ссылка.КратностьДокумента КАК КратностьДокумента,
	|	ЕСТЬNULL(Заявка.ДоговорКонтрагента.ВалютаВзаиморасчетов, &ВалютаРеглУчета) КАК ВалютаВзаиморасчетов,
	|	1 КАК КурсВзаиморасчетов,
	|	1 КАК КратностьВзаиморасчетов,
	|	ЕСТЬNULL(Заявка.ДоговорКонтрагента.РасчетыВУсловныхЕдиницах, ЛОЖЬ) КАК РасчетыВУсловныхЕдиницах,
	|	Заявка.Аналитика_1 КАК Аналитика_1,
	|	Заявка.Аналитика_2 КАК Аналитика_2,
	|	Заявка.Аналитика_3 КАК Аналитика_3,
	|	Заявка.Аналитика_4 КАК Аналитика_4,
	|	Заявка.Аналитика_5 КАК Аналитика_5,
	|	Заявка.Аналитика_6 КАК Аналитика_6,
	|	Заявка.Аналитика_7 КАК Аналитика_7
	//1С-ИжТиСи, Кондратьев, 03.2020, Обновление (
	|	,Заявка.Ок_СтатьяОборотовБДР КАК Ок_СтатьяОборотовБДР
	//1С-ИжТиСи, Кондратьев, 03.2020, Обновление )
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-10-06 (#3899)
	|	,Позиция.Ссылка.СчетУчетаБУ КАК СчетУчетаБУ
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-10-06 (#3899)
	|ИЗ
	|	Документ.бит_ЗаявкаНаРасходованиеСредств.Распределение КАК Заявка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.бит_ПлатежнаяПозиция.Распределение КАК Позиция
	|		ПО Заявка.ИД = Позиция.ИД
	|			И Заявка.Ссылка = Позиция.Ссылка.ДокументОснование
	|			И (Позиция.Ссылка В (&Документы))";
	
	Результат = Запрос.Выполнить();

	Возврат Результат.Выгрузить();
	
КонецФункции

Функция ПолучитьНазначениеПлатежа(Объект, ТолькоСумму = Ложь, ПолучитьСгенерированноеНазначение = Ложь, Принудительно = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Возврат Объект.НазначениеПлатежа;
	КонецЕсли;
	
	Если Принудительно Тогда
		Автозаполнение = Истина;
	Иначе    
		Автозаполнение = бит_Казначейство.ЗаполнятьНазначениеПлатежаВЗаявкеНаРасходованиеДС(Объект.Организация, Объект.Дата);
	КонецЕсли;
	
	Если Автозаполнение Тогда
		Контекст       = КонтекстПлатежногоДокумента(Объект);
		НазначениеПлатежа = Документы.СписаниеСРасчетногоСчета.ПолучитьНазначениеПлатежа(Контекст, 
		ТолькоСумму, ПолучитьСгенерированноеНазначение);
	Иначе
		НазначениеПлатежа = Объект.НазначениеПлатежа;
	КонецЕсли;
	
	Возврат НазначениеПлатежа;
	
КонецФункции 

// Формирует назначения платежа у платежной позиции.
//
// Параметры:
//  Объект					 - ДокументОбъект.бит_ПлатежнаяПозиция - документ у которого устанавливается реквизит.
//  АвтоНазначениеПлатежа	 - Строка - текст назначения платежа из заявки.
//
Процедура СформироватьНазначениеПлатежа(Объект, АвтоНазначениеПлатежа = "") Экспорт

	Автозаполнение = бит_Казначейство.ЗаполнятьНазначениеПлатежаВЗаявкеНаРасходованиеДС(Объект.Организация, Объект.Дата);
	
	ТекстНазначенияИзСчетаКонтрагента = "";
	Если ЗначениеЗаполнено(Объект.БанковскийСчетКонтрагент) Тогда
		ТекстНазначенияИзСчетаКонтрагента = СокрЛП(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Объект.БанковскийСчетКонтрагент, "ТекстНазначения")); 
	КонецЕсли; 
			
	Если Автозаполнение Тогда
		Если НЕ ПустаяСтрока(ТекстНазначенияИзСчетаКонтрагента) Тогда
			Объект.НазначениеПлатежа = ТекстНазначенияИзСчетаКонтрагента;
		Иначе
			Объект.НазначениеПлатежа = АвтоНазначениеПлатежа;
		КонецЕсли;
	Иначе	
		Если ПустаяСтрока(АвтоНазначениеПлатежа) Тогда
			Объект.НазначениеПлатежа = ТекстНазначенияИзСчетаКонтрагента; 
		Иначе
			Объект.НазначениеПлатежа = АвтоНазначениеПлатежа;
		КонецЕсли;  
	КонецЕсли; 
	
	Если Автозаполнение Тогда
		ФормаОплаты = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ДокументОснование, "ФормаОплаты");
		Если ФормаОплаты = Перечисления.бит_ВидыДенежныхСредств.Наличные Тогда
			Если НЕ ПустаяСтрока(Объект.НазначениеПлатежа) Тогда
				Объект.НазначениеПлатежа = Документы.бит_ПлатежнаяПозиция.ПолучитьНазначениеПлатежа(Объект, Истина);
			КонецЕсли; 
		Иначе	
			Объект.НазначениеПлатежа = ПолучитьНазначениеПлатежа(Объект, ЗначениеЗаполнено(Объект.НазначениеПлатежа));
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры
  
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КонтекстПлатежногоДокумента(Объект)

	УстановитьПривилегированныйРежим(Истина);
	
	СоответствиеОпераций = бит_КазначействоКлиентСервер.ВидыОперацийЗаявкиВидыОперацийРегл("СписаниеСРасчетногоСчета");
	
	РеквизитыОснования = "ВидОперации, ПлатежнаяВедомость, Распределение, Контрагент, ФизическоеЛицо";
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.ДокументОснование, РеквизитыОснования);
	
	Контекст = Документы.СписаниеСРасчетногоСчета.СоздатьДокумент();
	ЗаполнитьЗначенияСвойств(Контекст, Объект.ДокументОснование);
	
	Контекст.ВидОперации 		= СоответствиеОпераций.Получить(РеквизитыОснования.ВидОперации);
	Контекст.ПлатежнаяВедомость = РеквизитыОснования.ПлатежнаяВедомость;
	Контекст.Дата 				= Объект.Дата;
	Контекст.СуммаДокумента 	= Объект.Сумма;
	Контекст.Организация 		= Объект.Организация;
	Контекст.НазначениеПлатежа 	= Объект.НазначениеПлатежа;
	
	ОперацииСФизЛицом = Документы.бит_ЗаявкаНаРасходованиеСредств.ОперацииСФизЛицом();
	Если ОперацииСФизЛицом.Найти(РеквизитыОснования.ВидОперации) = Неопределено Тогда
		Контекст.Контрагент 	 = РеквизитыОснования.Контрагент;
		Контекст.СчетКонтрагента = Объект.БанковскийСчетКонтрагент;
	Иначе	
		Контекст.Контрагент = РеквизитыОснования.ФизическоеЛицо;
	КонецЕсли;  
	
	РаспределениеВОсновании = РеквизитыОснования.Распределение.Выгрузить();
	
	Для каждого СтрокаТаблицы Из Объект.Распределение Цикл
		НоваяСтрока = Контекст.РасшифровкаПлатежа.Добавить();
		НоваяСтрока.СтавкаНДС 		   = СтрокаТаблицы.СтавкаНДС;
		НоваяСтрока.СуммаНДС  		   = СтрокаТаблицы.НДС;
		РезультатПоиска = РаспределениеВОсновании.Найти(СтрокаТаблицы.ИД,"ИД");
		Если РезультатПоиска <> Неопределено Тогда
			НоваяСтрока.ДоговорКонтрагента = РезультатПоиска.ДоговорКонтрагента;
		КонецЕсли; 
	КонецЦикла; 
	
	Возврат Контекст;
	
КонецФункции

#КонецОбласти

#КонецЕсли
