#Область ОписаниеПеременных

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Перем ПроверкаКонтрагентовПараметрыОбработчикаОжидания Экспорт;
&НаКлиенте
Перем ФормаДлительнойОперации Экспорт;
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

&НаКлиенте
Перем ЭтоНоваяСтрока;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПоступлениеТоваровУслугФормы.ПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
	УстановитьВидимостьСчетовУчета();
	
	УстановитьУсловноеОформление();
	
	УчетНДС.ПрименитьПраваДоступаСчетаФактуры(
		СчетФактура,
		Элементы.СчетФактураПросмотр,
		Элементы.СчетФактураРедактирование);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриСозданииНаСервереДокумент(ЭтотОбъект, Параметры);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	ОбработкаНовостейПереопределяемый.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"БП.Документ.ПоступлениеТоваровУслуг",
		"ФормаДокументаАренда",
		НСтр("ru='Новости: Поступление (акты, накладные, УПД)'"),
		ИдентификаторыСобытийПриОткрытии
	);
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборот = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияС1СДокументооборот");
		МодульИнтеграцияС1СДокументооборот.ПриСозданииНаСервере(ЭтотОбъект);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	// бит_Финанс изменения кода. Начало.
	бит_ПриСозданииНаСервереЗавершение();
	// бит_Финанс добавление кода. Конец.
	
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-26 (#4407)
	ЗаполнитьКэшЗначений();
	
	// Отображение дополнительных измерений в табличных полях.
	бит_МеханизмДопИзмерений.ОтобразитьДополнительныеИзмеренияВТабличномПоле_Управляемая(ЭтотОбъект
																						, "Услуги"
																						, ЭтотОбъект.фКэшЗначений.ИзмеренияДоп
																						, ЭтотОбъект.фКэшЗначений.НастройкиИзмерений);
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-26 (#4407)
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Обработка.ПодборНоменклатуры.Форма.Форма" Тогда
		ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "Документ.СчетНаОплатуПоставщика.Форма.ФормаВыбора" Тогда
		ОбработкаЗаполненияПоСчетуНаКлиенте(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.ВыборПорядкаУчетаРасчетов" Тогда
		ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.РедактированиеСчетаЗатрат" Тогда
		ОбработкаВыбораАналитикиУчетаНаКлиенте(ВыбранноеЗначение);
		
	ИначеЕсли ИсточникВыбора.ИмяФормы = "ОбщаяФорма.УниверсальныйПередаточныйДокумент" Тогда
		ОбработкаВыбораУниверсальныйПередаточныйДокументНаСервере(ВыбранноеЗначение);
		УправлениеФормой(ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "Запись_СчетФактураПолученный"
		И ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ДокументыОснования") И Параметр.ДокументыОснования.Найти(Объект.Ссылка) <> Неопределено Тогда
		ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ЭтотОбъект, Параметр.РеквизитыСФ);
		СтатусыОригиналовДокументов = СтатусыОригиналовДокументов(Объект.Ссылка, СчетФактура);
		ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
		УправлениеФормой(ЭтотОбъект);
	ИначеЕсли ИмяСобытия = "ОбновитьДокументИБПослеЗаполнения" И Параметр.Найти(Объект.Ссылка) <> Неопределено Тогда
		Прочитать();
	ИначеЕсли ИмяСобытия = "ИзмененСтатусДокументов" Тогда
		// Статус документа изменен в форме списка поступлений или счетов-фактур.
		Если Не Объект.Ссылка.Пустая() 
			И (Параметр.Найти(Объект.Ссылка) <> Неопределено ИЛИ Параметр.Найти(СчетФактура) <> Неопределено) Тогда
			СтатусыОригиналовДокументов = СтатусыОригиналовДокументов(Объект.Ссылка, СчетФактура);
			ОригиналПолучен      = СтатусыОригиналовДокументов[Объект.Ссылка];
			ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_ДоговорыКонтрагентов" И Объект.ДоговорКонтрагента = Источник Тогда
		
		ТребуетсяПерерасчитатьСрокОплаты =
			ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты;
			
		Если ТребуетсяПерерасчитатьСрокОплаты Тогда
			ОбновитьСрокОплатыНаСервере();
		КонецЕсли;
		
	Иначе
		ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтотОбъект, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтотОбъект, ИмяСобытия, Параметр, Источник);
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОткрытииДокумент(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ПоступлениеТоваровУслугФормы.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормыКлиент.ПередЗаписью(
		ЭтотОбъект, Отказ, ПараметрыЗаписи);
			
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)

	// СтандартныеПодсистемы.УправлениеДоступом
	УправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.УправлениеДоступом

	ПоступлениеТоваровУслугФормы.ПослеЗаписиНаСервере(
		ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
		
	УчетНДСКлиентСервер.ОтобразитьПолеУПД(ЭтотОбъект, ТекущийОбъект.ЭтоУниверсальныйДокумент);
		
	УстановитьСостояниеДокумента();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПередЗаписьюНаСервереДокумент(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	// ИнтеграцияС1СДокументооборотом
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтеграцияС1СДокументооборотом") Тогда
		МодульИнтеграцияС1СДокументооборот = ОбщегоНазначения.ОбщийМодуль("ИнтеграцияС1СДокументооборот");
		МодульИнтеграцияС1СДокументооборот.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец ИнтеграцияС1СДокументооборотом
	
	ПоступлениеТоваровУслугФормы.ПередЗаписьюНаСервере(ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормы.ПриЗаписиНаСервере(
		ЭтотОбъект, Отказ, ТекущийОбъект, ПараметрыЗаписи);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентов.ПриЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПоступлениеТоваровУслугФормыКлиент.ПослеЗаписи(ЭтотОбъект, ПараметрыЗаписи);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НомерВходящегоДокументаПриИзменении(Элемент)
	
	ПоступлениеТоваровУслугФормыКлиент.НомерВходящегоДокументаПриИзменении(ЭтотОбъект, Элемент);

КонецПроцедуры

&НаКлиенте
Процедура ДатаВходящегоДокументаПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента) И НЕ Объект.Проведен Тогда
		Объект.Дата = Объект.ДатаВходящегоДокумента + (Объект.Дата - НачалоДня(Объект.Дата));
		ДатаПриИзмененииНаКлиенте();
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормыКлиент.ДатаВходящегоДокументаПриИзменении(ЭтотОбъект);

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

	// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-26 (#4407)
	Если Объект.Услуги.Количество() > 0 Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПериодВУслугахЗавершение", ЭтотОбъект), 
							НСтр("ru = 'Дата вх. документа будет установлена в поле ""Период"" в табличной части услуги. Продолжить?'"), 
							РежимДиалогаВопрос.ДаНет,
							30,
							КодВозвратаДиалога.Нет,
							,
							КодВозвратаДиалога.Нет);
	КонецЕсли;
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-26 (#4407)
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзменении(Элемент)

	ДатаПриИзмененииНаКлиенте();
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Объект.Дата);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Организация) Тогда
		ОрганизацияПриИзмененииНаСервере();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.Контрагент) Тогда
		КонтрагентПриИзмененииНаСервере();
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элемент);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораКонтрагенты");

КонецПроцедуры

&НаКлиенте
Процедура КонтрагентАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
		
КонецПроцедуры

&НаКлиенте
Процедура КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСКонтрагентамиБПКлиент.КонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорКонтрагентаПриИзменении(Элемент)

	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		
		ДоговорКонтрагентаПриИзмененииНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПорядокУчетаРасчетовНажатие(Элемент, СтандартнаяОбработка)
	
	АдресХранилищаЗачетАвансов = ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере();

	ПоступлениеТоваровУслугФормыКлиент.ПорядокУчетаРасчетовНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка, АдресХранилищаЗачетАвансов);

КонецПроцедуры

&НаКлиенте
Процедура ЦеныИВалютаНажатие(Элемент, СтандартнаяОбработка)

	ПоступлениеТоваровУслугФормыКлиент.ЦеныИВалютаНажатие(ЭтотОбъект, Элемент, СтандартнаяОбработка);

КонецПроцедуры

&НаКлиенте
Процедура СостояниеЭДОНажатие(Элемент, СтандартнаяОбработка)
	ОбменСКонтрагентамиКлиент.СостояниеЭДОНажатие_ФормаДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаФактурыПолученногоПриИзменении(Элемент)
	
	ПоступлениеТоваровУслугФормыКлиент.НомерСчетаФактурыПолученногоПриИзменении(ЭтотОбъект, Элемент);	
	
КонецПроцедуры

&НаКлиенте
Процедура УПДПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, ЭтотОбъект.Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеУПДНажатие(Элемент, СтандартнаяОбработка)
	
	АдресХранилищаПродавцы = ПоместитьТаблицуПродавцыВоВременноеХранилищеНаСервере();
	УчетНДСКлиент.ОткрытьФормуРеквизитовУПД(ЭтотОбъект, СтандартнаяОбработка, АдресХранилищаПродавцы);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособУчетаАрендыПриИзменении(Элемент)
	
	СпособУчетаАрендыПриИзмененииНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)

	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2021-09-08 (#4338)
	УстановитьДоступностьКнопокКопированияКолонокТЧ("Услуги", Элементы.Услуги);
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2021-09-08 (#4338)
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)

	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриНачалеРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ЭтоНоваяСтрока = НоваяСтрока;

	Если НЕ НоваяСтрока ИЛИ Копирование Тогда
		Возврат;
	КонецЕсли;

	СтрокаТаблицы = Элементы.Услуги.ТекущиеДанные;
	СтрокаТаблицы.ОтражениеВУСН = ПоступлениеТоваровУслугФормыКлиентСервер.ОтражениеВУСН(Объект.ВидОперации, Ложь);
	СтрокаТаблицы.АналитикаУчета = "<...>";
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)

	Если ОтменаРедактирования И НоваяСтрока Тогда
		// Закроем форму редактирования строки, т.к. пользователь отменяет ввод
		Оповестить("ФормаРедактированияСтроки_Закрыть", Неопределено, ЭтотОбъект);
	КонецЕсли;

	ЭтоНоваяСтрока = Ложь;

	Если Объект.НДСНеВыделять Тогда
		ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
		Если Не ЗначениеЗаполнено(ТекущиеДанные.СтавкаНДС) тогда
			ТекущиеДанные.СтавкаНДС = ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС")
		КонецЕсли;
	КонецЕсли;
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПриОкончанииРедактированияТабличнойЧасти(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураПриИзменении(Элемент)

	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;

	// Получим общие параметры обработки для реквизитов документа
	ПараметрыОбработки = ПоступлениеТоваровУслугФормыКлиент.ПодготовитьПараметрыОбработкиУслугиНоменклатураПриИзменении(
		ЭтотОбъект, ТекущиеДанные);
	
	// Дополнительные поля, требующиеся для заполнения добавленных колонок табличного поля текущей формы.
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("Всего", ТекущиеДанные.Всего);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("АналитикаУчета", ТекущиеДанные.АналитикаУчета);
	ПараметрыОбработки.ДанныеСтрокиТаблицы.Вставить("АналитикаУчетаЗаполнена", ТекущиеДанные.АналитикаУчетаЗаполнена);
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбработки.ДанныеОбъекта);
		
	УслугиНоменклатураПриИзмененииНаСервере(
		ПараметрыОбработки.ДанныеСтрокиТаблицы,
		ПараметрыОбработки.ДанныеОбъекта,
		ПараметрыОбработки.СчетаУчетаКЗаполнению);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ПараметрыОбработки.ДанныеСтрокиТаблицы);

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)

	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормыВыбораНоменклатура");

КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	РаботаСНоменклатуройКлиент.НоменклатураОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураАвтоПодбор(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиНоменклатураОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка)
	
	ПараметрыПолученияДанных.Вставить("ВидыНоменклатуры", "Услуги");
	
	РаботаСНоменклатуройКлиент.НоменклатураОкончаниеВводаТекста(
		Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСумма(ЭтотОбъект, "Услуги", 1, ПрименяютсяСтавки4и2);

КонецПроцедуры

&НаКлиенте
Процедура УслугиСтавкаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСтавкаНДС(ЭтотОбъект, "Услуги", ПрименяютсяСтавки4и2);
	РаботаСДоговорамиКонтрагентовБПКлиент.НастроитьСпособЗаполненияСтавкиНДС(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаКлиенте
Процедура УслугиСуммаНДСПриИзменении(Элемент)

	ОбработкаТабличныхЧастейКлиентСервер.ПриИзмененииСуммаНДС(ЭтотОбъект, "Услуги");

КонецПроцедуры

&НаКлиенте
Процедура УслугиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)

	Если Поле.Имя = "УслугиАналитикаУчета" Тогда
		ОткрытьФормуРедактированияСтроки(ВыбраннаяСтрока);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УслугиПриАктивизацииЯчейки(Элемент)
	
	Если Элементы.Услуги.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если ЭтоНоваяСтрока Тогда
		ТекущаяКолонкаТаблицы = Элементы.Услуги.ТекущийЭлемент;
		Если ТекущаяКолонкаТаблицы.Имя = "УслугиАналитикаУчета"
			И НЕ Элементы.Услуги.ТекущиеДанные.АналитикаУчетаЗаполнена Тогда
			ВыбраннаяСтрока = Элементы.Услуги.ТекущаяСтрока;
			ОткрытьФормуРедактированияСтроки(ВыбраннаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-24 (#4407)
	УстановитьДоступностьКнопокКопированияКолонокТЧ("Услуги", Элементы.Услуги);
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-24 (#4407)
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиАналитикаУчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ВыбраннаяСтрока = Элементы.Услуги.ТекущаяСтрока;
	ОткрытьФормуРедактированияСтроки(ВыбраннаяСтрока);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НадписьСчетФактураНажатие(Элемент, СтандартнаяОбработка)

	СтандартнаяОбработка = Ложь;
	БухгалтерскийУчетКлиентПереопределяемый.ОткрытьСчетФактуру(ЭтотОбъект, СчетФактура, "СчетФактураПолученный");
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборУслуги(Команда)

	ПоступлениеТоваровУслугФормыКлиент.ПодборНоменклатуры(ЭтотОбъект, "Услуги", Команда);

КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСчетФактуру(Команда)
	
	ПоступлениеТоваровУслугФормыКлиент.ЗарегистрироватьСчетФактуру(ЭтотОбъект, Команда);	
	УправлениеФормой(ЭтотОбъект);
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ЗапуститьПроверкуКонтрагентовВДокументе(ЭтотОбъект, Элементы.НадписьСчетФактура);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область СлужебныйПрограммныйИнтерфейс

&НаСервере
Процедура ЗаполнитьДобавленныеКолонкиТаблиц(ИмяТаблицы = "") Экспорт

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	Если ПустаяСтрока(ИмяТаблицы) ИЛИ ИмяТаблицы = "Услуги" Тогда
		Для Каждого СтрокаТаблицы Из Объект.Услуги Цикл
			ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, ПараметрыОбъекта);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьИтогиНаСервере() Экспорт

	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере() Экспорт
	
	УстановитьФункциональныеОпцииФормы();
	
	УстановитьСостояниеДокумента();
	
	ТекущаяДатаДокумента = Объект.Дата;
	
	ВалютаРегламентированногоУчета = ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
	
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) 
		и ЗначениеЗаполнено(Объект.Контрагент)
		и НЕ ЗначениеЗаполнено(Объект.Ссылка) 
		и НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ПоступлениеТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	КонецЕсли;
	
	СпособУчетаАренды = ?(Объект.ВедетсяУчетПредметовАренды, 1, 0);
	
	УстановитьПараметрыПоДоговору();
		
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);

	ПоступлениеТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(ЭтотОбъект);
	
	ОбновитьИтоги(ЭтотОбъект);
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьТребуетсяСчетФактура(ЭтотОбъект);
	ПоступлениеТоваровУслугФормыКлиентСервер.ЗаполнитьРеквизитыПроСчетФактуру(ЭтотОбъект);
	ПоступлениеТоваровУслугФормы.ЗаполнитьРеквизитыУПД(ЭтотОбъект);
	
	СтатусыОригиналовДокументов = СтатусыОригиналовДокументов(Объект.Ссылка, СчетФактура);
	ОригиналПолучен      = СтатусыОригиналовДокументов[Объект.Ссылка];
	ОригиналСчетаФактуры = СтатусыОригиналовДокументов[СчетФактура];
	
	УправлениеФормой(ЭтотОбъект);
	
	ПоступлениеТоваровУслугФормы.УстановитьЗаголовокФормы(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыПоДоговору()
    
	Если ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			Объект.ДоговорКонтрагента, 
			"ВидДоговора, ВалютаВзаиморасчетов, УчетАгентскогоНДС, РасчетыВУсловныхЕдиницах, НДСПоСтавкам4и2, ВидАгентскогоДоговора");
		ВидДоговора           = РеквизитыДоговора.ВидДоговора;
		ЭтоКомиссия           = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
		ЭтоКомиссияНаЗакупку  = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку;
		УчетАгентскогоНДС     = РеквизитыДоговора.УчетАгентскогоНДС;
		ВидАгентскогоДоговора = РеквизитыДоговора.ВидАгентскогоДоговора;
		ВалютаВзаиморасчетов  = РеквизитыДоговора.ВалютаВзаиморасчетов;
		РасчетыВУЕ            = РеквизитыДоговора.РасчетыВУсловныхЕдиницах;
		ПрименяютсяСтавки4и2  = РеквизитыДоговора.НДСПоСтавкам4и2;
	Иначе
		ВидДоговора           = Неопределено;
		ЭтоКомиссияНаЗакупку  = Ложь;
		УчетАгентскогоНДС     = Ложь;
		РасчетыВУЕ            = Ложь;
		ПрименяютсяСтавки4и2  = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивВидовДоговоров() Экспорт

	СписокВидовДоговоров = Новый Массив;

	СписокВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СПоставщиком"));
	СписокВидовДоговоров.Добавить(ПредопределенноеЗначение("Перечисление.ВидыДоговоровКонтрагентов.СКомиссионеромНаЗакупку"));

	Возврат СписокВидовДоговоров;

КонецФункции

&НаКлиенте
Процедура ПриИзмененииЦеныИВалюты(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь) Экспорт

	Если ПерезаполнитьЦены ИЛИ ПересчитатьЦены ИЛИ ПересчитатьНДС Тогда
		ПриИзмененииЦеныИВалютыНаСервере(ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены, ПересчитатьЦены, ПересчитатьНДС);
	КонецЕсли;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.УстановитьТребуетсяСчетФактура(ЭтотОбъект);
	ОбновитьИтоги(ЭтотОбъект);
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура УправлениеФормойНаСервере() Экспорт
	
	УправлениеФормой(ЭтотОбъект);
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПараметрыВыбораДляСчетовУчета() Экспорт
	
	// В форме услуг параметры выбора счетов учета не используются.
	// Процедура необходима для совместимости с другими формами документа.
	
КонецПроцедуры

&НаСервере
Процедура УстановитьФункциональныеОпцииФормы() Экспорт

	ОбщегоНазначенияБПКлиентСервер.УстановитьПараметрыФункциональныхОпцийФормыДокумента(ЭтотОбъект);
	
	ПлательщикНДС                    = УчетнаяПолитика.ПлательщикНДС(Объект.Организация, Объект.Дата);
	РаздельныйУчетНДСНаСчете19       = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	ПлательщикНалогаНаПрибыль        = УчетнаяПолитика.ПлательщикНалогаНаПрибыль(Объект.Организация, Объект.Дата);
	ПрименениеУСН                    = УчетнаяПолитика.ПрименяетсяУСН(Объект.Организация, Объект.Дата);
	ПрименениеУСНДоходы              = УчетнаяПолитика.ПрименяетсяУСНДоходы(Объект.Организация, Объект.Дата);
	СпособОценкиТоваровВРознице      = УчетнаяПолитика.СпособОценкиТоваровВРознице(Объект.Организация, Объект.Дата);
	ИспользоватьТипыЦенНоменклатуры  = Ложь;
	РаздельныйУчетНДС                = УчетнаяПолитика.РаздельныйУчетНДС(Объект.Организация, Объект.Дата);
	РаздельныйУчетНДСНаСчете19       = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Объект.Организация, Объект.Дата);
	ЕстьВалютныйУчет                 = БухгалтерскийУчетПереопределяемый.ИспользоватьВалютныйУчет();
	ИспользуетсяПланированиеПлатежей = ПолучитьФункциональнуюОпцию("ИспользоватьПланированиеПлатежейПоставщикам");
	ВестиУчетПоДоговорам             = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ОбратноеНачислениеНДС            = БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	СпособЗаполненияСтавкиНДС        = РаботаСДоговорамиКонтрагентовБП.СпособЗаполненияСтавкиНДСПоДоговору(
		Объект.ДоговорКонтрагента);

КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()

	НастройкиУсловногоОформления = Новый Структура();

	УсловноеОформление.Элементы.Очистить();

	ОбновитьУсловноеОформление(ЭтотОбъект);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьУсловноеОформление(Форма)

	НадоИнициализировать = Ложь;
	
	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ОбщееПроинициализировано") Тогда
		НадоИнициализировать = Истина;
	КонецЕсли;

	Если НЕ Форма.НастройкиУсловногоОформления.Свойство("ПрименяютсяСтавки4и2Проинициализировано") Тогда
		НадоИнициализировать = Истина;
	КонецЕсли;
	
	Если НадоИнициализировать Тогда
		Форма.УстановитьУсловноеОформлениеДокумента();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ОбновитьУсловноеОформлениеНаСервере() Экспорт

	Если ТипЗнч(НастройкиУсловногоОформления) = Тип("Структура") Тогда
		// При смене вида операции процедура вызывается из общего модуля 
		// ПоступлениеТоваровУслугФормы раньше, чем отработает 
		// УстановитьУсловноеОформление(). Поэтому вызываем обновление только
		// тогда, когда структура НастройкиУсловногоОформления уже была инициализирована.
		ОбновитьУсловноеОформление(ЭтотОбъект);
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеДокумента() Экспорт

	Если НЕ НастройкиУсловногоОформления.Свойство("ОбщееПроинициализировано") Тогда
		УстановитьУсловноеОформлениеОбщее();
	КонецЕсли;
	
	Если НЕ НастройкиУсловногоОформления.Свойство("ПрименяютсяСтавки4и2Проинициализировано") Тогда
		УстановитьУсловноеОформлениеПрименяютсяСтавки4и2();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеОбщее()

	НастройкиУсловногоОформления.Вставить("ОбщееПроинициализировано", Истина);

	ПоступлениеТоваровУслугФормы.УстановитьУсловноеОформлениеРеквизитыДокумента(ЭтотОбъект);

	// УслугиСуммаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСуммаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.БезНДС);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);


	// ПорядокУчетаРасчетов

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "ПорядокУчетаРасчетов");

	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ЭлементУО.Отбор,
		"СчетаРасчетовЗаполнены", ВидСравненияКомпоновкиДанных.Равно, Ложь);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НезаполненныйРеквизит);
	
		ЭлементУО = УсловноеОформление.Элементы.Добавить();

	// УслугиСчетУчетаНДС
		
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСчетУчетаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СуммаНДС", ВидСравненияКомпоновкиДанных.Равно, 0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СчетУчетаНДС", ВидСравненияКомпоновкиДанных.Заполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);
	
	
	// УслугиСпособУчетаНДС
		
	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСпособУчетаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(
		ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СуммаНДС", ВидСравненияКомпоновкиДанных.Равно, 0);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"Объект.Услуги.СпособУчетаНДС", ВидСравненияКомпоновкиДанных.Заполнено);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("ОтметкаНезаполненного", Ложь);

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформлениеПрименяютсяСтавки4и2()

	Если НЕ ПрименяютсяСтавки4и2 Тогда
		Возврат;
	КонецЕсли;

	НастройкиУсловногоОформления.Вставить("ПрименяютсяСтавки4и2Проинициализировано", Истина);


	// УслугиСтавкаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ПрименяютсяСтавки4и2", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС18);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС18_118);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '4 %'"));


	// УслугиСтавкаНДС

	ЭлементУО = УсловноеОформление.Элементы.Добавить();

	КомпоновкаДанныхКлиентСервер.ДобавитьОформляемоеПоле(ЭлементУО.Поля, "УслугиСтавкаНДС");

	ГруппаОтбора1 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ЭлементУО.Отбор.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ);

		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора1,
			"ПрименяютсяСтавки4и2", ВидСравненияКомпоновкиДанных.Равно, Истина);

		ГруппаОтбора2 = КомпоновкаДанныхКлиентСервер.ДобавитьГруппуОтбора(ГруппаОтбора1.Элементы, ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС10);

			ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ГруппаОтбора2,
				"Объект.Услуги.СтавкаНДС", ВидСравненияКомпоновкиДанных.Равно, Перечисления.СтавкиНДС.НДС10_110);

	ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = '2 %'"));

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	Объект   = Форма.Объект;
	
	Если Объект.ЭтоУниверсальныйДокумент Тогда
		НеОтображатьРеквизитыСчетаФактуры = Истина;
	Иначе
		НеОтображатьРеквизитыСчетаФактуры = Объект.НДСНеВыделять;
	КонецЕсли;
	
	// Счет-фактура
	УчетНДСКлиентСервер.НастроитьПоляСчетаФактуры(
		Элементы.СчетФактураКнопка,
		Элементы.СчетФактураСсылка,
		Элементы.НадписьСчетФактура,
		НеОтображатьРеквизитыСчетаФактуры,
		Форма.ТребуетсяСчетФактура,
		Форма.СчетФактура);
		
	Элементы.ОригиналСчетаФактуры.Видимость = Форма.ТребуетсяСчетФактура И Не НеОтображатьРеквизитыСчетаФактуры;
	
	УчетНДСКлиентСервер.ОтобразитьПолеУПД(Форма, НеОтображатьРеквизитыСчетаФактуры);
	
	Элементы.ИтогиВсегоНДС.Видимость = Не Объект.НДСНеВыделять;
	
	ПоступлениеТоваровУслугФормыКлиентСервер.СформироватьНадписьЦеныИВалюта(Форма);
	
	Элементы.ДоговорКонтрагента.Доступность = 
		ЗначениеЗаполнено(Объект.Организация) И ЗначениеЗаполнено(Объект.Контрагент);
	
	Элементы.ПодразделениеОрганизации.Доступность = ЗначениеЗаполнено(Объект.Организация);
	
	ДоступностьПорядокУчетаРасчетов = (НЕ Форма.ВестиУчетПоДоговорам И ЗначениеЗаполнено(Объект.Контрагент)) 
		ИЛИ ЗначениеЗаполнено(Объект.ДоговорКонтрагента);
	Элементы.ПорядокУчетаРасчетов.Доступность = ДоступностьПорядокУчетаРасчетов;
	Элементы.ПорядокУчетаРасчетов.Гиперссылка = ДоступностьПорядокУчетаРасчетов;
	
	Элементы.СпособУчетаАренды.Видимость =  
		Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды");
	
	ЗаполнитьСписокВыбораСтавокНДС(Форма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьСписокВыбораСтавокНДС(Форма)

	Элементы = Форма.Элементы;
	
	СписокВыбораСтавок = Элементы.УслугиСтавкаНДС.СписокВыбора;
	СписокВыбораСтавок.Очистить();
	
	Если Форма.ПрименяютсяСтавки4и2 Тогда
		
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"), "4%");
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10"), "2%");
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"));
		
	Иначе
		
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС18_118"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС10_110"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС0"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.БезНДС"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20"));
		СписокВыбораСтавок.Добавить(ПредопределенноеЗначение("Перечисление.СтавкиНДС.НДС20_120"));
		
	КонецЕсли; 

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УслугиНоменклатураПриИзмененииНаСервере(СтрокаТабличнойЧасти, Знач ДанныеОбъекта, Знач СчетаУчетаКЗаполнению)

	ПоступлениеТоваровУслугФормы.УслугиНоменклатураПриИзменении(СтрокаТабличнойЧасти, ДанныеОбъекта, СчетаУчетаКЗаполнению);
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТабличнойЧасти, ДанныеОбъекта);

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)

	Объект = Форма.Объект;

	Форма.ИтогиВсего    = Объект.Услуги.Итог("Всего");
	Форма.ИтогиВсегоНДС = Объект.Услуги.Итог("СуммаНДС");

КонецПроцедуры

&НаСервере
Процедура ОрганизацияПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ОрганизацияПриИзменении(ЭтотОбъект);
	
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаПриИзмененииНаКлиенте()

	Если НачалоДня(Объект.Дата) = НачалоДня(ТекущаяДатаДокумента) Тогда
		// Изменение времени не влияет на поведение документа.
		ТекущаяДатаДокумента = Объект.Дата;
		Возврат;
	КонецЕсли;

	// Общие проверки условий по датам.
	ТребуетсяВызовСервера = ОбщегоНазначенияБПКлиент.ТребуетсяВызовСервераПриИзмененииДатыДокумента(Объект.Дата, 
		ТекущаяДатаДокумента, Объект.ВалютаДокумента, ВалютаРегламентированногоУчета);
		
	ТребуетсяПерерасчитатьСрокОплаты = 
		ИспользуетсяПланированиеПлатежей И АвторасчетСрокаОплаты И (НачалоДня(Объект.Дата) <> НачалоДня(ТекущаяДатаДокумента));
		
	// Если определили, что изменение даты может повлиять на какие-либо параметры, 
	// то передаем обработку на сервер.
	Если ТребуетсяВызовСервера ИЛИ ТребуетсяПерерасчитатьСрокОплаты Тогда
		ДатаПриИзмененииНаСервере();
	КонецЕсли;
	
	// Запомним новую дату документа.
	ТекущаяДатаДокумента = Объект.Дата;
	
КонецПроцедуры

&НаСервере
Процедура ДатаПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ДатаПриИзменении(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура КонтрагентПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.КонтрагентПриИзменении(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаСервере
Процедура ДоговорКонтрагентаПриИзмененииНаСервере()

	ПоступлениеТоваровУслугФормы.ДоговорКонтрагентаПриИзменении(ЭтотОбъект);
	ЗаполнитьДобавленныеКолонкиТаблиц();
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, Знач ПараметрыОбъекта)
	
	СтрокаТаблицы.Всего = СтрокаТаблицы.Сумма + ?(ПараметрыОбъекта.СуммаВключаетНДС, 0, СтрокаТаблицы.СуммаНДС);
	
	Если ПараметрыОбъекта.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И Не ПараметрыОбъекта.ВедетсяУчетПредметовАренды Тогда
		ЗаполнитьДобавленныеКолонкиСтрокиАналитикиУчета(СтрокаТаблицы, ПараметрыОбъекта);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(Знач Форма, ПараметрыОбъекта)

	Объект = Форма.Объект;
	
	Если ТипЗнч(ПараметрыОбъекта) <> Тип("Структура") Тогда
		ПараметрыОбъекта = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбъекта.Вставить("СуммаВключаетНДС",               Объект.СуммаВключаетНДС);
	ПараметрыОбъекта.Вставить("НДСВключенВСтоимость",           Объект.НДСВключенВСтоимость);
	ПараметрыОбъекта.Вставить("ПлательщикНалогаНаПрибыль",      Форма.ПлательщикНалогаНаПрибыль);
	ПараметрыОбъекта.Вставить("ПрименениеУСН",                  Форма.ПрименениеУСН);
	ПараметрыОбъекта.Вставить("ПрименениеУСНДоходы",            Форма.ПрименениеУСНДоходы);
	ПараметрыОбъекта.Вставить("СчетУчетаРасчетовСКонтрагентом", Объект.СчетУчетаРасчетовСКонтрагентом);
	ПараметрыОбъекта.Вставить("УчетАгентскогоНДС",              Форма.УчетАгентскогоНДС);
	ПараметрыОбъекта.Вставить("РаздельныйУчетНДСНаСчете19",     Форма.РаздельныйУчетНДСНаСчете19);
	ПараметрыОбъекта.Вставить("ВидОперации",                    Объект.ВидОперации);
	ПараметрыОбъекта.Вставить("ВедетсяУчетПредметовАренды",     Объект.ВедетсяУчетПредметовАренды);
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЦеныИВалютыНаСервере(Знач ВалютаДоИзменения, КурсДоИзменения, КратностьДоИзменения, ПерезаполнитьЦены = Ложь, ПересчитатьЦены = Ложь, ПересчитатьНДС = Ложь)

	ПоступлениеТоваровУслугФормы.ЗаполнитьРассчитатьСуммы(
		ЭтотОбъект,
		ВалютаДоИзменения,
		КурсДоИзменения,
		КратностьДоИзменения,
		ПерезаполнитьЦены,
		ПересчитатьЦены,
		ПересчитатьНДС,
		ПрименяютсяСтавки4и2);

	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПодборНаСервере(ВыбранноеЗначение, ИмяТаблицы)

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	ДобавленныеСтроки = ПоступлениеТоваровУслугФормы.ОбработкаВыбораПодборВставкаИзБуфера(ЭтотОбъект, ВыбранноеЗначение, ИмяТаблицы);
	
	Для Каждого СтрокаТаблицы Из ДобавленныеСтроки.Услуги Цикл
		ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(СтрокаТаблицы, ПараметрыОбъекта);
	КонецЦикла;
	
	ОбновитьИтоги(ЭтотОбъект);

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораПорядокУчетаРасчетовНаСервере(ВыбранноеЗначение)

	ПоступлениеТоваровУслугФормы.ОбработкаВыбораПорядокУчетаРасчетов(ЭтотОбъект, ВыбранноеЗначение);

	ЗаполнитьДобавленныеКолонкиТаблиц();

КонецПроцедуры

&НаСервере
Процедура ОбработкаВыбораУниверсальныйПередаточныйДокументНаСервере(ВыбранноеЗначение)
	
	ПоступлениеТоваровУслугФормы.ОбработкаВыбораУниверсальныйПередаточныйДокумент(ЭтотОбъект, ВыбранноеЗначение);
	
КонецПроцедуры

&НаСервере
Функция ПоместитьТаблицуПродавцыВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(ЭтотОбъект.Продавцы.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаСервере
Функция ПоместитьЗачетАвансовВоВременноеХранилищеНаСервере()

	Возврат ПоместитьВоВременноеХранилище(Объект.ЗачетАвансов.Выгрузить(), УникальныйИдентификатор);

КонецФункции

&НаКлиенте
Процедура ОбработкаЗаполненияПоСчетуНаКлиенте(ВыбранноеЗначение, ТабличнаяЧасть)

	ТекстВопроса = "Заполнить документ по счету?";
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВыбранноеЗначение", ВыбранноеЗначение);
	ДополнительныеПараметры.Вставить("ТабличнаяЧасть", ТабличнаяЧасть);

	Оповещение = Новый ОписаниеОповещения("ВопросЗаполнитьДокументПоСчетуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);

КонецПроцедуры

&НаКлиенте
Процедура ВопросЗаполнитьДокументПоСчетуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОбработкаЗаполненияПоСчетуНаСервере(ДополнительныеПараметры.ВыбранноеЗначение, ДополнительныеПараметры.ТабличнаяЧасть);
	Иначе
		Объект.СчетНаОплатуПоставщика = ДополнительныеПараметры.ВыбранноеЗначение;
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаЗаполненияПоСчетуНаСервере(ВыбранноеЗначение, ТабличнаяЧасть)

	ПоступлениеТоваровУслугФормы.ОбработкаЗаполненияПоСчету(ЭтотОбъект, ВыбранноеЗначение, ТабличнаяЧасть);

	ЗаполнитьДобавленныеКолонкиТаблиц();
	
	УправлениеФормой(ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура СчетНаОплатуПоставщикаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ПоступлениеТоваровУслугФормыКлиент.ОткрытьФормуВыбораСчетаНаОплату(ЭтотОбъект, "Услуги");
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСрокОплатыНаСервере()
	
	ПоступлениеТоваровУслугФормы.УстановитьПорядокУчетаРасчетов(ЭтотОбъект);
	
КонецПроцедуры

#Область ПроверкаКонтрагентов

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ПоказатьПредложениеИспользоватьПроверкуКонтрагентов()
	ПроверкаКонтрагентовКлиент.ПредложитьВключитьПроверкуКонтрагентов(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаКлиенте
Процедура Подключаемый_ОбработатьРезультатПроверкиКонтрагентов()
	ПроверкаКонтрагентовКлиент.ОбработатьРезультатПроверкиКонтрагентовВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ОтобразитьРезультатПроверкиКонтрагента() Экспорт
	ПроверкаКонтрагентов.ОтобразитьРезультатПроверкиКонтрагентаВДокументе(ЭтотОбъект);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
&НаСервере
Процедура ПроверитьКонтрагентовФоновоеЗадание(ПараметрыФоновогоЗадания) Экспорт	
	ПроверкаКонтрагентов.ПроверитьКонтрагентовВДокументеФоновоеЗадание(ЭтотОбъект, ПараметрыФоновогоЗадания);
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами

#КонецОбласти

#Область СчетаУчета

&НаСервере
Процедура УстановитьВидимостьСчетовУчета() Экспорт
	
	ЭлементыСчетов = Новый Массив(); 
	ЭлементыСчетов.Добавить("УслугиСчетЗатрат");
	ЭлементыСчетов.Добавить("УслугиАналитикаУчета");
	ЭлементыСчетов.Добавить("УслугиСчетУчетаНДС");
	ЭлементыСчетов.Добавить("УслугиСпособУчетаНДС");
	
	// Поле формы ПорядокУчетаРасчетов содержит счета и другие реквизиты.
	// Поэтому видимостью этого поля управляем в УчетВзаиморасчетовФормы.УстановитьПорядокУчетаРасчетов()
	
	СчетаУчетаВДокументах.УстановитьВидимостьСчетовУчета(Элементы, ЭлементыСчетов);
	
	// Показываем или отдельные колонки счетов, или одну колонку со всей аналитикой.
	
	ПоказыватьАналитику = Объект.ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И Не Объект.ВедетсяУчетПредметовАренды;
	
	Элементы.УслугиСчетЗатрат.Видимость = Элементы.УслугиСчетЗатрат.Видимость И Не ПоказыватьАналитику;
	Элементы.УслугиСчетУчетаНДС.Видимость = Элементы.УслугиСчетУчетаНДС.Видимость И Не ПоказыватьАналитику;
	Элементы.УслугиАналитикаУчета.Видимость = Элементы.УслугиАналитикаУчета.Видимость И ПоказыватьАналитику;
	
КонецПроцедуры

#КонецОбласти

#Область СтатусыДокументов

&НаСервереБезКонтекста
Функция СтатусыОригиналовДокументов(Знач Ссылка, Знач СчетФактура)
	
	ДокументыДляПолученияСтатуса = Новый Массив;
	ДокументыДляПолученияСтатуса.Добавить(Ссылка);
	ДокументыДляПолученияСтатуса.Добавить(СчетФактура);
	
	КоллекцияСтатусов = РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокументов(ДокументыДляПолученияСтатуса);
	
	СтатусыОригиналовДокументов = Новый Соответствие;
	СтатусПоступления  = КоллекцияСтатусов[Ссылка].Статус = Перечисления.СтатусыДокументовПоступления.ОригиналПолучен;
	СтатусСчетаФактуры = КоллекцияСтатусов[СчетФактура].Статус = Перечисления.СтатусыДокументовПоступления.ОригиналПолучен;
	СтатусыОригиналовДокументов.Вставить(Ссылка, СтатусПоступления);
	СтатусыОригиналовДокументов.Вставить(СчетФактура, СтатусСчетаФактуры);
	
	Возврат СтатусыОригиналовДокументов;
	
КонецФункции

#КонецОбласти

&НаКлиенте
Процедура ПоказатьКонтекстныеНовости(Команда)

	ОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(
		ЭтотОбъект,
		Команда
	);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКонтрагентов(Команда)
	
	// ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	ПроверкаКонтрагентовКлиент.ПроверитьКонтрагентовВДокументеПоКнопке(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.РаботаСКонтрагентами
	
КонецПроцедуры

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, ИдентификаторыСобытийПриОткрытии);

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуЭДО(Команда)
	
	ЭлектронноеВзаимодействиеКлиент.ВыполнитьПодключаемуюКомандуЭДО(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОжиданияЭДО()

	ОбменСКонтрагентамиКлиент.ОбработчикОжиданияЭДО(ЭтотОбъект);

КонецПроцедуры

// ИнтеграцияС1СДокументооборотом
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуИнтеграции(Команда)
	
	МодульИнтеграцияС1СДокументооборотКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ИнтеграцияС1СДокументооборотКлиент");
	МодульИнтеграцияС1СДокументооборотКлиент.ВыполнитьПодключаемуюКомандуИнтеграции(Команда, ЭтотОбъект, Объект);
	
КонецПроцедуры
// Конец ИнтеграцияС1СДокументооборотом

#Область СлужебныеПроцедурыИФункцииБСП

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат) Экспорт
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Объект, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

&НаСервере
Процедура СпособУчетаАрендыПриИзмененииНаСервере()
	
	ВедетсяУчетПредметовАренды = СпособУчетаАренды = 1;
	Если Объект.ВедетсяУчетПредметовАренды <> ВедетсяУчетПредметовАренды Тогда
		Объект.ВедетсяУчетПредметовАренды = ВедетсяУчетПредметовАренды;
		ПоступлениеТоваровУслугФормы.ВедетсяУчетПредметовАрендыПриИзменении(ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьДобавленныеКолонкиСтрокиАналитикиУчета(ДанныеСтроки, Знач ПараметрыОбъекта)

	АналитикаУчетаТекст 	= "";
	АналитикаУчетаЗаполнена = Истина;

	ИспользоватьОднуНоменклатурнуюГруппу = БухгалтерскийУчетВызовСервераПовтИсп.ИспользоватьОднуНоменклатурнуюГруппу();
	СтроковыеПредставленияСубконто = Новый Структура();
	
	Если ДанныеСтроки.Свойство("СчетЗатрат") Тогда
		Если ЗначениеЗаполнено(ДанныеСтроки.СчетЗатрат) Тогда
			СвойстваСчета 		= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДанныеСтроки.СчетЗатрат);
			
			АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + СвойстваСчета.Код;
			
			Если СвойстваСчета.УчетПоПодразделениям Тогда
				Если ЗначениеЗаполнено(ДанныеСтроки.ПодразделениеЗатрат) Тогда
					ТекстСтроки 			= ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДанныеСтроки.ПодразделениеЗатрат, "Наименование");
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", " + ТекстСтроки;
				Иначе
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
				КонецЕсли;
			КонецЕсли;
			
			Для Н = 1 По СвойстваСчета.КоличествоСубконто Цикл
				ЗначениеСубконто = ДанныеСтроки["Субконто" + Н];
				Если ЗначениеЗаполнено(ЗначениеСубконто) Тогда
					ПредставлениеСубконто = "";
					ТипСубконто = ТипЗнч(ЗначениеСубконто);
					Если ИспользоватьОднуНоменклатурнуюГруппу И ТипСубконто = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
						Продолжить;
					ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
						ПредставлениеСубконто = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконто, "Представление");
					ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
						ПредставлениеСубконто = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконто, "Представление");
					Иначе
						ПредставлениеСубконто = Строка(ЗначениеСубконто);
					КонецЕсли;
					
					СтроковыеПредставленияСубконто.Вставить("ЗначениеСубконто" + Н, ЗначениеСубконто);
					СтроковыеПредставленияСубконто.Вставить("ПредставлениеСубконто" + Н, ПредставлениеСубконто);
					
					АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + ПредставлениеСубконто;
				Иначе
					АналитикаУчетаТекст = АналитикаУчетаТекст + ", <...>";
				КонецЕсли;
			КонецЦикла;
			
		Иначе
			АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
			АналитикаУчетаЗаполнена = Ложь;
		КонецЕсли;
	КонецЕсли;

	// СчетУчетаНДС - до затрат НУ, т.к. аналитика НУ чаще всего совпадает с БУ
	Если ДанныеСтроки.Свойство("СчетУчетаНДС") Тогда
		Если ПараметрыОбъекта.НДСВключенВСтоимость
			И НЕ ПараметрыОбъекта.УчетАгентскогоНДС
			И НЕ ПараметрыОбъекта.РаздельныйУчетНДСНаСчете19 Тогда
			// в этих случаях счет учета НДС не показывается
		Иначе
			Если ЗначениеЗаполнено(ДанныеСтроки.СчетУчетаНДС) Тогда
				СвойстваСчета 		= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДанныеСтроки.СчетУчетаНДС);
				АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + СвойстваСчета.Код;
			Иначе
				Если ДанныеСтроки.СуммаНДС <> 0 Тогда
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
					АналитикаУчетаЗаполнена = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеСтроки.Свойство("СпособУчетаНДС") Тогда
		Если ПараметрыОбъекта.РаздельныйУчетНДСНаСчете19 Тогда
			Если ЗначениеЗаполнено(ДанныеСтроки.СпособУчетаНДС) Тогда
				АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + Строка(ДанныеСтроки.СпособУчетаНДС);
			Иначе
				Если ДанныеСтроки.СуммаНДС <> 0 Тогда
					АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
					АналитикаУчетаЗаполнена = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// СчетУчетаЗатратНУ
	Если ДанныеСтроки.Свойство("СчетЗатратНУ") Тогда
		Если ПараметрыОбъекта.ПлательщикНалогаНаПрибыль Тогда
			Если ЗначениеЗаполнено(ДанныеСтроки.СчетЗатратНУ) Тогда
				СвойстваСчетаНУ 	= БухгалтерскийУчетВызовСервераПовтИсп.ПолучитьСвойстваСчета(ДанныеСтроки.СчетЗатратНУ);
				
				АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + СвойстваСчетаНУ.Код;
				
				Для Н = 1 По СвойстваСчетаНУ.КоличествоСубконто Цикл
					
					ЗначениеСубконтоНУ 	= ДанныеСтроки["СубконтоНУ" + Н];
					
					Если ЗначениеЗаполнено(ЗначениеСубконтоНУ) Тогда

						ПредставлениеСубконтоНУ = "";
						
						// Если совпадает с субконто БУ, то представление выводим как для БУ.
						ЗначениеСубконтоБУ = Неопределено;
						Если СтроковыеПредставленияСубконто.Свойство("ЗначениеСубконто" + Н, ЗначениеСубконтоБУ) Тогда
							Если ЗначениеСубконтоБУ = ЗначениеСубконтоНУ Тогда
								ПредставлениеСубконтоНУ = СтроковыеПредставленияСубконто["ПредставлениеСубконто" + Н];
							КонецЕсли;
						КонецЕсли;
						
						Если ПустаяСтрока(ПредставлениеСубконтоНУ) Тогда
							ТипСубконто = ТипЗнч(ЗначениеСубконтоНУ);
							Если ИспользоватьОднуНоменклатурнуюГруппу И ТипСубконто = Тип("СправочникСсылка.НоменклатурныеГруппы") Тогда
								Продолжить;
							ИначеЕсли Справочники.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
								ПредставлениеСубконтоНУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконтоНУ, "Представление");
							ИначеЕсли Документы.ТипВсеСсылки().СодержитТип(ТипСубконто) Тогда
								ПредставлениеСубконтоНУ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗначениеСубконтоНУ, "Представление");
							Иначе
								ПредставлениеСубконтоНУ = Строка(ЗначениеСубконтоНУ);
							КонецЕсли;
						КонецЕсли;
					
						АналитикаУчетаТекст = АналитикаУчетаТекст + ", " + ПредставлениеСубконтоНУ;
					
					Иначе
						АналитикаУчетаТекст = АналитикаУчетаТекст + ", <...>";
					КонецЕсли;
					
				КонецЦикла;

			Иначе
				АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
				АналитикаУчетаЗаполнена = Ложь;
			КонецЕсли;

		КонецЕсли;
	КонецЕсли;

	// ОтражениеВУСН
	Если НЕ ПараметрыОбъекта.ПрименениеУСН
		ИЛИ ПараметрыОбъекта.ПрименениеУСНДоходы Тогда	
		// в этих случаях ОтражениеВУСН не требуется
	Иначе
		Если ЗначениеЗаполнено(ДанныеСтроки.ОтражениеВУСН) Тогда
		    АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", " + Строка(ДанныеСтроки.ОтражениеВУСН);
		Иначе
			АналитикаУчетаТекст 	= АналитикаУчетаТекст + ", <...>";
			АналитикаУчетаЗаполнена = Ложь;
		КонецЕсли;
	КонецЕсли;

	ДанныеСтроки.АналитикаУчета 			= Сред(АналитикаУчетаТекст, 3);
	ДанныеСтроки.АналитикаУчетаЗаполнена 	= АналитикаУчетаЗаполнена;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияСтроки(ВыбранноеЗначение)

	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;

	Если Не ТолькоПросмотр Тогда
		ЗаблокироватьДанныеФормыДляРедактирования();
	КонецЕсли;

	ДанныеСтроки 	= Объект.Услуги.НайтиПоИдентификатору(ВыбранноеЗначение);
			
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ТолькоПросмотр",       ТолькоПросмотр);
	ПараметрыФормы.Вставить("ДатаДокумента",        Объект.Дата);
	ПараметрыФормы.Вставить("Организация",          Объект.Организация);
	ПараметрыФормы.Вставить("СчетУчетаНДС",         ДанныеСтроки.СчетУчетаНДС);
	ПараметрыФормы.Вставить("СпособУчетаНДС",       ДанныеСтроки.СпособУчетаНДС);
	ПараметрыФормы.Вставить("СуммаНДС",             ДанныеСтроки.СуммаНДС);
	ПараметрыФормы.Вставить("НДСВключенВСтоимость", Объект.НДСВключенВСтоимость);
	ПараметрыФормы.Вставить("УчетАгентскогоНДС",    УчетАгентскогоНДС);
	ПараметрыФормы.Вставить("ОтражениеВУСН",        ДанныеСтроки.ОтражениеВУСН);
	
	ПараметрыФормы.Вставить("СчетЗатрат",          ДанныеСтроки.СчетЗатрат);
	ПараметрыФормы.Вставить("ПодразделениеЗатрат", ДанныеСтроки.ПодразделениеЗатрат);
	ПараметрыФормы.Вставить("Субконто1",           ДанныеСтроки.Субконто1);
	ПараметрыФормы.Вставить("Субконто2",           ДанныеСтроки.Субконто2);
	ПараметрыФормы.Вставить("Субконто3",           ДанныеСтроки.Субконто3);
	ПараметрыФормы.Вставить("СчетЗатратНУ",        ДанныеСтроки.СчетЗатратНУ);
	ПараметрыФормы.Вставить("СубконтоНУ1",         ДанныеСтроки.СубконтоНУ1);
	ПараметрыФормы.Вставить("СубконтоНУ2",         ДанныеСтроки.СубконтоНУ2);
	ПараметрыФормы.Вставить("СубконтоНУ3",         ДанныеСтроки.СубконтоНУ3);
	ПараметрыФормы.Вставить("Номенклатура",        ДанныеСтроки.Номенклатура);
	ПараметрыФормы.Вставить("Контрагент",          Объект.Контрагент);
	ПараметрыФормы.Вставить("ДоговорКонтрагента",  Объект.ДоговорКонтрагента);
	
	ОткрытьФорму("ОбщаяФорма.РедактированиеСчетаЗатрат", ПараметрыФормы, ЭтотОбъект);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораАналитикиУчетаНаКлиенте(ВыбранноеЗначение)

	ПараметрыОбъекта = Неопределено;
	ЗаполнитьПараметрыОбъектаДляЗаполненияДобавленныхКолонок(ЭтотОбъект, ПараметрыОбъекта);

	ДанныеСтроки = Новый Структура("Сумма, СчетУчетаНДС, СуммаНДС, СпособУчетаНДС,
		|СчетЗатрат, Субконто1, Субконто2, Субконто3,
		|СчетЗатратНУ, СубконтоНУ1, СубконтоНУ2, СубконтоНУ3,
		|ПодразделениеЗатрат, ОтражениеВУСН,
		|ВсегоПоДокументу, СуммаПоДокументу, СуммаНДСПоДокументу,
		|Всего, АналитикаУчета, АналитикаУчетаЗаполнена");
		
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, ВыбранноеЗначение);
	
	ДанныеСтроки.Сумма               = ТекущиеДанные.Сумма;
	ДанныеСтроки.СуммаНДС            = ТекущиеДанные.СуммаНДС;
	
	ЗаполнитьДобавленныеКолонкиСтрокиТаблицыУслуги(ДанныеСтроки, ПараметрыОбъекта);
	
	ЗаполнитьЗначенияСвойств(ТекущиеДанные, ДанныеСтроки, , 
		"Сумма, СуммаНДС");
	
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область бит_МодификацияФормы

// бит_Финанс изменения кода. Начало:
// Расширение функционала обраотчика "ПриСозданииНаСервере".
//
&НаСервере
Процедура бит_ПриСозданииНаСервереЗавершение()

	Если НЕ ПравоДоступа("Просмотр", Метаданные.Документы.бит_РасходнаяПозиция) Тогда
		Возврат;	
	КонецЕсли; 
	
	ПолеРасходнаяПозиция			= Элементы.Вставить("бит_РасходнаяПозиция", Тип("ПолеФормы"), Элементы.ГруппаПодвал, Элементы.ГруппаКомментарийОтветственный); 
	ПолеРасходнаяПозиция.Вид		= ВидПоляФормы.ПолеВвода;
	ПолеРасходнаяПозиция.ПутьКДанным= "Объект.бит_РасходнаяПозиция";
	ПолеРасходнаяПозиция.Подсказка	= НСтр("ru = 'Ссылка на связанный документ ""Расходная позиция"", для формирования структуры подчиненности.'");

КонецПроцедуры // бит_Финанс изменения кода. Конец.

#КонецОбласти

// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-26 (#4407)
#Область ок_МодификацияФормы
// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-26 (#4407)

//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2021-09-08 (#4338)
#Область Команды_ЗаполнитьКолонку

&НаКлиенте
Процедура ок_ЗаполнитьКолонку_Услуги(Команда)

	ЗаполнитьКолонкуТЧ(ЭтаФорма, Элементы.Услуги);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступностьКнопокКопированияКолонокТЧ(ИмяТЧ, ТаблицаФормы)
	
	ок_ОбщегоНазначенияКлиент.УстановитьДоступностьКнопокКопированияКолонокТЧ(ЭтаФорма, Элементы["ок_ЗаполнитьКолонку_" + ИмяТЧ], ТаблицаФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьКолонкуТЧ(Форма, ТаблицаФормы)

	ТекущиеДанные = ТаблицаФормы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;

	ИмяТЧ 			= ТаблицаФормы.Имя;
	ИмяКолонки 		= СтрЗаменить(НРег(ТаблицаФормы.ТекущийЭлемент.Имя), НРег(ИмяТЧ), "");
	ЗначениеКолонки	= ТекущиеДанные[ИмяКолонки];
	
	ПараметрыОповещения = Новый Структура();
	ПараметрыОповещения.Вставить("ИмяКолонки", 		ИмяКолонки);
	ПараметрыОповещения.Вставить("ЗначениеКолонки", ЗначениеКолонки);
	ПараметрыОповещения.Вставить("ИмяТЧ", 			ИмяТЧ);
	
	Оповещение 		= Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаполнитьКолонкуТЧ", ЭтотОбъект, ПараметрыОповещения);
	ТекстВопроса 	= НСтр("ru = 'Выбранное значение ""%1"" будет заполнено во всех строках табличной части ""%2"".
								|Продолжить выполнение операции?'");
	ТекстВопроса 	= СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ЗначениеКолонки, ИмяТЧ, ИмяКолонки);
	Режим 			= РежимДиалогаВопрос.ДаНет;
	
	ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаполнитьКолонкуТЧ(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
        Возврат;
    КонецЕсли;

	ИмяКолонки 		= НРег(Параметры.ИмяКолонки);
	ЗначениеКолонки = Параметры.ЗначениеКолонки;
	ИмяТЧ 			= НРег(Параметры.ИмяТЧ);
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		
		СтрокаТЧ[ИмяКолонки] = ЗначениеКолонки;
		
		// Активируем изменяемую строку табличного поля, для того что бы обновить данные в значении свойства "ТекущиеДанные"
		// это необходимо делать, т.к. связанные события используют значение из свойства "ТекущиеДанные" табличного поля
		ИдентификаторСтроки = СтрокаТЧ.ПолучитьИдентификатор();
		Элементы[ИмяТЧ].ТекущаяСтрока = ИдентификаторСтроки;
		
		Если ИмяТЧ = НРег("Услуги") Тогда
		
			Если ИмяКолонки = НРег("СтавкаНДС") Тогда
				
				УслугиСтавкаНДСПриИзменении(Неопределено);
				
			КонецЕсли; 
			
			УслугиПриИзменении(Неопределено);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры
	
#КонецОбласти
//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2021-09-09 (#4338)

// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-26 (#4407)
&НаКлиенте
Процедура Подключаемый_ЗаполнитьУправленческиеАналитикиИзЗаявкиНаДоговор(Команда)
	
	МассивНезаполненныхПолей = Новый Массив;
	
	Если Объект.Контрагент.Пустая() Тогда
		МассивНезаполненныхПолей.Добавить("Контрагент");
	КонецЕсли;
	
	Если Объект.ДоговорКонтрагента.Пустая() Тогда
		МассивНезаполненныхПолей.Добавить("Договор контрагента");
	КонецЕсли;
	
	Если МассивНезаполненныхПолей.Количество() > 0 Тогда
		
		ТекстПредупреждения = НСтр("ru = 'Для продолжения необходимо указать значения следующих полей документа: %1'");
		ТекстПредупреждения = СтрШаблон(ТекстПредупреждения, СтрСоединить(МассивНезаполненныхПолей, ", "));
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
		Возврат;
		
	КонецЕсли;
	
	Если Объект.Ссылка.Пустая() Тогда
		
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьУправленческиеАналитикиИзЗаявкиНаДоговорЗаписатьИПродолжить", ЭтотОбъект),
							НСтр("ru = 'Для продолжения необходимо записать документ. Записать?'"),
							РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ОткрытьФормуВыбораАналитикиИзЗаявкиНаДоговор();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗаполнитьIDИзЗаявкиНаДоговор(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДоговорКонтрагента) Тогда
		
		ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru = 'Укажите договор контрагента'"),,, "Объект.ДоговорКонтрагента"); 
		Возврат;
		
	КонецЕсли; 
	
	СтруктураПараметрыВыбора = Новый Структура;
	СтруктураПараметрыВыбора.Вставить("РежимВыбора"			, Истина);
	СтруктураПараметрыВыбора.Вставить("МножественныйВыбор"	, Ложь);
	СтруктураПараметрыВыбора.Вставить("ЗакрыватьПриВыборе"	, Истина);
	
	Если ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента) Тогда
		
		СтруктураПараметрыВыбора.Вставить("ДатаНачала"	 , НачалоМесяца(Объект.ДатаВходящегоДокумента));
		СтруктураПараметрыВыбора.Вставить("ДатаОкончания", КонецМесяца(Объект.ДатаВходящегоДокумента));
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Дата) Тогда
		
		СтруктураПараметрыВыбора.Вставить("ДатаНачала"	 , НачалоМесяца(Объект.Дата));
		СтруктураПараметрыВыбора.Вставить("ДатаОкончания", КонецМесяца(Объект.Дата));
		
	Иначе
		
		СтруктураПараметрыВыбора.Вставить("ДатаНачала"	 , НачалоМесяца(ТекущаяДата()));
		СтруктураПараметрыВыбора.Вставить("ДатаОкончания", КонецМесяца(ТекущаяДата()));
		
	КонецЕсли;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Организация"		, Объект.Организация);
	Отбор.Вставить("Контрагент"			, Объект.Контрагент);
	Отбор.Вставить("ДоговорКонтрагента"	, Объект.ДоговорКонтрагента);
	Отбор.Вставить("Проведен"			, Истина);
	
	СтруктураПараметрыВыбора.Вставить("Отбор", Отбор);
	
	МассивДополнительныхКолонок = Новый Массив;
	МассивДополнительныхКолонок.Добавить("СтавкаНДС");
	
	СтруктураПараметрыВыбора.Вставить("МассивДополнительныхКолонок", МассивДополнительныхКолонок);
	
	СценарийОбработкиВыбора = "БезЗаполненияАналитик";
	
	ОткрытьФорму("Документ.рс_ЗаявкаНаДоговор.Форма.ФормаВыбораУправляемая_Дерево", 
									СтруктураПараметрыВыбора, 
									ЭтотОбъект, 
									УникальныйИдентификатор,
									,
									,
									Новый ОписаниеОповещения("ОбработатьВыборЗаявкиНаДоговор", ЭтотОбъект, СценарийОбработкиВыбора));
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Услугиок_ПериодПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда 
		ТекущиеДанные.ок_Период = НачалоМесяца(ТекущиеДанные.ок_Период);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Услугиок_ЦФОПриИзменении(Элемент)
	
	ЗаполнитьСубконтоПоАналитикеКлиент("ок_ЦФО");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_Услугиок_СтатьяОборотовПриИзменении(Элемент)
	
	ЗаполнитьСубконтоПоАналитикеКлиент("ок_СтатьяОборотов");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УслугиАналитика_2ПриИзменении(Элемент)
	
	ЗаполнитьСубконтоПоАналитикеКлиент("ок_Аналитика_2");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКэшЗначений()

	МетаданныеОбъекта = Метаданные.Документы.ПоступлениеТоваровУслуг;
	фКэшЗначений = Новый Структура;
			
	// Данные по дополнительным аналитикам
	фКэшЗначений.Вставить("НастройкиИзмерений"	  , бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_НастройкиДополнительныхИзмерений"));
	фКэшЗначений.Вставить("ИзмеренияДоп"      	  , бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Дополнительные", "Синоним"));
	
	// Произвольные измерения
	фКэшЗначений.Вставить("Измерения"         	  , бит_Бюджетирование.ПолучитьИзмеренияБюджетирования("Произвольные", "Синоним"));
	
	фКэшЗначений.Вставить("АналитикаНомерЗаявки"  , ПланыВидовХарактеристик.бит_ВидыДополнительныхАналитик.НайтиПоКоду("NЗАЯВКИ"));
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда 
		
		пНомерЗаявки = бит_МеханизмДопИзмерений.ПолучитьЗначениеДопАналитики(Объект.Ссылка,
			фКэшЗначений.АналитикаНомерЗаявки, ПредопределенноеЗначение("Документ.бит_ФормаВводаБюджета.ПустаяСсылка"));
		Если ЗначениеЗаполнено(пНомерЗаявки) Тогда 	
			фКэшЗначений.Вставить("НомерЗаявки",пНомерЗаявки);
		КонецЕсли;	
		  	
	КонецЕсли;
	
	ЭтотОбъект.фКэшЗначений = фКэшЗначений;
	
КонецПроцедуры // ЗаполнитьКэшЗначений()

&НаКлиенте
Процедура ЗаполнитьУправленческиеАналитикиИзЗаявкиНаДоговорЗаписатьИПродолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		Если Записать() Тогда
			ОткрытьФормуВыбораАналитикиИзЗаявкиНаДоговор();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораАналитикиИзЗаявкиНаДоговор()
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(Объект.ДатаВходящегоДокумента) Тогда
		
		ПараметрыОткрытияФормы.Вставить("ДатаНачала"	, НачалоМесяца(Объект.ДатаВходящегоДокумента));
		ПараметрыОткрытияФормы.Вставить("ДатаОкончания"	, КонецМесяца(Объект.ДатаВходящегоДокумента));
		
	ИначеЕсли ЗначениеЗаполнено(Объект.Дата) Тогда
		
		ПараметрыОткрытияФормы.Вставить("ДатаНачала"	, НачалоМесяца(Объект.Дата));
		ПараметрыОткрытияФормы.Вставить("ДатаОкончания"	, КонецМесяца(Объект.Дата));
		
	Иначе
		
		ПараметрыОткрытияФормы.Вставить("ДатаНачала"	, НачалоМесяца(ТекущаяДата()));
		ПараметрыОткрытияФормы.Вставить("ДатаОкончания"	, КонецМесяца(ТекущаяДата()));
		
	КонецЕсли;
	
	Отбор = Новый Структура;
	
	Если ПустаяСтрока(Объект.ОК_ID_Разноска) Тогда
		
		Отбор.Вставить("Проведен"			, Истина);
		Отбор.Вставить("Контрагент"			, Объект.Контрагент);
		Отбор.Вставить("ДоговорКонтрагента"	, Объект.ДоговорКонтрагента);
		
	Иначе
		Отбор.Вставить("ID", Объект.ОК_ID_Разноска);
	КонецЕсли;
	
	ПараметрыОткрытияФормы.Вставить("Отбор"				, Отбор);
	ПараметрыОткрытияФормы.Вставить("РежимВыбора"		, Истина);
	ПараметрыОткрытияФормы.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытияФормы.Вставить("ЗакрыватьПриВыборе", Истина);
	
	МассивДополнительныхКолонок = Новый Массив;
	МассивДополнительныхКолонок.Добавить("СтавкаНДС");
	
	ПараметрыОткрытияФормы.Вставить("МассивДополнительныхКолонок", МассивДополнительныхКолонок);
	
	СценарийОбработкиВыбора = "СЗаполнениемАналитик";
	
	ОткрытьФорму("Документ.рс_ЗаявкаНаДоговор.Форма.ФормаВыбораУправляемая_Дерево", 
						ПараметрыОткрытияФормы, 
						ЭтотОбъект, 
						УникальныйИдентификатор,
						,
						,
						Новый ОписаниеОповещения("ОбработатьВыборЗаявкиНаДоговор", ЭтотОбъект, СценарийОбработкиВыбора));
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборЗаявкиНаДоговор(РезультатЗакрытия, СценарийОбработкиВыбора) Экспорт
	
	Если РезультатЗакрытия = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(РезультатЗакрытия) Тогда
		Возврат;
	КонецЕсли; 
	
	ВыбраннаяЗаявкаНаДоговор = Неопределено;
	
	ВычислитьВыбраннуюЗаявкуНаДоговор(РезультатЗакрытия, ВыбраннаяЗаявкаНаДоговор);
	
	Если ВыбраннаяЗаявкаНаДоговор = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("СценарийОбработкиВыбора"	, СценарийОбработкиВыбора);
	ПараметрыЗаполнения.Вставить("ВыбраннаяЗаявкаНаДоговор"	, ВыбраннаяЗаявкаНаДоговор);
	
	ОбработатьВыборЗаявкиНаДоговорНаСервере(ПараметрыЗаполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВычислитьВыбраннуюЗаявкуНаДоговор(РезультатВыбора, ВыбраннаяЗаявкаНаДоговор)
	
	Если ТипЗнч(РезультатВыбора) = Тип("КлючСтрокиДинамическогоСписка") Тогда
		
		Для каждого КлючЗначение Из РезультатВыбора Цикл
			
			Если КлючЗначение.Ключ = "Ссылка"
				И ТипЗнч(КлючЗначение.Значение) = Тип("ДокументСсылка.рс_ЗаявкаНаДоговор") Тогда
			
				ВыбраннаяЗаявкаНаДоговор = КлючЗначение.Значение;
			
			КонецЕсли;
		
		КонецЦикла; 
		
	ИначеЕсли ТипЗнч(РезультатВыбора) = Тип("СтрокаГруппировкиДинамическогоСписка") Тогда
		
		Если РезультатВыбора.ИмяГруппировки = "Ссылка"
			И ТипЗнч(РезультатВыбора.Ключ) = Тип("ДокументСсылка.рс_ЗаявкаНаДоговор") Тогда
		
			ВыбраннаяЗаявкаНаДоговор = РезультатВыбора.Ключ;
			
		ИначеЕсли РезультатВыбора.РодительскаяГруппировка <> Неопределено Тогда 
			
			ВычислитьВыбраннуюЗаявкуНаДоговор(РезультатВыбора.РодительскаяГруппировка, ВыбраннаяЗаявкаНаДоговор);
			Возврат;
			
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(РезультатВыбора) = Тип("ДокументСсылка.рс_ЗаявкаНаДоговор") Тогда
		
		ВыбраннаяЗаявкаНаДоговор = РезультатВыбора;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборЗаявкиНаДоговорНаСервере(ПараметрыЗаполнения)
	
	Если ПараметрыЗаполнения.СценарийОбработкиВыбора = "СЗаполнениемАналитик" Тогда
		
		Модифицированность = Истина;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	НАЧАЛОПЕРИОДА(рс_ЗаявкаНаДоговорБаза.Период, МЕСЯЦ) КАК ок_Период,
		|	рс_ЗаявкаНаДоговорБаза.СтатьяОборотов КАК ок_СтатьяОборотов,
		|	рс_ЗаявкаНаДоговорБаза.ФункцияЦФО КАК ок_ЦФО,
		|	рс_ЗаявкаНаДоговорБаза.Объект КАК ок_Аналитика_2,
		|	рс_ЗаявкаНаДоговорБаза.Ссылка.СтавкаНДС КАК СтавкаНДС,
		|	рс_ЗаявкаНаДоговорБаза.СуммаБезНДС КАК Сумма,
		|	рс_ЗаявкаНаДоговорБаза.Ссылка.ID КАК ОК_ID_Разноска
		|ИЗ
		|	Документ.рс_ЗаявкаНаДоговор.База КАК рс_ЗаявкаНаДоговорБаза
		|ГДЕ
		|	рс_ЗаявкаНаДоговорБаза.Ссылка = &ВыбраннаяЗаявкаНаДоговор";
		
		Запрос.УстановитьПараметр("ВыбраннаяЗаявкаНаДоговор", ПараметрыЗаполнения.ВыбраннаяЗаявкаНаДоговор);
		
		РезультатЗапроса = Запрос.Выполнить();
		
		ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
		
		СчетчикСтрок = 1;
		
		Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
			
			Объект.ОК_ID_Разноска = ВыборкаДетальныеЗаписи.ОК_ID_Разноска;
			
			Если СчетчикСтрок > Объект.Услуги.Количество() Тогда
				ЗаполняемаяСтрока = Объект.Услуги.Добавить();
			Иначе
				ЗаполняемаяСтрока = Объект.Услуги[СчетчикСтрок - 1];
			КонецЕсли;
			
			ЗаполнитьЗначенияСвойств(ЗаполняемаяСтрока, ВыборкаДетальныеЗаписи);
			
			ЗаполняемаяСтрока.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(ЗаполняемаяСтрока.Сумма, 
																				Объект.СуммаВключаетНДС, 
																				УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(ЗаполняемаяСтрока.СтавкаНДС, ПрименяютсяСтавки4и2));
			ЗаполняемаяСтрока.Всего = ЗаполняемаяСтрока.Сумма + ?(Объект.СуммаВключаетНДС, 0, ЗаполняемаяСтрока.СуммаНДС);
			
			Если СпособУчетаАренды = 0 Тогда
				
				Если ЗначениеЗаполнено(ЗаполняемаяСтрока.ок_СтатьяОборотов) Тогда
					
					Если ЗначениеЗаполнено(ЗаполняемаяСтрока.СчетЗатрат) Тогда
						ЗаполнитьСубконтоПоАналитикеСервер("ок_СтатьяОборотов", ЗаполняемаяСтрока.ок_СтатьяОборотов, ЗаполняемаяСтрока.ПолучитьИдентификатор());
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ЗаполняемаяСтрока.СчетЗатратНУ) Тогда
						ЗаполнитьСубконтоПоАналитикеСервер("ок_СтатьяОборотов", ЗаполняемаяСтрока.ок_СтатьяОборотов, ЗаполняемаяСтрока.ПолучитьИдентификатор(), "НУ");
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗаполняемаяСтрока.ок_ЦФО) Тогда  
					
					Если ЗначениеЗаполнено(ЗаполняемаяСтрока.СчетЗатрат) Тогда
						ЗаполнитьСубконтоПоАналитикеСервер("ок_ЦФО", ЗаполняемаяСтрока.ок_ЦФО, ЗаполняемаяСтрока.ПолучитьИдентификатор());
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ЗаполняемаяСтрока.СчетЗатратНУ) Тогда
						ЗаполнитьСубконтоПоАналитикеСервер("ок_ЦФО", ЗаполняемаяСтрока.ок_ЦФО, ЗаполняемаяСтрока.ПолучитьИдентификатор(), "НУ");	
					КонецЕсли;
					
				КонецЕсли;
				
				Если ЗначениеЗаполнено(ЗаполняемаяСтрока.ок_Аналитика_2) Тогда 
					
					Если ЗначениеЗаполнено(ЗаполняемаяСтрока.СчетЗатрат) Тогда
						ЗаполнитьСубконтоПоАналитикеСервер("ок_Аналитика_2", ЗаполняемаяСтрока.ок_Аналитика_2, ЗаполняемаяСтрока.ПолучитьИдентификатор());
					КонецЕсли;
					
					Если ЗначениеЗаполнено(ЗаполняемаяСтрока.СчетЗатратНУ) Тогда
						ЗаполнитьСубконтоПоАналитикеСервер("ок_Аналитика_2", ЗаполняемаяСтрока.ок_Аналитика_2, ЗаполняемаяСтрока.ПолучитьИдентификатор(), "НУ");
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			СчетчикСтрок = СчетчикСтрок + 1;
			
		КонецЦикла;
		
	ИначеЕсли ПараметрыЗаполнения.СценарийОбработкиВыбора = "БезЗаполненияАналитик" Тогда
		
		Модифицированность = Истина;
		Объект.ОК_ID_Разноска = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЗаполнения.ВыбраннаяЗаявкаНаДоговор, "ID");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСубконтоПоАналитикеКлиент(ИмяРеквизитаТЧ)
	
	Если СпособУчетаАренды <> 0 Тогда // только для простой аренды нужно заполнять аналитики
		Возврат;
	КонецЕсли;
	
	ЗначениеРеквизитаТЧ = Неопределено;
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	Если ТекущиеДанные = Неопределено 
		 ИЛИ (Не ЗначениеЗаполнено(ТекущиеДанные.СчетЗатрат) И Не ЗначениеЗаполнено(ТекущиеДанные.СчетЗатратНУ))
		 ИЛИ (ТекущиеДанные.Свойство(ИмяРеквизитаТЧ, ЗначениеРеквизитаТЧ) И Не ЗначениеЗаполнено(ЗначениеРеквизитаТЧ)) Тогда 
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.СчетЗатрат) Тогда 
		ЗаполнитьСубконтоПоАналитикеСервер(ИмяРеквизитаТЧ, ЗначениеРеквизитаТЧ, ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ТекущиеДанные.СчетЗатратНУ) Тогда 
		ЗаполнитьСубконтоПоАналитикеСервер(ИмяРеквизитаТЧ, ЗначениеРеквизитаТЧ, ТекущиеДанные.ПолучитьИдентификатор(), "НУ");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСубконтоПоАналитикеСервер(ИмяРеквизитаТЧ, ЗначениеРеквизитаТЧ, ИДСтроки, БУНУ = "")
	
	ТекДанные = Объект.Услуги.НайтиПоИдентификатору(ИДСтроки);
	
	Если ТекДанные = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	пИндексСубконто = 1;
	
	Пока пИндексСубконто <=3 Цикл 
		
		Если ТипЗнч(ТекДанные["Субконто" + БУНУ+ пИндексСубконто]) = ТипЗнч(ЗначениеРеквизитаТЧ) Тогда 
			
			Если ИмяРеквизитаТЧ = "ок_Аналитика_2" Тогда
				
				Если Не ЗначениеЗаполнено(ЗначениеРеквизитаТЧ) Тогда 
					ТекДанные["Субконто" + БУНУ + пИндексСубконто] = Неопределено;
				Иначе
					ТекДанные["Субконто" + БУНУ + пИндексСубконто] = ОК_ОбщегоНазначения.ОпределитьОбъектСтроительстваОтносительноАналитикиБК(ТекДанные["Субконто" + БУНУ + пИндексСубконто], ЗначениеРеквизитаТЧ);
				КонецЕсли;
				
			ИначеЕсли ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.бит_СтатьиОборотов") 
					И (ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.СтатьиЗатрат") 
						ИЛИ ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) Тогда
				
				ТекДанные["Субконто" + БУНУ + пИндексСубконто] = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(ЗначениеРеквизитаТЧ, ЭтотОбъект.фКэшЗначений, ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
				Прервать;
				
			Иначе
				
				ТекДанные["Субконто" + БУНУ + пИндексСубконто] = ЗначениеРеквизитаТЧ;
				Прервать;
				
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(ЗначениеРеквизитаТЧ) = Тип("СправочникСсылка.бит_СтатьиОборотов")
				И (ТипЗнч(ТекДанные["Субконто" + БУНУ + пИндексСубконто]) = Тип("СправочникСсылка.СтатьиЗатрат")
					ИЛИ ТипЗнч(ТекДанные["Субконто" + БУНУ + пИндексСубконто]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы")) Тогда
			
			ТекДанные["Субконто" + БУНУ + пИндексСубконто] = Документы.ПоступлениеТоваровУслуг.ПолучитьСтатьюРеглПоСтатьеОборотов(ЗначениеРеквизитаТЧ, ЭтотОбъект.фКэшЗначений, ТипЗнч(ТекДанные["Субконто" + БУНУ + пИндексСубконто]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы"));
			Прервать;
			
		КонецЕсли;
		
		пИндексСубконто = пИндексСубконто + 1;
		
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПериодВУслугахЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда 
		
		НачалоПериода = НачалоМесяца(Объект.ДатаВходящегоДокумента);
		
		Для Каждого Строка Из Объект.Услуги Цикл 
			Строка.ок_Период = НачалоПериода;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-26 (#4407)

ЭтоНоваяСтрока = Ложь;
