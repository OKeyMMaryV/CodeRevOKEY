#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

// Признак для передачи в счет-фактуру для того, чтобы не устанавливать статус повторно.
Перем УстановленСтатусДокумента; 

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
	НомерВходящегоДокумента = "";
	ДатаВходящегоДокумента  = '00010101';

	ЗачетАвансов.Очистить();
	
	СтруктураКурсаВзаиморасчетов = РаботаСКурсамиВалют.ПолучитьКурсВалюты(
	ВалютаДокумента, Дата);
	
	КурсВзаиморасчетов      = СтруктураКурсаВзаиморасчетов.Курс;
	КратностьВзаиморасчетов = СтруктураКурсаВзаиморасчетов.Кратность;
	
	СчетНаОплатуПоставщика = Документы.СчетНаОплатуПоставщика.ПустаяСсылка();
	УдалитьТТНВходящаяЕГАИС = Документы.ТТНВходящаяЕГАИС.ПустаяСсылка();
	
	//ОК Довбешка Т. 13.08.2013
	ОК_ID_Разноска = "";
	ОК_РазнесениеОборотов = Документы.УдалитьИжтиси_бит_БК_РазнесениеОборотов.ПустаяСсылка();
	//Довбешка Т.
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-12 (#3048)		
	//// BIT AMerkulov 17-08-2015 --
	//Для Каждого Стр Из Товары Цикл
	//	
	//	Стр.Период 			= Неопределено;	
	//	Стр.ЦФО 			= Неопределено;	
	//	Стр.Объект 			= Неопределено;	
	//	Стр.СтатьяОборотов 	= Неопределено;	
	//КонецЦикла;
	//
	//Для Каждого Стр Из Услуги Цикл
	//	Стр.Период 			= Неопределено;	
	//	Стр.ЦФО 			= Неопределено;	
	//	Стр.Объект 			= Неопределено;	
	//	Стр.СтатьяОборотов 	= Неопределено;	
	//КонецЦикла;
	// BIT AMerkulov 17-08-2015 --
	//Заменено на:
	пИменаТЧ = "Оборудование,ОбъектыСтроительства,Товары,Услуги,АгентскиеУслуги";
	
	//ОКЕЙ Вдовиченко Г.В(СофтЛаб) Начало 2019-08-05 (#3364)
	Если Не ок_ОбменСКонтрагентамиВнутренний.ПолучитьПризнакОчисткиДанныхФВБВУчетномДокументеПриКопировании(ОбъектКопирования) Тогда
		пИменаТЧ = "";
	КонецЕсли;	
	//ОКЕЙ Вдовиченко Г.В(СофтЛаб) Конец 2019-08-05 (#3364)
	
	мИменаТЧ = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(пИменаТЧ);
	Для Каждого пИмяТЧ Из мИменаТЧ Цикл 
		Для Каждого Стр Из ЭтотОбъект[пИмяТЧ] Цикл
			Стр.ок_Период 			= Неопределено;	
			// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-27 (#4407)
			//Стр.ок_ЦФО 				= Неопределено;	
			//Стр.ок_Проект			= Неопределено;	
			//Стр.ок_Аналитика_2 		= Неопределено;	
			Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды Тогда
				Стр.ок_ЦФО 				= Неопределено;
				Стр.ок_Проект			= Неопределено;
				Стр.ок_Аналитика_2 		= Неопределено;
			КонецЕсли;
			// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-27 (#4407)
			
			// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-08-16 (#4289)
			//Стр.ок_СтатьяОборотов 	= Неопределено;			
			Если пИмяТЧ <> "Услуги" 
				И пИмяТЧ <> "АгентскиеУслуги" 
				И пИмяТЧ <> "Товары" Тогда
				Стр.ок_СтатьяОборотов = Неопределено;	
			КонецЕсли;
			// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-08-16 (#4289)
		КонецЦикла;
	КонецЦикла;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-12 (#3048)	
	
	Если ОбъектКопирования.ЕстьМаркируемаяПродукцияГИСМ Тогда
		Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "КиЗ_ГИСМ");
	КонецЕсли;
	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214) 
	ВедетсяУчетПрослеживаемыхТоваров = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);
		
	СведенияПрослеживаемости.Очистить();
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
	ШтрихкодыУпаковок.Очистить();
	
	РаботаСНоменклатурой.ОбновитьСодержаниеУслуг(Услуги, Дата);
	РаботаСНоменклатурой.ОбновитьСодержаниеУслуг(АгентскиеУслуги, Дата);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	РаботаСНоменклатурой.ОбновитьПризнакПрослеживаемости(Товары, ВедетсяУчетПрослеживаемыхТоваров);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
	РаботаСНоменклатурой.ОбновитьПризнакПрослеживаемости(ОсновныеСредства, ВедетсяУчетПрослеживаемыхТоваров, Истина);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405)
	
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2019-10-19 (#3443)
	ок_ВыгруженВAXAPTA = Ложь;
	ок_ВыгруженВAXAPTA_Актуальный = Ложь;
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2019-10-19 (#3443)
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-04 (#4384)
	ок_НомерЗаявки = Неопределено;
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-04 (#4384)
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	Если ДанныеЗаполнения <> Неопределено Тогда
		
		Если ТипДанныхЗаполнения <> Тип("Структура")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения;
		ИначеЕсли ТипДанныхЗаполнения = Тип("Структура")
			И ДанныеЗаполнения.Свойство("Основание")
			И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Основание.Метаданные()) Тогда
			ДокументОснование = ДанныеЗаполнения.Основание;
		КонецЕсли;

		Если ДокументОснование <> Неопределено Тогда
			ЗаполнитьПоДокументуОснованию(ДокументОснование);
		Иначе
			СуммаВключаетНДС = Ложь;
		КонецЕсли;
	Иначе
		СуммаВключаетНДС = Ложь;
	КонецЕсли;
	
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения, Истина);
	
	//	{Формирование реестра разрешенных оплат по заявкам #3417 СофтЛаб Цветков А. 18.09.2019 
	СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.НеЗачитывать;
	//	}Формирование реестра разрешенных оплат по заявкам #3417
	
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-19 (#4407)
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды Тогда 
		ВедетсяУчетПредметовАренды = Истина;
	КонецЕсли;
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-19 (#4407)
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
	РаботаСДоговорамиКонтрагентовБП.ЗаполнитьДоговорПередЗаписью(ЭтотОбъект);
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	ДанныеОТипеСклада      = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ТипСклада");
	
	ЭтоКомиссия = ЗначениеЗаполнено(ДоговорКонтрагента)
		И ВидДоговораКонтрагента = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ВозможнаКомиссияПоЗакупке = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
		И НЕ ЭтоКомиссия
		И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов");
		
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	ЭтоВвозИзЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
	НеИспользуемыеТабличныеЧасти = Документы.ПоступлениеТоваровУслуг.НеИспользуемыеТабличныеЧасти(
		ВидОперации,
		ЭтоКомиссия,
		ВозможнаКомиссияПоЗакупке);
		
	ОбщегоНазначенияБП.ОчиститьНеиспользуемыеТабличныеЧасти(ЭтотОбъект, НеИспользуемыеТабличныеЧасти);
	
	СпособОценкиТоваровВРознице = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата);

	РозницаВПродажныхЦенах = 
		(ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование)
		И СпособОценкиТоваровВРознице = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости
		И ДанныеОТипеСклада <> Перечисления.ТипыСкладов.ОптовыйСклад
		И НЕ ЭтоКомиссия;

	Если РозницаВПродажныхЦенах Тогда
		Для Каждого Строка Из Товары Цикл
			Если ДанныеОТипеСклада = Перечисления.ТипыСкладов.РозничныйМагазин Тогда
				Строка.СчетУчета    = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ;
				Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
			Иначе
				Строка.СчетУчета = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
				Строка.СчетУчетаНДС = ПланыСчетов.Хозрасчетный.НДСпоПриобретеннымМПЗ;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЭтоКомиссия И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов") Тогда
		Для Каждого Строка Из Товары Цикл
			Строка.Контрагент = Справочники.Контрагенты.ПустаяСсылка();
			Строка.ДоговорКонтрагента = Справочники.ДоговорыКонтрагентов.ПустаяСсылка();
			Строка.СчетРасчетов = ПланыСчетов.Хозрасчетный.ПустаяСсылка();
		КонецЦикла;
	КонецЕсли;

	Если УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата) Тогда
		НДСВключенВСтоимость = Ложь;
	КонецЕсли;
	
	// При групповом перепроведении реквизиты документов не меняются,
	// поэтому обновление связанных данных выполнять не требуется.
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	СчетаУчетаВДокументах.ЗаполнитьПередЗаписью(ЭтотОбъект, РежимЗаписи);
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект);

	ДоговорУказан = ЗначениеЗаполнено(ДоговорКонтрагента);
	Если ДоговорУказан Тогда
		РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "ВидДоговора, УчетАгентскогоНДС, ВидАгентскогоДоговора");
		ЭтоКомиссия       = РеквизитыДоговора.ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
		УчетАгентскогоНДС = РеквизитыДоговора.УчетАгентскогоНДС;
		НДСИсчисляетсяНалоговымАгентом = 
			РеквизитыДоговора.ВидАгентскогоДоговора = Перечисления.ВидыАгентскихДоговоров.РеализацияТоваров
			И БухгалтерскийУчетПереопределяемый.ИспользуетсяОбратноеНачислениеНДС();
	Иначе
		ЭтоКомиссия       = Ложь;
		УчетАгентскогоНДС = Ложь;
		НДСИсчисляетсяНалоговымАгентом = Ложь;
	КонецЕсли;
		
	ТребуетсяСчетФактура = НЕ УчетАгентскогоНДС И НЕ ЭтоКомиссия 
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку
		ИЛИ НДСИсчисляетсяНалоговымАгентом;
	
	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("ПометкаУдаления", ЭтотОбъект, "СчетФактураПолученный");
	Если Не ТребуетсяСчетФактура Тогда 	
		ПараметрыДействия.СостояниеФлага = Истина;
	КонецЕсли;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);
	
	Документы.КорректировкаПоступления.ОбновитьРеквизитыСвязанныхДокументовКорректировки(ЭтотОбъект, Отказ);
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВедетсяУчетПредметовАренды
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		Для Каждого Строка Из Услуги Цикл
			Строка.Количество = 0;
			Строка.Цена = Строка.Сумма;
			Строка.ПодразделениеЗатрат = ПодразделениеОрганизации;
			Строка.Субконто1 = Контрагент;
			Строка.Субконто2 = ДоговорКонтрагента;
			Строка.СчетЗатратНУ = Строка.СчетЗатрат;
			Строка.СубконтоНУ1 = Контрагент;
			Строка.СубконтоНУ2 = ДоговорКонтрагента;
		КонецЦикла;
	КонецЕсли;
	
	ИнтеграцияГИСМБП.УстановитьПризнакЕстьМаркируемаяПродукцияГИСМ(ЭтотОбъект);
	
	УчетОС.УстановитьАмортизационнуюГруппаОС(
		ЭтотОбъект.ОсновныеСредства.Выгрузить( ,"ОсновноеСредство, СрокПолезногоИспользования"));
	
	Если (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков)
		И БухгалтерскийУчетПереопределяемый.ВестиУчетПоПодразделениям() Тогда
		ПодразделениеОрганизации = МестонахождениеОС;
	КонецЕсли;
	
	//ОК 03.07.2019 #3377
	//Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги 
	//	И Справочники.Склады.ИспользуетсяНесколькоСкладов() Тогда
	//	
	//	Склад = Справочники.Склады.ПустаяСсылка();
	//КонецЕсли; 
	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
	Для Каждого ИмяТабличнойЧасти Из СтрРазделить("Товары,Услуги,АгентскиеУслуги,Оборудование,ОсновныеСредства", ",") Цикл
		Для Каждого СтрокаТабличнойЧасти Из ЭтотОбъект[ИмяТабличнойЧасти] Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.ИдентификаторСтроки) Тогда
				СтрокаТабличнойЧасти.ИдентификаторСтроки = Новый УникальныйИдентификатор;
			КонецЕсли; 
		КонецЦикла;
	КонецЦикла;
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405)
	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	ПрослеживаемыйТовар = Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОборудование = Оборудование.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
	ПрослеживаемоеОС = ОсновныеСредства.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405)
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
		Или ПрослеживаемоеОС.Количество() <> 0
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405)
		ИЛИ ПрослеживаемоеОборудование.Количество() <> 0;
	Если НЕ ЕстьПрослеживаемыйТовар 
		ИЛИ ЭтоВвозИзЕАЭС Тогда
		СведенияПрослеживаемости.Очистить();
	КонецЕсли;
	// Дополнительно очистим номера ТД для прослеживаемого товара.
	Если НЕ ЭтоВвозИзЕАЭС Тогда
		Для Каждого СтрокаСТоваром ИЗ ПрослеживаемыйТовар Цикл
			СтрокаСТоваром.НомерГТД = Неопределено;
		КонецЦикла;
		Для Каждого СтрокаСОборудованием ИЗ ПрослеживаемоеОборудование Цикл
			СтрокаСОборудованием.НомерГТД = Неопределено;
		КонецЦикла;
	КонецЕсли;
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка  Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьСтатусДокумента();
	
	Если ДополнительныеСвойства.РежимЗаписи = РежимЗаписиДокумента.Запись Тогда
		УчетНДСПереопределяемый.СинхронизироватьРеквизитыСчетаФактурыПолученного(ЭтотОбъект, НЕ УстановленСтатусДокумента);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
	
	Если ПроверяемыеРеквизиты.Найти("Склад") = Неопределено Тогда
		ПроверяемыеРеквизиты.Добавить("Склад");
	КонецЕсли;
	
	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	ДанныеОТипеСклада      = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Склад, "ТипСклада");
	ВестиУчетПоДоговорам   = ПолучитьФункциональнуюОпцию("ВестиУчетПоДоговорам");
	ЭтоКомиссия = ВестиУчетПоДоговорам И ЗначениеЗаполнено(ДоговорКонтрагента)
		И ВидДоговораКонтрагента = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	ВозможнаКомиссияПоЗакупке = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
		И НЕ ЭтоКомиссия
		И ПолучитьФункциональнуюОпцию("ОсуществляетсяЗакупкаТоваровУслугДляКомитентов");
		
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	ЭтоВвозИзЕАЭС = Справочники.Контрагенты.КонтрагентРезидентТаможенногоСоюза(Контрагент);
	ПрослеживаемыйТовар = Товары.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ПрослеживаемоеОборудование = Оборудование.НайтиСтроки(Новый Структура("ПрослеживаемыйТовар", Истина));
	ЕстьПрослеживаемыйТовар = ПрослеживаемыйТовар.Количество() <> 0
		ИЛИ ПрослеживаемоеОборудование.Количество() <> 0;
	КонтролироватьРеквизитыВходящегоДокумента = ЭтоВвозИзЕАЭС И ЕстьПрослеживаемыйТовар;	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
	РаздельныйУчетНДСНаСчете19 = УчетнаяПолитика.РаздельныйУчетНДСНаСчете19(Организация, Дата);
	
	ПроверятьСчетНДС = НЕ НДСВключенВСтоимость ИЛИ РаздельныйУчетНДСНаСчете19;
	
	МассивНепроверяемыхРеквизитов.Добавить("Оборудование.СтранаПроисхождения");
	МассивНепроверяемыхРеквизитов.Добавить("Товары.СтранаПроисхождения");

	Если НЕ ВестиУчетПоДоговорам Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ДоговорКонтрагента");
	КонецЕсли;
	
	Если НЕ ЭтоУниверсальныйДокумент 
	   И НЕ КонтролироватьРеквизитыВходящегоДокумента Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НомерВходящегоДокумента");
		МассивНепроверяемыхРеквизитов.Добавить("ДатаВходящегоДокумента");
	КонецЕсли;
	
	// Не проверяем заполненность табличных частей (включая реквизиты), 
	// которые не используются при определенных видах операции и будут очищены в ПередЗаписью
	НеИспользуемыеТабличныеЧасти = Документы.ПоступлениеТоваровУслуг.НеИспользуемыеТабличныеЧасти(
		ВидОперации, ЭтоКомиссия, ВозможнаКомиссияПоЗакупке);
	
	ОбщегоНазначенияБП.ИсключитьИзПроверкиНеиспользуемыеТабличныеЧасти(
		ПроверяемыеРеквизиты,
		НеИспользуемыеТабличныеЧасти);
		
	// Документ без данных о поступивших ценностях, соответствующих виду операции,
	// считаем заполненным некорректно.
	
	// Оставим в проверяемых реквизитах только основные табличные части  
	// выбранного вида операции
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия 
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Услуги");
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОсновныеСредства
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства");
	КонецЕсли;
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия Тогда
		// Если заполнен любой из "основных" списков, то проверять оставшиеся не следует
		ОсновныеСписки = Новый Массив();
		ОсновныеСписки.Добавить("Товары");
		ОсновныеСписки.Добавить("Услуги");
		ОсновныеСписки.Добавить("АгентскиеУслуги");
		ОсновныеСписки.Добавить("ВозвратнаяТара");
		
		ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ОсновныеСписки, НеИспользуемыеТабличныеЧасти);
		
		ОбщегоНазначенияБП.ИсключитьИзПроверкиОсновныеТабличныеЧасти(
			ЭтотОбъект, 
			ОсновныеСписки, 
			ПроверяемыеРеквизиты);
		
	КонецЕсли;
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Топливо Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Топливо");
	КонецЕсли;
	
	// Установка значений переменных для дальнейшей проверки
	
	Счет4112 = ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахНТТ;
	РазделятьПоСтавкамНДС = Счет4112.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.СтавкиНДС,
		"ВидСубконто") <> Неопределено;
	
	УчетВПродажныхЦенах = УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата)
		= Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;
	ОптовыйСклад = ДанныеОТипеСклада = Перечисления.ТипыСкладов.ОптовыйСклад;
	СкладНТТ = ДанныеОТипеСклада = Перечисления.ТипыСкладов.НеавтоматизированнаяТорговаяТочка;
	
	РозницаВПродажныхЦенах = УчетВПродажныхЦенах
		И (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
		И НЕ ОптовыйСклад
		И НЕ ЭтоКомиссия;
	
	НТТ = РозницаВПродажныхЦенах И СкладНТТ;
	
	// Проверка реквизитов шапки документа
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		// Объекты строительства не приходуются на склад
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	
	Если Товары.Количество() = 0 И ВозвратнаяТара.Количество() = 0 И Оборудование.Количество() = 0 Тогда
		// Нет материальных ценностей - нечего приходовать на склад
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
	КонецЕсли;
	
	Если ВестиУчетПоДоговорам И ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
		ТекстСообщения = "";
		Если НЕ УчетВзаиморасчетов.ПроверитьВозможностьПроведенияВРеглУчете(
			ЭтотОбъект, ДоговорКонтрагента, ТекстСообщения) Тогда
			ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(, "Корректность",
				НСтр("ru = 'Договор'"),,, ТекстСообщения);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,
				"ДоговорКонтрагента", "Объект", Отказ);
		КонецЕсли;
	КонецЕсли;
	
	// Табличная часть "Основные средства".
	Если НЕ ПроверятьСчетНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СтавкаНДС");
	КонецЕсли;
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПриобретениеЗемельныхУчастков Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СтавкаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СчетУчетаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СпособУчетаНДС");
		МассивНепроверяемыхРеквизитов.Добавить("ОсновныеСредства.СрокПолезногоИспользования");
	КонецЕсли;
	
	// Табличная часть "Оборудование".
	Если НЕ ПроверятьСчетНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Оборудование.СтавкаНДС");
	КонецЕсли;
	
	// Табличная часть "Объекты строительства".
	
	Если НЕ ПроверятьСчетНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ОбъектыСтроительства.СтавкаНДС");
	КонецЕсли;
	
	// Табличная часть "Товары".
	Если НЕ ПроверятьСчетНДС
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтавкаНДС");
	КонецЕсли;
	
	Если НТТ Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Номенклатура");
		МассивНепроверяемыхРеквизитов.Добавить("Товары.Количество");
	Иначе
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СуммаВРознице");
	КонецЕсли;
	
	
	Если НЕ НТТ ИЛИ НЕ РазделятьПоСтавкамНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Товары.СтавкаНДСВРознице");
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("Товары.ДоговорКонтрагента");
	
	Если ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		И ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
		ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, ЭтоВвозИзЕАЭС, "Товары", Отказ);
		ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, ЭтоВвозИзЕАЭС, "Оборудование", Отказ);
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
		ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, Ложь, "ОсновныеСредства", Отказ);
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405)
		
		Для каждого СтрокаТаблицы Из Товары Цикл
			Префикс = "Товары[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
			
			ИмяСписка = НСтр("ru = 'Товары'");
			
			// Проверка договора и счета расчетов с комитентом
			Если ВозможнаКомиссияПоЗакупке И ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
				Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Договор с комитентом'"),
						СтрокаТаблицы.НомерСтроки, ИмяСписка);
					Поле = Префикс + "ДоговорКонтрагента";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Табличная часть "Услуги"
	ПроверятьСтавкуНДС = (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Услуги
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Оборудование
		ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ОбъектыСтроительства)
		И НЕ ЭтоКомиссия
		И (НЕ НДСВключенВСтоимость ИЛИ РаздельныйУчетНДСНаСчете19)
		И НЕ РозницаВПродажныхЦенах;
	
	Если НЕ ПроверятьСтавкуНДС Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Услуги.СтавкаНДС");
	КонецЕсли;
	
	// Табличная часть "Зачет авансов"
	Если СпособЗачетаАвансов <> Перечисления.СпособыЗачетаАвансов.ПоДокументу Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	ИначеЕсли ЗачетАвансов.Количество() = 0 Тогда
		МассивНепроверяемыхРеквизитов.Добавить("ЗачетАвансов");
	
		ТекстСообщения = НСтр("ru = 'Не введено ни одной строки с документом аванса!'");
		Поле = "ПорядокУчетаРасчетов";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , Поле, Отказ);
	КонецЕсли;
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-16 (#3048)
	пНомерЗаявки = ПолучитьНомерЗаявки();
		
	пИменаНеобходимыхТаблиц = "Товары,Услуги,Оборудование,ОбъектыСтроительства,АгентскиеУслуги";
	мИменаТаблиц = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(пИменаНеобходимыхТаблиц,",");
	
	Если НЕ ЗначениеЗаполнено(пНомерЗаявки) Тогда 
				
		пИменаРеквизитовТЧ = "ок_Период,ок_ЦФО,ок_СтатьяОборотов,ок_Аналитика_2";
		мИменаРеквизитовТЧ = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(пИменаРеквизитовТЧ,",");			
				
		Для каждого пИмяТаблицы Из мИменаТаблиц Цикл
			
			Для Каждого стрТЧ Из ЭтотОбъект[пИмяТаблицы] Цикл 										
				Для Каждого пИмяРеквизитаТЧ Из мИменаРеквизитовТЧ Цикл 
					МассивНепроверяемыхРеквизитов.Добавить(пИмяТаблицы + "." + пИмяРеквизитаТЧ);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла; 
	Иначе
		
		пТаблицаПроверки = Новый ТаблицаЗначений;
		пТаблицаПроверки.Колонки.Добавить("ИмяТЧ"			, Новый ОписаниеТипов("Строка"));
		пТаблицаПроверки.Колонки.Добавить("НомерСтроки"		, Новый ОписаниеТипов("Число"));		
		пТаблицаПроверки.Колонки.Добавить("Проект"			, Новый ОписаниеТипов("СправочникСсылка.Проекты"));
		пТаблицаПроверки.Колонки.Добавить("СтатьяОборотов"	, Новый ОписаниеТипов("СправочникСсылка.бит_СтатьиОборотов"));
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-22 (#3141)
		пТаблицаПроверки.Колонки.Добавить("Аналитика_2"		, Новый ОписаниеТипов("СправочникСсылка.ОбъектыСтроительства"));
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-22 (#3141)
		
		Для каждого пИмяТаблицы Из мИменаТаблиц Цикл
			
			Для Каждого стрТЧ Из ЭтотОбъект[пИмяТаблицы] Цикл 										
				
				пСтрокаПроверки = пТаблицаПроверки.Добавить();
				
				пСтрокаПроверки.ИмяТЧ 		   = пИмяТаблицы;
				пСтрокаПроверки.НомерСтроки    = стрТЧ.НомерСтроки;
				пСтрокаПроверки.Проект		   = стрТЧ.ок_Проект;
				пСтрокаПроверки.СтатьяОборотов = стрТЧ.ок_СтатьяОборотов;
				
				//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-22 (#3141)
				пСтрокаПроверки.Аналитика_2 = стрТЧ.ок_Аналитика_2;
				//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-22 (#3141)
				
			КонецЦикла;
			
		КонецЦикла;
		
		// ОКЕЙ Сафронов А.А. (СофтЛаб) Начало 2021-10-19 (#4390)
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-22 (#3141)
		//пМВрТ = Новый МенеджерВременныхТаблиц;		
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-22 (#3141)
		//
		//пЗапросПроверкиПоПроектам = Новый Запрос("ВЫБРАТЬ
		//|	Выразить(ТаблицаПроверки.ИмяТЧ КАК Строка(50)) КАК ИмяТЧ,
		//|	ТаблицаПроверки.Проект КАК Проект,
		//|	ТаблицаПроверки.НомерСтроки КАК НомерСтроки,
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-22 (#3141)
		//|	ТаблицаПроверки.Аналитика_2 КАК Аналитика_2,				
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-22 (#3141)
		//|	ВЫРАЗИТЬ(ТаблицаПроверки.СтатьяОборотов КАК Справочник.бит_СтатьиОборотов) КАК СтатьяОборотов
		//|ПОМЕСТИТЬ ВТ0_ИсходныеДанные
		//|ИЗ
		//|	&ТаблицаПроверки КАК ТаблицаПроверки
		//|;
		//|
		//|////////////////////////////////////////////////////////////////////////////////
		//|ВЫБРАТЬ
		//|	ВТ0_ИсходныеДанные.НомерСтроки КАК НомерСтроки,
		//|	ВТ0_ИсходныеДанные.ИмяТЧ КАК ИмяТЧ
		//|ИЗ
		//|	ВТ0_ИсходныеДанные КАК ВТ0_ИсходныеДанные
		//|ГДЕ
		//|	(ВЫРАЗИТЬ(ВТ0_ИсходныеДанные.СтатьяОборотов.ВидСтатьи КАК Справочник.бит_ВидыСтатейОборотов)) <> ЗНАЧЕНИЕ(Справочник.бит_ВидыСтатейОборотов.ИнвестиционнаяДеятельность)
		//|	И ВТ0_ИсходныеДанные.Проект <> ЗНАЧЕНИЕ(Справочник.Проекты.ПустаяСсылка)
		//|
		//|УПОРЯДОЧИТЬ ПО
		//|	ИмяТЧ,
		//|	НомерСтроки
		//|ИТОГИ ПО
		//|	ИмяТЧ");
		//
		//пЗапросПроверкиПоПроектам.УстановитьПараметр("ТаблицаПроверки"	,	пТаблицаПроверки);
		//
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-22 (#3141)
		//пЗапросПроверкиПоПроектам.МенеджерВременныхТаблиц = пМВрТ;
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-22 (#3141)
		//
		//пРезультатПроверки = пЗапросПроверкиПоПроектам.Выполнить();
		//Если НЕ пРезультатПроверки.Пустой() Тогда 
		//	ВыборкаИмяТЧ = пРезультатПроверки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		//	Пока ВыборкаИмяТЧ.Следующий() Цикл 
		//		пНомераСтрокСОшибками = "";
		//		ВыборкаПоСтрокам = ВыборкаИмяТЧ.Выбрать();
		//		Пока ВыборкаПоСтрокам.Следующий() Цикл 
		//			пНомераСтрокСОшибками = пНомераСтрокСОшибками + "," + ВыборкаПоСтрокам.НомерСтроки;
		//		КонецЦикла;
		//		
		//		пТекстОшибки = НСтр("ru = 'В следующих строках таб. части %1 некорректно указан проект (используется только для инвест. статей): %2'");
		//		пТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(пТекстОшибки, СокрЛП(ВыборкаИмяТЧ.ИмяТЧ),
		//		Сред(пНомераСтрокСОшибками,2));
		//		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(пТекстОшибки,,,,Отказ); 																								 
		//	КонецЦикла;
		//КонецЕсли;
		// ОКЕЙ Сафронов А.А. (СофтЛаб) Конец 2021-10-19 (#4390)
		
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-22 (#3141)
		пЗапросПроверкиСоответствиюОбъектов = Новый Запрос;
		// ОКЕЙ Сафронов А.А. (СофтЛаб) Начало 2021-10-19 (#4390)
		//пЗапросПроверкиСоответствиюОбъектов.МенеджерВременныхТаблиц = пМВрТ;
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-02-13 (#3141)
		////пЗапросПроверкиСоответствиюОбъектов.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		////Заменено на:
		//пЗапросПроверкиСоответствиюОбъектов.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		////ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-02-13 (#3141)	
		пЗапросПроверкиСоответствиюОбъектов.УстановитьПараметр("ТаблицаПроверки"	,	пТаблицаПроверки);
		пЗапросПроверкиСоответствиюОбъектов.Текст = "ВЫБРАТЬ
		|	Выразить(ТаблицаПроверки.ИмяТЧ КАК Строка(50)) КАК ИмяТЧ,
		|	ТаблицаПроверки.Проект КАК Проект,
		|	ТаблицаПроверки.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПроверки.Аналитика_2 КАК Аналитика_2,				
		|	ВЫРАЗИТЬ(ТаблицаПроверки.СтатьяОборотов КАК Справочник.бит_СтатьиОборотов) КАК СтатьяОборотов
		|ПОМЕСТИТЬ ВТ0_ИсходныеДанные
		|ИЗ
		|	&ТаблицаПроверки КАК ТаблицаПроверки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|
		// ОКЕЙ Сафронов А.А. (СофтЛаб) Конец 2021-10-19 (#4390)
		|	ВТ0_ИсходныеДанные.НомерСтроки КАК НомерСтроки,
		|	ВТ0_ИсходныеДанные.ИмяТЧ КАК ИмяТЧ
		|ИЗ
		|	ВТ0_ИсходныеДанные КАК ВТ0_ИсходныеДанные
		|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Склады КАК Склады
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-02-13 (#3141)
		//|		ПО ВТ0_ИсходныеДанные.Аналитика_2 = Склады.Объект		
		//Заменено на:
		|		ПО ВТ0_ИсходныеДанные.Аналитика_2 <> Склады.Объект		
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-02-13 (#3141)		
		|ГДЕ
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-02-13 (#3141)
		//|	Склады.Ссылка <> &Склад И
		//Заменено на:
		|	Склады.Ссылка = &Склад И
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-02-13 (#3141)		
		|	ВТ0_ИсходныеДанные.ИмяТЧ <> ""ОбъектыСтроительства""
		|
		|УПОРЯДОЧИТЬ ПО
		|	ИмяТЧ,
		|	НомерСтроки
		|ИТОГИ ПО
		|	ИмяТЧ";
		
		пЗапросПроверкиСоответствиюОбъектов.УстановитьПараметр("Склад"	,	Склад);
		
		пРезультатПроверки = пЗапросПроверкиСоответствиюОбъектов.Выполнить();
		Если НЕ пРезультатПроверки.Пустой() Тогда 
			ВыборкаИмяТЧ = пРезультатПроверки.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
			Пока ВыборкаИмяТЧ.Следующий() Цикл 
				пНомераСтрокСОшибками = "";
				ВыборкаПоСтрокам = ВыборкаИмяТЧ.Выбрать();
				Пока ВыборкаПоСтрокам.Следующий() Цикл 
					пНомераСтрокСОшибками = пНомераСтрокСОшибками + "," + ВыборкаПоСтрокам.НомерСтроки;
				КонецЦикла;
				
				пТекстОшибки = НСтр("ru = 'В следующих строках таб. части %1 некорректно указан объект (не соответствует указанному складу): %2'");
				пТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(пТекстОшибки, СокрЛП(ВыборкаИмяТЧ.ИмяТЧ),
				Сред(пНомераСтрокСОшибками,2));
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(пТекстОшибки,,,,Отказ); 																								 
			КонецЦикла;
		КонецЕсли;
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-22 (#3141)
		
	КонецЕсли;		
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-16 (#3048)	
	
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2021-06-17 (#4231)
	МассивНепроверяемыхРеквизитов = ОбщегоНазначенияБПВызовСервера.УдалитьПовторяющиесяЭлементыМассива(МассивНепроверяемыхРеквизитов);
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2021-06-17 (#4231)
	
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-09-17 (#3758)
	Если МассивНепроверяемыхРеквизитов.Найти("Услуги.ок_СтатьяОборотов") <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Удалить(МассивНепроверяемыхРеквизитов.Найти("Услуги.ок_СтатьяОборотов"));
	КонецЕсли; 
	Если МассивНепроверяемыхРеквизитов.Найти("АгентскиеУслуги.ок_СтатьяОборотов") <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Удалить(МассивНепроверяемыхРеквизитов.Найти("АгентскиеУслуги.ок_СтатьяОборотов"));
	КонецЕсли; 
	Если МассивНепроверяемыхРеквизитов.Найти("Товары.ок_СтатьяОборотов") <> Неопределено Тогда
		МассивНепроверяемыхРеквизитов.Удалить(МассивНепроверяемыхРеквизитов.Найти("Товары.ок_СтатьяОборотов"));
	КонецЕсли; 
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-09-17 (#3758)
	
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Начало 2021-10-13 (#4407)
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиАренды
		Или ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.УслугиЛизинга Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Услуги.ок_СтатьяОборотов");
	КонецЕсли;
	// ОКЕЙ Смирнов М.В. (СофтЛаб) Конец 2021-10-13 (#4407)
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	УправлениеВнеоборотнымиАктивами.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОсновныеСредства", Новый Структура("ОсновноеСредство"), Отказ);
	
	РеквизитыЗаСсылками = Документы.ПоступлениеТоваровУслуг.РеквизитыЗаСсылками(ВидОперации);
	
	СчетаУчетаВДокументах.ПроверитьЗаполнение(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	
	ПроверкаЗаполненияДокументов.ПроверитьРеквизитыЗаСсылками(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты, РеквизитыЗаСсылками);
	
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2019-10-19 (#3443)
	ОК_ОбщегоНазначения.ВыполнитьПроверкуОднозначногоСоответствияСтатьиОборотовИСтавкиНДСКСчету(ЭтотОбъект, ПредопределенноеЗначение("ПланСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы"), Отказ);
	//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2019-10-19 (#3443)
	
	// ОКЕЙ Сафронов А.А. (СофтЛаб) Начало 2021-10-19 (#4382)
	ШаблонНезаполненногоРБП = "Не заполнено РБП в строке %1";
	Для Каждого СтрокаУслуги Из Услуги Цикл
		Если ТипЗнч(СтрокаУслуги.Субконто1) = Тип("СправочникСсылка.РасходыБудущихПериодов")
			И Не ЗначениеЗаполнено(СтрокаУслуги.Субконто1) Тогда 
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ШаблонНезаполненногоРБП, СтрокаУслуги.НомерСтроки),,"Объект.Услуги["+(СтрокаУслуги.НомерСтроки-1)+"].Субконто1",,Отказ);	
		КонецЕсли;	
	КонецЦикла;	
	// ОКЕЙ Сафронов А.А. (СофтЛаб) Конец 2021-10-19 (#4382)
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	ПроверитьКорректностьЗаполненияТабличнойЧастиТовары(Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	//{ bit SVKushnirenko Bit 27.01.2017 #2657
	ЭтотОбъект.Движения.бит_КонтрольныеЗначенияБюджетов.ДополнительныеСвойства.Вставить("бит_БК_УстановкаСтатусаПослеПроведения", Истина);
	//} bit SVKushnirenko Bit 27.01.2017 #2657	

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА

	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ПоступлениеТоваровУслуг.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	// BIT AMerkulov 17-08-2015 ++
	Если НЕ Отказ Тогда
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-12-24 (#3118)
		//Если Услуги.Количество() И бит_ДозаполненоПоУправленческимАналитикам Тогда
		//Заменено на:
		НомерЗаявки = ПолучитьНомерЗаявки();
		Если Услуги.Количество() > 0
			  И ЗначениеЗаполнено(НомерЗаявки)
		Тогда
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-12-24 (#3118)
		
			ПроверитьДокументПоУправленческимАналитикам(Отказ);
		КонецЕсли;
	КонецЕсли;
	// BIT AMerkulov 17-08-2015 --	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ИНФОРМАЦИОННОЙ БАЗЫ (АНАЛИЗ ОСТАТКОВ И Т.П.)

	// Таблица взаиморасчетов
	ТаблицаВзаиморасчеты = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
		ПараметрыПроведения.ТаблицаЗачетАвансов, ПараметрыПроведения.ЗачетАвансовРеквизиты, Отказ);

	// Таблицы документа с корректировкой сумм по курсу авансов
	СтруктураТаблицДокумента = УчетДоходовРасходов.ПодготовитьТаблицыПоступленияПоКурсуАвансов(ПараметрыПроведения.СтруктураТаблицДокумента,
		ТаблицаВзаиморасчеты, ПараметрыПроведения.ЗачетАвансовРеквизиты);
		
	ТаблицыНДС = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицыНДС(СтруктураТаблицДокумента);
	
	// Товары и услуги, закупленные для комитентов
	ТаблицаТоварыУслугиКомиссияПоЗакупке = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицуТоварыУслугиКомиссияПоЗакупке(
		СтруктураТаблицДокумента.ТаблицаТоварыУслугиКомиссияПоЗакупке, 
		ПараметрыПроведения.ЗачетАвансовКомиссияПоЗакупкеРеквизиты,
		ПараметрыПроведения.Реквизиты);

	// Взаиморасчеты с комитентами по договорам комиссии на закупку
	ТаблицаЗачетАвансовКомиссияПоЗакупке = Документы.ПоступлениеТоваровУслуг.ПодготовитьТаблицуЗачетАвансовКомиссияПоЗакупке(
		ТаблицаТоварыУслугиКомиссияПоЗакупке);
	ТаблицаВзаиморасчетыКомиссияПоЗакупке = УчетВзаиморасчетов.ПодготовитьТаблицуВзаиморасчетовЗачетАвансов(
		ТаблицаЗачетАвансовКомиссияПоЗакупке, Неопределено, ПараметрыПроведения.ЗачетАвансовКомиссияПоЗакупкеРеквизиты, Отказ);

	// Структура таблиц для отражения в налоговом учете УСН
	СтруктураТаблицУСН = Новый Структура("ТаблицаРасчетов", ТаблицаВзаиморасчеты);
	
	// Учет доходов и расходов ИП
	ТаблицаТоваровИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаТоваров,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаУслугИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаУслуг,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОборудованияИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаОборудования,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОсновныеСредстваИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаОсновныеСредства,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаПоступленияОСИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияОС(
		ТаблицаОсновныеСредстваИП,
		ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ИПРеквизиты);
		
	ТаблицаОплатыОСИП = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуОплатыОСиНМА(
		Новый Структура("МПЗ", ТаблицаПоступленияОСИП), 
		ПараметрыПроведения.Реквизиты);
		
	ТаблицаПрочееИП  = УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицуПоступленияМПЗ(
		ПараметрыПроведения.ИПТаблицаПрочее,
		ПараметрыПроведения.ИПРеквизиты);
	
	СтруктураТаблицМПЗ = Новый Структура;
	СтруктураТаблицМПЗ.Вставить("ТаблицаТоваров",      ТаблицаТоваровИП);
	СтруктураТаблицМПЗ.Вставить("ТаблицаУслуг",        ТаблицаУслугИП);
	СтруктураТаблицМПЗ.Вставить("ТаблицаОборудования", ТаблицаОборудованияИП);
	СтруктураТаблицМПЗ.Вставить("ТаблицаПрочее",       ТаблицаПрочееИП);
	
	ТаблицаПрочихРасчетов = УчетВзаиморасчетов.ПустаяТаблицаПоПрочимРасчетам();
	
	ТаблицыПоступленияОтФакторинговойКомпанииИП =
		УчетДоходовИРасходовПредпринимателя.ПодготовитьТаблицыПоступленияОтФакторинговойКомпании(
			ПараметрыПроведения.ЗачетАвансовТаблицаДокумента,
			ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
	
	Если ЗначениеЗаполнено(ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаПрочиеРасчеты) Тогда
		ОбщегоНазначенияБПВызовСервера.ЗагрузитьВТаблицуЗначений(
			ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаПрочиеРасчеты, ТаблицаПрочихРасчетов);
	КонецЕсли;
		
	Если НЕ ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		ТаблицаСтатусовСчетов = СтатусыДокументов.ПодготовитьТаблицуСтатусовПоступленияПоСчетам(
			ПараметрыПроведения.ПоступлениеТоваровУслугПоСчету,
			ПараметрыПроведения.ПоступлениеВозвратнойТарыПоСчету,
			ПараметрыПроведения.Реквизиты);
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	
	// Алгоритмы формирования проводок этого документа учитывают малоценку
	Движения.Хозрасчетный.ДополнительныеСвойства.Вставить("ДвиженияПоМалоценнымОбъектамСформированы", Истина);
	
	// Зачет аванса
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(ТаблицаВзаиморасчеты,
		ПараметрыПроведения.ЗачетАвансовРеквизиты, Движения, Отказ);

	// Поступление товаров
	УчетТоваров.СформироватьДвиженияПоступлениеТоваров(СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетТоваров.СформироватьДвиженияНачислениеТорговойНаценкиПоступлениеТоваров(СтруктураТаблицДокумента.ТаблицаТовары,
		ПараметрыПроведения.ПоступлениеТоваровТорговаяНаценкаРеквизиты, Движения, Отказ);

	// Поступление топлива
	УчетТоваров.СформироватьДвиженияПоступлениеПоТопливнымКартам(СтруктураТаблицДокумента.ТаблицаТопливо,
		ПараметрыПроведения.Реквизиты, Движения);
	
	// Поступление оборудования
	УчетОС.СформироватьДвиженияПоступлениеОборудования(СтруктураТаблицДокумента.ТаблицаОборудование,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление объектов строительства
	УчетОС.СформироватьДвиженияПоступлениеОбъектовСтроительства(СтруктураТаблицДокумента.ТаблицаОбъектыСтроительства,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление основных средств
	УчетОС.ПроверитьВозможностьИзмененияСостоянияОС(ПараметрыПроведения.ПроверкаСостоянияОСТаблица,
		ПараметрыПроведения.ПроверкаСостоянияОСРеквизиты, Отказ);
	
	УчетОС.СформироватьДвиженияПоступлениеОсновныхСредств(СтруктураТаблицДокумента.ТаблицаОсновныеСредства,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление услуг
	УчетДоходовРасходов.СформироватьДвиженияПоступлениеУслуг(СтруктураТаблицДокумента.ТаблицаУслуги,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление тары
	УчетТоваров.СформироватьДвиженияПоступлениеТары(ПараметрыПроведения.ТаблицаТара,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление товаров на комиссию
	УчетТоваров.СформироватьДвиженияПоступлениеТоваровНаКомиссию(ПараметрыПроведения.ПоступлениеТоваровНаКомиссиюТаблицаТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Поступление в переработку
	УчетТоваров.СформироватьДвиженияПоступлениеМатериаловВПереработку(ПараметрыПроведения.ПоступлениеТоваровВПереработкуТаблицаТовары,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Поступление закупленных товаров и услуг комитентов
	УчетТоваров.СформироватьДвиженияПоступлениеЗакупленныхТоваровУслугКомитентов(
		ТаблицаТоварыУслугиКомиссияПоЗакупке, ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Задолженность комитентов по закупленных для них товарам и услугам
	УчетВзаиморасчетов.СформироватьДвиженияПоступлениеЗакупленныхТоваровУслугКомитентов(
		ТаблицаТоварыУслугиКомиссияПоЗакупке, ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// Зачет аванса комитентов 
	УчетВзаиморасчетов.СформироватьДвиженияЗачетАвансов(
		ТаблицаВзаиморасчетыКомиссияПоЗакупке, ПараметрыПроведения.ЗачетАвансовКомиссияПоЗакупкеРеквизиты, Движения, Отказ);
		
	//Движения регистра "Рублевые суммы документов в валюте"
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаОборудование, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаОбъектыСтроительства, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаОсновныеСредства, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеПоступлениеСобственныхТоваровУслуг(СтруктураТаблицДокумента.ТаблицаУслуги, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеБезНДС(ПараметрыПроведения.ТаблицаТара, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ПараметрыПроведения.ПоступлениеТоваровНаКомиссиюТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалютеБезНДС(ПараметрыПроведения.ПоступлениеТоваровВПереработкуТаблицаТовары, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСБП.СформироватьДвиженияРублевыеСуммыДокументовВВалюте(ТаблицаТоварыУслугиКомиссияПоЗакупке, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Движения регистра  ИмпортТоваровТаможенныйСоюз
	РегистрыСведений.ИмпортТоваровТаможенныйСоюз.СформироватьДвиженияИмпортТоваровТС(ПараметрыПроведения.Реквизиты, Движения, Отказ);

	// Учет НДС
	УчетНДС.СформироватьДвиженияПоступлениеТоваровУслугОтПоставщика(
		ТаблицыНДС.Товары, ТаблицыНДС.Услуги, ПараметрыПроведения.НомераГТД,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	УчетНДСРаздельный.СформироватьДвиженияПоступлениеТоваровУслугОтПоставщика(
		ТаблицыНДС.Товары, ТаблицыНДС.Услуги, ПараметрыПроведения.Реквизиты,
		Движения, Отказ);
		
	УчетНДС.СформироватьДвиженияЖурналУчетаСчетовФактурРегистрация(
		ПараметрыПроведения.РегистрацияВЖурналеУчетаСчетовФактур, Движения, Отказ);
		
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	// Учет прослеживаемых товаров
	ПрослеживаемостьБП.СформироватьДвиженияПоступлениеТоваров(
		ПараметрыПроведения.ПрослеживаемыеТовары, 
		ПараметрыПроведения.ПрослеживаемыеОперации,
		ПараметрыПроведения.РНПТ,
		ПараметрыПроведения.Реквизиты, 
		Движения);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
	ПрослеживаемостьБП.СформироватьДвиженияПоступлениеОС(
		ПараметрыПроведения.ПрослеживаемыеОС, 
		ПараметрыПроведения.ПрослеживаемыеОперацииОС,
		ПараметрыПроведения.РНПТОС,
		ПараметрыПроведения.Реквизиты, 
		Движения);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405)
	
	// УСН
	СуммаСторноРасхода = 0;
	УчетУСН.ПоступлениеРасходовУСН(ПараметрыПроведения.УСНТаблицаРасходов,
		ПараметрыПроведения.УСНРеквизиты, СуммаСторноРасхода, Движения, Отказ);

	Если НЕ Отказ И Движения.РасходыПриУСН.Количество() > 0 Тогда
		Движения.РасходыПриУСН.Записать(Истина);
		Движения.РасходыПриУСН.Записывать = Ложь;
	КонецЕсли;

	НалоговыйУчетУСН.СформироватьДвиженияУСН(ЭтотОбъект, СтруктураТаблицУСН);
	
	// Учет доходов и расходов ИП
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеМПЗ(СтруктураТаблицМПЗ,
		ТаблицаВзаиморасчеты, ,ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияПоступлениеОС(ТаблицаПоступленияОСИП,
		ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетДоходовИРасходовПредпринимателя.РегистрацияСведенийОбОплатеОСиНМА(ТаблицаОплатыОСИП,
		ПараметрыПроведения.ИПРеквизиты, Движения, Отказ);
		
	УчетВзаиморасчетов.СформироватьДвиженияПоПрочимРасчетам(ТаблицаПрочихРасчетов, Движения, Отказ);
	
	УчетДоходовИРасходовПредпринимателя.СформироватьДвиженияЗачетОплатыПокупателя(
		ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаИПМПЗОтгруженные,
		ТаблицыПоступленияОтФакторинговойКомпанииИП.ТаблицаВзаиморасчеты, 
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
	
	// Переоценка валютных остатков - после формирования проводок всеми другими механизмами
	ТаблицаПереоценка = УчетДоходовРасходов.ПодготовитьТаблицуПереоценкаВалютныхОстатковПоПроводкамДокумента(
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетДоходовРасходов.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);

	УчетУСН.СформироватьДвиженияПереоценкаВалютныхОстатков(ТаблицаПереоценка,
		ПараметрыПроведения.Реквизиты, Движения, Отказ);
		
	// Статусы счетов на оплату поставщика.
	Если НЕ ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		СтатусыДокументов.СформироватьДвиженияСтатусовДокументов(
			ТаблицаСтатусовСчетов, ПараметрыПроведения.Реквизиты);
	КонецЕсли;
		
	// Отложенные расчеты с контрагентами.
	УчетВзаиморасчетовОтложенноеПроведение.ЗарегистрироватьОтложенныеРасчетыСКонтрагентами(
		ЭтотОбъект, Отказ, ПараметрыПроведения.РасчетыСКонтрагентамиОтложенноеПроведение);
		
	// Регистрация в последовательности
	Документы.ПоступлениеТоваровУслуг.ЗарегистрироватьОтложенныеРасчетыВПоследовательности(
		ЭтотОбъект, ПараметрыПроведения, Отказ);
		
	ПроведениеСервер.УстановитьЗаписьОчищаемыхНаборовЗаписей(ЭтотОбъект);
		
	Движения.Записать();

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ, НЕ УстановленСтатусДокумента);
	
	//БИТ Тртилек 23072013 выполним проверку по связанным документам
	Если УстановитьВидимостьРеализацияУСобственногоЮЛ() Тогда
		ИтоговаяСумма = бит_ДокументыСобственныхКонтрагентов.Итог("бит_СуммаБезНДС");
		
		Для Каждого СвязанныйДокумент Из бит_ДокументыСобственныхКонтрагентов Цикл
			Если СвязанныйДокумент.ДатаДокументаРеализацииПередачи > Дата Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен не буден, т.к. у связанного документа " + СвязанныйДокумент.ДокументРеализацииПередачи + " дата больше даты данного документа");
				Отказ = Истина;	
				Возврат;
			КонецЕсли;
			Если НЕ ЗначениеЗаполнено(СвязанныйДокумент.бит_СуммаБезНДС) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен не будет, т.к. в реализации у собственного юрлица в строке с документом: " + СвязанныйДокумент.ДокументРеализацииПередачи + " не заполнена сумма без НДС");
				Отказ = Истина;	
				Возврат;
			КонецЕсли;
		КонецЦикла;
		
		//проверим, что в каждой строке связанных документов заполнеа Сума без НДС		
		Если НЕ ИтоговаяСумма = СуммаДокумента - УчетНДСПереопределяемый.ПолучитьНДСДокумента(ЭтотОбъект) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Документ проведен не буден, т.к. сумма по связанным документам не равна сумме документа");
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	///БИТ Тртилек
	
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-09 (#3048)
	Если ПараметрыПроведения.Свойство("СтруктураТаблицДокумента") Тогда 
			
		стрТаблицыДляПроведенияПоБК = Новый Структура;
		
		пИменаНеобходимыхТаблиц = "Товары,Услуги,Оборудование,ОбъектыСтроительства,ТоварыУслугиКомиссияПоЗакупке";
		мИменаТаблиц = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(пИменаНеобходимыхТаблиц,",");
						
		пИменаКолонокТаблиц = "Ссылка,Период,НомерСтроки,ЦФО,СтатьяОборотов,ВидСтатьи,Проект,Аналитика_2,СуммаРуб,СуммаНДСРуб";
		пРасчетыВВалюте = ПараметрыПроведения.Реквизиты[0].ВалютаВзаиморасчетов<>ОбщегоНазначенияБПВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета();
		
		Если пРасчетыВВалюте Тогда 
			пИменаКолонокТаблиц = пИменаКолонокТаблиц + ",СуммаВзаиморасчетов,СуммаНДСВзаиморасчетов";
		КонецЕсли;
		
		Для Каждого пИмяТаблицы Из мИменаТаблиц Цикл 
									
			Если ПараметрыПроведения.СтруктураТаблицДокумента.Свойство("Таблица" + пИмяТаблицы) Тогда 
				Если ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы]<>Неопределено И 
					 ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Количество()>0
				Тогда 
				Если ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Колонки.Найти("ОК_ID_Разноска")<>Неопределено Тогда 
					Если СтрНайти(пИменаКолонокТаблиц,"ОК_ID_Разноска", НаправлениеПоиска.СКонца) = 0 Тогда 
						пИменаКолонокТаблиц = пИменаКолонокТаблиц + ",ОК_ID_Разноска";
					КонецЕсли;
					Иначе
						пИменаКолонокТаблиц = СтрЗаменить(пИменаКолонокТаблиц, ",ОК_ID_Разноска", "");
					КонецЕсли;
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-28 (#3192)
					//стрТаблицыДляПроведенияПоБК.Вставить("Таблица" + пИмяТаблицы, ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Скопировать(,пИменаКолонокТаблиц));
					//Заменено на:
					пИмяКолонкиСчета = "";
					Если ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Колонки.Найти("СчетЗатрат") <> Неопределено Тогда 
						пИмяКолонкиСчета = "СчетЗатрат"; 
					ИначеЕсли ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Колонки.Найти("СчетУчета") <> Неопределено Тогда 
						пИмяКолонкиСчета = "СчетУчета"; 
					ИначеЕсли ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Колонки.Найти("СчетРасчетов") <> Неопределено Тогда 
						пИмяКолонкиСчета = "СчетРасчетов"; 					
					КонецЕсли;
													
					пКопияТаблицы = ПараметрыПроведения.СтруктураТаблицДокумента["Таблица" + пИмяТаблицы].Скопировать(,пИменаКолонокТаблиц + ?(ПустаяСтрока(пИмяКолонкиСчета),"",","+пИмяКолонкиСчета));
										
					Если Не ПустаяСтрока(пИмяКолонкиСчета) Тогда 
						пКопияТаблицы.Колонки[пИмяКолонкиСчета].Имя = "Счет";						
					КонецЕсли;				
					
					стрТаблицыДляПроведенияПоБК.Вставить("Таблица" + пИмяТаблицы, пКопияТаблицы);
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-28 (#3192)					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		Если стрТаблицыДляПроведенияПоБК.Количество()>0 Тогда 
			Если НЕ пРасчетыВВалюте Тогда 
				Для Каждого КлючЗнчТаб Из стрТаблицыДляПроведенияПоБК Цикл 
					пТаблица = КлючЗнчТаб.Значение;
					пТаблица.Колонки.Добавить("СуммаВзаиморасчетов"	  , Новый ОписаниеТипов("Число"));
					пТаблица.Колонки.Добавить("СуммаНДСВзаиморасчетов", Новый ОписаниеТипов("Число"));
					Для Каждого СтрТаб Из пТаблица Цикл 
						СтрТаб.СуммаВзаиморасчетов	  = СтрТаб.СуммаРуб;
						СтрТаб.СуммаНДСВзаиморасчетов = СтрТаб.СуммаНДСРуб;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			ДополнительныеСвойства.Вставить("ТаблицыДокумента", стрТаблицыДляПроведенияПоБК);			
			
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-23 (#3192)
			//ОК_ПодпискиНаСобытия.ок_ПроведениеПоАналитикамБК(ЭтотОбъект, Отказ, РежимПроведения);
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-23 (#3192)
			
		КонецЕсли;
		
	КонецЕсли;
	//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-09 (#3048)
	
КонецПроцедуры

Процедура ОбработкаУдаленияПроведения(Отказ)

	//{ bit SVKushnirenko Bit 27.01.2017 #2657
	ЭтотОбъект.Движения.бит_КонтрольныеЗначенияБюджетов.ДополнительныеСвойства.Вставить("бит_БК_УстановкаСтатусаПередУдалением", Истина);
	//} bit SVKushnirenko Bit 27.01.2017 #2657
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКОтменеПроведения(ЭтотОбъект);
	Движения.Записать();

	ПараметрыДействия = УчетНДСПереопределяемый.НовыеПараметрыСостоянияСчетаФактуры("Проведен", ЭтотОбъект, "СчетФактураПолученный");	
	ПараметрыДействия.СостояниеФлага = Ложь;
	УчетНДСПереопределяемый.УстановитьСостояниеСчетаФактуры(ПараметрыДействия, Отказ);

	РаботаСПоследовательностями.ОтменитьРегистрациюВПоследовательности(ЭтотОбъект, Отказ);

	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	ПрослеживаемостьБП.УдалениеПроведенияПервичногоДокумента(Ссылка);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьТабличныеЧастиПоСчетуНаОплату(Счет, ИменаТабличныхЧастей)

	Если НЕ ЗначениеЗаполнено(Счет) Тогда
		Возврат;
	КонецЕсли;
	
	МассивИмен = СтрРазделить(ИменаТабличныхЧастей, ", ", Ложь);
	Для каждого ИмяТаблицы Из МассивИмен Цикл
		ЭтотОбъект[ИмяТаблицы].Очистить();
	КонецЦикла;
	ЗаполнитьТовары = (МассивИмен.Найти("Товары") <> Неопределено);
	ЗаполнитьУслуги = (МассивИмен.Найти("Услуги") <> Неопределено);
	ЗаполнитьТару   = (МассивИмен.Найти("ВозвратнаяТара") <> Неопределено);
	
	ПоступленияПоСчету = Документы.ПоступлениеТоваровУслуг.ПоступленияПоСчету(Счет);
	ТекущееПоступление = ПоступленияПоСчету.Найти(Ссылка);
	Если ТекущееПоступление <> Неопределено Тогда
		ПоступленияПоСчету.Удалить(ТекущееПоступление);
	КонецЕсли;
	
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
	ЗаполнятьПризнакПрослеживаемости = ПолучитьФункциональнуюОпцию("ВестиУчетПрослеживаемыхТоваров")
		И ПрослеживаемостьБРУ.ВедетсяУчетПрослеживаемыхТоваров(Дата);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	
	// Товары и услуги
	ТоварыУслугиКПоступлению = Документы.ПоступлениеТоваровУслуг.ТоварыУслугиКПоступлениюПоСчету(
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214) 
		//Счет, ПоступленияПоСчету);
		Счет, ПоступленияПоСчету, ЗаполнятьПризнакПрослеживаемости);
	//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
	Для каждого СтрокаОснования Из ТоварыУслугиКПоступлению Цикл
		Если ЗаполнитьУслуги И СтрокаОснования.ЭтоУслуга Тогда
			ЗаполнитьЗначенияСвойств(Услуги.Добавить(), СтрокаОснования);
		ИначеЕсли ЗаполнитьТовары Тогда
			ЗаполнитьЗначенияСвойств(Товары.Добавить(), СтрокаОснования);
		КонецЕсли;
	КонецЦикла;
		
	// Пересчет сумм
	ОснованиеСуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Счет, "СуммаВключаетНДС");
	Если ОснованиеСуммаВключаетНДС <> СуммаВключаетНДС Тогда
		Для каждого СтрокаТЧ Из Товары Цикл
			СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
			СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, 0, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
		КонецЦикла;
		Для каждого СтрокаТЧ Из Услуги Цикл
			СтрокаТЧ.Сумма = СтрокаТЧ.Сумма + ?(СуммаВключаетНДС, СтрокаТЧ.СуммаНДС, -СтрокаТЧ.СуммаНДС);
			СтрокаТЧ.Цена = ?(СтрокаТЧ.Количество = 0, СтрокаТЧ.Сумма, СтрокаТЧ.Сумма/СтрокаТЧ.Количество);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, СуммаВключаетНДС);
		КонецЦикла;
	КонецЕсли;
	
	// Возвратная тара
	Если ЗаполнитьТару
		И ПолучитьФункциональнуюОпцию("ИспользоватьВозвратнуюТару") Тогда
		ВозвратнаяТараКПоступлению = Документы.ПоступлениеТоваровУслуг.ВозвратнаяТараКПоступлениюПоСчету(
			Счет, ПоступленияПоСчету);
		ВозвратнаяТара.Загрузить(ВозвратнаяТараКПоступлению);
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьПоСчету(ИмяТаблицы, Счет) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Счет) Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьТабличныеЧастиПоСчетуНаОплату(Счет, ИмяТаблицы);
	
	ТабличнаяЧасть = ЭтотОбъект[ИмяТаблицы];
	
	СчетаУчетаВДокументах.ЗаполнитьСтроки(ТабличнаяЧасть, ИмяТаблицы, ЭтотОбъект, Документы.ПоступлениеТоваровУслуг);
	
КонецПроцедуры

Процедура ЗаполнитьПоДокументуОснованию(Основание)
	
	Если ТипЗнч(Основание) = Тип("ДокументСсылка.Доверенность") Тогда
		
		// Склад сразу заполняется значением по умолчанию, т.к. в доверенности его нет,
		// а от склада зависит, какие счета учета будут подставлены в табличную часть.
		Склад = БухгалтерскийУчетПереопределяемый.ПолучитьЗначениеПоУмолчанию("ОсновнойСклад");
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
		ВидОперации = Документы.ПоступлениеТоваровУслуг.ОпределитьВидОперацииПоДокументуОснованию(Основание);
		
		Если ТипЗнч(Основание.Сделка) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
			СчетНаОплатуПоставщика = Основание.Сделка;
		КонецЕсли; 
		
		// Тип цен и валюту берем по умолчанию из договора, т.к. в доверенности нет таких реквизитов,
		// курс и кратность взаиморасчетов будут подставлены по валюте договора в ЗаполнитьПоОснованию().
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "ВалютаВзаиморасчетов, ТипЦен");
			ВалютаДокумента   = РеквизитыДоговора.ВалютаВзаиморасчетов;
			ТипЦен            = РеквизитыДоговора.ТипЦен;
		КонецЕсли;
		
		// Флаги включения налогов.
		Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
			СуммаВключаетНДС = Ложь;
		Иначе
			СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипЦен, "ЦенаВключаетНДС");
		КонецЕсли;
		
		ЗаполнитьТоварыПоДоверенности(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") Тогда
		
		// Заполним реквизиты шапки по документу основанию.
		СчетНаОплатуПоставщика = Основание;
		ВидОперации = Документы.ПоступлениеТоваровУслуг.ОпределитьВидОперацииПоДокументуОснованию(Основание);
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание, Истина); // Копировать подразделение = Истина
		
		ЗаполнитьТабличныеЧастиПоСчетуНаОплату(Основание, "Товары,Услуги,ВозвратнаяТара");
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ТТНВходящаяЕГАИС") Тогда
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьПоОснованию(ЭтотОбъект, Основание);
		
		Если ЗначениеЗаполнено(Основание.ТорговыйОбъект) Тогда
			Склад = Основание.ТорговыйОбъект;
		КонецЕсли;
		
		МассивКонтрагентов = Новый Массив;
		МассивКонтрагентов.Добавить(Основание.Грузоотправитель);
		МассивКонтрагентов.Добавить(Основание.Грузополучатель);
		Если НЕ Основание.Поставщик.Пустая() Тогда
			МассивКонтрагентов.Добавить(Основание.Поставщик);
		КонецЕсли;
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Классификатор.Ссылка КАК Ссылка,
		|	Классификатор.Контрагент КАК Контрагент,
		|	Классификатор.СоответствуетОрганизации КАК СоответствуетОрганизации,
		|	Классификатор.ТорговыйОбъект КАК ТорговыйОбъект,
		|	0 КАК Приоритет
		|ИЗ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК Классификатор
		|ГДЕ
		|	Классификатор.Ссылка В(&МассивОрганизаций)
		|
		|ОБЪЕДИНИТЬ
		|
		|ВЫБРАТЬ
		|	КлассификаторОрганизацийЕГАИС.Ссылка,
		|	Контрагенты.Ссылка,
		|	ЛОЖЬ,
		|	КлассификаторОрганизацийЕГАИС.ТорговыйОбъект,
		|	1
		|ИЗ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Контрагенты КАК Контрагенты
		|		ПО КлассификаторОрганизацийЕГАИС.ИНН = Контрагенты.ИНН
		|			И КлассификаторОрганизацийЕГАИС.КПП = Контрагенты.КПП
		|ГДЕ
		|	КлассификаторОрганизацийЕГАИС.Ссылка В(&МассивОрганизаций)
		|
		|УПОРЯДОЧИТЬ ПО
		|	Приоритет";
		
		Запрос.УстановитьПараметр("МассивОрганизаций",МассивКонтрагентов);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Если Выборка.Ссылка = Основание.Грузоотправитель И Грузоотправитель.Пустая() Тогда
				Грузоотправитель = Выборка.Контрагент;
			ИначеЕсли Выборка.Ссылка = Основание.Грузополучатель Тогда
				
				Если Организация.Пустая() И Выборка.СоответствуетОрганизации Тогда
					Организация  = Выборка.Контрагент;
				ИначеЕсли Грузополучатель.Пустая() И НЕ Выборка.СоответствуетОрганизации Тогда 
					Грузополучатель  = Выборка.Контрагент;
				КонецЕсли;
				
				Если Склад.Пустая() Тогда
					Склад = Выборка.ТорговыйОбъект;
				КонецЕсли;
			ИначеЕсли (НЕ Основание.Поставщик.Пустая())
				И Выборка.Ссылка = Основание.Поставщик
				И Контрагент.Пустая()
				И (НЕ Выборка.СоответствуетОрганизации) Тогда
				Контрагент = Выборка.Контрагент;
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
			Контрагент = Грузоотправитель;
		КонецЕсли;
		
		НомерВходящегоДокумента = Основание.НомерТТН;
		ДатаВходящегоДокумента  = Основание.ДатаТТН;
		
		ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия;
		
		// Тип цен и валюту берем по умолчанию из договора
		Если ЗначениеЗаполнено(ДоговорКонтрагента) Тогда
			РеквизитыДоговора = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДоговорКонтрагента, "ВалютаВзаиморасчетов, ТипЦен");
			ВалютаДокумента   = РеквизитыДоговора.ВалютаВзаиморасчетов;
			ТипЦен            = РеквизитыДоговора.ТипЦен;
		КонецЕсли;
		
		// Флаги включения налогов.
		Если НЕ ЗначениеЗаполнено(ТипЦен) Тогда
			СуммаВключаетНДС = Ложь;
		Иначе
			СуммаВключаетНДС = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ТипЦен, "ЦенаВключаетНДС");
		КонецЕсли;
		
		ЗаполнитьТоварыЕГАИС(Основание);
		
	ИначеЕсли ТипЗнч(Основание) = Тип("ДокументСсылка.ВходящаяТранспортнаяОперацияВЕТИС") Тогда
		
		ИнтеграцияВЕТИСБП.ЗаполнитьПоступлениеПоВходящейОперацииВЕТИС(ЭтотОбъект, Основание);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьТоварыЕГАИС(ТТНВходящаяЕГАИС)

	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", ТТНВходящаяЕГАИС);
	
	Если УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата) Тогда
		ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
	Иначе
		ОтражениеВУСН = Неопределено;
	КонецЕсли;
	Запрос.УстановитьПараметр("ОтражениеВУСН", ОтражениеВУСН);
	Запрос.УстановитьПараметр("СтавкаНДСПоУмолчанию", УчетНДСКлиентСервер.СтавкаНДСПоУмолчанию(Дата));
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТТНВходящаяЕГАИСТовары.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА ТТНВходящаяЕГАИСТовары.Ссылка.ЕстьРасхождения
	|			ТОГДА ТТНВходящаяЕГАИСТовары.КоличествоФакт
	|		ИНАЧЕ ТТНВходящаяЕГАИСТовары.Количество
	|	КОНЕЦ КАК Количество,
	|	ТТНВходящаяЕГАИСТовары.Цена КАК Цена,
	|	1 КАК КоэффициентПересчетаУпаковки,
	|	ТТНВходящаяЕГАИСТовары.Сумма,
	|	ТТНВходящаяЕГАИСТовары.НомерСтроки
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТТНВходящаяЕГАИСТовары
	|ГДЕ
	|	ТТНВходящаяЕГАИСТовары.Ссылка = &Основание
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	КоэффициентПересчетаУпаковки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Количество КАК Количество,
	|	ТаблицаТовары.Цена,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
	|	&СтавкаНДСПоУмолчанию КАК СтавкаНДС,
	|	&ОтражениеВУСН КАК ОтражениеВУСН
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.КоэффициентПересчетаУпаковки = 1
	|	И ТаблицаТовары.Количество <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаТовары.Номенклатура,
	|	ТаблицаТовары.Количество * ТаблицаТовары.КоэффициентПересчетаУпаковки,
	|	ВЫБОР
	|		КОГДА ТаблицаТовары.Количество * ТаблицаТовары.КоэффициентПересчетаУпаковки = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаТовары.Сумма / (ТаблицаТовары.Количество * ТаблицаТовары.КоэффициентПересчетаУпаковки)
	|	КОНЕЦ,
	|	ТаблицаТовары.Сумма,
	|	ТаблицаТовары.НомерСтроки,
	|	&СтавкаНДСПоУмолчанию,
	|	&ОтражениеВУСН
	|ИЗ
	|	ТаблицаТовары КАК ТаблицаТовары
	|ГДЕ
	|	ТаблицаТовары.КоэффициентПересчетаУпаковки <> 1
	|	И ТаблицаТовары.Количество <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	 
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ДанныеОбъекта = Новый Структура(
		"Дата, ВидОперации, Организация, Склад, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЭтотОбъект);
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь);
	
	
	Для каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = Товары.Добавить();
		
		ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаТоваров);
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре[СтрокаТоваров.Номенклатура];
		Если ЗначениеЗаполнено(СтрокаТоваров.Номенклатура) И СведенияОНоменклатуре <> Неопределено Тогда
			СтрокаТабличнойЧасти.ЕдиницаИзмерения		= СведенияОНоменклатуре.ЕдиницаИзмерения;
			СтрокаТабличнойЧасти.Коэффициент			= СведенияОНоменклатуре.Коэффициент;
			СтрокаТабличнойЧасти.СтавкаНДС				= СведенияОНоменклатуре.СтавкаНДС;
			СтрокаТабличнойЧасти.НомерГТД				= СведенияОНоменклатуре.НомерГТД;
			СтрокаТабличнойЧасти.СтранаПроисхождения	= СведенияОНоменклатуре.СтранаПроисхождения;
		КонецЕсли;
		
		СтавкаНДС = УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТабличнойЧасти.СтавкаНДС);

		СтрокаТабличнойЧасти.Сумма = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.Сумма, Истина, СуммаВключаетНДС, СтавкаНДС);
		СтрокаТабличнойЧасти.Цена  = УчетНДСКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТабличнойЧасти.Цена, Истина, СуммаВключаетНДС, СтавкаНДС);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
	КонецЦикла;

КонецПроцедуры

Процедура ЗаполнитьТоварыПоДоверенности(Основание)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Основание", Основание);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СпрНоменклатура.Ссылка КАК Номенклатура,
	|	ДоверенностьТовары.Количество КАК Количество,
	|	ДоверенностьТовары.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ДоверенностьТовары.ЕдиницаПоКлассификатору = СпрНоменклатура.ЕдиницаИзмерения
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК СовпадаетЕдиницаИзмерения
	|ПОМЕСТИТЬ ВТ_Доверенность
	|ИЗ
	|	Документ.Доверенность.Товары КАК ДоверенностьТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Номенклатура КАК СпрНоменклатура
	|		ПО ((ВЫРАЗИТЬ(ДоверенностьТовары.НаименованиеТовара КАК СТРОКА(1000))) = (ВЫРАЗИТЬ(СпрНоменклатура.НаименованиеПолное КАК СТРОКА(1000))))
	|ГДЕ
	|	ДоверенностьТовары.Ссылка = &Основание
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Доверенность.НомерСтроки КАК НомерСтроки,
	|	МАКСИМУМ(ВТ_Доверенность.СовпадаетЕдиницаИзмерения) КАК СовпадаетЕдиницаИзмерения
	|ПОМЕСТИТЬ ВТ_СовпадениеПоСтрокам
	|ИЗ
	|	ВТ_Доверенность КАК ВТ_Доверенность
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Доверенность.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Доверенность.НомерСтроки КАК НомерСтроки,
	|	МАКСИМУМ(ВТ_Доверенность.Номенклатура) КАК Номенклатура,
	|	ВТ_Доверенность.Количество КАК Количество
	|ИЗ
	|	ВТ_Доверенность КАК ВТ_Доверенность
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_СовпадениеПоСтрокам КАК ВТ_СовпадениеПоСтрокам
	|		ПО  ВТ_Доверенность.НомерСтроки = ВТ_СовпадениеПоСтрокам.НомерСтроки
	|			И ВТ_Доверенность.СовпадаетЕдиницаИзмерения = ВТ_СовпадениеПоСтрокам.СовпадаетЕдиницаИзмерения
	|
	|СГРУППИРОВАТЬ ПО
	|	ВТ_Доверенность.Количество,
	|	ВТ_Доверенность.НомерСтроки
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	ТаблицаТоваров = Запрос.Выполнить().Выгрузить();
	
	ДанныеОбъекта = Новый Структура(
		"Дата, ВидОперации, Организация, Склад, ТипЦен, СуммаВключаетНДС,
		|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов,
		|ЭтоКомиссия");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, ЭтотОбъект);
	
	ВидДоговора = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	
	ДанныеОбъекта.ЭтоКомиссия = ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
		И ВидДоговора = Перечисления.ВидыДоговоровКонтрагентов.СКомитентом;
	
	СоответствиеСведенийОНоменклатуре = БухгалтерскийУчетПереопределяемый.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(ТаблицаТоваров, "Номенклатура", Истина), ДанныеОбъекта, Ложь);
	
	Если УчетнаяПолитика.ПрименяетсяУСНДоходыМинусРасходы(Организация, Дата) Тогда
		Если ДанныеОбъекта.ЭтоКомиссия Тогда
			ОтражениеВУСН = Перечисления.ОтражениеВУСН.НеПринимаются;
		ИначеЕсли ВидОперации <> Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
			ОтражениеВУСН = Перечисления.ОтражениеВУСН.Принимаются;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеОбъекта.ЭтоКомиссия Тогда
		СпособЗачетаАвансов = Перечисления.СпособыЗачетаАвансов.НеЗачитывать;
	КонецЕсли;
	
	Для каждого СтрокаТоваров Из ТаблицаТоваров Цикл
		
		СтрокаТабличнойЧасти = Товары.Добавить();
		
		СтрокаТабличнойЧасти.Номенклатура	= СтрокаТоваров.Номенклатура;
		СтрокаТабличнойЧасти.Количество	= СтрокаТоваров.Количество;
		
		СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТоваров.Номенклатура);
		Если СведенияОНоменклатуре = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.ЕдиницаИзмерения		= СведенияОНоменклатуре.ЕдиницаИзмерения;
		СтрокаТабличнойЧасти.Коэффициент			= СведенияОНоменклатуре.Коэффициент;
		СтрокаТабличнойЧасти.Цена					= СведенияОНоменклатуре.Цена;
		СтрокаТабличнойЧасти.СтавкаНДС				= СведенияОНоменклатуре.СтавкаНДС;
		СтрокаТабличнойЧасти.НомерГТД				= СведенияОНоменклатуре.НомерГТД;
		СтрокаТабличнойЧасти.СтранаПроисхождения	= СведенияОНоменклатуре.СтранаПроисхождения;
		СтрокаТабличнойЧасти.ОтражениеВУСН			= ОтражениеВУСН;
		
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТабличнойЧасти);
		ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТабличнойЧасти, СуммаВключаетНДС);
		
КонецЦикла;

КонецПроцедуры

Процедура ПроверитьКорректностьЗаполненияТабличнойЧастиТовары(Отказ)

	ВалютаРегламентированногоУчета = Константы.ВалютаРегламентированногоУчета.Получить();

	УчетВПродажныхЦенах =
		УчетнаяПолитика.СпособОценкиТоваровВРознице(Организация, Дата) = Перечисления.СпособыОценкиТоваровВРознице.ПоПродажнойСтоимости;

	ВидДоговораКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДоговорКонтрагента, "ВидДоговора");
	ДанныеСклада      = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Склад, "ТипСклада, ТипЦенРозничнойТорговли");

	Если ДанныеСклада.ТипСклада <> Перечисления.ТипыСкладов.ОптовыйСклад И УчетВПродажныхЦенах
		И (ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ПокупкаКомиссия
			ИЛИ ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.Товары)
		И ВидДоговораКонтрагента <> Перечисления.ВидыДоговоровКонтрагентов.СКомитентом Тогда
		РозницаВПродажныхЦенах = Истина;
	Иначе
		РозницаВПродажныхЦенах = Ложь;
	КонецЕсли;

	АТТ = (РозницаВПродажныхЦенах И ДанныеСклада.ТипСклада = Перечисления.ТипыСкладов.РозничныйМагазин);

	Если АТТ Тогда
		// Установим параметры запроса
		ПорядокСубконто = Новый Массив();
		ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Номенклатура);
		ПорядокСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоХозрасчетные.Склады);

		МассивНоменклатуры = Товары.ВыгрузитьКолонку("Номенклатура");

		Запрос = Новый Запрос;
		Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("Склад",                          Склад);
		Запрос.УстановитьПараметр("КонецПериода",                   Дата);
		Запрос.УстановитьПараметр("Организация",                    Организация);
		Запрос.УстановитьПараметр("Подразделение",                  ПодразделениеОрганизации);
		Запрос.УстановитьПараметр("ПорядокСубконто",                ПорядокСубконто);
		Запрос.УстановитьПараметр("Номенклатура",                   МассивНоменклатуры);
		Запрос.УстановитьПараметр("Дата",                           Дата);
		Запрос.УстановитьПараметр("СкладТипЦен",                    ДанныеСклада.ТипЦенРозничнойТорговли);
		Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета", ВалютаРегламентированногоУчета);
		Запрос.УстановитьПараметр("СчетАТТ",						ПланыСчетов.Хозрасчетный.ТоварыВРозничнойТорговлеВПродажныхЦенахАТТ);

		Запрос.Текст = "
		|// Курсы валют (временная таблица)
		|ВЫБРАТЬ
		|	КурсыВалют.Валюта,
		|	КурсыВалют.Курс,
		|	КурсыВалют.Кратность
		|ПОМЕСТИТЬ ВТ_КурсыВалют
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(
		|		&КонецПериода,
		|	) КАК КурсыВалют
		|ИНДЕКСИРОВАТЬ ПО
		|	Валюта
		|;
		|
		|// Запрос к ценам номенклатуры (временная таблица)
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура,
		|	ЦеныНоменклатуры.Цена,
		|	ВЫБОР
		|		КОГДА (ВТ_КурсыВалют.Валюта ЕСТЬ NULL) ИЛИ (ВТ_КурсыВалют.Валюта = &ВалютаРегламентированногоУчета) ТОГДА
		|			ЦеныНоменклатуры.Цена
		|		КОГДА НЕ ВТ_КурсыВалют.Валюта ЕСТЬ NULL И ВТ_КурсыВалют.Кратность = 0 ТОГДА
		|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс
		|		ИНАЧЕ
		|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс * ВТ_КурсыВалют.Кратность
		|	КОНЕЦ КАК ЦенаВВалютеРегламентированногоУчета,
		|	ЦеныНоменклатуры.Валюта
		|ПОМЕСТИТЬ ВТ_ЦеныНоменклатуры
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|		&КонецПериода,
		|		Номенклатура В (&Номенклатура) И (ТипЦен = &СкладТипЦен)
		|	) КАК ЦеныНоменклатуры
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВТ_КурсыВалют
		|	ПО
		|		ЦеныНоменклатуры.Валюта = ВТ_КурсыВалют.Валюта
		|ИНДЕКСИРОВАТЬ ПО
		|	Номенклатура
		|;
		|
		|// Запрос к ценам номенклатуры
		|ВЫБРАТЬ
		|	ЦеныНоменклатуры.Номенклатура,
		|	ЦеныНоменклатуры.Цена,
		|	ВЫБОР
		|		КОГДА (ВТ_КурсыВалют.Валюта ЕСТЬ NULL) ИЛИ (ВТ_КурсыВалют.Валюта = &ВалютаРегламентированногоУчета) ТОГДА
		|			ЦеныНоменклатуры.Цена
		|		КОГДА НЕ ВТ_КурсыВалют.Валюта ЕСТЬ NULL И ВТ_КурсыВалют.Кратность = 0 ТОГДА
		|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс
		|		ИНАЧЕ
		|			ЦеныНоменклатуры.Цена * ВТ_КурсыВалют.Курс * ВТ_КурсыВалют.Кратность
		|	КОНЕЦ КАК ЦенаВВалютеРегламентированногоУчета,
		|	ЦеныНоменклатуры.Валюта
		|ИЗ
		|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
		|		&КонецПериода,
		|		Номенклатура В (&Номенклатура) И (ТипЦен = &СкладТипЦен)
		|	) КАК ЦеныНоменклатуры
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВТ_КурсыВалют
		|	ПО
		|		ЦеныНоменклатуры.Валюта = ВТ_КурсыВалют.Валюта
		|;
		|
		|// Основной запрос
		|ВЫБРАТЬ
		|	ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Номенклатура) КАК Номенклатура,
		|	ВЫБОР
		|		КОГДА Остатки.КоличествоОстатокДт <> 0
		|			ТОГДА ВЫРАЗИТЬ(Остатки.СуммаОстатокДт / Остатки.КоличествоОстатокДт КАК ЧИСЛО(15, 2))
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК УчетнаяЦена,
		|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Цена, 0) КАК Цена,
		|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.ЦенаВВалютеРегламентированногоУчета, 0) КАК ЦенаВВалютеРегламентированногоУчета,
		|	ЕСТЬNULL(ВТ_ЦеныНоменклатуры.Валюта, ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)) КАК Валюта
		|ИЗ
		|	РегистрБухгалтерии.Хозрасчетный.Остатки(&КонецПериода, Счет = &СчетАТТ,
		|		&ПорядокСубконто, Организация = &Организация И (Подразделение = &Подразделение ИЛИ Подразделение ЕСТЬ NULL) И Субконто1 В (&Номенклатура) И Субконто2 = &Склад) КАК Остатки
		|
		|	ЛЕВОЕ СОЕДИНЕНИЕ
		|	ВТ_ЦеныНоменклатуры
		|	ПО
		|		ВЫРАЗИТЬ(Остатки.Субконто1 КАК Справочник.Номенклатура) = ВТ_ЦеныНоменклатуры.Номенклатура
		|
		|ГДЕ
		|	Остатки.КоличествоОстаток > 0
		|";

		Результат = Запрос.ВыполнитьПакет();
		ТаблицаЦенНоменклатуры = Результат[2].Выгрузить();
		ТаблицаЦенНоменклатуры.Индексы.Добавить("Номенклатура");
		ТаблицаУчетныхЦен      = Результат[3].Выгрузить();
		ТаблицаУчетныхЦен.Индексы.Добавить("Номенклатура");

		Для Каждого СтрокаТаблицы Из Товары Цикл
			Префикс = "Товары[%1].";
			Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));

			ИмяСписка = НСтр("ru = 'Товары'");

			УчетнаяЦена   = 0;
			РозничнаяЦена = 0;

			СтрокаТаблицыЦен = ТаблицаУчетныхЦен.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
			Если СтрокаТаблицыЦен <> Неопределено Тогда
				УчетнаяЦена = СтрокаТаблицыЦен.УчетнаяЦена;
			Иначе
				УчетнаяЦена = 0;
			КонецЕсли;
			СтрокаТаблицыРозничныхЦен = ТаблицаЦенНоменклатуры.Найти(СтрокаТаблицы.Номенклатура, "Номенклатура");
			Если СтрокаТаблицыРозничныхЦен = Неопределено Тогда
				РозничнаяЦена = 0;
			Иначе
				РозничнаяЦена = СтрокаТаблицыРозничныхЦен.ЦенаВВалютеРегламентированногоУчета;
			КонецЕсли;

			//Допустимое отклонение цены составляет цену минимальной единицы количества (0.001)
			ДопустимоеОтклонение = УчетнаяЦена * 0.001;

			Если РозничнаяЦена = 0 Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Розничная цена товара ""%1"" из строки %2 списка ""%3"" не установлена.
					|Установите цену документом ""Установка цен номенклатуры"".'"),
					СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки, ИмяСписка);
				Поле = Префикс + "Номенклатура";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			ИначеЕсли УчетнаяЦена = 0 Тогда
				// Учетная цена неизвестна.
			ИначеЕсли РозничнаяЦена - УчетнаяЦена > ДопустимоеОтклонение
				ИЛИ РозничнаяЦена - УчетнаяЦена < - ДопустимоеОтклонение Тогда
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'Розничная цена товара ""%1"" из строки %2 списка ""%3"" (%4) не равна учетной цене (%5).
					|Измените цену документом ""Установка цен номенклатуры"" или проведите переоценку документом ""Переоценка товаров в рознице"".'"),
					СтрокаТаблицы.Номенклатура, СтрокаТаблицы.НомерСтроки, ИмяСписка, РозничнаяЦена, УчетнаяЦена);
				Поле = Префикс + "Номенклатура";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

КонецПроцедуры

Процедура УстановитьСтатусДокумента()
	
	Если ПроведениеСервер.ГрупповоеПерепроведение(ЭтотОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("СтатусДокумента")
		И УчетНДСПереопределяемый.НайтиПодчиненныйСчетФактуруПолученный(ЭтотОбъект.Ссылка) <> Неопределено Тогда
		// Записываем из списка документов. Движения по статусам сформирует счет-фактура.
		Возврат;
	КонецЕсли;
	
	Статусы = Новый Структура("СтатусДокумента, СтатусСчетаФактуры");
	ЗаполнитьЗначенияСвойств(Статусы, ДополнительныеСвойства);
	
	Если Статусы.СтатусСчетаФактуры = Неопределено Или ПометкаУдаления Тогда
		// При записи из формы не определили статус счета-фактуры. Рассчитаем его по реквизитам документа.
		Статусы.СтатусСчетаФактуры = СтатусСчетаФактурыПоУмолчанию();
	КонецЕсли;
	
	УстановленСтатусДокумента = Истина;
	РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокумента(ЭтотОбъект.Ссылка, Статусы.СтатусДокумента, , Статусы.СтатусСчетаФактуры);
	
КонецПроцедуры

Функция СтатусСчетаФактурыПоУмолчанию()
	
	Если ВидОперации = Перечисления.ВидыОперацийПоступлениеТоваровУслуг.ВПереработку Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	КонецЕсли;
	
	Если НДСНеВыделять
		Или Не Справочники.ДоговорыКонтрагентов.ТребуетсяСчетФактураПолученный(ДоговорКонтрагента) Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	ИначеЕсли НЕ РаботаСДоговорамиКонтрагентовБП.ДокументСНДС(ЭтотОбъект.Ссылка) Тогда
		Возврат Перечисления.СтатусыСчетаФактуры.НеТребуется;
	КонецЕсли;
	
	Возврат Перечисления.СтатусыСчетаФактуры.Отсутствует;
	
КонецФункции

//БИТ Тртилек 12022013 в процедуре устанавливается видимость вкладки собственных реализаций в зависимости от контрагента 
Функция УстановитьВидимостьРеализацияУСобственногоЮЛ() Экспорт
	
	ВидимостьВкладкиРеализацияУСобственногоЮЛ = ЛОЖЬ;
	
	Отбор = Новый Структура("Контрагент, ВидСвязи");
	Отбор.Контрагент = Контрагент;
	Отбор.ВидСвязи = Перечисления.бит_ВидыСобственныхКонтрагентов.Организация;
	НаборЗаписейАналитики = бит_ОбщегоНазначения.ПрочитатьНаборЗаписейРегистраСведений(Отбор,"бит_СобственныеКонтрагенты");
	
	//МассивОрганизаций = НаборЗаписейАналитики.ВыгрузитьКолонку("Объект");
	
	//Если НЕ МассивОрганизаций.Найти(Организация) = Неопределено Тогда
	Если НаборЗаписейАналитики.Количество() > 0 Тогда

		
		Если Оборудование.Количество() > 0 И ЗначениеЗаполнено(Оборудование[0].СчетУчета) Тогда
			Если бит_ОбщегоНазначения.ОбъектПодчинен(Оборудование[0].СчетУчета, ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке) ИЛИ бит_ОбщегоНазначения.ОбъектПодчинен(Оборудование[0].СчетУчета, ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы) Тогда
				ВидимостьВкладкиРеализацияУСобственногоЮЛ = Истина;	
			КонецЕсли;
		КонецЕсли;
		
		Если ОбъектыСтроительства.Количество() > 0 И ЗначениеЗаполнено(ОбъектыСтроительства[0].СчетУчета) Тогда
			Если бит_ОбщегоНазначения.ОбъектПодчинен(ОбъектыСтроительства[0].СчетУчета, ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке) ИЛИ бит_ОбщегоНазначения.ОбъектПодчинен(ОбъектыСтроительства[0].СчетУчета, ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы) Тогда
				ВидимостьВкладкиРеализацияУСобственногоЮЛ = Истина;	
			КонецЕсли;
		КонецЕсли;
		
		Если Услуги.Количество() > 0 И ЗначениеЗаполнено(Услуги[0].СчетЗатрат) Тогда
			Если бит_ОбщегоНазначения.ОбъектПодчинен(Услуги[0].СчетЗатрат, ПланыСчетов.Хозрасчетный.ОборудованиеКУстановке) ИЛИ бит_ОбщегоНазначения.ОбъектПодчинен(Услуги[0].СчетЗатрат, ПланыСчетов.Хозрасчетный.ВложенияВоВнеоборотныеАктивы) Тогда
				ВидимостьВкладкиРеализацияУСобственногоЮЛ = Истина;	
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;	
	
	Возврат ВидимостьВкладкиРеализацияУСобственногоЮЛ;
	
КонецФункции

// BIT AMerkulov 17-08-2015 ++
Процедура ПроверитьДокументПоУправленческимАналитикам(Отказ)
	
	Для Каждого СтрокаТабличнойЧасти Из Услуги Цикл
				
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
		//Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Объект) Тогда
		//Заменено на:
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_Аналитика_2) Тогда
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
			Для инд=1 По 3 Цикл
				Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.ОбъектыСтроительства") Тогда
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
					//Если СтрокаТабличнойЧасти["Субконто"+инд] <> СтрокаТабличнойЧасти.Объект Тогда
					//Заменено на:
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-15 (#3156)
					//Если СтрокаТабличнойЧасти["Субконто"+инд] <> СтрокаТабличнойЧасти.ок_Аналитика_2 Тогда
					//Заменено на:
					Если СтрокаТабличнойЧасти["Субконто"+инд]<>
						 ОК_ОбщегоНазначения.ОпределитьОбъектСтроительстваОтносительноАналитикиБК(СтрокаТабличнойЧасти["Субконто"+инд],СтрокаТабличнойЧасти.ок_Аналитика_2)
					Тогда 						 
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-15 (#3156)
					
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
						Сообщить("Несовпадение бухгалтерской и управленческой аналитик ""Объект"" в строке " + СтрокаТабличнойЧасти.НомерСтроки);
						Отказ = Истина;
					КонецЕсли;
				КонецЕсли;						
			КонецЦикла;
		КонецЕсли;
		
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)		
		//Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ЦФО) Тогда
		//Заменено на:
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_ЦФО) Тогда
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
			Для инд=1 По 3 Цикл
				Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.Подразделения") Тогда
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
					//Если СтрокаТабличнойЧасти["Субконто"+инд] <> СтрокаТабличнойЧасти.ЦФО Тогда
					//Заменено на:
					Если СтрокаТабличнойЧасти["Субконто"+инд] <> СтрокаТабличнойЧасти.ок_ЦФО Тогда
					//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
						Сообщить("Несовпадение бухгалтерской и управленческой аналитик ""ЦФО"" в строке " + СтрокаТабличнойЧасти.НомерСтроки);
						Отказ = Истина;							
					КонецЕсли;
				КонецЕсли;						
			КонецЦикла;
		КонецЕсли;
		
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
		//Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтатьяОборотов) Тогда
		//Заменено на:
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_СтатьяОборотов) Тогда
		//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
			
			Запрос = Новый Запрос;	
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов,
			|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл
			|ИЗ
			|	РегистрСведений.бит_СтатьиОборотов_СтатьиРегл КАК бит_СтатьиОборотов_СтатьиРегл
			|ГДЕ
			|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл ССЫЛКА Справочник.СтатьиЗатрат 
			| И бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов = &Статья";					   
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
			//Запрос.УстановитьПараметр("Статья",СтрокаТабличнойЧасти.СтатьяОборотов);
			//Заменено на:
			Запрос.УстановитьПараметр("Статья",СтрокаТабличнойЧасти.ок_СтатьяОборотов);
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
			ТаблицаСтатейЗатрат = Запрос.Выполнить().Выгрузить();
			
			Запрос = Новый Запрос;	
			Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов,
			|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл
			|ИЗ
			|	РегистрСведений.бит_СтатьиОборотов_СтатьиРегл КАК бит_СтатьиОборотов_СтатьиРегл
			|ГДЕ
			|	бит_СтатьиОборотов_СтатьиРегл.СтатьяРегл ССЫЛКА Справочник.ПрочиеДоходыИРасходы 
			| И бит_СтатьиОборотов_СтатьиРегл.СтатьяОборотов = &Статья";					   
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
			//Запрос.УстановитьПараметр("Статья",СтрокаТабличнойЧасти.СтатьяОборотов);
			//Заменено на:
			Запрос.УстановитьПараметр("Статья",СтрокаТабличнойЧасти.ок_СтатьяОборотов);
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
			ТаблицаПрочихДоходов = Запрос.Выполнить().Выгрузить();
			
			
			Если ТаблицаСтатейЗатрат.Количество() Тогда
				Для инд=1 По 3 Цикл
					Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.СтатьиЗатрат") Тогда
						Если ТаблицаСтатейЗатрат.Найти(СтрокаТабличнойЧасти["Субконто"+инд]) = Неопределено Тогда
							Сообщить("Несовпадение бухгалтерской и управленческой аналитик ""Статья затрат"" в строке " + СтрокаТабличнойЧасти.НомерСтроки);
							Отказ = Истина;														
						КонецЕсли;						
					КонецЕсли;						                                      
				КонецЦикла;
			КонецЕсли;	
			
			Если ТаблицаПрочихДоходов.Количество() Тогда
				Для инд=1 По 3 Цикл
					Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") Тогда						
						//ОК+ Аверьянова исправление ошибки AT-1403466 от 25.05.16
						//Если ТаблицаСтатейЗатрат.Найти(СтрокаТабличнойЧасти["Субконто"+инд]) = Неопределено Тогда
						Если ТаблицаПрочихДоходов.Найти(СтрокаТабличнойЧасти["Субконто"+инд]) = Неопределено Тогда
						//ОК-
							Сообщить("Несовпадение бухгалтерской и управленческой аналитик ""Прочие доходы/расходы"" в строке " + СтрокаТабличнойЧасти.НомерСтроки);
							Отказ = Истина;														
						КонецЕсли;						
					КонецЕсли;				
				КонецЦикла;
			КонецЕсли;			
		КонецЕсли;
		
		Для инд=1 По 3 Цикл			
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)			
			//Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.НоменклатурныеГруппы") И ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд]) И ЗначениеЗаполнено(СтрокаТабличнойЧасти.Объект) И СтрокаТабличнойЧасти.Объект <> СтрокаТабличнойЧасти["Субконто"+инд].Объект Тогда
			//Заменено на:	
			Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.НоменклатурныеГруппы") И ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд]) И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_Аналитика_2) И СтрокаТабличнойЧасти.ок_Аналитика_2 <> СтрокаТабличнойЧасти["Субконто"+инд].Объект Тогда
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)
			
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2019-01-29 (#3141)
			//Сообщить("Несовпадение бухгалтерской и управленческой аналитик ""Номенклатурная группа"" в строке " + СтрокаТабличнойЧасти.НомерСтроки);
			//Заменено на:
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Реквизит «объект» в номенклатурной группе в БУ не совпадает с объектом в управленческой аналитике'"));
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2019-01-29 (#3141)				
				Отказ = Истина;
			КонецЕсли;
			
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-07-11 (#3048)
			//Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.ОсновныеСредства") И ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд])  И ЗначениеЗаполнено(СтрокаТабличнойЧасти.Объект) И СтрокаТабличнойЧасти.Объект <> СтрокаТабличнойЧасти["Субконто"+инд].Объект   Тогда
			//Заменено на:
			Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.ОсновныеСредства") И ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд])  И ЗначениеЗаполнено(СтрокаТабличнойЧасти.ок_Аналитика_2) И СтрокаТабличнойЧасти.ок_Аналитика_2 <> СтрокаТабличнойЧасти["Субконто"+инд].Объект   Тогда
			//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-07-11 (#3048)			
				Сообщить("Несовпадение бухгалтерской и управленческой аналитик ""Основные средства"" в строке " + СтрокаТабличнойЧасти.НомерСтроки);
				Отказ = Истина;
			КонецЕсли;
			//ОКЕЙ Поздняков А.С. (СофтЛаб) Начало 2020-06-01 (#3713)
			Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.СтатьиЗатрат") 
				И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд]) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В табличной части ""Услуги"" в строке (" + СтрокаТабличнойЧасти.НомерСтроки + ") не заполнено поле ""Субконто " + инд + """ (Статья затрат).");
				Отказ = Истина;
			КонецЕсли;
			Если ТипЗнч(СтрокаТабличнойЧасти["Субконто"+инд]) = Тип("СправочникСсылка.ПрочиеДоходыИРасходы") 
				И НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти["Субконто"+инд]) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю("В табличной части ""Услуги"" в строке (" + СтрокаТабличнойЧасти.НомерСтроки + ") не заполнено поле ""Субконто " + инд + """ (Прочие доходы и расходы).");
				Отказ = Истина;
			КонецЕсли;
			//ОКЕЙ Поздняков А.С. (СофтЛаб) Конец 2020-06-01 (#3713)
		КонецЦикла;		
		
	КонецЦикла;
	
	
КонецПроцедуры

//ОКЕЙ Лобанов В.И.(СофтЛаб) Начало 2018-12-24 (#3048)
Функция ПолучитьНомерЗаявки()
	
	Перем пНомерЗаявки;
		
	Если ДополнительныеСвойства.Свойство("ДополнительныеАналитики") И 
		 ДополнительныеСвойства.ДополнительныеАналитики.Получить("NЗАЯВКИ")<>Неопределено
	Тогда 
		
		пНомерЗаявки =  ДополнительныеСвойства.ДополнительныеАналитики["NЗАЯВКИ"];	
		
	Иначе 
		
		пНомерЗаявки = бит_МеханизмДопИзмерений.ПолучитьЗначениеДопАналитики(Ссылка,
									   	   ПланыВидовХарактеристик.бит_ВидыДополнительныхАналитик.НайтиПоКоду("NЗАЯВКИ"), ПредопределенноеЗначение("Документ.бит_ФормаВводаБюджета.ПустаяСсылка"));
		
	КонецЕсли;

	Если ТипЗнч(пНомерЗаявки) <> Тип("ДокументСсылка.бит_ФормаВводаБюджета") Тогда 
		пНомерЗаявки = ПредопределенноеЗначение("Документ.бит_ФормаВводаБюджета.ПустаяСсылка");
	КонецЕсли;
	
	Возврат пНомерЗаявки;
	
КонецФункции
//ОКЕЙ Лобанов В.И.(СофтЛаб) Конец 2018-12-24 (#3048)

#КонецОбласти

//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-07-21 (#4214)
Процедура ПроверитьПоляПрослеживаемости(ВозможнаКомиссияПоЗакупке, ЭтоВвозИзЕАЭС, ИмяТаблицы, Отказ)
	
	Для Каждого СтрокаТаблицы Из ЭтотОбъект[ИмяТаблицы] Цикл
		Префикс = ИмяТаблицы + "[%1].";
		Префикс = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Префикс, Формат(СтрокаТаблицы.НомерСтроки - 1, "ЧН=0; ЧГ="));
		
		// Проверка договора и счета расчетов с комитентом
		Если ИмяТаблицы = "Товары"
			И ВозможнаКомиссияПоЗакупке И ЗначениеЗаполнено(СтрокаТаблицы.Контрагент) Тогда
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.ДоговорКонтрагента) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Договор с комитентом'"),
				СтрокаТаблицы.НомерСтроки, ИмяТаблицы);
				Поле = Префикс + "ДоговорКонтрагента";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
		КонецЕсли;
		
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Начало 2021-11-03 (#4405)
		//Если СтрокаТаблицы.ПрослеживаемыйТовар 
		//	И НЕ ЭтоВвозИзЕАЭС
		Если ИмяТаблицы = "ОсновныеСредства"
			И СтрокаТаблицы.ПрослеживаемыйТовар Тогда
			Отбор = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
			МассивСтрокСведенияПрослеживаемости = СведенияПрослеживаемости.Выгрузить(Отбор);
			КоличествоРНПТ = МассивСтрокСведенияПрослеживаемости.Количество();
			Если КоличествоРНПТ = 0 Тогда
				ТекстСообщенияРасшифрвка = НСтр("ru = 'Для прослеживаемого основного средства необходимо указать номер РНПТ.'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка","Корректность", НСтр("ru = 'РНПТ'"),
				СтрокаТаблицы.НомерСтроки, НСтр("ru = 'Основные средства'"), ТекстСообщенияРасшифрвка);
				Поле = Префикс + "РНПТ";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			ИначеЕсли КоличествоРНПТ > 1 Или Не ЗначениеЗаполнено(МассивСтрокСведенияПрослеживаемости[0].Номенклатура) Тогда
				СуммаБезНДС = ?(СуммаВключаетНДС, СтрокаТаблицы.Сумма - СтрокаТаблицы.СуммаНДС, СтрокаТаблицы.Сумма);
				Если СуммаБезНДС < МассивСтрокСведенияПрослеживаемости.Итог("Сумма") Тогда
					ТекстСообщенияРасшифрвка = НСтр("ru = 'Итоговая сумма по таблице с данными РНПТ превышает стоимость основного средства (без учета НДС).'");
					ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Список","Корректность", НСтр("ru = 'РНПТ'"),
					СтрокаТаблицы.НомерСтроки, НСтр("ru = 'РНПТ'"), ТекстСообщенияРасшифрвка);
					Поле = Префикс + "РНПТ";
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
			
		Если СтрокаТаблицы.ПрослеживаемыйТовар
			И НЕ ИмяТаблицы = "ОсновныеСредства"
			И НЕ ЭтоВвозИзЕАЭС
		//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-11-03 (#4405) 
			И СтрокаТаблицы.Количество > 0 Тогда
			// Проверка заполнения таблицы СведенияПрослеживаемости
			СтруктураОтбора = Новый Структура("ИдентификаторСтроки", СтрокаТаблицы.ИдентификаторСтроки);
			МассивСтрокСведенияПрослеживаемости = СведенияПрослеживаемости.НайтиСтроки(СтруктураОтбора);
			Если МассивСтрокСведенияПрослеживаемости.Количество() = 0 
				ИЛИ (НЕ ЕстьРасхождения И ПрослеживаемостьБП.КоличествоРНПТКСтроке(МассивСтрокСведенияПрослеживаемости) <> СтрокаТаблицы.Количество) Тогда
				ТекстСообщенияРасшифрвка = НСтр("ru = 'Количество РНПТ не совпадает с количеством товара в строке.'");
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка","Корректность", НСтр("ru = 'РНПТ'"),
				СтрокаТаблицы.НомерСтроки, ИмяТаблицы, ТекстСообщенияРасшифрвка);
				Поле = Префикс + "РНПТ";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			
			// Для прослеживаемого товара обязательно указывается страна происхождения.
			Если НЕ ЗначениеЗаполнено(СтрокаТаблицы.СтранаПроисхождения) Тогда
				ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения("Колонка",, НСтр("ru = 'Страна происхождения'"),
					СтрокаТаблицы.НомерСтроки, ИмяТаблицы);
				Поле = Префикс + "СтранаПроисхождения";
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			КонецЕсли;
			
		КонецЕсли
		
	КонецЦикла;

КонецПроцедуры
//ОКЕЙ Назаренко Д.В. (СофтЛаб) Конец 2021-07-21 (#4214) 
#Область Инициализация

УстановленСтатусДокумента = Ложь;

#КонецОбласти

#КонецЕсли