
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.Проведен Тогда
		ЭтаФорма.ТолькоПросмотр = Истина;		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНаСервере()
	
	ДокОбъект = РеквизитФормыВЗначение("Объект");
	ок_ДетализацияЗатратНаРемонтОС.ЗаполнитьОбновитьЗатратыРасшифровки(ДокОбъект);	
	ЗначениеВРеквизитФормы(ДокОбъект, "Объект");
	
КонецПроцедуры

&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияЗатратПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияЗатратСуммаПриИзменении(Элемент)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Функция ПолучитьМассивОС(Знач Организация, Знач ОбъектСтроительства, Знач БИТ_ГруппаОСУУ)
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ОсновныеСредства.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(, Организация = &Организация) КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ОсновныеСредства КАК ОсновныеСредства
	                      |		ПО ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = ОсновныеСредства.Ссылка
	                      |			И (ОсновныеСредства.Объект = &Объект)
	                      |			И (ОсновныеСредства.БИТ_ГруппаОСУУ = &БИТ_ГруппаОСУУ)
	                      |			И (НЕ ОсновныеСредства.ПометкаУдаления)
	                      |			И (НЕ ОсновныеСредства.ЭтоГруппа)");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Объект", ОбъектСтроительства);
	Запрос.УстановитьПараметр("БИТ_ГруппаОСУУ", БИТ_ГруппаОСУУ);
	
	Возврат Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Ссылка");
	
КонецФункции

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ЗаписьДетализацииЗатратОС", Новый Структура("ДокументРасшифровки", Объект.Ссылка));
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияЗатратПриАктивизацииЯчейки(Элемент)
	
	Если Элемент.ТекущийЭлемент = Элементы.ДетализацияЗатратОсновноеСредство  Тогда 
		
		ТекущиеДанные = Элемент.ТекущиеДанные;
		
		Если  ТекущиеДанные <> Неопределено Тогда 
			
			Организация = Объект.Организация;
			ОбъектСтроительства = ТекущиеДанные.Объект;
			БИТ_ГруппаОСУУ = ок_ДетализацияЗатратНаРемонтОС.ПолучитьСоответствиеСтатьиЗатратГруппеОС(ТекущиеДанные.СтатьяОборотов);
			
			МассивОС = ПолучитьМассивОС(Организация,ОбъектСтроительства,БИТ_ГруппаОСУУ);
			
			МассивПараметровВыбора = Новый Массив();	
			ПараметрВыбора = Новый ПараметрВыбора("Отбор.Ссылка",МассивОС);	
			МассивПараметровВыбора.Добавить(ПараметрВыбора);
			МассивПараметровВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
			Элементы.ДетализацияЗатратОсновноеСредство.ПараметрыВыбора = МассивПараметровВыбора;
			
		КонецЕсли;
		
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияЗатратПослеУдаления(Элемент)
	
	ЗаполнитьНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДетализацияЗатратОсновноеСредствоОчисткаНаСервере()
	ТаблицаСвертки = Объект.ДетализацияЗатрат.Выгрузить();
	ТаблицаСвертки.Свернуть("ПериодЗатрат,ЦФО,Объект,СтатьяОборотов,ОсновноеСредство","Сумма");
	Объект.ДетализацияЗатрат.Загрузить(ТаблицаСвертки);
	ЗаполнитьНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияЗатратОсновноеСредствоОчистка(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элементы.ДетализацияЗатрат.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		ТекущиеДанные.ОсновноеСредство = Неопределено;
		ДетализацияЗатратОсновноеСредствоОчисткаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДетализацияЗатратОсновноеСредствоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДетализацияЗатрат.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И Не ЗначениеЗаполнено(ТекущиеДанные.ОсновноеСредство)  Тогда 
		ДетализацияЗатратОсновноеСредствоОчисткаНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтруктураПодчиненности(Команда)
	
	ФормаСтруктурыПодчиненности = ПолучитьФорму("ОбщаяФорма.бит_ФормаСтруктурыПодчиненностиТонкийКлиент");
	Если ФормаСтруктурыПодчиненности.Открыта() Тогда
		ФормаСтруктурыПодчиненности.Закрыть();
	КонецЕсли;
	ФормаСтруктурыПодчиненности.ДокументСсылка = Объект.Ссылка;
	ФормаСтруктурыПодчиненности.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗакрыть(Команда)
	
	Если Записать() Тогда 
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// стандартные действия при создании на сервере
	бит_РаботаСДиалогамиСервер.ФормаОбъектаПриСозданииНаСервере(Отказ, СтандартнаяОбработка, ЭтаФорма, Объект);
	
КонецПроцедуры





