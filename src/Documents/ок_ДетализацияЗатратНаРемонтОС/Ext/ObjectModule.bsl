
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	// стандартная процедура для заполнения основных типовых реквизитов для нового документа
	ЗаполнениеДокументов.Заполнить(ЭтотОбъект, ДанныеЗаполнения);
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.бит_ФормаВводаБюджета") Тогда
		
		Дата = ДанныеЗаполнения.Дата;
		ДокументОснование = ДанныеЗаполнения;
		Организация = ДанныеЗаполнения.бит_БК_Организация;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	Движения.ок_ДетализацияЗатратНаРемонтОС.Записывать = Истина;
	
	Для Каждого СтрокаДетализации Из ДетализацияЗатрат Цикл	
		Запись = Движения.ок_ДетализацияЗатратНаРемонтОС.Добавить(); 
		ЗаполнитьЗначенияСвойств(Запись,СтрокаДетализации);
		Запись.ЗаявкаФВБ = ДокументОснование;
		Запись.Период = Дата;
	КонецЦикла;
	
КонецПроцедуры

Процедура КонтрольСоответствияСуммРасшифровки(Отказ)
	
	Если Организация <> ДокументОснование.бит_БК_Организация Тогда 
		Сообщить("Компания-плательщик ФВБ заявки и организация документа Детализация не равны.");
		Отказ = Истина;
		Возврат;
	КонецЕсли;	
		
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	бит_ФормаВводаБюджетаБДДС.ЦФО КАК ЦФО,
	                      |	бит_ФормаВводаБюджетаБДДС.СтатьяОборотов КАК СтатьяОборотов,
	                      |	бит_ФормаВводаБюджетаБДДС.Аналитика_2 КАК Объект,
	                      |	бит_ФормаВводаБюджетаБДДС.Период КАК ПериодЗатрат,
	                      |	СУММА(бит_ФормаВводаБюджетаБДДС.бит_БК_СуммаБезНДС) КАК Сумма
	                      |ПОМЕСТИТЬ втДанныеЗаявки
	                      |ИЗ
	                      |	Документ.бит_ФормаВводаБюджета.БДДС КАК бит_ФормаВводаБюджетаБДДС
	                      |ГДЕ
	                      |	бит_ФормаВводаБюджетаБДДС.Ссылка = &ФормаВводаБюджета
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	бит_ФормаВводаБюджетаБДДС.Период,
	                      |	бит_ФормаВводаБюджетаБДДС.СтатьяОборотов,
	                      |	бит_ФормаВводаБюджетаБДДС.ЦФО,
	                      |	бит_ФормаВводаБюджетаБДДС.Аналитика_2
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	бит_СоответствияАналитик.ЛеваяАналитика_1 КАК ЛеваяАналитика_1,
	                      |	бит_СоответствияАналитик.ЛеваяАналитика_2 КАК ЛеваяАналитика_2,
	                      |	бит_СоответствияАналитик.ЛеваяАналитика_3 КАК ЛеваяАналитика_3,
	                      |	бит_СоответствияАналитик.ПраваяАналитика_1 КАК ПраваяАналитика_1,
	                      |	бит_СоответствияАналитик.ПраваяАналитика_2 КАК ПраваяАналитика_2,
	                      |	бит_СоответствияАналитик.ПраваяАналитика_3 КАК ПраваяАналитика_3
	                      |ПОМЕСТИТЬ втСоотстветствияАналитик
	                      |ИЗ
	                      |	РегистрСведений.бит_СоответствияАналитик КАК бит_СоответствияАналитик
	                      |ГДЕ
	                      |	бит_СоответствияАналитик.ВидСоответствия = &ВидСоответствия
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	втЗаявка.ЦФО КАК ЦФО,
	                      |	втЗаявка.СтатьяОборотов КАК СтатьяОборотов,
	                      |	втЗаявка.Объект КАК Объект,
	                      |	втЗаявка.Сумма КАК Сумма,
	                      |	втЗаявка.ПериодЗатрат КАК ПериодЗатрат
	                      |ПОМЕСТИТЬ ВТ_ФормаВводаБюджета
	                      |ИЗ
	                      |	втДанныеЗаявки КАК втЗаявка
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСоотстветствияАналитик КАК втСоотстветствияАналитик
	                      |		ПО втЗаявка.ЦФО = втСоотстветствияАналитик.ЛеваяАналитика_1
	                      |			И втЗаявка.СтатьяОборотов = втСоотстветствияАналитик.ПраваяАналитика_1
	                      |
	                      |ОБЪЕДИНИТЬ
	                      |
	                      |ВЫБРАТЬ
	                      |	втЗаявка.ЦФО,
	                      |	втЗаявка.СтатьяОборотов,
	                      |	втЗаявка.Объект,
	                      |	втЗаявка.Сумма,
	                      |	втЗаявка.ПериодЗатрат
	                      |ИЗ
	                      |	втДанныеЗаявки КАК втЗаявка
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСоотстветствияАналитик КАК втСоотстветствияАналитик
	                      |		ПО втЗаявка.ЦФО = втСоотстветствияАналитик.ЛеваяАналитика_2
	                      |			И втЗаявка.СтатьяОборотов = втСоотстветствияАналитик.ПраваяАналитика_2
	                      |
	                      |ОБЪЕДИНИТЬ
	                      |
	                      |ВЫБРАТЬ
	                      |	втЗаявка.ЦФО,
	                      |	втЗаявка.СтатьяОборотов,
	                      |	втЗаявка.Объект,
	                      |	втЗаявка.Сумма,
	                      |	втЗаявка.ПериодЗатрат
	                      |ИЗ
	                      |	втДанныеЗаявки КАК втЗаявка
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втСоотстветствияАналитик КАК втСоотстветствияАналитик
	                      |		ПО втЗаявка.ЦФО = втСоотстветствияАналитик.ЛеваяАналитика_3
	                      |			И втЗаявка.СтатьяОборотов = втСоотстветствияАналитик.ПраваяАналитика_3
	                      |;
	                      |
	                      |////////////////////////////////////////////////////////////////////////////////
	                      |ВЫБРАТЬ
	                      |	ВложенныйЗапрос.ПериодЗатрат КАК ПериодЗатрат,
	                      |	ВложенныйЗапрос.ЦФО КАК ЦФО,
	                      |	ВложенныйЗапрос.СтатьяОборотов КАК СтатьяОборотов,
	                      |	ВложенныйЗапрос.Объект КАК Объект,
	                      |	СУММА(ВложенныйЗапрос.Сумма) КАК Сумма
	                      |ИЗ
	                      |	(ВЫБРАТЬ
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.ПериодЗатрат КАК ПериодЗатрат,
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.ЦФО КАК ЦФО,
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.СтатьяОборотов КАК СтатьяОборотов,
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.Объект КАК Объект,
	                      |		-СУММА(ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.Сумма) КАК Сумма
	                      |	ИЗ
	                      |		Документ.ок_ДетализацияЗатратНаРемонтОС.ДетализацияЗатрат КАК ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат
	                      |	ГДЕ
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.Ссылка = &ДетализацияЗатратНаРемонтОС
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.ПериодЗатрат,
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.СтатьяОборотов,
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.ЦФО,
	                      |		ок_ДетализацияЗатратНаРемонтОСДетализацияЗатрат.Объект
	                      |	
	                      |	ОБЪЕДИНИТЬ ВСЕ
	                      |	
	                      |	ВЫБРАТЬ
	                      |		бит_ФормаВводаБюджетаБДДС.ПериодЗатрат,
	                      |		бит_ФормаВводаБюджетаБДДС.ЦФО,
	                      |		бит_ФормаВводаБюджетаБДДС.СтатьяОборотов,
	                      |		бит_ФормаВводаБюджетаБДДС.Объект,
	                      |		СУММА(бит_ФормаВводаБюджетаБДДС.Сумма)
	                      |	ИЗ
	                      |		ВТ_ФормаВводаБюджета КАК бит_ФормаВводаБюджетаБДДС
	                      |	
	                      |	СГРУППИРОВАТЬ ПО
	                      |		бит_ФормаВводаБюджетаБДДС.ПериодЗатрат,
	                      |		бит_ФормаВводаБюджетаБДДС.СтатьяОборотов,
	                      |		бит_ФормаВводаБюджетаБДДС.ЦФО,
	                      |		бит_ФормаВводаБюджетаБДДС.Объект) КАК ВложенныйЗапрос
	                      |
	                      |СГРУППИРОВАТЬ ПО
	                      |	ВложенныйЗапрос.СтатьяОборотов,
	                      |	ВложенныйЗапрос.ЦФО,
	                      |	ВложенныйЗапрос.ПериодЗатрат,
	                      |	ВложенныйЗапрос.Объект
	                      |
	                      |ИМЕЮЩИЕ
	                      |	СУММА(ВложенныйЗапрос.Сумма) <> 0");
	
	Запрос.УстановитьПараметр("ДетализацияЗатратНаРемонтОС",Ссылка);
	Запрос.УстановитьПараметр("ФормаВводаБюджета",ДокументОснование);
	Запрос.УстановитьПараметр("ВидСоответствия", Справочники.бит_ВидыСоответствийАналитик.ЦФО_СтатьяОборотов);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если Не РезультатЗапроса.Пустой() Тогда 
		
		Отказ = Истина;
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл 
			
			Если Выборка.Сумма < 0 Тогда
				ДопТекст = "превышает сумму заявки на " + Строка(-Выборка.Сумма);
			Иначе 
				ДопТекст = "меньше суммы заявки на " + Строка(Выборка.Сумма);
			КонецЕсли;	
						
			ТекстСообщения = "Сумма детализации по аналитикам: 
			                 |Период - %1 
			                 |ЦФО - %2 
			                 |Объект - %3 
			                 |Статья оборотов - %4 
			                 |%5. Внесите изменения.";
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,Выборка.ПериодЗатрат,Выборка.ЦФО,Выборка.Объект,Выборка.СтатьяОборотов,ДопТекст);
			Сообщить(ТекстСообщения);
			
		КонецЦикла;	
		
	КонецЕсли;	
	
КонецПроцедуры	

Процедура ПриЗаписи(Отказ)
	
	Если ДополнительныеСвойства.Свойство("КонтролироватьСоответствиеСуммРасшифровки") Тогда
		КонтролироватьСоответствиеСуммРасшифровки  = ДополнительныеСвойства.КонтролироватьСоответствиеСуммРасшифровки;
	Иначе 
		КонтролироватьСоответствиеСуммРасшифровки = Истина;
	КонецЕсли;	
	
	Если КонтролироватьСоответствиеСуммРасшифровки = Истина Тогда 
		КонтрольСоответствияСуммРасшифровки(Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриУстановкеНовогоНомера(СтандартнаяОбработка, Префикс)
	
	Префикс = ПрефиксацияОбъектовБПСобытия.ПрефиксИнформационнойБазыОрганизацииПодразделения(Организация, Неопределено, Префикс);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если Не ЭтоНовый() Тогда
		
		СтарыеРеквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка,"Дата,Организация");
		
		Если Организация <> СтарыеРеквизиты.Организация
			ИЛИ Не ПрефиксацияОбъектовСлужебный.ДатыОбъектаОдногоПериода(СтарыеРеквизиты.Дата,Дата,Ссылка) Тогда
			Номер = "";
		КонецЕсли;
		
	КонецЕсли;
	
	// Посчитать суммы документа и записать ее в соответствующий реквизит шапки для показа в журналах
	СуммаДокумента = УчетНДСПереопределяемый.ПолучитьСуммуДокументаСНДС(ЭтотОбъект, "ДетализацияЗатрат");
		
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)

	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	Ответственный = Пользователи.ТекущийПользователь();
	
КонецПроцедуры