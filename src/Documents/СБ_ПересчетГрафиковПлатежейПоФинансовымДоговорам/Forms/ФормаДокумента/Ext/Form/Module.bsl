&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимость();
	
	Если Параметры.Ключ.Пустая() Тогда
		УстановитьСостояниеДокумента();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь; // 1c-izhtc, ChuckNorris, 26.08.2015
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.СБ_ВидыОперацийПересчетГрафиковПлатежейПоФинансовымДоговорам.ПереоценкаВалютныхСумм")
		И Объект.ПереоценкаВалютныхСумм.Количество() <> 0 Тогда
		ТекстВопроса = НСтр("ru='При смене вида операции данные по переоценке валютных сумм будут очищены. Продолжить?'");
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.СБ_ВидыОперацийПересчетГрафиковПлатежейПоФинансовымДоговорам.ПересчетПроцентовПоПлавающимСтавкам")
		И Объект.ПересчетПроцентовПоПлавающимСтавкам.Количество() <> 0 Тогда
		ТекстВопроса = НСтр("ru='При смене вида операции данные по пересчету процентов по плавающим ставкам будут очищены. Продолжить?'");
	Иначе
		Модифицированность = Истина;
		Объект.ВидОперации = ВыбранноеЗначение;	
		УстановитьВидимость();
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВидОперацииОбработкаВыбораЗавершение", ЭтотОбъект, Новый Структура("ВыбранноеЗначение", ВыбранноеЗначение));
	ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидОперацииОбработкаВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда Возврат; КонецЕсли;
	
	Модифицированность = Истина;
	Объект.ВидОперации = ДополнительныеПараметры.ВыбранноеЗначение;
	Объект.ПереоценкаВалютныхСумм.Очистить();
	Объект.ПересчетПроцентовПоПлавающимСтавкам.Очистить();
	УстановитьВидимость();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимость()

	Элементы.ПереоценкаВалютныхСумм.Видимость = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.СБ_ВидыОперацийПересчетГрафиковПлатежейПоФинансовымДоговорам.ПереоценкаВалютныхСумм");
	Элементы.ПересчетПроцентовПоПлавающимСтавкам.Видимость = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.СБ_ВидыОперацийПересчетГрафиковПлатежейПоФинансовымДоговорам.ПересчетПроцентовПоПлавающимСтавкам");
		
Конецпроцедуры

&НаКлиенте
Процедура ЗаполнитьПереоценку(Команда)
	
	Если Объект.ПереоценкаВалютныхСумм.Количество() <> 0 Тогда
		Оповещение = Новый ОписаниеОповещения("НеобходимаОчисткаПереоценкаВалютныхСуммЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Перед заполнением табличная часть будет очищена. Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПереоценкуНаСервере();
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура НеобходимаОчисткаПереоценкаВалютныхСуммЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда Возврат; КонецЕсли;
	Объект.ПереоценкаВалютныхСумм.Очистить();
	ЗаполнитьПереоценкуНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПересчет(Команда)
	
	Если Объект.ПересчетПроцентовПоПлавающимСтавкам.Количество() <> 0 Тогда
		Оповещение = Новый ОписаниеОповещения("НеобходимаОчисткаПересчетПроцентовПоПлавающимСтавкамЗавершение", ЭтотОбъект);
		ТекстВопроса = НСтр("ru='Перед заполнением табличная часть будет очищена. Продолжить?'");
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	Иначе
		ЗаполнитьПересчетНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НеобходимаОчисткаПересчетПроцентовПоПлавающимСтавкамЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда Возврат; КонецЕсли;
	Объект.ПересчетПроцентовПоПлавающимСтавкам.Очистить();
	ЗаполнитьПересчетНаСервере();
	
КонецПроцедуры

// Процедура производит заполнение табличной части "ПереоценкаВалютныхСумм"
&НаСервере
Функция ЗаполнитьПереоценкуНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_му_Настройки.Значение
	               |ПОМЕСТИТЬ ВТ_СтатьиОборотов
	               |ИЗ
	               |	РегистрСведений.бит_му_Настройки КАК бит_му_Настройки
	               |ГДЕ
	               |	бит_му_Настройки.Группа = ""Пересчет графиков платежей по финансовым договорам""
	               |	И (бит_му_Настройки.ИмяНастройки = ""Статья оборотов – тело займа""
	               |			ИЛИ бит_му_Настройки.ИмяНастройки = ""Статья оборотов – проценты по займу"")
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	бит_ГрафикиДоговоровОбороты.Контрагент,
	               |	бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента,
	               |	бит_ГрафикиДоговоровОбороты.ДокументРегистратор КАК Документ,
	               |	бит_ГрафикиДоговоровОбороты.Период КАК ПериодПлатежа,
	               |	бит_ГрафикиДоговоровОбороты.Сценарий,
	               |	бит_ГрафикиДоговоровОбороты.ЦФО,
	               |	бит_ГрафикиДоговоровОбороты.СтатьяОборотов,
	               |	бит_ГрафикиДоговоровОбороты.СоставляющаяПлатежа,
	               |	СУММА(бит_ГрафикиДоговоровОбороты.СуммаСценарийОборот) КАК СуммаСценария,
	               |	СУММА(бит_ГрафикиДоговоровОбороты.СуммаУпрОборот) КАК СуммаУпр,
	               |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) КАК КурсВалюты,
	               |	СУММА(бит_ГрафикиДоговоровОбороты.СуммаСценарийОборот * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0)) КАК СуммаУпрПослеПереоценки,
	               |	СУММА(бит_ГрафикиДоговоровОбороты.СуммаСценарийОборот * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0) - бит_ГрафикиДоговоровОбороты.СуммаУпрОборот) КАК СуммаПереоценки
	               |ИЗ
	               |	РегистрНакопления.бит_ГрафикиДоговоров.Обороты(
	               |			&Дата,
	               |			,
	               |			Регистратор,
	               |			СтатьяОборотов В
	               |					(ВЫБРАТЬ
	               |						ВТ.Значение
	               |					ИЗ
	               |						ВТ_СтатьиОборотов КАК ВТ)
	               |				И ДоговорКонтрагента.Организация = &Организация
	               |				И ДоговорКонтрагента.ВалютаВзаиморасчетов.Код <> ""643"") КАК бит_ГрафикиДоговоровОбороты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютСрезПоследних
	               |		ПО бит_ГрафикиДоговоровОбороты.Сценарий.Валюта = КурсыВалютСрезПоследних.Валюта
	               |ГДЕ
	               |	бит_ГрафикиДоговоровОбороты.Регистратор <> &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	бит_ГрафикиДоговоровОбороты.Контрагент,
	               |	бит_ГрафикиДоговоровОбороты.Период,
	               |	бит_ГрафикиДоговоровОбороты.Сценарий,
	               |	бит_ГрафикиДоговоровОбороты.ДоговорКонтрагента,
	               |	бит_ГрафикиДоговоровОбороты.ЦФО,
	               |	бит_ГрафикиДоговоровОбороты.СтатьяОборотов,
	               |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 0),
	               |	бит_ГрафикиДоговоровОбороты.СоставляющаяПлатежа,
	               |	бит_ГрафикиДоговоровОбороты.ДокументРегистратор"; 
				   
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	Объект.ПереоценкаВалютныхСумм.Загрузить(РезультатЗапроса);
	
КонецФункции

// Процедура производит заполнение табличной части "ПересчетПроцентовПоПлавающимСтавкам"
&НаСервере
Процедура ЗаполнитьПересчетНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	бит_му_Настройки.Значение,
	               |	бит_му_Настройки.ИмяНастройки
	               |ПОМЕСТИТЬ ВТ_СтатьиОборотов
	               |ИЗ
	               |	РегистрСведений.бит_му_Настройки КАК бит_му_Настройки
	               |ГДЕ
	               |	бит_му_Настройки.Группа = ""Пересчет графиков платежей по финансовым договорам""
	               |	И (бит_му_Настройки.ИмяНастройки = ""Статья оборотов – тело займа""
	               |			ИЛИ бит_му_Настройки.ИмяНастройки = ""Статья оборотов – проценты по займу"")
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОборотыТекущие.Сценарий,
	               |	ОборотыТекущие.ЦФО,
	               |	ОборотыТекущие.СтатьяОборотов,
	               |	ОборотыТекущие.ДокументРегистратор,
	               |	ОборотыТекущие.Регистратор,
	               |	ОборотыТекущие.ТипСтатьи,
	               |	ОборотыТекущие.Контрагент,
	               |	ОборотыТекущие.ДоговорКонтрагента,
	               |	ОборотыТекущие.Проект,
	               |	ОборотыТекущие.НоменклатурнаяГруппа,
	               |	ОборотыТекущие.Аналитика_1,
	               |	ОборотыТекущие.Аналитика_2,
	               |	ОборотыТекущие.Аналитика_3,
	               |	ОборотыТекущие.Аналитика_4,
				   // 1c-izhtc, ChuckNorris, 25.08.2015 ( 
				   |	ОборотыТекущие.Аналитика_5,
				   // 1c-izhtc, ChuckNorris, 25.08.2015 ) 
	               |	ОборотыТекущие.СоставляющаяПлатежа,
	               |	ОборотыТекущие.СБ_ДатаДокумента,
	               |	СУММА(ОборотыТекущие.СуммаРеглОборот) КАК СуммаРеглОборот,
	               |	СУММА(ОборотыТекущие.СуммаБезНДСРеглОборот) КАК СуммаБезНДСРеглОборот,
	               |	СУММА(ОборотыТекущие.СуммаУпрОборот) КАК СуммаУпрОборот,
	               |	СУММА(ОборотыТекущие.СуммаБезНДСУпрОборот) КАК СуммаБезНДСУпрОборот,
	               |	СУММА(ОборотыТекущие.СуммаСценарийОборот) КАК СуммаСценарийОборот,
	               |	СУММА(ОборотыТекущие.СуммаБезНДССценарийОборот) КАК СуммаБезНДССценарийОборот,
	               |	ОборотыТекущие.Период
	               |ПОМЕСТИТЬ ОборотыГрафиков
	               |ИЗ
	               |	РегистрНакопления.бит_ГрафикиДоговоров.Обороты(
	               |			,
	               |			,
	               |			Регистратор,
	               |			СтатьяОборотов В
	               |					(ВЫБРАТЬ
	               |						ВТ.Значение
	               |					ИЗ
	               |						ВТ_СтатьиОборотов КАК ВТ)
	               |				И ДоговорКонтрагента.Организация = &Организация
	               |				И СоставляющаяПлатежа <> ЗНАЧЕНИЕ(Справочник.бит_СоставляющиеПлатежейПоФинДоговорам.ПустаяСсылка)) КАК ОборотыТекущие
	               |ГДЕ
	               |	(ОборотыТекущие.СоставляющаяПлатежа = ЗНАЧЕНИЕ(Справочник.бит_СоставляющиеПлатежейПоФинДоговорам.ПогашениеОсновногоДолга)
	               |			ИЛИ ОборотыТекущие.СоставляющаяПлатежа = ЗНАЧЕНИЕ(Справочник.бит_СоставляющиеПлатежейПоФинДоговорам.Проценты))
	               |	И ОборотыТекущие.Регистратор <> &Ссылка
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОборотыТекущие.ДоговорКонтрагента,
	               |	ОборотыТекущие.Проект,
	               |	ОборотыТекущие.Аналитика_1,
	               |	ОборотыТекущие.НоменклатурнаяГруппа,
				   // 1c-izhtc, ChuckNorris, 25.08.2015 ( 
	               |	ОборотыТекущие.Аналитика_5,
				   // 1c-izhtc, ChuckNorris, 25.08.2015 ) 
	               |	ОборотыТекущие.Аналитика_2,
	               |	ОборотыТекущие.ЦФО,
	               |	ОборотыТекущие.ДокументРегистратор,
	               |	ОборотыТекущие.Регистратор,
	               |	ОборотыТекущие.ТипСтатьи,
	               |	ОборотыТекущие.Контрагент,
	               |	ОборотыТекущие.Аналитика_3,
	               |	ОборотыТекущие.Аналитика_4,
	               |	ОборотыТекущие.Сценарий,
	               |	ОборотыТекущие.СтатьяОборотов,
	               |	ОборотыТекущие.СоставляющаяПлатежа,
	               |	ОборотыТекущие.СБ_ДатаДокумента,
	               |	ОборотыТекущие.Период
	               |
	               |ИМЕЮЩИЕ
	               |	СУММА(ОборотыТекущие.СуммаСценарийОборот) <> 0
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка КАК ДокументДопУсловий,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.ДатаТранша,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.ДатаПогашения,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.СуммаТранша,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.МесяцПервогоПлатежа,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.ПредставлениеМесяцаПервогоПлатежа,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.КоличествоПериодовВыплат,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.СБ_ПроцентнаяСтавкаПоТраншу,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка.Контрагент,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка.ДоговорКонтрагента,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка.Сценарий,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка.ЦФО,
	               |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка.СтатьяОборотов
	               |ПОМЕСТИТЬ Транши
	               |ИЗ
	               |	Документ.бит_ДополнительныеУсловияПоДоговору.ГрафикВыдачиТраншей КАК бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей
				   |ГДЕ
				   |	бит_ДополнительныеУсловияПоДоговоруГрафикВыдачиТраншей.Ссылка В
				   |			(ВЫБРАТЬ РАЗЛИЧНЫЕ
				   |				ВТ.ДокументРегистратор
				   |			ИЗ
				   |				ОборотыГрафиков КАК ВТ)
		           |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ОборотыГрафиков.Контрагент,
	               |	ОборотыГрафиков.ДоговорКонтрагента,
	               |	ОборотыГрафиков.Регистратор КАК ДокументРегистратор,
	               |	НАЧАЛОПЕРИОДА(ОборотыГрафиков.Период, ДЕНЬ) КАК ПериодПлатежа,
	               |	ОборотыГрафиков.Сценарий,
	               |	ОборотыГрафиков.ЦФО,
	               |	ОборотыГрафиков.СтатьяОборотов,
	               |	ОборотыГрафиков.СоставляющаяПлатежа,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_СтатьиОборотов.ИмяНастройки = ""Статья оборотов – тело займа""
	               |				ТОГДА ОборотыГрафиков.СуммаСценарийОборот
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаТелаЗайма,
	               |	СУММА(ВЫБОР
	               |			КОГДА ВТ_СтатьиОборотов.ИмяНастройки = ""Статья оборотов – проценты по займу""
	               |				ТОГДА ОборотыГрафиков.СуммаСценарийОборот
	               |			ИНАЧЕ 0
	               |		КОНЕЦ) КАК СуммаПроцентов,
	               |	ЕСТЬNULL(бит_му_ДополнительныеСтавкиСрезПоследних.Ставка, 0) + ЕСТЬNULL(бит_му_НоминальныеСтавкиСрезПоследних.Ставка, 0) КАК ЗначениеСтавкиНовое,
	               |	ОборотыГрафиков.ДокументРегистратор КАК ДокументДопУсловий
	               |ПОМЕСТИТЬ ВТ_Итоги
	               |ИЗ
	               |	ОборотыГрафиков КАК ОборотыГрафиков
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ЗначенияПараметровФинансовыхДоговоров КАК ПараметрТипСтавки
	               |		ПО (ПараметрТипСтавки.Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.бит_ВидыПараметровФинансовыхДоговоров.ТипПроцентнойСтавки))
	               |			И ОборотыГрафиков.ДоговорКонтрагента = ПараметрТипСтавки.ДоговорКонтрагента
	               |		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СтатьиОборотов КАК ВТ_СтатьиОборотов
	               |		ПО ОборотыГрафиков.СтатьяОборотов = ВТ_СтатьиОборотов.Значение
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ЗначенияПараметровФинансовыхДоговоров КАК ПараметрВидСтавки
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_НоминальныеСтавки.СрезПоследних(&Дата, ) КАК бит_му_НоминальныеСтавкиСрезПоследних
	               |			ПО ПараметрВидСтавки.ЗначениеПараметра = бит_му_НоминальныеСтавкиСрезПоследних.ДополнительнаяСоставляющая
	               |				И (ПараметрВидСтавки.ДоговорКонтрагента = бит_му_НоминальныеСтавкиСрезПоследних.ДоговорКонтрагента
	               |					ИЛИ ПараметрВидСтавки.ДоговорКонтрагента.бит_ПроектДоговора = бит_му_НоминальныеСтавкиСрезПоследних.ДоговорКонтрагента)
	               |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_му_ДополнительныеСтавки.СрезПоследних(&Дата, ) КАК бит_му_ДополнительныеСтавкиСрезПоследних
	               |			ПО ПараметрВидСтавки.ЗначениеПараметра = бит_му_ДополнительныеСтавкиСрезПоследних.ВидСтавки
	               |		ПО (ПараметрВидСтавки.Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.бит_ВидыПараметровФинансовыхДоговоров.СБ_ВидПлавающейСтавки))
	               |			И ОборотыГрафиков.ДоговорКонтрагента = ПараметрВидСтавки.ДоговорКонтрагента
				   |ГДЕ
				   |	ПараметрТипСтавки.ЗначениеПараметра = ЗНАЧЕНИЕ(Перечисление.бит_ТипыПроцентныхСтавокПоФинансовымДоговорам.Плавающая)
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ОборотыГрафиков.Контрагент,
	               |	ОборотыГрафиков.ДоговорКонтрагента,
	               |	ОборотыГрафиков.Регистратор,
	               |	ОборотыГрафиков.Сценарий,
	               |	ОборотыГрафиков.ЦФО,
	               |	ОборотыГрафиков.СтатьяОборотов,
	               |	ОборотыГрафиков.СоставляющаяПлатежа,
	               |	ЕСТЬNULL(бит_му_ДополнительныеСтавкиСрезПоследних.Ставка, 0) + ЕСТЬNULL(бит_му_НоминальныеСтавкиСрезПоследних.Ставка, 0),
	               |	НАЧАЛОПЕРИОДА(ОборотыГрафиков.Период, ДЕНЬ),
	               |	ОборотыГрафиков.ДокументРегистратор
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	Транши.Контрагент,
	               |	Транши.ДоговорКонтрагента,
	               |	Транши.ДокументДопУсловий,
	               |	Транши.ДатаТранша,
	               |	Транши.Сценарий,
	               |	Транши.ЦФО,
	               |	Транши.СтатьяОборотов,
	               |	ЗНАЧЕНИЕ(Справочник.бит_СоставляющиеПлатежейПоФинДоговорам.ПустаяСсылка),
	               |	Транши.СуммаТранша,
	               |	0,
	               |	0,
	               |	Транши.ДокументДопУсловий
	               |ИЗ
	               |	Транши КАК Транши
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.бит_ЗначенияПараметровФинансовыхДоговоров КАК ПараметрТипСтавки
	               |		ПО Транши.ДоговорКонтрагента = ПараметрТипСтавки.ДоговорКонтрагента
	               |			И (ПараметрТипСтавки.Параметр = ЗНАЧЕНИЕ(ПланВидовХарактеристик.бит_ВидыПараметровФинансовыхДоговоров.ТипПроцентнойСтавки))
	               |ГДЕ
	               |	ПараметрТипСтавки.ЗначениеПараметра = ЗНАЧЕНИЕ(Перечисление.бит_ТипыПроцентныхСтавокПоФинансовымДоговорам.Плавающая)
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ_Итоги.Контрагент,
	               |	ВТ_Итоги.ДоговорКонтрагента,
	               |	ВТ_Итоги.ПериодПлатежа КАК ПериодПлатежа,
	               |	ВТ_Итоги.ДокументРегистратор КАК Документ,
	               |	ВТ_Итоги.Сценарий,
	               |	ВТ_Итоги.ЦФО,
	               |	ВТ_Итоги.СтатьяОборотов,
	               |	ВТ_Итоги.СоставляющаяПлатежа КАК СоставляющаяПлатежа,
	               |	СУММА(ВТ_Итоги.СуммаТелаЗайма) КАК СуммаТелаЗайма,
	               |	СУММА(ВТ_Итоги.СуммаПроцентов) КАК СуммаПроцентов,
	               |	ВТ_Итоги.ЗначениеСтавкиНовое,
	               |	ВТ_Итоги.ДокументДопУсловий
	               |ИЗ
	               |	ВТ_Итоги КАК ВТ_Итоги
	               |
	               |СГРУППИРОВАТЬ ПО
	               |	ВТ_Итоги.Контрагент,
	               |	ВТ_Итоги.ДоговорКонтрагента,
	               |	ВТ_Итоги.ПериодПлатежа,
	               |	ВТ_Итоги.ДокументРегистратор,
	               |	ВТ_Итоги.Сценарий,
	               |	ВТ_Итоги.ЦФО,
	               |	ВТ_Итоги.СтатьяОборотов,
	               |	ВТ_Итоги.СоставляющаяПлатежа,
	               |	ВТ_Итоги.ЗначениеСтавкиНовое,
	               |	ВТ_Итоги.ДокументДопУсловий
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	Документ,
	               |	ПериодПлатежа,
	               |	СоставляющаяПлатежа";
				   
	Запрос.УстановитьПараметр("Дата", Объект.Дата);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	ПересчетПроцентовПоПлавающимСтавкам = Запрос.Выполнить().Выгрузить();	
	//+СБ Пискунова #2387 22-09-2016
	//В выгруженной из запроса тааблице не хватает колонок, а далее мы их заполняем
	ПересчетПроцентовПоПлавающимСтавкам.Колонки.Добавить("СуммаПроцентовПослеПересчета");
	ПересчетПроцентовПоПлавающимСтавкам.Колонки.Добавить("ЗадолженностьПоОсновномуДолгу");
	ПересчетПроцентовПоПлавающимСтавкам.Колонки.Добавить("Дельта");
	//-СБ Пискунова #2387 22-09-2016
	
	// процедура пересчета
	ПредыдущийДокумент = Неопределено;
	
	Для Каждого Строка Из ПересчетПроцентовПоПлавающимСтавкам Цикл
		
		Если ТипЗнч(Строка.Документ) = Тип("ДокументСсылка.СБ_ПересчетГрафиковПлатежейПоФинансовымДоговорам") Тогда
			Строка.Дельта = - Строка.СуммаПроцентов;
			Продолжить;
		КонецЕсли;
		
		Если Строка.Документ <> ПредыдущийДокумент Тогда
			// новый документ
			Если Не ЗначениеЗаполнено(Строка.СоставляющаяПлатежа) Тогда
				// поступление транша
				ТекЗадолженность = Строка.СуммаТелаЗайма;
				ПредЗадолженность = Строка.СуммаТелаЗайма;
				ПредыдущийПериод = Строка.ПериодПлатежа;
				ЕстьТранш = Истина;
			Иначе
				Сообщить("Не определен транш для документа " + Строка.Документ + "!");
				ЕстьТранш = Ложь;
				ТекЗадолженность = 0;
				ПредЗадолженность = 0;
				Продолжить;
			КонецЕсли;
			ПредыдущийДокумент = Строка.Документ;
		Иначе
			// обработка текущего документа
			Если Не ЕстьТранш Тогда
				// ошибка при расчете
			ИначеЕсли Не ЗначениеЗаполнено(Строка.СоставляющаяПлатежа) Тогда
				// новый транш
				ТекЗадолженность = ТекЗадолженность + Строка.СуммаТелаЗайма;
			ИначеЕсли Строка.СоставляющаяПлатежа = Справочники.бит_СоставляющиеПлатежейПоФинДоговорам.ПогашениеОсновногоДолга Тогда
				// выплата по основному долгу
				ПредЗадолженность = ТекЗадолженность;
				Строка.ЗадолженностьПоОсновномуДолгу = ПредЗадолженность;
				ТекЗадолженность = ТекЗадолженность - Строка.СуммаТелаЗайма;
			ИначеЕсли Строка.СоставляющаяПлатежа = Справочники.бит_СоставляющиеПлатежейПоФинДоговорам.Проценты Тогда
				// выплата процентов
				Строка.ЗадолженностьПоОсновномуДолгу = ПредЗадолженность;
				
				Если ПредыдущийПериод = Строка.ПериодПлатежа Тогда
					ПредыдущийПериод = ДобавитьМесяц(Строка.ПериодПлатежа, -1);
					Если КонецДня(Строка.ПериодПлатежа) = КонецМесяца(Строка.ПериодПлатежа) Тогда
						ПредыдущийПериод = КонецМесяца(ПредыдущийПериод);
					КонецЕсли;
				КонецЕсли;
				
		        КоличествоДнейВГоду = Окр((КонецГода(ПредыдущийПериод)-НачалоГода(ПредыдущийПериод))/60/60/24);        
		        КоличествоДнейОплаты = Число(Строка.ПериодПлатежа-ПредыдущийПериод)/60/60/24;                             
		        Строка.СуммаПроцентовПослеПересчета = Строка.ЗадолженностьПоОсновномуДолгу * (Строка.ЗначениеСтавкиНовое / 100 / КоличествоДнейВГоду)*КоличествоДнейОплаты;
				
				Строка.Дельта = Строка.СуммаПроцентовПослеПересчета - Строка.СуммаПроцентов;
				
				ПредыдущийПериод = Строка.ПериодПлатежа;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	МассивСтрокДляУдаления = Новый Массив;
	
	Для Каждого Строка Из ПересчетПроцентовПоПлавающимСтавкам Цикл
		Если ЗначениеЗаполнено(Строка.СоставляющаяПлатежа) И Строка.ПериодПлатежа < Объект.Дата Тогда
			МассивСтрокДляУдаления.Добавить(Строка);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого УдаляемаяСтрока Из МассивСтрокДляУдаления Цикл
		ПересчетПроцентовПоПлавающимСтавкам.Удалить(УдаляемаяСтрока);
	КонецЦикла;
	
	Объект.ПересчетПроцентовПоПлавающимСтавкам.Загрузить(ПересчетПроцентовПоПлавающимСтавкам); // 1c-izhtc, ChuckNorris, 25.08.2015	
	
КонецПроцедуры

// 1c-izhtc, ChuckNorris, 25.08.2015 ( 
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УстановитьСостояниеДокумента();
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеДокумента()
	
	СостояниеДокумента = ОбщегоНазначенияБП.СостояниеДокумента(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	ОбщегоНазначенияБПКлиент.ОбработкаОповещенияФормыДокумента(ЭтаФорма, Объект.Ссылка, ИмяСобытия, Параметр, Источник);
	
КонецПроцедуры
// 1c-izhtc, ChuckNorris, 25.08.2015 ) 