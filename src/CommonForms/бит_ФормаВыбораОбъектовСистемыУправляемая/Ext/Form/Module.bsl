
#Область ОбработчикиСобытийФормы
        
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВидыОбъектов") Тогда
		ВидыОбъектов 		 = Параметры.ВидыОбъектов;
	КонецЕсли;
	Если Параметры.Свойство("ТекущийОбъектСистемы") Тогда
		ТекущийОбъектСистемы = Параметры.ТекущийОбъектСистемы;
    КонецЕсли; 
	Если Параметры.Свойство("ДоступныеОбъектыСистемы") Тогда
		ДоступныеОбъектыСистемы = Параметры.ДоступныеОбъектыСистемы;
    КонецЕсли; 
	
	мКэшЗначений = Новый Структура;
	ОбновитьДерево(мКэшЗначений);
	
	Если мКэшЗначений.ТекущийОбъектСистемы <> Неопределено Тогда	
		 Элементы.ОбъектыСистемы.ТекущаяСтрока = мКэшЗначений.ТекущийОбъектСистемы;		 
	КонецЕсли;	   	
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбъектыСистемы

&НаКлиенте
Процедура ОбъектыСистемыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоКнопкеОК(Элементы.ОбъектыСистемы.ТекущиеДанные);

КонецПроцедуры // ОбъектыСистемыВыбор()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОбновитьДерево(Команда)
	
	ОбновитьДерево(мКэшЗначений);
	
КонецПроцедуры // КомандаОбновитьДерево

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ПоКнопкеОК(Элементы.ОбъектыСистемы.ТекущиеДанные);
	Если ЭтаФорма.Открыта() Тогда
		
		Закрыть();
		
	КонецЕсли; 
	
КонецПроцедуры // КомандаОК()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура обрабатывает нажатие на кнопку ОК.
// 
// Параметры:
//  ТекущаяСтрока - СтрокаДереваЗначения.
// 
&НаКлиенте
Процедура ПоКнопкеОК(ТекущаяСтрока)

	Если ТекущаяСтрока = Неопределено Тогда 	
		Возврат;	
	КонецЕсли; 
	
	Если ЗначениеЗаполнено(ТекущаяСтрока.ОбъектСистемы) Тогда	
		 ОповеститьОВыборе(ТекущаяСтрока.ОбъектСистемы); 	
	КонецЕсли; 

КонецПроцедуры // ПоКнопкеОК()

// Процедура заполняет дерево "ОбъектыСистемы" по справочнику с учетом отбора.
// 
// Параметры:
//   КэшЗначений - Струкутра
// 
&НаСервере
Процедура ОбновитьДерево(КэшЗначений)
	
	КэшЗначений.Очистить();
	КэшЗначений.Вставить("ТекущийОбъектСистемы");
	
	
	ДанныеДерева = ОбъектыСистемы.ПолучитьЭлементы();
	ДанныеДерева.Очистить();
	
	// Получаем объекты системы
	Запрос = Новый Запрос;
	Запрос.Текст =  "ВЫБРАТЬ
	                |	бит_ОбъектыСистемы.Ссылка КАК ОбъектСистемы,
					|   бит_ОбъектыСистемы.Наименование КАК СинонимОбъекта,
	                |	бит_ОбъектыСистемы.ВидОбъекта КАК ВидОбъекта
	                |ИЗ
	                |	Справочник.бит_ОбъектыСистемы КАК бит_ОбъектыСистемы
	                |ГДЕ
	                |	(НЕ бит_ОбъектыСистемы.ЭтоГруппа)";
					
	Если ВидыОбъектов.Количество()>0 Тогда
		
		Запрос.УстановитьПараметр("ВидОбъекта",ВидыОбъектов);
		Запрос.Текст = Запрос.Текст +"
	                |	И бит_ОбъектыСистемы.ВидОбъекта В(&ВидОбъекта)";		
	
	КонецЕсли; 				
	
	Если ДоступныеОбъектыСистемы.Количество() > 0 Тогда
	
	    Запрос.УстановитьПараметр("ДоступныеОбъектыСистемы",ДоступныеОбъектыСистемы);
		Запрос.Текст = Запрос.Текст +"
		               |И бит_ОбъектыСистемы.Ссылка В (&ДоступныеОбъектыСистемы)";
	
	КонецЕсли; 
	
	Запрос.Текст = Запрос.Текст+"					
	                |ИТОГИ ПО
	                |	ВидОбъекта,
	                |	ОбъектСистемы
					|АВТОУПОРЯДОЧИВАНИЕ";
					

	 Результат = Запрос.Выполнить();
	 
	 ВыборкаВид = Результат.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	 
	 Пока ВыборкаВид.Следующий() Цикл
		 
		   ЭлементДереваВерх = ДанныеДерева.Добавить();
		   ЭлементДереваВерх.Синоним = ВыборкаВид.ВидОбъекта;
		   ЭлементДереваВерх.ВидОбъекта = ВыборкаВид.ВидОбъекта;
		   КартинкаВида = бит_УправлениеОбъектамиСистемы.ОпределитьКартинкуВидаОбъекта(ВыборкаВид.ВидОбъекта);
		   Если КартинкаВида <> Неопределено Тогда
			   
			   ЭлементДереваВерх.Картинка   = КартинкаВида;
			   
		   КонецЕсли; 
		   
		   КоллекцияЭлементовВерх = ЭлементДереваВерх.ПолучитьЭлементы();
		   
	 	   Выборка = ВыборкаВид.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		   
		   Пока Выборка.Следующий() Цикл
			   
			   ОбъектСистемы = Выборка.ОбъектСистемы;
			   
			   
			   ЭлементДерева = КоллекцияЭлементовВерх.Добавить();
			   ЭлементДерева.Синоним       = Выборка.СинонимОбъекта;
			   ЭлементДерева.ОбъектСистемы = ОбъектСистемы;
			   ЭлементДерева.ВидОбъекта    = ВыборкаВид.ВидОбъекта;
			   
			   Если ОбъектСистемы = ТекущийОбъектСистемы Тогда
			   
			   	    КэшЗначений.ТекущийОбъектСистемы = ЭлементДерева.ПолучитьИдентификатор();
			   
			   КонецЕсли; 
			   
			   
		   КонецЦикла;  // По объектам системы
		   
	 КонецЦикла; // По видам объекта
	 
	 // Если указан один вид объектов для заполнения, тогда.
	 Если ВидыОбъектов.Количество() = 1 Тогда
		 
		 // Раскроем все уровни дерева объектов системы.
		 Элементы.ОбъектыСистемы.НачальноеОтображениеДерева = НачальноеОтображениеДерева.РаскрыватьВсеУровни;
		 
	 КонецЕсли;
	 
КонецПроцедуры // ОбновитьДерево()

#КонецОбласти
