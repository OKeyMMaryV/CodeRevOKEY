
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
  Если Параметры.Свойство("АвтоТест") Тогда
    Возврат;
  КонецЕсли;
 
  Если Не ЗначениеЗаполнено(Параметры.АдресТаблицы) Тогда
    ТекстСообщения = НСтр("ru='Непосредственное открытие этой формы не предусмотрено.
						      |Для открытия формы можно воспользоваться командой ""Платежные позиции"" в формах документов.'");
    ВызватьИсключение ТекстСообщения;
  КонецЕсли;
  
  Организация  = Параметры.Организация;
  Контрагент   = Параметры.Контрагент;
  АдресТаблицы = Параметры.АдресТаблицы;
  
  ПлатежныеПозиции.Загрузить(ПолучитьИзВременногоХранилища(АдресТаблицы));
  ЗаполнитьДобавленыеКолонкиТаблиц();
  
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыПлатежныеПозиции

&НаКлиенте
Процедура ПлатежныеПозицииПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	ЗаполнитьДобавленыеКолонкиТаблиц();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Готово(Команда)
	
	ГотовоНаСервере();
	
	ПараметрыПередачи = Новый Структура(); 
	ПараметрыПередачи.Вставить("АдресТаблицы",АдресТаблицы);
	
	Закрыть(ПараметрыПередачи);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ГотовоНаСервере()

	ПоместитьВоВременноеХранилище(ПлатежныеПозиции.Выгрузить(), АдресТаблицы);

КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДобавленыеКолонкиТаблиц()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	бит_ПлатежнаяПозиция.Ссылка КАК ПлатежнаяПозиция,
	|	бит_ПлатежнаяПозиция.Сумма КАК Сумма
	|ИЗ
	|	Документ.бит_ПлатежнаяПозиция КАК бит_ПлатежнаяПозиция
	|ГДЕ
	|	бит_ПлатежнаяПозиция.Ссылка В(&Позиции)";
	
	Запрос.УстановитьПараметр("Позиции", ПлатежныеПозиции.Выгрузить().ВыгрузитьКолонку("ПлатежнаяПозиция"));
	
	Результат = Запрос.Выполнить().Выгрузить();
	Для каждого СтрокаТаблицы Из ПлатежныеПозиции Цикл
		НайденнаяСтрока = Результат.Найти(СтрокаТаблицы.ПлатежнаяПозиция,"ПлатежнаяПозиция");
		Если НайденнаяСтрока <> Неопределено Тогда
			СтрокаТаблицы.Сумма = НайденнаяСтрока.Сумма;
		КонецЕсли; 
	КонецЦикла; 
	
	Итого = ПлатежныеПозиции.Итог("Сумма");
	
КонецПроцедуры

#КонецОбласти
