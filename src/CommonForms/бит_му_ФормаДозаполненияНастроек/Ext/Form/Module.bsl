&НаКлиенте
Процедура СоздатьНедостающиеНастройки(Команда)
	
	Попытка
		
		СоздатьНедостающие();
		
	Исключение
		
		Сообщить("Не удалось создать настройки по причине внутренней ошибки:
		|" + ИнформацияОбОшибке().Описание, СтатусСообщения.Важное);
		
	КонецПопытки;					
	
	ЭтаФорма.Закрыть();
	
КонецПроцедуры

&НаСервере
Процедура СоздатьНедостающие()
	
	МассивДоНастроек = ТЧНедостающиеНастройки;
	
	НаборНастроек = РегистрыСведений.бит_му_Настройки.СоздатьНаборЗаписей();
	НаборНастроек.Отбор.Группа.Установить(Группа);
	НаборНастроек.Прочитать();
	
	ТаблицаИмеющихсяНастроек = НаборНастроек.Выгрузить();
	
	Для Каждого ДобавляемаяНастройка Из МассивДоНастроек Цикл
		
		НайденнаяСтрока = ТаблицаИмеющихсяНастроек.Найти(ДобавляемаяНастройка.ИмяНастройки, "ИмяНастройки");
		
		Если НЕ НайденнаяСтрока = Неопределено Тогда
			НайденнаяСтрока.Значение = ДобавляемаяНастройка.Значение;
		Иначе
			НоваяСтрока = ТаблицаИмеющихсяНастроек.Добавить();
			НоваяСтрока.Группа = Группа;
			НоваяСтрока.ИмяНастройки = ДобавляемаяНастройка.ИмяНастройки;
			НоваяСтрока.Значение = ДобавляемаяНастройка.Значение;
		КонецЕсли;
		
	КонецЦикла;
	
	НаборНастроек.Загрузить(ТаблицаИмеющихсяНастроек);
	НаборНастроек.Записать();
	
КонецПроцедуры

// 1c-izhtc spawn 12.08.15 (

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Свойство("ОткрытаИзОбщегоМодуля") И Параметры.ОткрытаИзОбщегоМодуля Тогда 
		ТаблицаДоНастроек = ПолучитьИзВременногоХранилища(Параметры.АдресТаблицаДоНастроек);
		ЗначениеВДанныеФормы(ТаблицаДоНастроек , ТЧНедостающиеНастройки);
		Группа = Параметры.ИмяГруппы;
	КонецЕсли;
КонецПроцедуры

// 1c-izhtc spawn 12.08.15 )
