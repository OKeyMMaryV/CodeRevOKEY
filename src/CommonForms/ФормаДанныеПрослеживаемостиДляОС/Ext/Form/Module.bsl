#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресВременногоХранилища         = Параметры.АдресВременногоХранилища;
	ИдентификаторСтроки              = Параметры.ИдентификаторСтроки;
	Сумма                            = Параметры.Сумма;
	ЭтоПродажа                       = Параметры.ЭтоПродажа;
	СтавкаНДС                        = Параметры.СтавкаНДС;
	СуммаНДС                         = Параметры.СуммаНДС;
	ОсновноеСредство                 = Параметры.ОсновноеСредство;
	ВалютаДокумента                  = Параметры.ВалютаДокумента;
	
	ПодготовитьФормуНаСервере();
	
	УстановитьВидимостьЭлементовФормы();
		
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	Ошибки = Неопределено;
	СуммаПоТЧ = СведенияПрослеживаемости.Итог("Сумма");
	
	Если СуммаПоТЧ > Сумма Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
		"Список", 
		"Корректность","Сумма",,
		НСтр("ru = 'Сумма без НДС'"), 
		НСтр("ru = '""Сумма без НДС"" по таблице ""Данные по РНПТ"" больше суммы без НДС по основному средству'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если ЭтоПродажа И СведенияПрослеживаемости.Итог("СуммаНДС") > СуммаНДС Тогда
		ТекстСообщения = ОбщегоНазначенияКлиентСервер.ТекстОшибкиЗаполнения(
		"Список", 
		"Корректность","СуммаНДС",,
		НСтр("ru = 'Сумма НДС'"), 
		НСтр("ru = '""Сумма НДС"" по таблице ""Данных по РНПТ"" больше суммы НДС по основному средству'"));
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,,,,Отказ);
	КонецЕсли;
	
	Если Не НесколькоРНПТ Тогда
		Если Не ЗначениеЗаполнено(СтранаПроисхождения) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
			Ошибки, 
			"СтранаПроисхождения",
			Нстр("ru='Не указана страна происхождения'"));
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(РНПТ) Тогда
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
			Ошибки, 
			"РНПТ",
			Нстр("ru='Не указан номер РНПТ'"));
		КонецЕсли;
		
	Иначе
		
		Для Каждого СтрокаТЧ Из СведенияПрослеживаемости Цикл
			
			ИндексСтроки = СведенияПрослеживаемости.Индекс(СтрокаТЧ);
			
			Если НЕ ЗначениеЗаполнено(СтрокаТЧ.Номенклатура) Тогда
				
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].Номенклатура",
				Нстр("ru='Не указана номенклатура'"),
				"",
				ИндексСтроки);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.Номенклатура)
				И НЕ СтрокаТЧ.ПРослеживаемыйТовар Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].Номенклатура",
				Нстр("ru='В качестве номенклатуры может быть указана только прослеживаемая номенклатура'"),
				"",
				ИндексСтроки);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.СтранаПроисхождения) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].СтранаПроисхождения",
				Нстр("ru='Не указана страна происхождения'"),
				"",
				ИндексСтроки);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.Количество) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].Количество",
				Нстр("ru='Не указано количество'"),
				"",
				ИндексСтроки);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.РНПТ) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].РНПТ",
				Нстр("ru='Не указан номер РНПТ'"),
				"",
				ИндексСтроки);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.Сумма) Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].Сумма",
				Нстр("ru='Не указана сумма'"),
				"",
				ИндексСтроки);
			КонецЕсли;
			
			Если Не ЗначениеЗаполнено(СтрокаТЧ.СуммаНДС) И ТребуетсяСуммаНДС Тогда
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"СведенияПрослеживаемости[%1].СуммаНДС",
				Нстр("ru='Не указана сумма НДС'"),
				"",
				ИндексСтроки);
			КонецЕсли;
			
		КонецЦикла;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы И Модифицированность Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Данные были изменены. Сохранить изменения?'");
		
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемФормыЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНетОтмена, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
	Если НЕ Отказ И Модифицированность Тогда
		Отказ = Не ПроверитьЗаполнение();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НесколькоРНПТПриИзменении(Элемент)
	
	КоличествоРНПТ = СведенияПрослеживаемости.Количество();
	Если НЕ НесколькоРНПТ И КоличествоРНПТ > 1 Тогда
		ПоказатьПредупреждение( , НСтр("ru = 'Несколько строк с РНПТ может быть только у составного основного средства'"),);
		НесколькоРНПТ = Истина;
	ИначеЕсли НесколькоРНПТ И КоличествоРНПТ <=1 Тогда
		ЗаполнитьСведенияПоРНПТДляСоставногоОС(КоличествоРНПТ);
	Иначе
		Если КоличествоРНПТ > 0 Тогда
			ЗаполнитьСведенияПоРНПТДляПростогоОС();
		КонецЕсли;
	КонецЕсли;
	УстановитьВидимостьЭлементовФормы();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормыСведенияПрослеживаемости

&НаКлиенте
Процедура СведенияПрослеживаемостиПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	ТекущиеДанные.ИдентификаторСтроки = ИдентификаторСтроки;
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияПрослеживаемостиНоменклатураПриИзменении(Элемент)
	Элементы.СведенияПрослеживаемости.ЗакончитьРедактированиеСтроки(Ложь);
	ТекущиеДанные = Элементы.СведенияПрослеживаемости.ТекущиеДанные;
	ТекущиеДанные.ПрослеживаемыйТовар = ПрослеживаемостьФормыВызовСервера.ПолучитьПризнакПрослеживаемости(ТекущиеДанные.Номенклатура);
	Если Не ТекущиеДанные.ПрослеживаемыйТовар Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
				Нстр("ru='В качестве номенклатуры может быть указана только прослеживаемая номенклатура'"));
		ТекущиеДанные.Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");
		ТекущиеДанные.СтранаПроисхождения = ПредопределенноеЗначение("Справочник.СтраныМира.ПустаяСсылка");
		ТекущиеДанные.РНПТ = ПредопределенноеЗначение("Справочник.НомераГТД.ПустаяСсылка");
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СведенияПрослеживаемостиКоличествоПриИзменении(Элемент)
	ТекущиеДанные = Элементы.СведенияПрослеживаемости.ТекущиеДанные;
	ТекущиеДанные.КоличествоПрослеживаемости = ТекущиеДанные.Количество;
КонецПроцедуры

&НаКлиенте
Процедура СведенияПрослеживаемостиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СведенияПрослеживаемостиСтранаПроисхождения" Тогда
		ТекущиеДанные = Элементы.СведенияПрослеживаемости.ТекущиеДанные;
		Если Не ТекущиеДанные.ПрослеживаемыйТовар Тогда
			СтандартнаяОбработка = Ложь;
		Конецесли;
	КонецЕсли;
	
		Если Поле.Имя = "СведенияПрослеживаемостиРНПТ" Тогда
		ТекущиеДанные = Элементы.СведенияПрослеживаемости.ТекущиеДанные;
		Если Не ТекущиеДанные.ПрослеживаемыйТовар Тогда
			СтандартнаяОбработка = Ложь;
		Конецесли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СведенияПрослеживаемостиСуммаПриИзменении(Элемент)
	
	Если ЭтоПродажа Тогда
		Строка = Элементы.СведенияПрослеживаемости.ТекущиеДанные;
		Строка.СуммаНДС = УчетНДСКлиентСервер.РассчитатьСуммуНДС(Строка.Сумма, Ложь, УчетНДСВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтавкаНДС,Ложь));
	КонецЕсли;
	ОбновитьИтоги(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СведенияПрослеживаемостиСуммаНДСПриИзменении(Элемент)
	ОбновитьИтоги(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура СведенияПрослеживаемостиПослеУдаления(Элемент)
	ОбновитьИтоги(ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ЗакрытьФормуИВернутьРезультат(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	ЗакрытьФормуИВернутьРезультат(Ложь);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	Заголовок = СтрШаблон(НСтр("ru = 'Данные по РНПТ: %1'"), ОсновноеСредство);
	
	ДанныеПоРНПТ = ПолучитьИзВременногоХранилища(АдресВременногоХранилища);
	
	Если ДанныеПоРНПТ <> Неопределено Тогда
		СведенияПрослеживаемости.Загрузить(ДанныеПоРНПТ);
		Для Каждого Строка Из СведенияПрослеживаемости Цикл
			Строка.ПрослеживаемыйТовар = Истина;
		КонецЦикла
	КонецЕсли;
	
	КоличествоСтрокСРНПТ = СведенияПрослеживаемости.Количество();
	Если КоличествоСтрокСРНПТ > 1 Тогда 
			НесколькоРНПТ = Истина;
	ИначеЕсли КоличествоСтрокСРНПТ = 1
			И ЗначениеЗаполнено(СведенияПрослеживаемости[0].Номенклатура) Тогда
			НесколькоРНПТ = Истина;
	КонецЕсли;
	Если Не НесколькоРНПТ И КоличествоСтрокСРНПТ > 0 Тогда 
		СтранаПроисхождения = СведенияПрослеживаемости[0].СтранаПроисхождения;
		РНПТ                = СведенияПрослеживаемости[0].РНПТ;
	КонецЕсли;
	
	ОбновитьИтоги(ЭтотОбъект);
	
	Если ЭтоПродажа И СтавкаНДС <> Перечисления.СтавкиНДС.БезНДС
		И СтавкаНДС <> Перечисления.СтавкиНДС.НДС0 Тогда
		ТребуетсяСуммаНДС = Истина;
	Иначе 
		ТребуетсяСуммаНДС = Ложь;
	КонецЕсли;
	
	Элементы.СведенияПрослеживаемостиСуммаНДС.АвтоОтметкаНезаполненного = ТребуетсяСуммаНДС;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.СведенияПрослеживаемости.Видимость                  = НесколькоРНПТ;
	Элементы.ГруппаПростоеОС.Видимость                           = Не НесколькоРНПТ;
	Элементы.СведенияПрослеживаемостиСуммаНДС.Видимость          = ЭтоПродажа;
	Элементы.ГруппаПодвал.Видимость                              = НесколькоРНПТ;
	Элементы.ИтогиВсегоНДС.Видимость                             = ЭтоПродажа;

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьИтоги(Форма)
	
	Форма.ИтогиВсего = Форма.СведенияПрослеживаемости.Итог("Сумма");
	Если Форма.ЭтоПродажа тогда
		Форма.ИтогиВсегоНДС = Форма.СведенияПрослеживаемости.Итог("СуммаНДС");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияПоРНПТДляПростогоОС()
	
	СтранаПроисхождения = СведенияПрослеживаемости[0].СтранаПроисхождения;
	РНПТ                = СведенияПрослеживаемости[0].РНПТ;
	Количество          = СведенияПрослеживаемости[0].Количество;
	СведенияПрослеживаемости[0].Номенклатура = ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка");

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСведенияПоРНПТДляСоставногоОС(КоличествоРНПТ)
	
	Если КоличествоРНПТ > 0 Тогда
		СведенияПрослеживаемости[0].СтранаПроисхождения = СтранаПроисхождения;
		СведенияПрослеживаемости[0].РНПТ = РНПТ ;
		СведенияПрослеживаемости[0].Количество = 1;
		СведенияПрослеживаемости[0].КоличествоПрослеживаемости = 1;
	Иначе
		НоваяСтрока = СведенияПрослеживаемости.Добавить();
		НоваяСтрока.СтранаПроисхождения = СтранаПроисхождения;
		НоваяСтрока.РНПТ = РНПТ ;
		НоваяСтрока.Количество = 1;
		НоваяСтрока.КоличествоПрослеживаемости = 1;
		НоваяСтрока.ИдентификаторСтроки = ИдентификаторСтроки;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПоместитьВоВременноеХранилищеСведения()

	ПоместитьВоВременноеХранилище(СведенияПрослеживаемости.Выгрузить(), АдресВременногоХранилища);

КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемФормыЗавершение(Результат, ДополнительныеПараметры) Экспорт
		
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗакрытьФормуИВернутьРезультат(Истина);
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		ЗакрытьФормуИВернутьРезультат(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуИВернутьРезультат(ПередатьВладельцуРезультат)
	
	РезультатЗакрытия = Неопределено;
	Если ПередатьВладельцуРезультат Тогда
		Если НЕ ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли; 
		
		Если Не НесколькоРНПТ Тогда
			КоличествоРНПТ = СведенияПрослеживаемости.Количество();
			ЗаполнитьСведенияПоРНПТДляСоставногоОС(КоличествоРНПТ)
		КонецЕсли;
		
		ПоместитьВоВременноеХранилищеСведения();
		
		РезультатЗакрытия = "ОК";
	КонецЕсли;
	
	Модифицированность = Ложь;
	Закрыть(РезультатЗакрытия);
	
КонецПроцедуры

#КонецОбласти