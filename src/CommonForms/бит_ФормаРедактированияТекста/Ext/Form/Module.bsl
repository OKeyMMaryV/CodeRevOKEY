
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Хранит служебное слово "Процедура" или "Функция".
	Перем ПроцедураФункция;
	
	Если Параметры.Свойство("Текст") Тогда
		
		ТекстДляРедактирования = Параметры.Текст;
		
	КонецЕсли; 
	
	Если Параметры.Свойство("Заголовок") Тогда
		
		ЭтаФорма.Заголовок = Параметры.Заголовок;
		
	КонецЕсли; 
	
	Параметры.Свойство("ИмяТабличногоПоля", ИмяТабличногоПоля);
	Параметры.Свойство("ИмяПоля", ИмяПоля);
	Параметры.Свойство("ПроцедураФункция", ПроцедураФункция);
	
	ЭтоПроцедураФункция = ?(Врег(ПроцедураФункция) = ВРег("Процедура") ИЛИ Врег(ПроцедураФункция) = ВРег("Функция"), Истина, Ложь);
	ЭтоФункция = ?(Врег(ПроцедураФункция) = ВРег("Функция"), Истина, Ложь);
	
	Элементы.ГруппаНачалоПроцедуры.Видимость = ЭтоПроцедураФункция;
	Элементы.ГруппаВозврат.Видимость = ЭтоФункция;
	Элементы.ДекорацияКонецПроцедуры.Видимость = ЭтоПроцедураФункция;
	
	Элементы.ДекорацияПроцедура.Заголовок = ?(ЭтоФункция, НСтр("ru = 'Функция'"),  НСтр("ru = 'Процедура'"));	
	Элементы.ДекорацияКонецПроцедуры.Заголовок   = ?(ЭтоФункция, НСтр("ru = 'КонецФункции'"),  НСтр("ru = 'КонецПроцедуры'"));
	
	Если Параметры.Свойство("СигнатураМетода") Тогда
		
		Элементы.ДекорацияИмяПроцедуры.Заголовок = Параметры.СигнатураМетода;
		
	КонецЕсли; 
	
	Если Параметры.Свойство("ИмяПеременнойВозврат") Тогда
		
		Элементы.ДекорацияЗначениеВозврата.Заголовок = Параметры.ИмяПеременнойВозврат;
		
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)
	
	РезСтруктура = Новый Структура("Текст, ИмяТабличногоПоля, ИмяПоля"
	                                 , ТекстДляРедактирования
									 , ИмяТабличногоПоля
									 , ИмяПоля);
									 
	Оповестить("ФормаРедактированияТекста", РезСтруктура, ЭтаФорма);		
	Закрыть(РезСтруктура);
	
КонецПроцедуры

#КонецОбласти
