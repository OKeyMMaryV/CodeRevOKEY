
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ОбъектДляЗаполнения") Тогда
		Отказ = Истина;
		Возврат;	
	КонецЕсли;
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(ТипЗнч(Параметры.ОбъектДляЗаполнения.Ссылка));
	СтруктураОъектаСистемы = бит_ОбщегоНазначения.ПолучитьИмяОбъектаПоОписаниюТипов(Новый ОписаниеТипов(МассивТипов), "Документы", "ДокументСсылка");
	СинонимДокумента = СтруктураОъектаСистемы.Синоним;
	ИмяТипаОбъекта   = СтрЗаменить(СтруктураОъектаСистемы.ИмяТипа, "ДокументСсылка.", "ДокументОбъект."); 	
	
	ОбъектДляЗаполнения = ДанныеФормыВЗначение(Параметры.ОбъектДляЗаполнения, Тип(ИмяТипаОбъекта));
	Если ИмяТипаОбъекта = "ДокументОбъект.бит_ДополнительныеУсловияПоДоговору" Тогда		
		ЗначениеВДанныеФормы(ОбъектДляЗаполнения, ОбъектДляЗаполнения_ДанныеФормы_ДопУслПоДог);
	ИначеЕсли ИмяТипаОбъекта = "ДокументОбъект.бит_му_ПринятиеКУчетуОС" Тогда
		ЗначениеВДанныеФормы(ОбъектДляЗаполнения, ОбъектДляЗаполнения_ДанныеФормы_ПрУчОсМу);
	КонецЕсли;
	
	Если Параметры.Свойство("ИмяТабЧасти") Тогда
		ИмяТабЧасти = Параметры.ИмяТабЧасти;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НастройкаДляЗагрузкиПриИзменении(Элемент)
	
	НастройкаДляЗагрузкиПриИзмененииСервер();
	
КонецПроцедуры // НастройкаДляЗагрузкиПриИзменении()

&НаКлиенте
Процедура ФайлИсточникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Диалог = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	Диалог.Фильтр = бит_ОбменДанными_Excel.ПолучитьФильтрДляВыбораФайлаExcel();
	Диалог.МножественныйВыбор = Ложь;
	
	ФайловаяСистемаКлиент.ПоказатьДиалогВыбора(Новый ОписаниеОповещения("ФайлИсточникНачалоВыбораЗавершение", ЭтотОбъект), Диалог);
	
КонецПроцедуры

// Обработчик завершения выбора каталога файлов.
//  См. Синтакс-помощник: ДиалогВыбораФайла.Показать().
//
&НаКлиенте
Процедура ФайлИсточникНачалоВыбораЗавершение(ВыбранныеФайлы, Дополнительно) Экспорт

	Если ВыбранныеФайлы <> Неопределено Тогда
		ФайлИсточник = ВыбранныеФайлы[0];
	КонецЕсли; 

КонецПроцедуры
 
&НаКлиенте
Процедура ФайлИсточникОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НЕ ПустаяСтрока(ФайлИсточник) Тогда
		ФайловаяСистемаКлиент.ОткрытьФайл(ФайлИсточник);
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыполнитьЗагрузку(Команда)
	
	Отказ = Ложь;
	
	Если Не ЗначениеЗаполнено(НастройкаДляЗагрузки) Тогда
		ТекстСообщения = "ru = 'Не заполнено значение реквизита ""Настройка для загрузки""!'";
		бит_ОбщегоНазначения.СообщитьОбОшибке(НСтр(ТекстСообщения), Отказ);
	КонецЕсли;  	
	Если Не ЗначениеЗаполнено(ФайлИсточник) Тогда
		ТекстСообщения = "ru = 'Не заполнено значение реквизита ""Файл источник""!'";
		бит_ОбщегоНазначения.СообщитьОбОшибке(НСтр(ТекстСообщения), Отказ);
	КонецЕсли; 	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяТипаОбъекта = "ДокументОбъект.бит_ДополнительныеУсловияПоДоговору" Тогда
		
		Если Не ОбъектДляЗаполнения_ДанныеФормы_ДопУслПоДог[ИмяТабЧасти].Количество() = 0 Тогда
			
			Если ИмяТабЧасти = "ГрафикПлатежей" Тогда
				ТекстВопроса = "ru = 'График платежей будет перезаполнен. Продолжить?'";
			Иначе
				ТекстВопроса = "ru = 'Пообъектный график будет перезаполнен. Продолжить?'";
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ВопросПерезаполнитьТчЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр(ТекстВопроса), РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);
						
		Иначе
			
			ЗагрузкаДанныхИзExcel();
			
		КонецЕсли;
		
	ИначеЕсли ИмяТипаОбъекта = "ДокументОбъект.бит_му_ПринятиеКУчетуОС" Тогда
		
		Если Не ОбъектДляЗаполнения_ДанныеФормы_ПрУчОсМу[ИмяТабЧасти].Количество() = 0 Тогда
			
			Если ИмяТабЧасти = "ОсновныеСредства" Тогда
				ТекстВопроса = "ru = 'Табличная часть ""ОсновныеСредства"" будет перезаполнена. Продолжить?'";
			КонецЕсли;
			
			Оповещение = Новый ОписаниеОповещения("ВопросПерезаполнитьТчЗавершение", ЭтотОбъект);
			ПоказатьВопрос(Оповещение, НСтр(ТекстВопроса), РежимДиалогаВопрос.ДаНет, 30, КодВозвратаДиалога.Нет);
			
		Иначе
			
			ЗагрузкаДанныхИзExcel();
			
		КонецЕсли;
		
	Иначе
		
		Возврат;
		
	КонецЕсли;
		
КонецПроцедуры // ВыполнитьЗагрузку()

// Процедура - обработчик события "ВопросПерезаполнитьТчЗавершение".
// 
&НаКлиенте
Процедура ВопросПерезаполнитьТчЗавершение(Ответ, ДополнительныеДанные) Экспорт
	
	Если Не Ответ = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗагрузкаДанныхИзExcel();
	
КонецПроцедуры // ВопросПерезаполнитьТчЗавершение

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура выполняет загрузку данных из MS Excel.
// 
// Параметры:
//  Параметр1 - Строка.
// 
&НаКлиенте
Процедура ЗагрузкаДанныхИзExcel()

	ТекстСообщения = Нстр("ru = 'Выполняется загрузка данных по настройке ""%1%"" (%2%).'");
	ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Строка(НастройкаДляЗагрузки), Формат(ТекущаяДата(), "ДЛФ=DT"));
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);   		

    ФайлНаДиске = Новый Файл(ФайлИсточник);
    РасширениеФайла = ФайлНаДиске.Расширение;
    ПередатьФайлНаСервер(ФайлИсточник);
	ЗагрузитьДанныеИзExcel();
	
	Если ИмяТипаОбъекта = "ДокументОбъект.бит_ДополнительныеУсловияПоДоговору" Тогда 		
		Ссылка = ОбъектДляЗаполнения_ДанныеФормы_ДопУслПоДог;
	ИначеЕсли ИмяТипаОбъекта = "ДокументОбъект.бит_му_ПринятиеКУчетуОС" Тогда
		Ссылка = ОбъектДляЗаполнения_ДанныеФормы_ПрУчОсМу;
	Иначе
		Ссылка = Неопределено;
	КонецЕсли;
	
	ПараметрыОповещения = Новый Структура; 
	ПараметрыОповещения.Вставить("Ссылка", Ссылка);
	ПараметрыОповещения.Вставить("ИмяТабЧасти", ИмяТабЧасти);
	Оповестить("ВосстановитьЗагруженныеИзExcelДанные",,ВладелецФормы.УникальныйИдентификатор);
	
	ТекстСообщения = Нстр("ru = 'Загрузка данных по настройке завершена (%1%).'");
	ТекстСообщения = бит_ОбщегоНазначенияКлиентСервер.ПодставитьПараметрыСтроки(ТекстСообщения, Формат(ТекущаяДата(), "ДЛФ=DT"));
	бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
	
КонецПроцедуры // ЗагрузкаДанныхИзExcel() 

// Процедура помещает файл во временный для передачи на сервер.
//
&НаКлиенте
Процедура ПередатьФайлНаСервер(ИмяФайла)

    ОповещениеПомещенияФайла = Новый ОписаниеОповещения("ЗавершениеПомещенияФайла", ЭтотОбъект);
    НачатьПомещениеФайла(ОповещениеПомещенияФайла, , ИмяФайла, Ложь, УникальныйИдентификатор);
  
КонецПроцедуры // ПередатьФайлНаСервер()

// Процедура - оповещение о завершении помещенияфайла во временное хранилимще,
// для получения адреса.
//
&НаКлиенте
Процедура ЗавершениеПомещенияФайла(Результат, АдресФ, ВыбранноеИмяФайла, ДопПараметры) Экспорт

    АдресХранилища = АдресФ;   	
    
КонецПроцедуры // ЗавершениеПомещенияФайла()

// Выполняет действия необходимые при изменении настройки для загрузки.
// 
&НаСервере
Процедура НастройкаДляЗагрузкиПриИзмененииСервер()
	
	ФайлИсточник = НастройкаДляЗагрузки.ФайлИсточник;
	
КонецПроцедуры // НастройкаДляЗагрузкиПриИзмененииСервер()
 
// Процедура выполняет загрузку данных из Excel.
// 
// Параметры:
//  ТипОбъекта - Тип.
// 
&НаСервере
Процедура ЗагрузитьДанныеИзExcel()
    
    ИмяВременногоФайла = ПолучитьИмяВременногоФайла(РасширениеФайла);
    ПолучитьИзВременногоХранилища(АдресХранилища).Записать(ИмяВременногоФайла);
    
	Если ИмяТипаОбъекта = "ДокументОбъект.бит_ДополнительныеУсловияПоДоговору" Тогда		
		ОбъектДляЗаполнения = ДанныеФормыВЗначение(ОбъектДляЗаполнения_ДанныеФормы_ДопУслПоДог, Тип(ИмяТипаОбъекта));	
	ИначеЕсли ИмяТипаОбъекта = "ДокументОбъект.бит_му_ПринятиеКУчетуОС" Тогда
		ОбъектДляЗаполнения = ДанныеФормыВЗначение(ОбъектДляЗаполнения_ДанныеФормы_ПрУчОсМу, Тип(ИмяТипаОбъекта));
	КонецЕсли;
	
	ОбъектДляЗаполнения[ИмяТабЧасти].Очистить();
	
	// Создадим обработку для загрузки.
	ОбработкаЗагрузки = Обработки.бит_ИмпортОбъектовИзExcel.Создать();
	
	// Заполним шапку обработки.
	ОбработкаЗагрузки.НастройкаДляЗагрузки  = НастройкаДляЗагрузки;
	ОбработкаЗагрузки.ФайлИсточник		    = ИмяВременногоФайла;
	ОбработкаЗагрузки.ИмяЛиста		   	    = НастройкаДляЗагрузки.ИмяЛиста;
	ОбработкаЗагрузки.НомерНачальнойСтроки  = НастройкаДляЗагрузки.НомерСтрокиСИменамиКолонок;
	ОбработкаЗагрузки.НомерНачальнойКолонки = НастройкаДляЗагрузки.НомерНачальнойКолонки;
	
	// Устанавливаем режим загрузки "Обновлять".
	ОбработкаЗагрузки.РежимЗаписи = 1;
	ОбработкаЗагрузки.РежимОбновленияРеквизитов = Истина;
	
	// Укажем объект для которого необходимо выполнить обновление.
	ОбработкаЗагрузки.мОбъектДляОбновления = ОбъектДляЗаполнения;
	
	// Отключим вывод сообщений о ходе загрузки.
	ОбработкаЗагрузки.мСообщатьОХодеЗагрузки = Ложь;
	
	// Укажем обновляемые реквизиты - ТЧ БДДС.
	ОбновляемыеРеквизиты = Новый Структура;
	
	Если ИмяТипаОбъекта = "ДокументОбъект.бит_ДополнительныеУсловияПоДоговору" Тогда		
		ОбновляемыеРеквизиты.Вставить("ТабличныеЧасти_" + ИмяТабЧасти);    		
	КонецЕсли;
			
	// Выполним перенос.
	ОбработкаЗагрузки.ВыполнитьПеренос(ОбновляемыеРеквизиты);
	
	Если ИмяТипаОбъекта = "ДокументОбъект.бит_ДополнительныеУсловияПоДоговору" Тогда
		ЗначениеВДанныеФормы(ОбъектДляЗаполнения, ОбъектДляЗаполнения_ДанныеФормы_ДопУслПоДог);	
	ИначеЕсли ИмяТипаОбъекта = "ДокументОбъект.бит_му_ПринятиеКУчетуОС" Тогда
		ЗначениеВДанныеФормы(ОбъектДляЗаполнения, ОбъектДляЗаполнения_ДанныеФормы_ПрУчОсМу);
	КонецЕсли;
		
КонецПроцедуры // ЗагрузитьДанныеИзExcel()

#КонецОбласти
