
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ОписаниеТипов") Тогда
		СтандартнаяОбработка = Ложь;
		ДанныеВыбора = ДанныеВыбораДляТипа(Параметры.ОписаниеТипов);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ДанныеВыбораДляТипа(ОписаниеТипов)

	ВидыСравнения = Новый Массив();
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыСравнения, ВидыСравненияИнтервал(), Истина);
	
	Для каждого Тип Из ОписаниеТипов.Типы() Цикл
		Если ОбщегоНазначения.ЭтоСсылка(Тип) Тогда
			ВидОбъектаПоТипу = ОбщегоНазначения.ВидОбъектаПоТипу(Тип);
			Если НЕ (ВидОбъектаПоТипу = "Документ" ИЛИ ВидОбъектаПоТипу = "Перечисление") Тогда
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыСравнения, ВидыСравненияИерархия(), Истина);
			КонецЕсли; 
		ИначеЕсли Тип = Тип("Строка") Тогда
			ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ВидыСравнения, ВидыСравненияСтрока(), Истина);
		КонецЕсли; 
	КонецЦикла; 
	
	ДанныеВыбора = Новый СписокЗначений(); 
	ДанныеВыбора.ЗагрузитьЗначения(ВидыСравнения);
		
	Возврат ДанныеВыбора;

КонецФункции

Функция ВидыСравненияСтрока()

	Менеддер = Перечисления.бит_ВидыСравненияТрансляции; 
	Значения = Новый Массив(); 
	Значения.Добавить(Менеддер.НачинаетсяС);
	Значения.Добавить(Менеддер.НеНачинаетсяС);
	Значения.Добавить(Менеддер.НеСодержит);
	Значения.Добавить(Менеддер.Содержит);
	
	Возврат Значения;
	
КонецФункции

Функция ВидыСравненияИнтервал()

	Менеддер = Перечисления.бит_ВидыСравненияТрансляции; 
	Значения = Новый Массив(); 
	Значения.Добавить(Менеддер.Меньше);
	Значения.Добавить(Менеддер.МеньшеИлиРавно);
	Значения.Добавить(Менеддер.Больше);
	Значения.Добавить(Менеддер.БольшеИлиРавно);
	Значения.Добавить(Менеддер.НеРавно);
	Значения.Добавить(Менеддер.Равно);
	
	Возврат Значения;
	
КонецФункции

Функция ВидыСравненияИерархия()

	Менеддер = Перечисления.бит_ВидыСравненияТрансляции; 
	Значения = Новый Массив(); 
	Значения.Добавить(Менеддер.ВГруппе);
	Значения.Добавить(Менеддер.НЕВГруппе);
	
	Возврат Значения;
	
КонецФункции

#КонецОбласти

#КонецЕсли