#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем мНастройкиИзмерений Экспорт; // Хранит настройки дополнительных измерений.

#КонецОбласти

#Область ОбработчикиСобытий
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	ПроверитьВозможностьФормированияОтчета(СтандартнаяОбработка);
	
	Если НЕ СтандартнаяОбработка Тогда
		Возврат;
	КонецЕсли;
	
	ТекущиеПараметры = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	
	ПараметрВидОтчета = ТекущиеПараметры.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВидОтчета"));
	
	Если ПараметрВидОтчета = Неопределено
		ИЛИ НЕ ЗначениеЗаполнено(ПараметрВидОтчета.Значение) Тогда
		
		ТекстСообщения = НСтр("ru='Для текущего варианта отчета не указан параметр ""Вид отчета""'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
		Возврат;
	КонецЕсли;
	
	ИспользуемыйВидОтчета = ПараметрВидОтчета.Значение;
	
	// При расшифровке если повторно нажать кнопку Расшифровать почему то слетает ВидОтчета.
	Если ЭтоРасшифровка Тогда
		
		Если ПустаяСтрока(ВидОтчета) Тогда
			
			ВидОтчета = ИспользуемыйВидОтчета;
			
		Иначе
			
			ИспользуемыйВидОтчета = ВидОтчета;
			
		КонецЕсли;
		
	Иначе
		ВидОтчета = ИспользуемыйВидОтчета;
	КонецЕсли;
	
	// В СКД 2 набора данных, данные формируются только по тому набору, который соответствует выбранному виду отчета.
	НаборДанныхСКД = СхемаКомпоновкиДанных.НаборыДанных[0].Элементы.Найти(ИспользуемыйВидОтчета);
	
	Если НаборДанныхСКД = Неопределено Тогда
		
		ТекстСообщения = НСтр("ru='Не найден набор данных для используемого вида отчета!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
		СтандартнаяОбработка = Ложь;
		
		Возврат;
		
	КонецЕсли;
	
	НомерТаблицы = 1;
	ТекстЗапроса = "";
	
	СтруктураПараметров = Новый Структура;
	
	// Результат будем выводить в порядке возрастания Даты начала по сценарию.
	ОтсортированныеСценарии = Сценарии.Выгрузить();
	ОтсортированныеСценарии.Сортировать("ДатаНачала Возр");
	
	// Сформируем текст запроса выбранного набора данных.
	Для Каждого ТекущийСценарий Из ОтсортированныеСценарии Цикл
		
		Если ИспользуемыйВидОтчета = "ПоСтатьямОборотов" Тогда
			ТекстЗапросаПоСценарию = ПолучитьТекстЗапросаПоСценариям(НомерТаблицы);
		Иначе
			ТекстЗапросаПоСценарию = ПолучитьТекстЗапросаПоСценариямДляБюджета(НомерТаблицы);
		КонецЕсли;
		
		ТекстЗапроса = ТекстЗапроса + 
						?(НомерТаблицы=1, "", Символы.ПС + " ОБЪЕДИНИТЬ ВСЕ " + Символы.ПС) + 
						ТекстЗапросаПоСценарию;
						
		// Запомним параметры для текущего сценария
		СтруктураПараметров.Вставить("НачалоПериода"+НомерТаблицы, НачалоДня(ТекущийСценарий.ДатаНачала));
		СтруктураПараметров.Вставить("КонецПериода"+НомерТаблицы, КонецДня(ТекущийСценарий.ДатаОкончания));
		СтруктураПараметров.Вставить("Сценарий"+НомерТаблицы, ТекущийСценарий.Сценарий);
		
		НомерТаблицы = НомерТаблицы + 1;
		
	КонецЦикла;
	
	// Укажем общие параметры
	СтруктураПараметров.Вставить("ВидОтчета"				, ИспользуемыйВидОтчета);
	СтруктураПараметров.Вставить("ПоБюджету"				, НЕ (ИспользуемыйВидОтчета="ПоСтатьямОборотов"));
	СтруктураПараметров.Вставить("Бюджет"					, Бюджет);
	СтруктураПараметров.Вставить("ОтображатьПустыеСтроки"	, ОтображатьПустыеСтроки);
	
	Если ИспользуемыйВидОтчета = "ПоСтатьямОборотов" Тогда
		ОкончаниеТекстаЗапроса = ПолучитьТекстЗапросаОбработкаДанных();
	Иначе
		ОкончаниеТекстаЗапроса = ПолучитьТекстЗапросаОбработкаДанных(Истина);
	КонецЕсли;
	
	ТекстЗапроса = ТекстЗапроса + ";" + Символы.ПС + ОкончаниеТекстаЗапроса;
	
	// Заменим текст запроса в наборе данных
	НаборДанныхСКД.Запрос = ТекстЗапроса;
	
	
	СписокПараметровНаФорме = Новый СписокЗначений;
	
	СписокПараметровНаФорме.Добавить("Периодичность");
	
	Для каждого ЭлементСписка Из СписокПараметровНаФорме Цикл
		
		ИмяПараметра = ЭлементСписка.Значение;
		бит_ОтчетыСервер.УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроек, 
																 ЭтотОбъект[ИмяПараметра], 
																 ИмяПараметра);
		
	КонецЦикла;  	 
	
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	// КомпоновщикНастроек.ЗагрузитьНастройки(СхемаКомпоновкиДанных.НастройкиПоУмолчанию);
	
	// Заполним параметры СКД
	ПараметрыКомпоновки = КомпоновщикНастроек.Настройки.ПараметрыДанных;
	
	Для Каждого ТекущийПараметр Из ПараметрыКомпоновки.Элементы Цикл
		
		ЗначениеПараметра = Неопределено;
		
		Если НЕ СтруктураПараметров.Свойство(Строка(ТекущийПараметр.Параметр), ЗначениеПараметра) Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийПараметр.Использование 							= Истина;
		ТекущийПараметр.ИдентификаторПользовательскойНастройки  = Новый УникальныйИдентификатор;
		ТекущийПараметр.РежимОтображения 						= РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный;
		ТекущийПараметр.Значение 								= ЗначениеПараметра;
		
	КонецЦикла;
	
	Для Каждого ТекущийПараметр Из СтруктураПараметров Цикл
		
		Если НЕ ТипЗнч(ТекущийПараметр.Значение) = Тип("Дата") Тогда
			Продолжить;
		КонецЕсли;
		
		бит_ОтчетыСервер.УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроек, 
																 ТекущийПараметр.Значение, 
																 ТекущийПараметр.Ключ);
		
	КонецЦикла;
	
КонецПроцедуры // ПриКомпоновкеРезультата()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция формирует текст запроса для выборки данных по конкретному сценарию.
// 
// Параметры:
// 	НомерТаблицы - число - порядковый номер сценария(нумирация начинается с 1)
// 							, используется для именования параметров запроса.
// 
// Возвращаемое значение:
// 	ТекстЗапроса - строка.
// 
Функция ПолучитьТекстЗапросаПоСценариям(НомерТаблицы)
	
	Если НомерТаблицы = 1 Тогда
		ВыбратьРазрешенные 	= "РАЗРЕШЕННЫЕ";
		ТекстПОМЕСТИТЬ 		= "ПОМЕСТИТЬ ТаблицаПоСценариям";
		ПоляПостроителя 	= 
		"{ВЫБРАТЬ
		|	Период,
		|	Сценарий.*,
		|	ЦФО.*,
		|	СтатьяОборотов.*,
		|	Контрагент.*,
		|	ДоговорКонтрагента.*,
		|	Проект.*,
		|	НоменклатурнаяГруппа.*,
		|	БанковскийСчет.*,
		|	Валюта.*,
		|	Аналитика_1.*,
		|	Аналитика_2.*,
		|	Аналитика_3.*,
		|	Аналитика_4.*,
		|	Аналитика_5.*,
		|	Аналитика_6.*,
		|	Аналитика_7.*,		
		|	Регистратор.*}";
	Иначе
		ВыбратьРазрешенные 	= "";
		ТекстПОМЕСТИТЬ 		= "";
		ПоляПостроителя 	= "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ // %РАЗРЕШЕННЫЕ%
	|	ВЫБОР
	|		КОГДА &Периодичность = 6
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕНЬ)
	|		КОГДА &Периодичность = 7
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, НЕДЕЛЯ)
	|		КОГДА &Периодичность = 8
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕКАДА)
	|		КОГДА &Периодичность = 9
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, МЕСЯЦ)
	|		КОГДА &Периодичность = 10
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, КВАРТАЛ)
	|		КОГДА &Периодичность = 11
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ПОЛУГОДИЕ)
	|		КОГДА &Периодичность = 12
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ГОД)
	|		ИНАЧЕ бит_ОборотыПоБюджетамОбороты.Период
	|	КОНЕЦ КАК Период,
	|	бит_ОборотыПоБюджетамОбороты.Регистратор 			КАК Регистратор,
	|	бит_ОборотыПоБюджетамОбороты.Сценарий 				КАК Сценарий,
	|	бит_ОборотыПоБюджетамОбороты.ЦФО 					КАК ЦФО,
	|	бит_ОборотыПоБюджетамОбороты.СтатьяОборотов 		КАК СтатьяОборотов,
	|	бит_ОборотыПоБюджетамОбороты.Контрагент 			КАК Контрагент,
	|	бит_ОборотыПоБюджетамОбороты.ДоговорКонтрагента 	КАК ДоговорКонтрагента,
	|	бит_ОборотыПоБюджетамОбороты.Проект 				КАК Проект,
	|	бит_ОборотыПоБюджетамОбороты.НоменклатурнаяГруппа 	КАК НоменклатурнаяГруппа,
	|	бит_ОборотыПоБюджетамОбороты.БанковскийСчет 		КАК БанковскийСчет,
	|	бит_ОборотыПоБюджетамОбороты.Валюта 				КАК Валюта,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_1 КАК Аналитика_1,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_2 КАК Аналитика_2,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_3 КАК Аналитика_3,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_4 КАК Аналитика_4,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_5 КАК Аналитика_5,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_6 КАК Аналитика_6,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_7 КАК Аналитика_7,	
	|	СУММА(ЕстьNULL(бит_ОборотыПоБюджетамОбороты.КоличествоОборот, 0)) 	 КАК Количество,
	|	СУММА(ЕстьNULL(бит_ОборотыПоБюджетамОбороты.СуммаОборот, 0)) 	 	 КАК Сумма,
	|	СУММА(ЕстьNULL(бит_ОборотыПоБюджетамОбороты.СуммаРеглОборот, 0)) 	 КАК СуммаРегл,
	|	СУММА(ЕстьNULL(бит_ОборотыПоБюджетамОбороты.СуммаУпрОборот, 0)) 	 КАК СуммаУпр,
	|	СУММА(ЕстьNULL(бит_ОборотыПоБюджетамОбороты.СуммаСценарийОборот, 0)) КАК СуммаСценарий
	|	// %НомерТаблицыЧисло% КАК НомерПоВозрастанию
	|// %ТекстПОМЕСТИТЬ%
	// |// %ПоляПостроителя%
	|ИЗ
	|	РегистрНакопления.бит_ОборотыПоБюджетам.Обороты(&НачалоПериода {(&НачалоПериода)}, &КонецПериода {(&КонецПериода)}, Регистратор, Сценарий = &Сценарий) КАК бит_ОборотыПоБюджетамОбороты
	|{ГДЕ
	|	бит_ОборотыПоБюджетамОбороты.Сценарий.*,
	|	бит_ОборотыПоБюджетамОбороты.ЦФО.*,
	|	бит_ОборотыПоБюджетамОбороты.СтатьяОборотов.*,
	|	бит_ОборотыПоБюджетамОбороты.Контрагент.*,
	|	бит_ОборотыПоБюджетамОбороты.ДоговорКонтрагента.*,
	|	бит_ОборотыПоБюджетамОбороты.Проект.*,
	|	бит_ОборотыПоБюджетамОбороты.НоменклатурнаяГруппа.*,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_1,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_2,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_3,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_4,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_5,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_6,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_7,	
	|	бит_ОборотыПоБюджетамОбороты.БанковскийСчет.*,
	|	бит_ОборотыПоБюджетамОбороты.Валюта.*,
	|	бит_ОборотыПоБюджетамОбороты.Регистратор.*}
	|
	|СГРУППИРОВАТЬ ПО
	|	бит_ОборотыПоБюджетамОбороты.Сценарий,
	|	бит_ОборотыПоБюджетамОбороты.ЦФО,
	|	ВЫБОР
	|		КОГДА &Периодичность = 6
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕНЬ)
	|		КОГДА &Периодичность = 7
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, НЕДЕЛЯ)
	|		КОГДА &Периодичность = 8
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕКАДА)
	|		КОГДА &Периодичность = 9
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, МЕСЯЦ)
	|		КОГДА &Периодичность = 10
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, КВАРТАЛ)
	|		КОГДА &Периодичность = 11
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ПОЛУГОДИЕ)
	|		КОГДА &Периодичность = 12
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ГОД)
	|		ИНАЧЕ бит_ОборотыПоБюджетамОбороты.Период
	|	КОНЕЦ,
	|	бит_ОборотыПоБюджетамОбороты.Контрагент,
	|	бит_ОборотыПоБюджетамОбороты.СтатьяОборотов,
	|	бит_ОборотыПоБюджетамОбороты.НоменклатурнаяГруппа,
	|	бит_ОборотыПоБюджетамОбороты.БанковскийСчет,
	|	бит_ОборотыПоБюджетамОбороты.Валюта,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_1,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_2,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_3,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_4,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_5,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_6,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_7,	
	|	бит_ОборотыПоБюджетамОбороты.Проект,
	|	бит_ОборотыПоБюджетамОбороты.ДоговорКонтрагента,
	|	бит_ОборотыПоБюджетамОбороты.Регистратор";
	
	// Дополним необходимым текстом
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %РАЗРЕШЕННЫЕ%", ВыбратьРазрешенные);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ТекстПОМЕСТИТЬ%", ТекстПОМЕСТИТЬ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ПоляПостроителя%", ПоляПостроителя);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %НомерТаблицыЧисло%", ","+НомерТаблицы);
	
	// Опишем параметры
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоПериода", "&НачалоПериода"+НомерТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КонецПериода", "&КонецПериода"+НомерТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Сценарий", "&Сценарий"+НомерТаблицы);
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ПолучитьТекстЗапросаПоСценариямДляБюджета(НомерТаблицы)
	
	Если НомерТаблицы = 1 Тогда
		ВыбратьРазрешенные 	= "РАЗРЕШЕННЫЕ";
		ТекстПОМЕСТИТЬ 		= "ПОМЕСТИТЬ ТаблицаПоСценариям";
		ПоляПостроителя 	= 
		"{ВЫБРАТЬ
		|	Сценарий.*,
		|	ЦФО.*,
		|	СтатьяОборотов.*,
		|	Контрагент.*,
		|	ДоговорКонтрагента.*,
		|	Проект.*,
		|	НоменклатурнаяГруппа.*,
		|	БанковскийСчет.*,
		|	Валюта.*,
		|	Количество,
		|	Сумма,
		|	СуммаРегл,
		|	СуммаУпр,
		|	СуммаСценарий,
		|	Период,
		|	СтатьяБюджета.*,
		|	Бюджет.*,
		|	Регистратор.*,
		|	Аналитика_1.*,
		|	Аналитика_2.*,
		|	Аналитика_3.*,
		|	Аналитика_4.*,
		|	Аналитика_5.*,
		|	Аналитика_6.*,		
		|	Аналитика_7.*}";
	Иначе
		ВыбратьРазрешенные 	= "";
		ТекстПОМЕСТИТЬ 		= "";
		ПоляПостроителя 	= "";
	КонецЕсли;
	
	ТекстЗапроса = 
	"ВЫБРАТЬ // %РАЗРЕШЕННЫЕ%
	|	ВЫБОР
	|		КОГДА &Периодичность = 6
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕНЬ)
	|		КОГДА &Периодичность = 7
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, НЕДЕЛЯ)
	|		КОГДА &Периодичность = 8
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕКАДА)
	|		КОГДА &Периодичность = 9
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, МЕСЯЦ)
	|		КОГДА &Периодичность = 10
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, КВАРТАЛ)
	|		КОГДА &Периодичность = 11
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ПОЛУГОДИЕ)
	|		КОГДА &Периодичность = 12
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ГОД)
	|		ИНАЧЕ бит_ОборотыПоБюджетамОбороты.Период
	|	КОНЕЦ КАК Период,
	|	бит_ОборотыПоБюджетамОбороты.Регистратор 			КАК Регистратор,
	|	бит_ОборотыПоБюджетамОбороты.Сценарий 				КАК Сценарий,
	|	бит_ОборотыПоБюджетамОбороты.ЦФО 					КАК ЦФО,
	|	бит_ОборотыПоБюджетамОбороты.СтатьяОборотов 		КАК СтатьяОборотов,
	|	бит_ОборотыПоБюджетамОбороты.Контрагент 			КАК Контрагент,
	|	бит_ОборотыПоБюджетамОбороты.ДоговорКонтрагента 	КАК ДоговорКонтрагента,
	|	бит_ОборотыПоБюджетамОбороты.Проект 				КАК Проект,
	|	бит_ОборотыПоБюджетамОбороты.НоменклатурнаяГруппа 	КАК НоменклатурнаяГруппа,
	|	бит_ОборотыПоБюджетамОбороты.БанковскийСчет 		КАК БанковскийСчет,
	|	бит_ОборотыПоБюджетамОбороты.Валюта 				КАК Валюта,
	|	ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.КоличествоОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0)    КАК Количество,
	|	ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 		   КАК Сумма,
	|	ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаРеглОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 	   КАК СуммаРегл,
	|	ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаУпрОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 	   КАК СуммаУпр,
	|	ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаСценарийОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) КАК СуммаСценарий,
	|	бит_СтатьиБюджета.Ссылка КАК СтатьяБюджета,
	|	бит_СтатьиБюджета.Владелец КАК Бюджет,
	|	ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.КоличествоОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 
	|		+ ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 
	|		+ ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаРеглОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 
	|		+ ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаУпрОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 
	|		+ ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаСценарийОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) 
	|		КАК КонтрольнаяСумма,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_1 КАК Аналитика_1,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_2 КАК Аналитика_2,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_3 КАК Аналитика_3,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_4 КАК Аналитика_4,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_5 КАК Аналитика_5,
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_6 КАК Аналитика_6,	
	|	бит_ОборотыПоБюджетамОбороты.Аналитика_7 КАК Аналитика_7
	|	// %НомерТаблицыЧисло% КАК НомерПоВозрастанию
	|// %ТекстПОМЕСТИТЬ%
	// |// %ПоляПостроителя%
	|ИЗ
	|	Справочник.бит_СтатьиБюджета КАК бит_СтатьиБюджета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.бит_ОборотыПоБюджетам.Обороты(
	|				&НачалоПериода {(&НачалоПериода)},
	|				&КонецПериода {(&КонецПериода)},
	|				Регистратор,
	|				Сценарий = &Сценарий
	|				И СтатьяОборотов В
	|					(ВЫБРАТЬ
	|						бит_СтатьиБюджета.СтатьяОборотов
	|					ИЗ
	|						Справочник.бит_СтатьиБюджета КАК бит_СтатьиБюджета
	|					ГДЕ
	|						(НЕ бит_СтатьиБюджета.НеОтображать)
	|						И бит_СтатьиБюджета.Владелец = &Бюджет) {(ЦФО), (Сценарий), (СтатьяОборотов), (Проект), (НоменклатурнаяГруппа), (Контрагент), (ДоговорКонтрагента), (БанковскийСчет), (Валюта), (Аналитика_1), (Аналитика_2), (Аналитика_3), (Аналитика_4), (Аналитика_5), (Аналитика_6), (Аналитика_7)}) КАК бит_ОборотыПоБюджетамОбороты
	|		ПО (бит_ОборотыПоБюджетамОбороты.СтатьяОборотов = бит_СтатьиБюджета.СтатьяОборотов)
	|ГДЕ
	|	бит_СтатьиБюджета.Владелец = &Бюджет
	|	И ВЫБОР
	|			КОГДА &ОтображатьПустыеСтроки
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.КоличествоОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) + ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) + ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаРеглОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) + ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаУпрОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) + ЕСТЬNULL(бит_ОборотыПоБюджетамОбороты.СуммаСценарийОборот, 0) * ЕСТЬNULL(бит_СтатьиБюджета.Коэффициент, 0) <> 0
	|						ТОГДА ИСТИНА
	|					ИНАЧЕ ЛОЖЬ
	|				КОНЕЦ
	|		КОНЕЦ
	|	И (НЕ бит_СтатьиБюджета.НеОтображать)
	|{ГДЕ
	|	ВЫБОР
	|		КОГДА &Периодичность = 6
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕНЬ)
	|		КОГДА &Периодичность = 7
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, НЕДЕЛЯ)
	|		КОГДА &Периодичность = 8
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ДЕКАДА)
	|		КОГДА &Периодичность = 9
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, МЕСЯЦ)
	|		КОГДА &Периодичность = 10
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, КВАРТАЛ)
	|		КОГДА &Периодичность = 11
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ПОЛУГОДИЕ)
	|		КОГДА &Периодичность = 12
	|			ТОГДА НАЧАЛОПЕРИОДА(бит_ОборотыПоБюджетамОбороты.Период, ГОД)
	|		ИНАЧЕ бит_ОборотыПоБюджетамОбороты.Период
	|	КОНЕЦ,
	|	бит_СтатьиБюджета.Ссылка.*,
	|	бит_СтатьиБюджета.Владелец.*,
	|	бит_ОборотыПоБюджетамОбороты.Регистратор.*}";
	
	// Дополним необходимым текстом
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %РАЗРЕШЕННЫЕ%", ВыбратьРазрешенные);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ТекстПОМЕСТИТЬ%", ТекстПОМЕСТИТЬ);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ПоляПостроителя%", ПоляПостроителя);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %НомерТаблицыЧисло%", ","+НомерТаблицы);
	
	// Опишем параметры
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&НачалоПериода", "&НачалоПериода"+НомерТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&КонецПериода", "&КонецПериода"+НомерТаблицы);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&Сценарий", "&Сценарий"+НомерТаблицы);
	     
	Возврат ТекстЗапроса;
	
КонецФункции

// Функция формирует окончание текста пакетного запроса.
// 
// Параметры:
// 	ПоБюджету - булево - Истина - текст запроса формируется для вывода данных по статьям бюджета.
// 
// Возвращаемое значение:
// 	ТекстЗапроса - строка.
// 
Функция ПолучитьТекстЗапросаОбработкаДанных(ПоБюджету = Ложь)
	
	ТекстЗапроса =  
	"////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаПоСценариям.Период 				КАК Период,
	|	ТаблицаПоСценариям.Регистратор 			КАК Регистратор,
	|	ТаблицаПоСценариям.Сценарий 			КАК Сценарий,
	|	ТаблицаПоСценариям.ЦФО 					КАК ЦФО,
	|	ТаблицаПоСценариям.СтатьяОборотов 		КАК СтатьяОборотов,
	|	ТаблицаПоСценариям.Контрагент 			КАК Контрагент,
	|	ТаблицаПоСценариям.ДоговорКонтрагента 	КАК ДоговорКонтрагента,
	|	ТаблицаПоСценариям.Проект 				КАК Проект,
	|	ТаблицаПоСценариям.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ТаблицаПоСценариям.БанковскийСчет 		КАК БанковскийСчет,
	|	ТаблицаПоСценариям.Валюта 				КАК Валюта,
	|	ТаблицаПоСценариям.Аналитика_1 КАК Аналитика_1,
	|	ТаблицаПоСценариям.Аналитика_2 КАК Аналитика_2,
	|	ТаблицаПоСценариям.Аналитика_3 КАК Аналитика_3,
	|	ТаблицаПоСценариям.Аналитика_4 КАК Аналитика_4,
	|	ТаблицаПоСценариям.Аналитика_5 КАК Аналитика_5,
	|	ТаблицаПоСценариям.Аналитика_6 КАК Аналитика_6,
	|	ТаблицаПоСценариям.Аналитика_7 КАК Аналитика_7,	
	|	СУММА(ТаблицаПоСценариям.Количество) 	КАК Количество,
	|	СУММА(ТаблицаПоСценариям.Сумма) 	 	КАК Сумма,
	|	СУММА(ТаблицаПоСценариям.СуммаРегл)  	КАК СуммаРегл,
	|	СУММА(ТаблицаПоСценариям.СуммаУпр) 	 	КАК СуммаУпр,
	|	СУММА(ТаблицаПоСценариям.СуммаСценарий) КАК СуммаСценарий,
	|	ТаблицаПоСценариям.НомерПоВозрастанию
	|	// %ПоляПоБюджету%
	|{ВЫБРАТЬ
	|	Период,
	|	Регистратор.*,
	|	Сценарий.*,
	|	ЦФО.*,
	|	СтатьяОборотов.*,
	|	Контрагент.*,
	|	ДоговорКонтрагента.*,
	|	Проект.*,
	|	НоменклатурнаяГруппа.*,
	|	БанковскийСчет.*,
	|	Валюта.*,
	|	Аналитика_1.*,
	|	Аналитика_2.*,
	|	Аналитика_3.*,
	|	Аналитика_4.*,
	|	Аналитика_5.*,
	|	Аналитика_6.*,
	|	Аналитика_7.*,	
	|	Количество,
	|	Сумма,
	|	СуммаРегл,
	|	СуммаУпр,
	|	СуммаСценарий,
	|	НомерПоВозрастанию
	|	// %ПоляПостроителяПоБюджету%
	|}
	|ИЗ
	|	ТаблицаПоСценариям КАК ТаблицаПоСценариям
	|{ГДЕ
	|	ТаблицаПоСценариям.Период,
	|	ТаблицаПоСценариям.Регистратор.*,
	|	ТаблицаПоСценариям.Сценарий.*,
	|	ТаблицаПоСценариям.ЦФО.*,
	|	ТаблицаПоСценариям.СтатьяОборотов.*,
	|	ТаблицаПоСценариям.Контрагент.*,
	|	ТаблицаПоСценариям.ДоговорКонтрагента.*,
	|	ТаблицаПоСценариям.Проект.*,
	|	ТаблицаПоСценариям.НоменклатурнаяГруппа.*,
	|	ТаблицаПоСценариям.БанковскийСчет.*,
	|	ТаблицаПоСценариям.Валюта.*,
	|	ТаблицаПоСценариям.Аналитика_1.*,
	|	ТаблицаПоСценариям.Аналитика_2.*,
	|	ТаблицаПоСценариям.Аналитика_3.*,
	|	ТаблицаПоСценариям.Аналитика_4.*,
	|	ТаблицаПоСценариям.Аналитика_5.*,
	|	ТаблицаПоСценариям.Аналитика_6.*,
	|	ТаблицаПоСценариям.Аналитика_7.*,	
	|	ТаблицаПоСценариям.Количество,
	|	ТаблицаПоСценариям.Сумма,
	|	ТаблицаПоСценариям.СуммаРегл,
	|	ТаблицаПоСценариям.СуммаУпр,
	|	ТаблицаПоСценариям.СуммаСценарий
	|   // %ПоляПоБюджету%
	|}
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаПоСценариям.Период,
	|	ТаблицаПоСценариям.Регистратор,
	|	ТаблицаПоСценариям.Сценарий,
	|	ТаблицаПоСценариям.ЦФО,
	|	ТаблицаПоСценариям.СтатьяОборотов,
	|	ТаблицаПоСценариям.Контрагент,
	|	ТаблицаПоСценариям.ДоговорКонтрагента,
	|	ТаблицаПоСценариям.Проект,
	|	ТаблицаПоСценариям.НоменклатурнаяГруппа,
	|	ТаблицаПоСценариям.БанковскийСчет,
	|	ТаблицаПоСценариям.Валюта,
	|	ТаблицаПоСценариям.Аналитика_1,
	|	ТаблицаПоСценариям.Аналитика_2,
	|	ТаблицаПоСценариям.Аналитика_3,
	|	ТаблицаПоСценариям.Аналитика_4,
	|	ТаблицаПоСценариям.Аналитика_5,
	|	ТаблицаПоСценариям.Аналитика_6,
	|	ТаблицаПоСценариям.Аналитика_7,	
	|	ТаблицаПоСценариям.НомерПоВозрастанию
	|	// %ПоляГруппировки%
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ТаблицаПоСценариям";
	
	Если ПоБюджету Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ПоляПоБюджету%", ",ТаблицаПоСценариям.Бюджет КАК Бюджет,
																	|ТаблицаПоСценариям.СтатьяБюджета КАК СтатьяБюджета");
																	
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ПоляПостроителяПоБюджету%", ",СтатьяБюджета.*,
																				|	Бюджет.*");
																				
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ПоляГруппировки%", ",ТаблицаПоСценариям.Бюджет,
																	|ТаблицаПоСценариям.СтатьяБюджета");																		
	Иначе
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "// %ПоляПоБюджету%", "");
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

// Процедура выполняет проверку заполнения необходимых параметров.
// 
// Параметры:
// 	СтандартнаяОбработка - Булево - Истина - проверка прошла успешно.
// 
Процедура ПроверитьВозможностьФормированияОтчета(СтандартнаяОбработка)
	
	Если Сценарии.Количество() = 0 Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ТекстСообщения = НСтр("ru='Не указаны сценарии для прогнозирования!'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

мНастройкиИзмерений = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_НастройкиДополнительныхИзмерений");

бит_МеханизмДопИзмерений.СформироватьЗаголовкиПолейДополнительныхИзмеренийВСКД(СхемаКомпоновкиДанных, 
					"НаборДанных1", 
					мНастройкиИзмерений);

#КонецОбласти

#КонецЕсли
