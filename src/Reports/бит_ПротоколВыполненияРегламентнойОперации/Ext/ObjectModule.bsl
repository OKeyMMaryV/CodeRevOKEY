#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныйПрограммныйИнтерфейс

// Процедура выполняет формирование отчета.
// 
// ДокументРезультат - ТабличныйДокумент - результат формирования отчета.
//
Процедура СформироватьОтчет(ДокументРезультат) Экспорт

	ВозможностьПостроения = ОпределитьВозможностьПолученияОтчета();
	Если Не ВозможностьПостроения Тогда
		Возврат;
	КонецЕсли;

	ДокументРезультат.Очистить();
	
	ТабДок = ПолучитьПротоколПроведения(Документ);
	ДокументРезультат.Вывести(ТабДок);

	ДокументРезультат.ОтображатьГруппировки = Истина;
	ДокументРезультат.ОтображатьЗаголовки = Ложь;
	ДокументРезультат.ОтображатьСетку = Ложь;
	ДокументРезультат.ТолькоПросмотр = Истина;
	ДокументРезультат.Автомасштаб = Истина;
	
КонецПроцедуры

#КонецОбласти 

#Область СлужебныеПроцедурыИФункции
		
// Функция возвращает протокол проведения документа.
// 
// Параметры:
//  Док - ДокументСсылка.
// 
// Возвращаемое значение:
//  Результат - ТабличныйДокумент.
// 
Функция ПолучитьПротоколПроведения(Док)

	ТекстЗапроса = "ВЫБРАТЬ
	               |	бит_ПротоколВыполненияРегламентныхОпераций.Протокол
	               |ИЗ
	               |	РегистрСведений.бит_ПротоколВыполненияРегламентныхОпераций КАК бит_ПротоколВыполненияРегламентныхОпераций
	               |ГДЕ
	               |	бит_ПротоколВыполненияРегламентныхОпераций.Регистратор = &Документ";
	
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Документ", Док);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Протокол.Получить();
	Иначе
		Возврат Новый ТабличныйДокумент;
	КонецЕсли;
	
КонецФункции // ПолучитьПротоколПроведения()

// Функция определяет можно ли получить отчет по документу или нет.
// 
Функция ОпределитьВозможностьПолученияОтчета()
	
	Если Не ЗначениеЗаполнено(Документ) Тогда
		
		#Если Клиент Тогда
			ПоказатьПредупреждение(, "Запишите и проведите документ, по которому Вы фомируете отчет");
		#КонецЕсли
		
		Возврат Ложь;
	Иначе
		Возврат Истина;
	КонецЕсли;
	
КонецФункции

#КонецОбласти 

#КонецЕсли
