
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Параметры.Свойство("ТекущийОбъект") = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	
	МетаданныеОбъекта = Метаданные.Отчеты.бит_ПланФактныйАнализУниверсальный;
	фПолноеИмяОтчета  = МетаданныеОбъекта.ПолноеИмя();
	                 	
	// Загрузка параметров формы
	Если Параметры.Свойство("фСписокПараметровНаФорме") Тогда
		фСписокПараметровНаФорме = Параметры.фСписокПараметровНаФорме;
	Иначе
		фСписокПараметровНаФорме = Новый СписокЗначений;
	КонецЕсли;
		
	// Загрузка данных отчета
	ОтчетОбъект = ДанныеФормыВЗначение(Параметры.ТекущийОбъект, Тип("ОтчетОбъект.бит_ПланФактныйАнализУниверсальный"));
	ЗначениеВДанныеФормы(ОтчетОбъект, Отчет);
	
	УправлениеВидимостьюПоВидуОтчета();
		
КонецПроцедуры // ПриСозданииНаСервере()

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗавершитьРедактирование(Команда)
	
	СтруктураПараметров = Новый Структура;
	Для каждого ЭлементСписка Из фСписокПараметровНаФорме Цикл
		ИмяПараметра = ЭлементСписка.Значение;
		СтруктураПараметров.Вставить(ИмяПараметра , Отчет[ИмяПараметра]);
	КонецЦикла;
	СтруктураПараметров.Вставить("ПользовательскиеНастройки" 			  , Отчет.КомпоновщикНастроек.ПользовательскиеНастройки);
		
	// Оповещение формы отчета
	Оповестить("ИзмененыНастройки_" + фПолноеИмяОтчета, СтруктураПараметров);
	
	Закрыть();
	
КонецПроцедуры // КомандаЗавершитьРедактирование()

// Процедура - обработчик события "ПриИзменении" полей воода - простых параметров на форме.
// 
&НаКлиенте
Процедура ПростойПараметрПриИзменении(Элемент)
	
	ИмяПараметра = Элемент.Имя;
	бит_ОтчетыКлиент.УстановитьЗначениеПараметраКомпоновщика(Отчет.КомпоновщикНастроек, 
															Отчет[ИмяПараметра], 
															ИмяПараметра);
															
КонецПроцедуры // ПростойПараметрПриИзменении()
														
&НаКлиенте
Процедура КомпоновщикНастроекПользовательскиеНастройкиПриИзменении(Элемент)
	
	Для каждого ИдЭлемента Из Элемент.ВыделенныеСтроки Цикл
		
		НастройкаКд  = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы.Найти(ИдЭлемента);
		Если ТипЗнч(НастройкаКд) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			
			ИмяПараметра = Строка(НастройкаКд.Параметр);
			
			Если фСписокПараметровНаФорме.НайтиПоЗначению(ИмяПараметра) <> Неопределено Тогда
				
				Отчет[ИмяПараметра] = НастройкаКд.Значение;
						
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры // КомпоновщикНастроекПользовательскиеНастройкиПриИзменении()														

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура управляет видимостью элементов формы в зависимоти от вида отчета.
// 
// Параметры:
//  Нет
// 
&НаСервере
Процедура УправлениеВидимостьюПоВидуОтчета()

	ЭтоОтчетПоБюджету = Отчет.ВидОтчета = "ПоБюджету";
	Элементы.ГруппаПараметрыПоБюджету.Видимость = ЭтоОтчетПоБюджету;	

КонецПроцедуры // УправлениеВидимостьюПоВидуОтчета()

#КонецОбласти

