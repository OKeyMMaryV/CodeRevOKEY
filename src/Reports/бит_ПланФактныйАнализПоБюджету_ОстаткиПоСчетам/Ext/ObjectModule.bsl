#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

Перем мНастройкиИзмерений Экспорт; // Хранит настройки дополнительных измерений.

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)

	СписокПараметровНаФорме = Новый СписокЗначений;
	
	СписокПараметровНаФорме.Добавить("Период");
	СписокПараметровНаФорме.Добавить("Бюджет");
	СписокПараметровНаФорме.Добавить("Сценарий");
	СписокПараметровНаФорме.Добавить("СценарийЭталон");
	СписокПараметровНаФорме.Добавить("Периодичность");
	СписокПараметровНаФорме.Добавить("ОтображатьПустыеСтроки");
	
	Для каждого ЭлементСписка Из СписокПараметровНаФорме Цикл
		
		ИмяПараметра = ЭлементСписка.Значение;
		бит_ОтчетыСервер.УстановитьЗначениеПараметраКомпоновщика(КомпоновщикНастроек, 
																 ЭтотОбъект[ИмяПараметра], 
																 ИмяПараметра);
		
	КонецЦикла; 

	Если ЗначениеЗаполнено(Сценарий) Тогда
		ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаСценария"));
        ЗначениеПараметра.Значение = Сценарий.Валюта;
        Если Не ЗначениеПараметра.Использование Тогда
            ЗначениеПараметра.Использование = Истина;
		КонецЕсли; 
	КонецЕсли;
	Если ЗначениеЗаполнено(СценарийЭталон) Тогда
		ЗначениеПараметра = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВалютаСценарияЭталон"));
        ЗначениеПараметра.Значение = СценарийЭталон.Валюта;
        Если Не ЗначениеПараметра.Использование Тогда
            ЗначениеПараметра.Использование = Истина;
        КонецЕсли;
	КонецЕсли;  	
	
	Для каждого СтруктураПолей Из КомпоновщикНастроек.Настройки.Структура Цикл
		Если ТипЗнч(СтруктураПолей) = Тип("ТаблицаКомпоновкиДанных") Тогда
			Для каждого КолонкаГруппировки Из СтруктураПолей.Колонки Цикл
				Если ВРег(КолонкаГруппировки.Имя) = ВРег("ПериодОстаткиНач")
					ИЛИ ВРег(КолонкаГруппировки.Имя) = ВРег("ПериодОбороты")
					ИЛИ ВРег(КолонкаГруппировки.Имя) = ВРег("ПериодОстаткиКон") Тогда
					
					КолонкаГруппировки.Отбор.Элементы.Очистить();
					
					Если Не ОтображатьПериодыБезОборотов Тогда
						ОтборГруппировка = КолонкаГруппировки.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
						ОтборГруппировка.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
						
						ИмяПоля1 = ?(ВРег(КолонкаГруппировки.Имя) = ВРег("ПериодОбороты"), "об_Сумма", "СуммаРеглОборот");
						Элемент1 = ОтборГруппировка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				        Элемент1.ЛевоеЗначение = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяПоля1)).Поле;
				        Элемент1.ПравоеЗначение = 0;
						Элемент1.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
						
						ИмяПоля2 = ?(ВРег(КолонкаГруппировки.Имя) = ВРег("ПериодОбороты"), "об_эталон_Сумма", "эталон_СуммаРеглОборот");
						Элемент2 = ОтборГруппировка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
				        Элемент2.ЛевоеЗначение = КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора.НайтиПоле(Новый ПолеКомпоновкиДанных(ИмяПоля2)).Поле;
				        Элемент2.ПравоеЗначение = 0;
						Элемент2.ВидСравнения = ВидСравненияКомпоновкиДанных.НеРавно;
					КонецЕсли; 
				КонецЕсли; 
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла;
	
	// Изменение кода. Начало. 04.10.2017{{
	СтандартнаяОбработка = Ложь;
	Макет = ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных");
	Для каждого Набор Из Макет.НаборыДанных Цикл
		бит_ОтчетыСервер.УстановитьЗаголовкиПолейСумма(Набор.Поля, Строка(ЭтотОбъект.СценарийЭталон), Строка(ЭтотОбъект.Сценарий));
	КонецЦикла; 
		
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(Макет,КомпоновщикНастроек.ПолучитьНастройки(),ДанныеРасшифровки);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, , ДанныеРасшифровки, Истина);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	// Изменение кода. Начало. 04.10.2017{{
	
КонецПроцедуры // ПриКомпоновкеРезультата()

#КонецОбласти

#Область Инициализация

мНастройкиИзмерений = бит_ОбщиеПеременныеСервер.ЗначениеПеременной("бит_НастройкиДополнительныхИзмерений");

бит_МеханизмДопИзмерений.СформироватьЗаголовкиПолейДополнительныхИзмеренийВСКД(СхемаКомпоновкиДанных, 
					"НаборДанных1", 
					мНастройкиИзмерений);
					
// Изменение синонима поля аналитики "Организация" на "Организация_Аналитика".
Поля = СхемаКомпоновкиДанных.НаборыДанных["НаборДанных1"].Поля;
	
Для Каждого Поле Из Поля Цикл
		
	Если Поле.Заголовок 	= "Организация" Тогда
		Поле.Заголовок = Поле.Заголовок + "_Аналитика";
		Прервать
	КонецЕсли;
		
КонецЦикла;

#КонецОбласти

#КонецЕсли
