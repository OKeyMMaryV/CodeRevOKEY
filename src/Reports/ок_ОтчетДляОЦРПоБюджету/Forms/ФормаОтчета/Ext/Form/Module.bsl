&НаСервере
Перем НачальныеПользовательскиеНастройки;


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	БылУстановленПривилегированныйРежим = Ложь;
	
	Если Параметры.Свойство("ПривилегированныйРежим") 
	   И Параметры.ПривилегированныйРежим = Истина	
	   И НЕ ПривилегированныйРежим()
	Тогда 
		УстановитьПривилегированныйРежим(Истина);
		БылУстановленПривилегированныйРежим = Истина;
	КонецЕсли;
	
	//ОКЕЙ Морозов А.В. (СофтЛаб) Начало 2020-09-04 (#3775)
	Если Параметры.Свойство("ТолстыйКлиент") 
	   И Параметры.ТолстыйКлиент
	Тогда 
		Отчет.ТолстыйКлиент = Истина;
	КонецЕсли;
	//ОКЕЙ Морозов А.В. (СофтЛаб) Конец 2020-09-04 (#3775)
	
	УстановитьСтандартныеНастройкиНаСервере();
		
	//ЗаполнитьДоступныеЗначенияПолей();
	
	Если БылУстановленПривилегированныйРежим 
	   И ПривилегированныйРежим()	
	Тогда 
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДоступныеЗначенияПолей()
	
	ОтчетОб = РеквизитФормыВЗначение("Отчет");
	СКД = ОтчетОб.ПолучитьМакет("СКД");
	НаборКЗБ = СКД.НаборыДанных.Найти("КЗБ");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	|	ОК_БК_ОтветственныеЗаЦентрРезультата.СтатьяОборотов КАК СтатьяОборотов,
	|	ОК_БК_ОтветственныеЗаЦентрРезультата.ОбъектСтроительства КАК ОбъектСтроительства,
	|	ОК_БК_ОтветственныеЗаЦентрРезультата.ЦФО КАК ЦФО
	|ПОМЕСТИТЬ ВТ0_Данные
	|ИЗ
	|	РегистрСведений.ОК_БК_ОтветственныеЗаЦентрРезультата КАК ОК_БК_ОтветственныеЗаЦентрРезультата
	|ГДЕ
	|	ОК_БК_ОтветственныеЗаЦентрРезультата.Инициатор = &Инициатор
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ0_Данные.СтатьяОборотов КАК Значение,
	|	""СтатьяОборотовКонтролируемая"" КАК Поле,
	|	ВТ0_Данные.СтатьяОборотов.ВидСтатьи КАК ВидСтатьиОборотов,
	|	ВТ0_Данные.СтатьяОборотов.Наименование КАК ПолеСортировки
	|ИЗ
	|	ВТ0_Данные КАК ВТ0_Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ0_Данные.ЦФО,
	|	""ЦФО"",
	|	NULL,
	|	ВТ0_Данные.ЦФО.Наименование
	|ИЗ
	|	ВТ0_Данные КАК ВТ0_Данные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ВТ0_Данные.ОбъектСтроительства,
	|	""ОбъектСтроительства"",
	|	NULL,
	|	ВТ0_Данные.ОбъектСтроительства.Наименование
	|ИЗ
	|	ВТ0_Данные КАК ВТ0_Данные
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПолеСортировки
	|ИТОГИ ПО
	|	Поле";
	
	Запрос.УстановитьПараметр("Инициатор"	,	ПараметрыСеанса.бит_бк_ТекущийИнициатор);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоле = РезультатЗапроса.Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	Отчет.СписокВидовСтатейДоступныхИнициатору.Очистить();
	
	Пока ВыборкаПоле.Следующий() Цикл 
		
		Поле			   	    = НаборКЗБ.Поля.Найти(ВыборкаПоле.Поле);
		Если Поле = Неопределено Тогда 
			Продолжить;
		КонецЕсли;
		
		СпЗначений = Новый СписокЗначений;
		
		ВыборкаДетали = ВыборкаПоле.Выбрать();
		Пока ВыборкаДетали.Следующий() Цикл 
			
			СпЗначений.Добавить(ВыборкаДетали.Значение);
			Если ЗначениеЗаполнено(ВыборкаДетали.ВидСтатьиОборотов) Тогда 
				Если Отчет.СписокВидовСтатейДоступныхИнициатору.НайтиПоЗначению(ВыборкаДетали.ВидСтатьиОборотов) = Неопределено Тогда 
					Отчет.СписокВидовСтатейДоступныхИнициатору.Добавить(ВыборкаДетали.ВидСтатьиОборотов);
				КонецЕсли;
			КонецЕсли;	
		КонецЦикла;
		
		Поле.УстановитьДоступныеЗначения(СпЗначений);
		
	КонецЦикла;
	
	ИсточникДоступныхНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СКД);
	ОтчетОб.КомпоновщикНастроек.Инициализировать(ИсточникДоступныхНастроек);
		
	ЗначениеВРеквизитФормы(ОтчетОб, "Отчет");                                                                                                       	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьНастройки(Команда)
	
	Элементы.КомпоновщикНастроекПользовательскиеНастройки.Видимость = Не Элементы.КомпоновщикНастроекПользовательскиеНастройки.Видимость;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьСтандартныеНастройкиНаСервере()
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	СКД = ОтчетОбъект.ПолучитьМакет("СКД");
	//ОКЕЙ Морозов А.В. (СофтЛаб) Начало 2020-09-04 (#3775)
	//Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(СКД.НастройкиПоУмолчанию);
	Если Отчет.ТолстыйКлиент Тогда
		НастройкиПоУмолчанию = СКД.НастройкиПоУмолчанию;
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(НастройкиПоУмолчанию.Отбор, "ОЦР", Справочники.бит_БК_Инициаторы.ПустаяСсылка(), ВидСравненияКомпоновкиДанных.Равно, , Ложь, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ, Новый УникальныйИдентификатор());
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НастройкиПоУмолчанию);
	КонецЕсли;
	//ОКЕЙ Морозов А.В. (СофтЛаб) Конец 2020-09-04 (#3775)
	НачальныеПользовательскиеНастройки = Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
	ЗаполнитьДоступныеЗначенияПолей();

КонецПроцедуры

&НаКлиенте
Процедура УстановитьСтандартныеНастройки(Команда)
	УстановитьСтандартныеНастройкиНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПриОбновленииСоставаПользовательскихНастроекНаСервере(СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	Если НачальныеПользовательскиеНастройки<>Неопределено Тогда 
		Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(НачальныеПользовательскиеНастройки);
	КонецЕсли;
КонецПроцедуры

//ОКЕЙ Морозов А.В. (СофтЛаб) Начало 2020-09-04 (#3775)
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьСтандартныеНастройкиНаСервере();
КонецПроцедуры
//ОКЕЙ Морозов А.В. (СофтЛаб) Конец 2020-09-04 (#3775)
