
////////////////////////////////////////////////////////////////////////////////
// ПЕРЕМЕННЫЕ МОДУЛЯ

// Хранит соответствие результатов формирования отчёта
&НаКлиенте
Перем мСоответствиеРезультатов;


////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ
            
// бит_ASubbotina Процедура - обработчик события "ПриОткрытии" формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если фОтказ Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	мСоответствиеРезультатов = Новый Соответствие;
	
	Если Не ЗначениеЗаполнено(Отчет.ДатаС) Тогда
		Отчет.ДатаС = НачалоГода(ТекущаяДата());	
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Отчет.ДатаПо) Тогда
		Отчет.ДатаПо = КонецГода(ТекущаяДата());
	КонецЕсли;
	
	//Отчет.СоздатьДляПериметра = Ложь;
	
	УстановитьВидимостьПоляПериметра();
	
	//БИТ Тртилек 01062014 правка в связи с изменением плана счетов
	//Отчет.СчетВыручки = ПланыСчетов.бит_Дополнительный_2.НайтиПоКоду("62602");
// +СБ Полякова Ирина. 2016-11-21 Redmine 2705
//Счет выручки заполняется первым значением из массива допустимых счетов при создании на сервере формы
	//Отчет.СчетВыручки = Бит_Дополнительный_2НайтиПоКоду("653");
// +СБ Полякова Ирина
	//БИТ Тртилек
	 	
КонецПроцедуры // ПриОткрытии()

&НаСервереБезКонтекста
Функция Бит_Дополнительный_2НайтиПоКоду(Код)
	
	Возврат ПланыСчетов.бит_Дополнительный_2.НайтиПоКоду(Код)
	
КонецФункции

// бит_ASubbotina Процедура - обработчик события "ОбработкаОповещения" формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = ("СохраненаНастройка_" + фПолноеИмяОтчета) Тогда
		
		ОбновитьПанельСохраненныхНастроек(Истина, Параметр);
				
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()


////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ КОМАНД ФОРМЫ

// бит_ASubbotina Процедура - обработчик команды "КомандаФильтроватьНастройкиПоВариантам"
//
&НаКлиенте
Процедура КомандаФильтроватьНастройкиПоВариантам(Команда)
	
	фФильтроватьНастройкиПоВарианту = Не фФильтроватьНастройкиПоВарианту;
	
	Элементы.КомандаФильтроватьНастройкиПоВариантам.Пометка = фФильтроватьНастройкиПоВарианту;
	ИзменитьФильтрНастроек(фФильтроватьНастройкиПоВарианту);
	
КонецПроцедуры // КомандаФильтроватьНастройкиПоВариантам()

// бит_ASubbotina Процедура - обработчик команды "КомандаПанельНастроек"
//
&НаКлиенте
Процедура КомандаПанельНастроек(Команда)
	
	фСкрытьПанельНастроек = Не фСкрытьПанельНастроек;
	
	Элементы.ФормаКомандаПанельНастроек.Пометка = Не фСкрытьПанельНастроек;
	Элементы.ГруппаПанельНастроек.Видимость     = Не фСкрытьПанельНастроек;	
	
КонецПроцедуры // КомандаПанельНастроек()

// бит_ASubbotina Процедура - обработчик команды "КомандаПанельСохраненныхНастроек"
//
&НаКлиенте
Процедура КомандаПанельСохраненныхНастроек(Команда)
	
	фСкрытьПанельСохранённыхНастроек = Не фСкрытьПанельСохранённыхНастроек;
	
	Элементы.ФормаКомандаПанельСохраненныхНастроек.Пометка   = Не фСкрытьПанельСохранённыхНастроек;
	Элементы.ГруппаПанельВыбораСохраненныхНастроек.Видимость = Не фСкрытьПанельСохранённыхНастроек;
	
КонецПроцедуры // КомандаПанельСохраненныхНастроек()

// бит_ASubbotina Процедура - обработчик команды "КомандаПанельСохраненныхНастроек"
//
&НаКлиенте
Процедура КомандаОбновитьПанельСохраненныхНастроек(Команда)
	
	ОбновитьПанельСохраненныхНастроек(Истина);	
	
КонецПроцедуры // КомандаОбновитьПанельСохраненныхНастроек()


// бит_ASubbotina Процедура - обработчик команды "Результат_ПоказатьВОтдельномОкне"
//
&НаКлиенте
Процедура Результат_ПоказатьВОтдельномОкне(Команда)
	
	бит_ОтчетыКлиент.ПоказатьКопиюРезультата(Результат);	
		
КонецПроцедуры // Результат_ПоказатьВОтдельномОкне()


////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

// бит_ASubbotina Процедура - обработчик события "Нажатие" элемента формы "ДекорацияНастройки<№>".
//
&НаКлиенте
Процедура ДекорацияСохраненнойНастройкиНажатие(Элемент)
	
	// Сохраним результат
	Если ЗначениеЗаполнено(фИмяЭлемента_ВыбраннаяНастройка) 
		И фСтруктураСохраненныхНастроек.Свойство(фИмяЭлемента_ВыбраннаяНастройка) Тогда
		СтруктураСохр = Новый Структура("Результат, ДанныеРасшифровки", Результат, ДанныеРасшифровки);
		КлючНастройки = фСтруктураСохраненныхНастроек[фИмяЭлемента_ВыбраннаяНастройка].КлючНастройки;
		мСоответствиеРезультатов.Вставить(КлючНастройки, СтруктураСохр);
	КонецЕсли;
	
	// Обновление пользовательских настроек
	ИмяЭлемента = Элемент.Имя;
	НастройкиОбновлены = ОбновитьНастройки(ИмяЭлемента, мСоответствиеРезультатов);
	Если Не НастройкиОбновлены Тогда
		ТекстСообщения = Нстр("ru = 'Настройка не найдена. Обновите панель сохраненных настроек.'");
		бит_ОбщегоНазначенияКлиентСервер.ВывестиСообщение(ТекстСообщения); 	
	КонецЕсли;
	
	бит_ОтчетыКлиент.ОбработатьНажатиеНаПолеСохраненнойНастройки(Элементы, 
																Элемент, 
																фИмяЭлемента_ВыбраннаяНастройка);
																	     			
КонецПроцедуры // ДекорацияСохраненнойНастройкиНажатие()


////////////////////////////////////////////////////////////////////////////
// СЕРВЕРНЫЕ ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// бит_ASubbotina Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МетаданныеОбъекта = Метаданные.Отчеты.бит_ПроверочныйОтчетПоВыручкеШаг9;
	
	// Вызов механизма защиты
	фПолноеИмяОтчета = МетаданныеОбъекта.ПолноеИмя();
	бит_ЛицензированиеБФCервер.ПроверитьВозможностьРаботы(ЭтаФорма, фПолноеИмяОтчета, фОтказ);
	
	СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(Отчеты.бит_ПроверочныйОтчетПоВыручкеШаг9.ПолучитьМакет("ОсновнаяСхемаКомпоновкиДанных"), Истина);
	
	фЗагружатьНастройки   = Истина;
	
	УправлениеВидимостьюДоступностью();
	
	ОбновитьПанельСохраненныхНастроек();
	ЗаполнитьДополнительныеСписки();
	
// +СБ Полякова Ирина. 2016-11-21 Redmine 2705
	ДопустимыеСчетаВыручки = Новый Массив;
	ДопустимыеСчетаВыручки.Добавить(ПланыСчетов.бит_Дополнительный_2.ПрочиеДоходы);
	ДопустимыеСчетаВыручки.Добавить(ПланыСчетов.бит_Дополнительный_2.ВыручкаОтРеализацииОСиПрочихАктивов);
	
	Элементы.СчетВыручки.ТолькоПросмотр = Ложь;
	Элементы.СчетВыручки.СписокВыбора.ЗагрузитьЗначения(ДопустимыеСчетаВыручки);
	Элементы.СчетВыручки.РежимВыбораИзСписка = Истина;

	Отчет.СчетВыручки = ДопустимыеСчетаВыручки[0];
// +СБ Полякова Ирина
		
КонецПроцедуры // ПриСозданииНаСервере()

// бит_ASubbotina Процедура - обработчик события "ПриЗагрузкеДанныхИзНастроекНаСервере" формы.
//
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	фСкрытьПанельНастроек 			 = Настройки.Получить("фСкрытьПанельНастроек");
	фСкрытьПанельСохранённыхНастроек = Настройки.Получить("фСкрытьПанельСохранённыхНастроек");
	фФильтроватьНастройкиПоВарианту  = Настройки.Получить("фФильтроватьНастройкиПоВарианту");
	
	// Видимость панели настроек
	Элементы.ФормаКомандаПанельНастроек.Пометка = Не фСкрытьПанельНастроек;	
	Элементы.ГруппаПанельНастроек.Видимость     = Не фСкрытьПанельНастроек;
	
	// Видимость панели сохранённых настроек
	Элементы.ФормаКомандаПанельСохраненныхНастроек.Пометка 	 = Не фСкрытьПанельСохранённыхНастроек;
	Элементы.ГруппаПанельВыбораСохраненныхНастроек.Видимость = Не фСкрытьПанельСохранённыхНастроек;
	
	// Фильтр сохранённых настроек по варианту
	Элементы.КомандаФильтроватьНастройкиПоВариантам.Пометка = фФильтроватьНастройкиПоВарианту;
	ИзменитьФильтрНастроек(фФильтроватьНастройкиПоВарианту);
	
	УправлениеВидимостьюДоступностью();
	
КонецПроцедуры // ПриЗагрузкеДанныхИзНастроекНаСервере()

// бит_ASubbotina Процедура - обработчик события "ПриСохраненииВариантаНаСервере" формы.
//
&НаСервере
Процедура ПриСохраненииВариантаНаСервере(Настройки)
	
	Если фКлючТекущегоВарианта <> КлючТекущегоВарианта Тогда
		УстановитьТекущийВариант(КлючТекущегоВарианта); 		
	КонецЕсли;
	
КонецПроцедуры // ПриСохраненииВариантаНаСервере()

// бит_ASubbotina Процедура - обработчик события "ПриЗагрузкеВариантаНаСервере" формы.
//
&НаСервере
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)
	
	фКлючТекущегоВарианта = КлючТекущегоВарианта;
		
	КлючОбъекта = ?(ЗначениеЗаполнено(КлючТекущегоВарианта), 
					фПолноеИмяОтчета + "/" + КлючТекущегоВарианта, 
					фПолноеИмяОтчета);
		
	бит_ОтчетыСервер.ИзменитьВидимостьСохраненныхНастроек(Элементы, фСтруктураСохраненныхНастроек, КлючОбъекта, фФильтроватьНастройкиПоВарианту, Истина);
	
	Если фЗагружатьНастройки Тогда
		
		// Установка настройки, используемой при открытии, если такая указана в справочнике
		КлючНастройкиПоУмолчанию = бит_ОтчетыСервер.НайтиНастройкуПоУмолчаниюДляОбъекта(КлючОбъекта, Истина);
		
		Если ЗначениеЗаполнено(КлючНастройкиПоУмолчанию) Тогда
			УстановитьТекущиеПользовательскиеНастройки(КлючНастройкиПоУмолчанию);
		Иначе
			УстановитьСтандартныеНастройкиСервер();
			фИмяЭлемента_ВыбраннаяНастройка = "";
		КонецЕсли;
		
		Результат.Очистить();
	
	КонецЕсли;
		
КонецПроцедуры // ПриЗагрузкеВариантаНаСервере()
 
// бит_ASubbotina Процедура - обработчик события "ПриЗагрузкеПользовательскихНастроекНаСервере" формы.
// 
&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	Если Настройки = Неопределено ИЛИ Не фЗагружатьНастройки Тогда 
		Возврат;
	КонецЕсли;
	
	Если Настройки.ДополнительныеСвойства.Свойство("КлючНастройки") Тогда
		ТекКлючНастройки = Настройки.ДополнительныеСвойства.КлючНастройки;
		бит_ОтчетыСервер.ВыделитьТекущуюСохраненнуюНастройку(Элементы, 
															фСтруктураСохраненныхНастроек, 
															фИмяЭлемента_ВыбраннаяНастройка,
															фФильтроватьНастройкиПоВарианту,
															ТекКлючНастройки);
	КонецЕсли;
			
КонецПроцедуры // ПриЗагрузкеПользовательскихНастроекНаСервере()


////////////////////////////////////////////////////////////////////////////
// СЕРВЕРНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

// бит_ASubbotina Процедура управляет видимостью и доступностью элементов формы
//
// Параметры:
//  Нет
//
&НаСервере
Процедура УправлениеВидимостьюДоступностью()
	
	// Установка видимости и доступности элементов формы в зависимости от типа отчёта 
	// - обычный или расшифровка 
	бит_ОтчетыСервер.УстановитьВидимостьДоступностьЭлементов(Элементы, 
															Параметры.КлючВарианта, 
															Параметры.ПредставлениеВарианта);
		          	
КонецПроцедуры // УправлениеВидимостьюДоступностью()

// бит_ASubbotina Процедура включает/отключает фильтр настроек по варианту
//
// Параметры:
//  Фильтровать  - Булево
//
&НаСервере
Процедура ИзменитьФильтрНастроек(Фильтровать)

	КлючОбъекта = ?(ЗначениеЗаполнено(КлючТекущегоВарианта), 
					фПолноеИмяОтчета + "/" + КлючТекущегоВарианта, 
					фПолноеИмяОтчета);
	бит_ОтчетыСервер.ИзменитьВидимостьСохраненныхНастроек(Элементы, фСтруктураСохраненныхНастроек, КлючОбъекта, фФильтроватьНастройкиПоВарианту);	

КонецПроцедуры // ИзменитьФильтрНастроек()

// бит_ASubbotina Функция обновляет настройки отчета
//
// Параметры:
//  ИмяЭлемента  		 	- Строка
//  СоответствиеРезультатов - Соответствие
//
// ВозращаемоеЗначение:
//  Булево - настройки обновлены
//
&НаСервере
Функция ОбновитьНастройки(ИмяЭлемента, СоответствиеРезультатов)

	НастройкиОбновлены = Ложь;
	
	Результат.Очистить();
	
	СтруктураНастроек = фСтруктураСохраненныхНастроек[ИмяЭлемента];
	
	Настройки = бит_ОтчетыСервер.ПолучитьНастройкиОтчета(СтруктураНастроек);
	
	Если Настройки <> Неопределено Тогда
			
		КлючНастройки = СтруктураНастроек.КлючНастройки;    	
		КлючОбъекта = СтрЗаменить(СтруктураНастроек.КлючОбъекта, фПолноеИмяОтчета + "/", "");
		
		Если КлючОбъекта <> КлючТекущегоВарианта Тогда
			фЗагружатьНастройки = Ложь;	
			УстановитьТекущийВариант(КлючОбъекта);
			фЗагружатьНастройки = Истина;
		КонецЕсли;
		
		УстановитьТекущиеПользовательскиеНастройки(КлючНастройки);
		НастройкиОбновлены = Истина;
		
	КонецЕсли;
	
	// Выведем результат, если он уже формировался для текущей настройки
	Если НастройкиОбновлены Тогда		
		СтруктураРез = СоответствиеРезультатов.Получить(КлючНастройки);
		Если СтруктураРез <> Неопределено Тогда
			Результат.Вывести(СтруктураРез.Результат);
			ДанныеРасшифровки = СтруктураРез.ДанныеРасшифровки;
		КонецЕсли; 		
	КонецЕсли;
	       	
	Возврат НастройкиОбновлены;

КонецФункции // ОбновитьНастройки()

// бит_ASubbotina Процедура обновляет панель сохраненных настроек
//
// Параметры:
//  Очищать  - Булево (Необязательный, по умолчанию = Ложь)
//  ТекКлючНастройки  (Необязательный)
//
&НаСервере
Процедура ОбновитьПанельСохраненныхНастроек(Очищать = Ложь, ТекКлючНастройки = Неопределено)

	ГруппаПанели = Элементы.ГруппаПанельВыбораСохраненныхНастроек;
	
	СтруктураДоступности = бит_ОтчетыСервер.ПроверитьДоступностьВариантовНастроек(фПолноеИмяОтчета, Истина);
	фСписокНеДоступныхВариантов = СтруктураДоступности.СписокНеДоступныхВариантов;
	фСписокНеДоступныхНастроек  = СтруктураДоступности.СписокНеДоступныхНастроек;
	лИспользуемыйПриОткрытииВариант = СтруктураДоступности.ИспользуемыйПриОткрытииВариант;
		
	Если Очищать Тогда 	
		
		бит_РаботаСДиалогамиСервер.УдалитьЭлементыГруппыФормы(Элементы, ГруппаПанели); 			
		
	Иначе
		
		Если лИспользуемыйПриОткрытииВариант = Неопределено Тогда
			КлючТекущегоВарианта = "";
			Возврат;
		Иначе
			КлючТекущегоВарианта = лИспользуемыйПриОткрытииВариант;		
		КонецЕсли;
	
	КонецЕсли; 	
	
	КлючОбъекта = фПолноеИмяОтчета + "/" + КлючТекущегоВарианта;
				
	бит_ОтчетыСервер.ОбновитьПанельСохраненныхНастроек(Элементы, 
													ГруппаПанели, 
													КлючОбъекта, 
													фСтруктураСохраненныхНастроек,
													СтруктураДоступности,
													фФильтроватьНастройкиПоВарианту,
													фИмяЭлемента_ВыбраннаяНастройка);
	
	Если ТекКлючНастройки <> Неопределено Тогда
		бит_ОтчетыСервер.ВыделитьТекущуюСохраненнуюНастройку(Элементы, 
															фСтруктураСохраненныхНастроек, 
															фИмяЭлемента_ВыбраннаяНастройка,
															фФильтроватьНастройкиПоВарианту,
															ТекКлючНастройки);	
	Иначе
		фИмяЭлемента_ВыбраннаяНастройка = "";														
	КонецЕсли;
	
КонецПроцедуры // ОбновитьПанельСохраненныхНастроек()

// бит_ASubbotina Процедура устанавливает стандартные настройки варианта 
// и обновляет по ним элементы формы.
// Заменяет типовую команду "СтандартныеНастройки".
//
&НаСервере
Процедура УстановитьСтандартныеНастройкиСервер()
	
	бит_ОтчетыСервер.УстановитьСтандартныеНастройкиСервер(Отчет, Истина, фСписокПараметровНаФорме);
		
	бит_ОтчетыСервер.ВыделитьТекущуюСохраненнуюНастройку(Элементы, 
														фСтруктураСохраненныхНастроек, 
														фИмяЭлемента_ВыбраннаяНастройка,
														фФильтроватьНастройкиПоВарианту);
															
КонецПроцедуры // УстановитьСтандартныеНастройкиСервер()

// бит_DFedotov Процедура заполняет дополнительные списки
//
// Параметры:
//  Нет
//
&НаСервере
Процедура ЗаполнитьДополнительныеСписки()

	// Список имён параметров СКД, заполняемых пользователем через элементы формы
	фСписокПараметровНаФорме.Добавить("ДатаС");
	фСписокПараметровНаФорме.Добавить("ДатаПо");
	фСписокПараметровНаФорме.Добавить("СчетВыручки");
	фСписокПараметровНаФорме.Добавить("ОрганизацияЗаказчик");
	фСписокПараметровНаФорме.Добавить("ОрганизацияПодрядчик");
	фСписокПараметровНаФорме.Добавить("ПериметрКонсолидации");
	фСписокПараметровНаФорме.Добавить("Объект");
	фСписокПараметровНаФорме.Добавить("СоздатьДляПериметра");
	// Список дополнительных свойств
	//фСписокДополнительныхСвойств.Добавить("Комментарий");
	// Пример в отчёте бит_ПланФактныйАнализУниверсальный
	
КонецПроцедуры // ЗаполнитьДополнительныеСписки()

&НаКлиенте
Процедура УстановитьВидимостьПоляПериметра()	
	

	Элементы.ПериметрКонсолидации.Видимость = Отчет.СоздатьДляПериметра;
	Элементы.ОрганизацияЗаказчик.Видимость = Не Отчет.СоздатьДляПериметра;
	Элементы.ОрганизацияПодрядчик.Видимость = Не Отчет.СоздатьДляПериметра;
	
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьДляПериметраПриИзменении(Элемент)
	
	Если (Отчет.СоздатьДляПериметра И (ЗначениеЗаполнено(Отчет.ОрганизацияЗаказчик) ИЛИ ЗначениеЗаполнено(Отчет.ОрганизацияПодрядчик))) ИЛИ (НЕ Отчет.СоздатьДляПериметра И ЗначениеЗаполнено(Отчет.ПериметрКонсолидации)) Тогда 
		Ответ = Вопрос(?(Отчет.СоздатьДляПериметра, "Организации будут очищены.", "Периметр консолидации будет очищен.") + " Продолжить", РежимДиалогаВопрос.ДаНет);
		Если Ответ = КодВозвратаДиалога.Нет Тогда
			Отчет.СоздатьДляПериметра = НЕ Отчет.СоздатьДляПериметра;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Отчет.СоздатьДляПериметра Тогда 
		Отчет.ОрганизацияЗаказчик = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
		Отчет.ОрганизацияПодрядчик = ПредопределенноеЗначение("Справочник.Организации.ПустаяСсылка");
	Иначе
		Отчет.ПериметрКонсолидации = ПредопределенноеЗначение("Справочник.бит_му_ПериметрыКонсолидации.ПустаяСсылка");
	КонецЕсли;
	
	УстановитьВидимостьПоляПериметра();
	
КонецПроцедуры

// Возвращает массив, по которому следует расшифровать отчет
&НаСервере
Функция ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровки, ТекущийОтчет = Неопределено, ВключатьРесурсы = Ложь) Экспорт
	
	МассивПолейРасшифровки = Новый СписокЗначений;
	
	Если ТипЗнч(Расшифровка) <> Тип("ИдентификаторРасшифровкиКомпоновкиДанных") 
		И ТипЗнч(Расшифровка) <> Тип("ДанныеРасшифровкиКомпоновкиДанных") Тогда
		Возврат МассивПолейРасшифровки;
	КонецЕсли;
	
	мДанныеРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	Если ТекущийОтчет = Неопределено Тогда
		ТекущийОтчетСервер = мДанныеРасшифровки;
	Иначе
		ТекущийОтчетСервер = ТекущийОтчет;
	КонецЕсли;
	
	// Добавим поля родительских группировок
	ДобавитьРодителей(мДанныеРасшифровки.Элементы[Расшифровка], ТекущийОтчетСервер, МассивПолейРасшифровки, ВключатьРесурсы);
	
	Количество = МассивПолейРасшифровки.Количество();
	Для Индекс = 1 По Количество Цикл
		ОбратныйИндекс = Количество - Индекс;
		Для ИндексВнутри = 0 По ОбратныйИндекс - 1 Цикл
			Если МассивПолейРасшифровки[ОбратныйИндекс].Представление = МассивПолейРасшифровки[ИндексВнутри].Представление Тогда
				МассивПолейРасшифровки.Удалить(ОбратныйИндекс);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат МассивПолейРасшифровки;
	
КонецФункции

&НаСервере
Функция ДобавитьРодителей(ЭлементРасшифровки, ТекущийОтчет, МассивПолейРасшифровки, ВключатьРесурсы = Ложь)  Экспорт
	
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		Для каждого Поле Из ЭлементРасшифровки.ПолучитьПоля() Цикл
			ДоступноеПоле = ПолучитьДоступноеПолеПоПолюКомпоновкиДанных(Новый ПолеКомпоновкиДанных(Поле.Поле), ТекущийОтчет);
			Если ДоступноеПоле = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Не ВключатьРесурсы И ДоступноеПоле.Ресурс Тогда
				Продолжить;
			КонецЕсли;
			МассивПолейРасшифровки.Добавить(Поле.Значение, Поле.Поле);
		КонецЦикла;
	КонецЕсли;
	Для каждого Родитель Из ЭлементРасшифровки.ПолучитьРодителей() Цикл
		ДобавитьРодителей(Родитель, ТекущийОтчет, МассивПолейРасшифровки, ВключатьРесурсы);
	КонецЦикла;
	
КонецФункции

// Возвращает доступное поле по полю компоновки
&НаСервере
Функция ПолучитьДоступноеПолеПоПолюКомпоновкиДанных(ПолеКомпоновкиДанных, ОбластьПоиска)
	
	Если ТипЗнч(ПолеКомпоновкиДанных) = Тип("Строка") Тогда
		ПолеПоиска = Новый ПолеКомпоновкиДанных(ПолеКомпоновкиДанных);
	Иначе
		ПолеПоиска = ПолеКомпоновкиДанных;
	КонецЕсли;
	
	Если ТипЗнч(ОбластьПоиска) = Тип("КомпоновщикНастроекКомпоновкиДанных")
		ИЛИ ТипЗнч(ОбластьПоиска) = Тип("ДанныеРасшифровкиКомпоновкиДанных")
		ИЛИ ТипЗнч(ОбластьПоиска) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат ОбластьПоиска.Настройки.ДоступныеПоляВыбора.НайтиПоле(ПолеПоиска);
	Иначе
		Возврат ОбластьПоиска.НайтиПоле(ПолеПоиска);
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ВыполнитьДействие1(Расшифровка, ДанныеРасшифровки)
	
	МассивПолейРасшифровки = ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровки);
	
	ФормаРегистра = ПолучитьФорму("РегистрСведений.бит_му_НРП_ВНА.ФормаСписка");
	
	СчетВыручки = ПредопределенноеЗначение("ПланСчетов.бит_Дополнительный_2.ПустаяСсылка");
	Объект      = ПредопределенноеЗначение("Справочник.ОбъектыСтроительства.ПустаяСсылка");
	Регистратор = Неопределено;
	
	Для Каждого ЭлементМассива ИЗ МассивПолейРасшифровки Цикл
		Если ЭлементМассива.Представление = "СчетВыручки" Тогда
			СчетВыручки = ЭлементМассива.Значение;
		КонецЕсли;
		Если ЭлементМассива.Представление = "Объект" Тогда
			Объект = ЭлементМассива.Значение;
		КонецЕсли;
		Если ЭлементМассива.Представление = "Регистратор" Тогда
			Регистратор = ЭлементМассива.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СчетВыручки) Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "СчетВыручки", СчетВыручки, Истина, ВидСравненияКомпоновкиДанных.Равно);
		Если ЗначениеЗаполнено(Объект) Тогда
			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "Субконто1СчетаВыручки", Объект, Истина, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Регистратор) Тогда
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "ДокументРеализации", Регистратор, Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "Период", Отчет.ДатаС, Истина, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ФормаРегистра.Список.КомпоновщикНастроек.Настройки.Отбор, "Период", ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
								КонецДня(Отчет.ДатаПо), "Период", Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьДействие2(Расшифровка, ДанныеРасшифровки)
	
	МассивПолейРасшифровки = ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровки);
	
	ФормаРегистра = ПолучитьФорму("РегистрБухгалтерии.бит_Дополнительный_2.ФормаСписка");
	
	СчетВыручки = ПредопределенноеЗначение("ПланСчетов.бит_Дополнительный_2.ПустаяСсылка");
	Объект      = ПредопределенноеЗначение("Справочник.ОбъектыСтроительства.ПустаяСсылка");
	Регистратор = Неопределено;
	
	Для Каждого ЭлементМассива ИЗ МассивПолейРасшифровки Цикл
		Если ЭлементМассива.Представление = "СчетВыручки" Тогда
			СчетВыручки = ЭлементМассива.Значение;
		КонецЕсли;
		Если ЭлементМассива.Представление = "Объект" Тогда
			Объект = ЭлементМассива.Значение;
		КонецЕсли;
		Если ЭлементМассива.Представление = "Регистратор" Тогда
			Регистратор = ЭлементМассива.Значение;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(СчетВыручки) Тогда
		ФормаРегистра.ОтборСчет = СчетВыручки;
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "Счет", СчетВыручки, Истина, ВидСравненияКомпоновкиДанных.Равно);
		Если ЗначениеЗаполнено(Объект) Тогда
			ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "Субконто1", Объект, Истина, ВидСравненияКомпоновкиДанных.Равно);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Регистратор) Тогда
		ФормаРегистра.ОтборРегистратор = Регистратор;
		ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "Регистратор", Регистратор, Истина, ВидСравненияКомпоновкиДанных.Равно);
	КонецЕсли;
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ФормаРегистра.Список, "Период", Отчет.ДатаС, Истина, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
	ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(ФормаРегистра.Список.КомпоновщикНастроек.Настройки.Отбор, "Период", ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,
								КонецДня(Отчет.ДатаПо), "Период", Истина, РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
	
	ФормаРегистра.Открыть();
	
КонецПроцедуры

&НаКлиенте
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка=Ложь;
	
	ОбработкаРасшифровки=Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	ДополнительныеДействия=Новый СписокЗначений;
	
	ДополнитьДополнительныеДествияНаСервере(ДополнительныеДействия, Расшифровка);
	
	СтандартныеДействия = Новый Массив;
	СтандартныеДействия.Добавить(ДействиеОбработкиРасшифровкиКомпоновкиДанных.ОткрытьЗначение);
	ДополнительныеПараметры = Новый Структура("Расшифровка, ДанныеРасшифровки", Расшифровка, ДанныеРасшифровки);
	ОписаниеОповещения = Новый ОписаниеОповещения("РезультатОбработкаРасшифровкиПослеВыбора", ЭтотОбъект, ДополнительныеПараметры);
	ОбработкаРасшифровки.ПоказатьВыборДействия(ОписаниеОповещения, Расшифровка, СтандартныеДействия, ДополнительныеДействия);	
	
КонецПроцедуры

&НаСервере
Функция ДополнитьДополнительныеДествияНаСервере(ДополнительныеДействия, Расшифровка)
	
	мДанныеРасшифровки = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	Если мДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Поле = "СуммаНРПВНА"
			ИЛИ мДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Поле = "Дельта" Тогда                                                                               
		ДополнительныеДействия.Добавить("ОткрытьНРПВНА","НРП ВНА");  // добавляем свое действие
	КонецЕсли;
	
	Если мДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Поле = "СуммаМУ"
			ИЛИ мДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Поле = "Дельта" Тогда	
		ДополнительныеДействия.Добавить("ОткрытьМСФО","Международный");  // добавляем свое действие
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура РезультатОбработкаРасшифровкиПослеВыбора(Результат, Значение, ДополнительныеПараметры) Экспорт
	
	Если Значение <> Неопределено Тогда
		ОткрытьЗначение(Значение);
	Иначе
		Если Результат = "ОткрытьНРПВНА" Тогда
			ВыполнитьДействие1(ДополнительныеПараметры.Расшифровка, ДополнительныеПараметры.ДанныеРасшифровки);       // процедура выполняемая при выборе пункта меню "Дополнительное действие 1"
		ИначеЕсли Результат = "ОткрытьМСФО" Тогда
			ВыполнитьДействие2(ДополнительныеПараметры.Расшифровка, ДополнительныеПараметры.ДанныеРасшифровки);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры
