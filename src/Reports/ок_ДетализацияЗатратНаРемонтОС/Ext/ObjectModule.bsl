
Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьПараметрыСКД();	
	
КонецПроцедуры

Процедура УстановитьПараметрыСКД()
	
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("Пользователь", 									ТекущийПользователь);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_1_ПолныеПрава", 					Ложь);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_2_СоответствиеФД_ЦФО", 			Ложь);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_3_ОтветственныеЗаЦентрРезультата", Ложь);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_4_ИнициаторФВБ",					Ложь);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам",									"");
	
	ГруппаПолныеПраваНаОтчет = ОК_ОбщегоНазначения.ПолучитьЗначениеУниверсальнойНастройки("Детализация затрат на ремонт ОС", "Полные права на отчет");
	Если ТипЗнч(ГруппаПолныеПраваНаОтчет) = Тип("СправочникСсылка.ГруппыПользователей") И ЗначениеЗаполнено(ГруппаПолныеПраваНаОтчет) Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
		                      |	ГруппыПользователейСостав.НомерСтроки КАК НомерСтроки
		                      |ИЗ
		                      |	Справочник.ГруппыПользователей.Состав КАК ГруппыПользователейСостав
		                      |ГДЕ
		                      |	ГруппыПользователейСостав.Ссылка = &Ссылка
		                      |	И ГруппыПользователейСостав.Пользователь = &Пользователь");
		
		Запрос.УстановитьПараметр("Ссылка",ГруппаПолныеПраваНаОтчет);
		Запрос.УстановитьПараметр("Пользователь",ТекущийПользователь);
		
		Если НЕ Запрос.Выполнить().Пустой() Тогда 
			КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_1_ПолныеПрава", Истина);
			КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам", "Полные права");
			Возврат 
		КонецЕсли;
		
	КонецЕсли;
		
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	СБ_СоответствиеФД_ЦФО.ЦФО КАК ЦФО
	                      |ИЗ
	                      |	РегистрСведений.СБ_СоответствиеФД_ЦФО КАК СБ_СоответствиеФД_ЦФО
	                      |ГДЕ
	                      |	(СБ_СоответствиеФД_ЦФО.Инициатор.Пользователь = &Пользователь
	                      |			ИЛИ СБ_СоответствиеФД_ЦФО.РуководительФункции.Пользователь = &Пользователь
	                      |			ИЛИ СБ_СоответствиеФД_ЦФО.ОтветственныйЗаКорректировкуБК.Пользователь = &Пользователь)");
	
	Запрос.УстановитьПараметр("Пользователь",ТекущийПользователь);
	
	СписокЦФО = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ЦФО");
	
	Если СписокЦФО.Количество() Тогда 
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_2_СоответствиеФД_ЦФО", Истина);
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_2_СписокЦФО", СписокЦФО);
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам", "Соответствие РФ, ФД и ЦФО");
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	                      |	ОК_БК_ОтветственныеЗаЦентрРезультата.СтатьяОборотов КАК СтатьяОборотов,
	                      |	ОК_БК_ОтветственныеЗаЦентрРезультата.ОбъектСтроительства КАК Объект,
	                      |	ОК_БК_ОтветственныеЗаЦентрРезультата.ЦФО КАК ЦФО
	                      |ИЗ
	                      |	РегистрСведений.ОК_БК_ОтветственныеЗаЦентрРезультата КАК ОК_БК_ОтветственныеЗаЦентрРезультата
	                      |ГДЕ
	                      |	ОК_БК_ОтветственныеЗаЦентрРезультата.Инициатор.Пользователь = &Пользователь");
	
	Запрос.УстановитьПараметр("Пользователь",ТекущийПользователь);
	
	Если НЕ Запрос.Выполнить().Пустой() Тогда 
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_3_ОтветственныеЗаЦентрРезультата", Истина);
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам", "Ответственные за центр результата");
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(бит_БК_ТекущийИнициатор) Тогда
		КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ИнициаторФВБ", бит_БК_ТекущийИнициатор);
	КонецЕсли;
	
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам_4_ИнициаторФВБ", Истина);
	КомпоновщикНастроек.Настройки.ПараметрыДанных.УстановитьЗначениеПараметра("ОтборПоПравам", "Инициатор заявки ФВБ");
	
КонецПроцедуры	
